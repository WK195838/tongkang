     /****************************************************************/
     /*                                                              */
     /*            1.PROGRAM-ID    MIP011P                           */
     /*            2.PROGRAM-TYPE  CLP                               */
     /*            3.AUTHOR        JEFF                              */
     /*            4.FUNCTION     部門別領退料傳票代開             */
     /*            5.DATE-WRITTEN  83/07/28                          */
     /*            6.UPDATE        97/06/10 2008MT229 S00TPC(9706A)  */
     /*                            98/01/14 2009MT308 S00TPC(9801B)  */
     /*                            98/07/20 2009MT366 S00TPC(9807A)  */
     /*                            99/11/10 2010MT478 S00TPC(9911A)  */
     /*                           100/11/09 1001108MT S00TPC(0011A)  */
     /*                           100/12/20 2011MT576 S00TPC(0012A)  */
     /*                           101/12/07 2012MT635 S00TPC(0112A)  */
     /*                           102/04/16 2012MT635 S00TPC(0204A)  */
     /*                                                              */
     /****************************************************************/
             PGM
/*0112A*/    DCL        VAR(&IFRSYM) TYPE(*CHAR) LEN(6)
/*9706A*/    DCL        VAR(&P#YYMM) TYPE(*CHAR) LEN(6)
             DCL        VAR(&P#CODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&INT)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&OUTQ)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03)  TYPE(*LGL)  LEN(1)
/*9706A*/    DCL        VAR(&DATE)  TYPE(*DEC)  LEN(6)
/*9706A*/    DCL        VAR(&DATEA) TYPE(*CHAR) LEN(6)
/*9706A*/    DCL        VAR(&DATEE) TYPE(*DEC)  LEN(8)
/*9706A*/    DCL        VAR(&DATEN) TYPE(*CHAR) LEN(8)
/*9706A*/    DCL        VAR(&DATEB) TYPE(*CHAR) LEN(8)
/*9911A*/    DCL        VAR(&DATEX) TYPE(*CHAR) LEN(8)
/*9911A*/    DCL        VAR(&DATEY) TYPE(*CHAR) LEN(8)
             DCL        VAR(&AREA)  TYPE(*CHAR) LEN(1)
/*9706A*/    DCL        VAR(&Y)     TYPE(*CHAR) LEN(4)
             DCL        VAR(&M)     TYPE(*CHAR) LEN(2)
/*9911A*/    DCL        VAR(&YM)    TYPE(*CHAR) LEN(6)
             DCL        VAR(&DTARA) TYPE(*CHAR) LEN(12)
/*0011A*/    DCL        VAR(&P#VRNS) TYPE(*CHAR) LEN(6)
/*0011A*/    DCL        VAR(&P#VRNE) TYPE(*CHAR) LEN(6)
             RTVDTAARA  DTAARA(*LDA (1021 1)) RTNVAR(&AREA)
             CHGVAR     VAR(&DTARA) VALUE('ACCTLDTA' *CAT &AREA)
             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT)
             IF         COND(&INT *EQ '1') THEN(DO)
/*0112A*/    RTVDTAARA  DTAARA(AMIFRSCTL (1 6)) RTNVAR(&IFRSYM)
/*9706A*/    RTVDTAARA  DTAARA(&DTARA (9 8)) RTNVAR(&DATEB)
 DISPLY:     CALL       PGM(MIP011RS) PARM(&IN03 &DATE &AREA &DATEB &DATEE)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)
             CHGVAR     VAR(&DATEA) VALUE(&DATE)
             CHGVAR     VAR(&DATEN) VALUE(&DATEE)
/*9706A*/    CHGDTAARA  DTAARA(*LDA (1 6)) VALUE(&DATEA)
/*9706A*/    CHGDTAARA  DTAARA(*LDA (9 1)) VALUE(&AREA)
/*9706A*/    CHGDTAARA  DTAARA(*LDA (11 8)) VALUE(&DATEN)
/*0112A*/    CHGDTAARA  DTAARA(*LDA (472 6)) VALUE(&IFRSYM)
/*9706A*/    RTVDTAARA  DTAARA(*LDA (1 6)) RTNVAR(&P#YYMM)
             CALL    PGM(ACP101R) PARM(&P#YYMM &P#CODE)
             IF         COND(&P#CODE *EQ '1') THEN(DO)
             GOTO       CMDLBL(DISPLY)
             ENDDO
/*9706A      SBMJOB     JOB(MIP011P) JOBD(ACJOBD) OUTQ(&OUTQ) +
                          RQSDTA('CALL MIP011P')   */
/*9706A      RETURN        */
             ENDDO
 BATCH:
/*9706A*/    RTVDTAARA  DTAARA(*LDA (1 6)) RTNVAR(&DATEA)
/*9706A*/    RTVDTAARA  DTAARA(*LDA (1 4)) RTNVAR(&Y)
/*9706A*/    RTVDTAARA  DTAARA(*LDA (5 2)) RTNVAR(&M)
/*9911A*/    RTVDTAARA  DTAARA(*LDA (1 6)) RTNVAR(&YM)
/*9706A*/    RTVDTAARA  DTAARA(*LDA (9 1)) RTNVAR(&AREA)
             CHGVAR     VAR(&DATEX) VALUE(&YM *CAT '01')
             CHGVAR     VAR(&DATEY) VALUE(&YM *CAT '31')

             /*部門別領料代開*/
/*9706A*/    OVRDBF     FILE(MTTRNS) TOFILE(MTTRNS) SHARE(*YES)
/*0012A*/    OPNQRYF    FILE((MTTRNS)) +
                        QRYSLT('(CHAR6 *GE ''' || &DATEX || ''') +
                          *AND  (CHAR6 *LE ''' || &DATEY || ''') +
                          *AND (I4LOCA *EQ ''' || &AREA || ''') +
                          *AND (I4FORM *EQ "MI21" *OR +
                                I4FORM *EQ "MI22") +
                          *AND (I4ENID *EQ " ") +
                          *AND (I4COMT *EQ " ") +
                          *AND (%SST(I4BK12 3 10) *EQ " ") +
                          *AND (I4ACD *NE "D")') +
                        KEYFLD((I4LOCA) (I4CTDV) (I4ACNO) (I4EXPS) +
                               (I4MLNO) (I4DATE)) +
                        MAPFLD((CHAR6 I4DATE *CHAR 8))
/*9706A*/    OVRDBF     FILE(ACVOUR) TOFILE(('AC' || &AREA || 'R'))
/*0204A*/    OVRDBF     FILE(BCNAME) TOFILE(DALIBR/BCNAME) /* 4碼會計科目*/
             CALL       PGM(MIP011RC)
/*9706A*/    OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 158) +
                          CPI(12) OVRFLW(64) SAVE(*YES) +
                          USRDTA('領料代開')
             CALL       PGM(MIP011R)
             CLOF       OPNID(MTTRNS)

             /*工程別領料代開（研發費用）*/
/*9807A*/    DLTF       FILE(QTEMP/MIP011W)
/*9807A*/    MONMSG     MSGID(CPF0000)
/*9807A*/    CRTDUPOBJ  OBJ(MIP011W) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(MIP011W) DATA(*NO)
/*9807A*/    OVRDBF     FILE(MIP011W) TOFILE(QTEMP/MIP011W)
/*9801B*/    OVRDBF     FILE(MTTRNS) TOFILE(MTTRNS) SHARE(*YES)
/*0012A*/    OPNQRYF    FILE((MTTRNS)) +
                        QRYSLT('(CHAR6 *GE ''' || &DATEX || ''') +
                          *AND  (CHAR6 *LE ''' || &DATEY || ''') +
                          *AND (I4LOCA *EQ ''' || &AREA || ''') +
                          *AND (I4FORM *EQ "MI21" *OR +
                                I4FORM *EQ "MI22") +
                          *AND (I4ENID *NE " ") +
                          *AND (I4COMT *EQ " ") +
                          *AND (%SST(I4BK12 3 10) *EQ " ") +
                          *AND (I4ACD *NE "D")  +
                          *AND (I4ACNO *NE "1371") +
                          *AND (I4ACNO *NE "1374")') +
                        KEYFLD((I4LOCA) (I4ENID) (I4COMT) (I4CTDV) (I4ACNO) +
                               (I4EXPS) (I4DEPT) (I4MLNO) (I4DATE) +
                               (I4FORM) (I4IVNO) (I4SEQN)) +
                        MAPFLD((CHAR6 I4DATE *CHAR 8))
/*9807A*/    CALL       PGM(MIP011R2)
/*9801B*/    CLOF       OPNID(MTTRNS)

/*9807A*/    OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 158) +
                          CPI(12) OVRFLW(64) SAVE(*YES) +
                          USRDTA('工程領代')
/*9807A*/    OVRDBF     FILE(MTTRNS) TOFILE(QTEMP/MIP011W) +
                          SHARE(*YES)
/*9807A*/    OPNQRYF    FILE((MTTRNS)) QRYSLT(*ALL) +
                        KEYFLD((I4LOCA) (I4ENID) (I4COMT) (I4CTDV) (I4ACNO) +
                               (I4EXPS) (I4DEPT) (I4MLNO) (I4DATE) +
                               (I4FORM) (I4IVNO) (I4SEQN))
/*9807A*/    CALL       PGM(MIP011RA)
/*9807A*/    CLOF       OPNID(MTTRNS)

             /*工程別領料代開（未完工程）*/
/*9801B*/    OVRDBF     FILE(MTTRNS) TOFILE(MTTRNS) SHARE(*YES)
/*0012A*/    OPNQRYF    FILE((MTTRNS)) +
                        QRYSLT('(CHAR6 *GE ''' || &DATEX || ''') +
                          *AND  (CHAR6 *LE ''' || &DATEY || ''') +
                          *AND (I4LOCA *EQ ''' || &AREA || ''') +
                          *AND (I4FORM *EQ "MI21" *OR +
                                I4FORM *EQ "MI22") +
                          *AND (I4ENID *NE " ") +
                          *AND (I4COMT *EQ " ") +
                          *AND (%SST(I4BK12 3 10) *EQ " ") +
                          *AND (I4ACD *NE "D")  +
                          *AND ((I4ACNO *EQ "1371") +
                          *OR   (I4ACNO *EQ "1374"))') +
                        KEYFLD((I4LOCA) (I4ENID) (I4COMT) (I4ACNO) +
                               (I4EXPS) (I4DEPT) (I4MLNO) (I4DATE) +
                               (I4FORM) (I4IVNO) (I4SEQN)) +
                        MAPFLD((CHAR6 I4DATE *CHAR 8))
/*9801B*/    OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 158) +
                          CPI(12) OVRFLW(64) SAVE(*YES) +
                          USRDTA('工程領代')
/*9801B*/    CALL       PGM(MIP011RB)
/*9801B*/    CLOF       OPNID(MTTRNS)

             DLTOVR     FILE(*ALL)

/*0112A*/    RTVDTAARA  DTAARA(*LDA (472 6)) RTNVAR(&IFRSYM)
/*0112A*/    IF         COND(&YM *LT &IFRSYM) THEN(DO)
             /*區間傳票代開同步(IFRS同步ROC) */
/*0011A*/    RTVDTAARA  DTAARA(*LDA (31 6)) RTNVAR(&P#VRNS)
/*0011A*/    RTVDTAARA  DTAARA(*LDA (41 6)) RTNVAR(&P#VRNE)
/*0011A*/    CALL       PGM(MTS888P) PARM(&AREA &YM &P#VRNS &P#VRNE)
/*0112A*/    ENDDO
/*0112A*/    ELSE       CMD(DO)
             /*區間傳票代開會計科目轉換(IFRS) */
/*0112A*/    RTVDTAARA  DTAARA(*LDA (31 6)) RTNVAR(&P#VRNS)
/*0112A*/    RTVDTAARA  DTAARA(*LDA (41 6)) RTNVAR(&P#VRNE)
/*0112A*/    CALL       PGM(MTS666P) PARM(&AREA &YM &P#VRNS &P#VRNE)
/*0112A*/    ENDDO

             RETURN     /* BATCH JOB */
             ENDPGM
