     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARE037RN
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02YSH
     H*            4.FUNCTION     新版營業繳款輸入程式（外銷專用）
     H*            5.DATE-WRITTEN  91/05/08
     H*            6.DATE-UPDATE   92/02/18 BY JONATHAN
     H*                            92/03/30 BY S02CSF
     H*                            94/06/23 BY S02CSF  新增會計課目7142
     H*                            98/04/16 2009AR294 S00WCJ (9804A)
     H*                            變更讀取匯率方式，原來是一張訂單只
     H*                            有一張繳款單，現變更為若一張訂單有
     H*                            二個以上匯率，則開兩張繳款單，匯率
     H*                            由原來讀取HSCINVR改為讀取USER
     H*                            螢幕上所輸入之報關匯率。
     H*                            98/10/29 2009AR370 S00WCJ (9810A)
     H*                            99/05/17 2010AR478 S00WCJ (9905A)
     H*                            99/06/22 2010AR493 S00WCJ (9906A)
     H*                            99/08/24 2010AR517 S00WCJ (9908A)
     H*                           101/06/20 2012AR652 S00WCJ (0106A)
     H*                            增加會計科目8149
     H*                           101/06/20 S00WCJ (0106A)
     H*                           101/09/19 2012AR682 S00WCJ (0109A)
     H*                           102/05/13 2013AR744 S00WCJ (0205A)
     H*                            會計科目110101庫存現金出現在C
     H*                            借方，須做為兌換收益(損失)的
     H*                            減項
     H*                           102/05/17 2013AR747 S00WCJ (0205B)
     H*                            會計科目110101庫存現金若為台幣
     H*                            則不列入兌換收益(損失)的計算
     H*                           103/05/20 2014AR829 S00WCJ (0305A)
     H*                            增加會計科目110102
     H*                           103/12/03 S00WCJ (0312A)
     H*                            繳款單H69613，由於前端資料錯誤
     H*                            造成廠區做調整後仍會出現客戶錯誤
     H*                            之訊息而無法繳款，讀取資料時，僅讀
     H*                            取客戶為EH150之資料
     H*                           104/08/03 S00WCJ (0408A)
     H*                            修改同一張磅單若未一次沖銷完畢，再
     H*                            做沖銷會有讀錯資料之BUG
     H*                           106/11/02 S00WCJ (0611A)
     H*                            由於
     H*                           107/04/02 S00WCJ (0704A)
     H*                            修正會計科目110102，為台幣繳款之
     H*                             BUG
     H*                           107/05/29 2018AR1169  S00WCJ (0705A)
     H*                            設定帳號B01LLF僅可使用查詢功能
     H*                           107/07/17 2018AR1179  S00WCJ (0707A)
     H*                           外銷BST由於課稅關係，繳款時需直接
     H*                           帶入磅單金額，不再重新計算，皆為一
     H*                           次沖銷，故數量直接帶入
     H*                           107/11/08 P1070117 S00WCJ (0711A)
     H*                            設定帳號D00LCC.H293.D00CHH僅可使
     H*                            用查詢功能
     H*                           110/09/02 S00WCJ (1009A)
     H*                            增加檢核會計科目211301、710109
     H*                            時需輸入訂單編號，並檢核訂單編號是
     H*                            否正確
     H*                           110/10/25 2021AR00044  S00WCJ (1010A)
     H*                            增加繳款單號查詢功能
     H*                           112/04/18 P1120056 S00WCJ (1204A)
     H*                            設定帳號H837僅可使用查詢功能
     H*
     H* 說明：本程式由外銷業務輸入繳款資料至SARCVF，經過修改、
     H*       沖銷等動作，在進行確認後，便可印出報表交由財會；而
     H*       財會人員在審核無誤後，便進行過入RCVDTL的動作。
     H*        2159 視同1114
     H*        7142 視同7119
     H*
     H*****************************************************************
     H        1   Y                                     1
9512 FARE037SNCF  E                    WORKSTN
     F                                        RRN1  KSFILE AR037F1
     F                                        RRN2  KSFILE AR037F2
     F                                        RRN3  KSFILE AR037F3
     F                                        RRN5  KSFILE AR037F5
     FCBCUST  IF  E           K        DISK
0611AFHSCINVL3IF  E           K        DISK
     FGENSEQ  UF  E           K        DISK
     FSARCVF  UF  E           K        DISK                      A
9810AF*SARCVFL4IF  E           K        DISK
9810AFSARCVFL5IF  E           K        DISK
     F            SRREC                             KRENAMESRRECL
     FSARVIN  UF  E           K        DISK                      A
     F*SARVINL1IF  E           K        DISK
     F*            SIREC                             KRENAMESIRECL
     FSARVOR  UF  E           K        DISK                      A
     FSARVORL1IF  E           K        DISK
     F            SOREC                             KRENAMESORECL1
     FSARVORL2IF  E           K        DISK
     F            SOREC                             KRENAMESORECL2
     FSARVORL3IF  E           K        DISK
     F            SOREC                             KRENAMESORECL3
     FTRNDTL  IF  E           K        DISK
     FTRNDTLL4IF  E           K        DISK
     F            TXREC                             KRENAMETXRECL4
     FLLCMSTL3IF  E           K        DISK
9804AFARCINVL1IF  E           K        DISK
0707AFTREXPTL1IF  E           K        DISK
0707AF            TXREC                             KRENAMETREXPL1
1009AFHSMAST  IF  E           K        DISK
     E*****************************************************************
0109AE                    ARY        20 30                檢核重複
1009AE                    ERR     1  25 70                錯誤訊息
     E*----------------------------------------------------------------
     ISORECL1
     I              SORVNO                          W1RVNO
     I              SOSTNO                          W1STNO
     I              SONOXA                          W1NOXA
     I              SOSTIT                          W1STIT
     I*
     ISORECL2
     I              SORVNO                          W2RVNO
     I              SOSTNO                          W2STNO
     I              SONOXA                          W2NOXA
     I              SOSTIT                          W2STIT
     I*
     ISORECL3
     I              SOCODE                          W3CODE
     I              SOQTY                           W3QTY
0707AITREXPL1
0707AI              TXFLAG                          EXFLAG
0707AI              TXNO                            EXNO
0707AI              TXITEM                          EXITEM
0707AI              TXDATE                          EXDATE
0707AI              TXACDT                          EXACDT
0707AI              TXCUNO                          EXCUNO
0707AI              TXCUNM                          EXCUNM
0707AI              TXORNO                          EXORNO
0707AI              TXPDNM                          EXPDNM
0707AI              TXQTY                           EXQTY
0707AI              TXUPRC                          EXUPRC
0707AI              TXAMT                           EXAMT
0707AI              TXTAX                           EXTAX
0707AI              TXTXAR                          EXTXAR
0707AI              TXRESV                          EXRESV
0707AI              TXORN5                          EXORN5
     I*
     I            DS
     I                                        1   6 SRRVNO
     I                                        1   1 D#RVNO
     I            DS
     I                                        1   6 D#RVNX
     I                                        1   1 D#RVN1
     I                                        2   6 D#RVN2
     I            DS
0109AI                                        1  30 D#ARY
     I                                        1   1 D#ACTP
0109AI                                        2   7 D#ACNO
0109AI                                        8  192D#RAMT
0109AI                                       20  274D#EXC2
0109AI                                       28  30 D#CURY
     I            DS
     I                                        1   9 TXORNO
     I                                        1   6 D#ORNO
     I                                        7   90D#OITM
     I            DS
     I                                        1  10 TXIVNO
     I                                        1   1 D#IVN1
     I            DS
     I                                        1   2 W#OEOF
     I                                        1   1 D#OE
     I                                        2   2 D#OF
     I            DS
9701 I                                        1   8 TXNO
     I                                        1   1 D#AREA
1009AI            DS
1009AI                                        1   6 W1ORNO
1009AI                                        1   1 S1OREA
1009AI                                        2   60S1ORNO
1009AI            DS
1009AI                                        1   6 D#CUNO
1009AI                                        1   1 S1KIND
1009AI                                        2   2 S1CUN1
1009AI                                        3   5 S1CUN2
1009AI                                        6   6 S1CUN3
     I           UDS
     I                                      300 300 D#INDI
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 U#USID
     I                                     10011003 U#USDP
     I                                     10011001 U#USTP
     I                                     10211021 U#AREA
0109AIIFRS       UDS                             50
0109AI                                        1   80D#IFRS
     C*****************************************************************
     C*        KEY   LIST
     C*****************************************************************
     C*FILE -> SARCVFL4
     C           K#SAR4    KLIST
     C                     KFLD           SRCUNO
     C                     KFLD           SRRVDT
     C                     KFLD           SRRVNO
     C                     KFLD           SRITEM
     C*FILE -> SARCVF
     C           K#SARC    KLIST
     C                     KFLD           SRRVNO
     C                     KFLD           SRITEM
     C*FILE -> SARVIN
     C           K#RVIN    KLIST
     C                     KFLD           SIRVNO
     C                     KFLD           SIITEM
     C*FILE -> GENSEQ
     C           K#NSEQ    KLIST
     C                     KFLD           GEKIND
     C                     KFLD           GEPRIN
     C*FILE -> TRNDTLL4
     C*          K#TRL4    KLIST
     C*                    KFLD           TXORN5
     C*FILE -> HSCINV
     C           K#CINV    KLIST
     C                     KFLD           C1ORNO
     C                     KFLD           C1OSEQ
     C                     KFLD           C1OITM
     C*FILE -> SARVOR
     C           K#RVOR    KLIST
     C                     KFLD           SORVNO           繳款編號
     C                     KFLD           SOITEM           項次
     C*FILE -> SARVORL2
     C           K#SOL2    KLIST
     C                     KFLD           K#STNO  8        磅單編號
     C                     KFLD           K#ORNO 10        ＣＩ訂單
     C*FILE -> SARVORL3
     C           K#SOL3    KLIST
     C                     KFLD           K3ORNO 10        ＣＩ訂單
     C                     KFLD           K3STNO  8        磅單編號
0408AC                     KFLD           K3STIT  40       磅單項次
     C*FILE -> TRNDTL
     C           K#TRND    KLIST
     C                     KFLD           K#CODE  4        單據代號
     C                     KFLD           K#STNO  8        磅單編號
     C*
1009AC           KEY01     KLIST
1009AC                     KFLD           S1OREA
1009AC                     KFLD           S1ORNO
     C*
     C*****************************************************************
     C*        MAIN  PROGRAM
     C*****************************************************************
0109AC           *DATE     SUB  19000000  U#SYSD  80
0109AC           *NAMVAR   DEFN ARIFRSCTL IFRS
0109AC           U#SYSD    IFLT D#IFRS
0109AC                     MOVEL*ON       *IN78
0109AC                     ELSE
0109AC                     MOVEL*OFF      *IN78
0109AC                     ENDIF
0109AC                     UNLCKIFRS
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C           W#PRID    CASEQ'02'      SR2000           畫面二
     C           W#PRID    CASEQ'03'      SR3000           READC
     C           W#PRID    CASEQ'04'      SR4000           畫面三
     C           W#PRID    CASEQ'05'      SR5000           畫面五
     C                     ENDCS
     C                     ENDDO
     C                     MOVE *ON       *INLR
     C*****************************************************************
     C           SR0000    BEGSR
     C*****************************************************************
     C                     MOVE '01'      W#PRID  2
     C                     MOVE *OFF      *IN99
     C                     MOVEL''      W#OEOF
1010AC                     MOVEL*ON       *IN85            客戶編號查
     C*部門限定
     C                     SELEC
     C           U#USTP    WHEQ 'S'                        系統工程部
     C                     MOVEL'*'       W#HEAD  1
     C*
     C           U#USTP    WHEQ 'B'                        營業部
     C           U#USTP    OREQ 'J'                        鄭梅蘭
0711AC           U#USTP    OREQ 'D'                        稽核室
0711AC           U#USID    OREQ 'H293'                     陳冠文
1204AC           U#USID    OREQ 'H837'                     張亦謙
     C                     SELEC
     C           U#USDP    WHEQ 'B01'
0705AC           U#USID    ANDNE'B01LLF'
     C           U#USDP    OREQ 'J00'
     C                     MOVEL'P'       W#HEAD           台北
     C           U#USDP    WHEQ 'B02'
     C                     MOVEL'T'       W#HEAD           桃園
     C           U#USDP    WHEQ 'B03'
     C                     MOVEL'K'       W#HEAD           高雄
     C           U#USDP    WHEQ 'B04'
     C                     MOVEL'M'       W#HEAD           台中
     C           U#USDP    WHEQ 'B05'
0705AC           U#USID    OREQ 'B01LLF'
0711AC           U#USID    OREQ 'D00LCC'
0711AC           U#USID    OREQ 'H293'
0711AC           U#USID    OREQ 'D00CHH'
1204AC           U#USID    OREQ 'H837'
     C                     MOVEL'H'       W#HEAD           苗栗
     C                     ENDSL
     C*
     C                     OTHER                           其他
     C*
     C           U#USDP    IFEQ 'TYB'                      桃園
     C                     MOVEL'T'       W#HEAD
     C                     ELSE                            結束
     C                     MOVEL'00'      W#PRID
     C                     ENDIF
     C*
     C                     ENDSL
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR1000    BEGSR
     C*****************************************************************
     C*W#PRID='01'--->ARE037SC-1畫面
     C           *IN99     IFEQ *OFF                        無錯誤
     C                     Z-ADD0         RRN1    40
     C                     Z-SUB12        S#NBR1            SFLRCDNBR
     C*
     C                     MOVE *ON       *IN73             SFLCLR
     C                     WRITEAR037F1C
     C                     MOVE *OFF      *IN73
     C*
1010AC           S#KEY1    IFNE *BLANKS
1010AC                     MOVEL*ON       *IN85
1010AC                     ENDIF
1010AC*
1010AC           S#KEY2    IFNE *BLANKS
1010AC                     MOVEL*OFF      *IN85            繳款單查
1010AC                     ENDIF
1010AC*
1010AC           S#KEY1    IFNE *BLANKS
1010AC           S#KEY2    ANDNE*BLANKS
1010AC                     MOVEL*ON       *IN85
1010AC                     MOVEL*BLANKS   S#KEY2
1010AC                     ENDIF
1010AC*
1010AC   85                MOVE *ON       *IN30             DSTATR(PC)
     C*
1010AC   85                MOVE S#KEY1    SRCUNO           客戶代號
1010AC   85                MOVE *HIVAL    SRRVDT           日期
1010AC   85                MOVE *HIVAL    SRRVNO           繳款單號
     C                     Z-ADD0         SRITEM           項次
1010AC   85      K#SAR4    SETLLSRRECL
1010AC  N85      S#KEY2    SETLLSRREC
     C                     EXSR SR1100                     讀入
     C                     ENDIF                            IN99
     C*迴圈開始
     C           W#PRID    DOWEQ'01'
     C*
     C           RRN1      IFEQ 0
     C                     MOVE *ON       *IN72             SFLDSP OFF
     C                     MOVELERR,1     S#ERR1
     C*
     C           S#KEY1    IFEQ *BLANK
     C                     MOVE *OFF      *IN32             DSPATR(RI)
     C                     ELSE
     C                     MOVE *ON       *IN32
     C                     ENDIF
     C*
     C                     ELSE
     C                     MOVE *OFF      *IN72
     C                     ENDIF
     C*
     C           W#CNT     IFGE 13
     C                     MOVE *ON       *IN74             SFLEND OFF
     C                     ELSE
     C                     MOVE *OFF      *IN74
     C                     ENDIF
     C*
     C                     WRITEAR037F1M
     C                     EXFMTAR037F1C
     C*
     C                     MOVE *BLANK    S#ERR1
     C*IN03 IN12
     C           *IN03     IFEQ *ON
     C           *IN12     OREQ *ON
     C                     MOVE '00'      W#PRID
     C                     ITER
     C                     ENDIF
     C*新增
     C           *IN06     IFEQ *ON
0705AC           U#USID    ANDNE'B01LLF'
0711AC           U#USID    ANDNE'D00LCC'
0711AC           U#USID    ANDNE'H293'
0711AC           U#USID    ANDNE'D00CHH'
1204AC           U#USID    ANDNE'H837'
     C                     Z-ADD1         W#OPT   10        OPT暫存值
     C                     MOVE '03'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*
     C           *IN91     IFEQ *ON
     C  N90                EXSR SR1100
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVE '02'      W#PRID
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR1100    BEGSR
     C*****************************************************************
     C*讀入AR037F1資料
     C                     Z-ADD0         W#CNT   20
     C           W#CNT     DOWLE12
1010AC   85                READ SRRECL                   90
1010AC  N85                READ SRREC                    90
     C   90                LEAVE
     C*
     C*
     C           W#HEAD    IFNE '*'                        依部門過濾
     C           W#HEAD    ANDNED#RVNO                     只給看自己
     C                     ITER
     C                     ENDIF
     C*
     C           SRITEM    IFNE 1                          首項即可
     C           SRMKTP    ORNE '1'                        外銷
     C                     ITER
     C                     ENDIF
     C*
     C                     CLEARAR037F1
     C*
     C                     Z-ADDSRRVDT    S#RVDT           繳款日期
     C                     MOVELSRRVNO    S#RVNO           繳款單號
     C                     MOVELSRCUNO    S#CUNO           客戶編號
     C                     Z-ADDSRNAMT    S#NAMT           繳款金額
     C                     Z-ADDSRXAMT    S#XAMT           沖銷金額
     C                     MOVELSRFL01    S#FL01           已確認碼
     C                     MOVELSRFL02    S#FL02           已過入碼
     C                     MOVELSRFL03    S#FL03           已列印碼
     C*
     C           S#CUNO    CHAINCBCUST               69    客戶名稱
     C           *IN69     IFEQ '1'
     C                     MOVEL*BLANK    S#CUNM
     C                     ELSE
     C                     MOVELCBCUNM    S#CUNM
     C                     ENDIF
     C*
     C                     ADD  1         W#CNT
     C                     ADD  1         RRN1
     C*
     C                     WRITEAR037F1
     C*
     C                     ENDDO
     C*
     C           W#CNT     IFGE 1
     C                     ADD  13        S#NBR1            SFLRCDNBR
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2000    BEGSR
     C*****************************************************************
     C*READ C AR037F1
     C                     MOVE *OFF      *IN99
     C*                    Z-ADD1         RRN1
     C           W#PRID    DOWEQ'02'
     C                     READCAR037F1                  90
     C   90                MOVE '01'      W#PRID
     C   90                LEAVE
     C*
     C           S#OPT1    IFEQ 0
     C                     ITER
     C                     ENDIF
     C*
     C           S#OPT1    IFNE 5                          查詢
     C                     EXSR SR2100                     檢核
     C                     ENDIF
     C*有錯
     C           *IN99     IFEQ *ON
     C                     MOVE *ON       *IN31             DSPATR(RI)
     C                     UPDATAR037F1
     C                     MOVE '01'      W#PRID
     C                     LEAVE
     C                     ELSE
     C*沒錯
1010AC   85                MOVE S#CUNO    S#KEY1
1010AC  N85                MOVELS#RVNO    S#KEY2
     C                     Z-ADDS#OPT1    W#OPT
     C                     Z-ADD0         S#OPT1
     C                     UPDATAR037F1
     C                     ENDIF
     C*
     C                     SELEC
     C           W#OPT     WHEQ 3                          試算
     C                     EXSR SR2400
     C                     MOVELERR,16    S#ERR1
     C                     MOVE '01'      W#PRID
     C           W#OPT     WHEQ 4                          刪除
     C                     EXSR SR2200
     C           W#OPT     WHEQ 6                          沖銷
     C                     MOVE '04'      W#PRID
     C           W#OPT     WHEQ 8                          確認
     C           W#OPT     OREQ 9                          確認還原
     C                     EXSR SR2300
     C                     OTHER
     C                     MOVE '03'      W#PRID
     C                     ENDSL
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2100    BEGSR
     C*****************************************************************
     C*
0705AC           U#USID    IFEQ 'B01LLF'
0705AC           S#OPT1    ANDNE5
0711AC           U#USID    OREQ 'D00LCC'
0711AC           S#OPT1    ANDNE5
0711AC           U#USID    OREQ 'H293'
0711AC           S#OPT1    ANDNE5
0711AC           U#USID    OREQ 'D00CHH'
0711AC           S#OPT1    ANDNE5
1204AC           U#USID    OREQ 'H837'
1204AC           S#OPT1    ANDNE5
0705AC                     MOVELERR,18    S#ERR1
0705AC                     MOVEL*ON       *IN99
0705AC   99                Z-ADDRRN1      S#NBR1
0705AC                     GOTO END210
0705AC                     ENDIF
0705AC*
     C           S#FL02    IFNE *BLANK
     C                     MOVELERR,2     S#ERR1
     C                     MOVE *ON       *IN99
     C                     ENDIF
     C*
     C  N99      S#OPT1    IFNE 9                          確認還原
     C           S#FL01    ANDNE*BLANK                     已確認
     C                     MOVELERR,3     S#ERR1
     C                     MOVE *ON       *IN99
     C                     ENDIF
     C*
     C  N99      S#OPT1    IFEQ 9                          確認還原
     C           S#FL01    ANDEQ*BLANK                     已確認
     C                     MOVELERR,4     S#ERR1
     C                     MOVE *ON       *IN99
     C                     ENDIF
     C*
     C  N99      S#OPT1    IFEQ 8                          確認
     C           S#NAMT    IFNE S#XAMT
     C                     MOVELERR,13    S#ERR1
     C                     MOVE *ON       *IN99
     C                     ENDIF
     C                     ENDIF
     C*
     C   99                Z-ADDRRN1      S#NBR1
     C*
     C           END210    ENDSR
     C*****************************************************************
     C           SR2200    BEGSR
     C*****************************************************************
     C*刪除主檔
     C                     MOVE S#RVNO    SRRVNO
     C                     Z-ADD0         SRITEM
     C           K#SARC    SETLLSRREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SRREC                    90
     C   90                LEAVE
     C           SRRVNO    IFNE S#RVNO                     繳款單號
     C                     LEAVE
     C                     ENDIF
     C                     DELETSRREC
     C                     ENDDO
     C*刪除明細
     C                     MOVE S#RVNO    SIRVNO
     C                     Z-ADD0         SIITEM
     C           K#RVIN    SETLLSIREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SIREC                    90
     C   90                LEAVE
     C           SIRVNO    IFNE S#RVNO                     繳款單號
     C                     LEAVE
     C                     ENDIF
     C                     DELETSIREC
     C                     ENDDO
     C*刪除磅單明細
     C                     MOVE S#RVNO    SORVNO
     C                     Z-ADD0         SOITEM
     C           K#RVOR    SETLLSOREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SOREC                    90
     C   90                LEAVE
     C           SORVNO    IFNE S#RVNO                     繳款單號
     C                     LEAVE
     C                     ENDIF
     C                     DELETSOREC
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2300    BEGSR
     C*****************************************************************
     C*確認或還原
     C                     MOVE S#RVNO    SRRVNO
     C                     Z-ADD0         SRITEM
     C           K#SARC    SETLLSRREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SRREC                    90
     C   90                LEAVE
     C           SRRVNO    IFNE S#RVNO                     繳款單號
     C                     LEAVE
     C                     ENDIF
     C*
     C           W#OPT     IFEQ 8                          確認
     C                     MOVE 'Y'       SRFL01
     C                     ELSE                            還原
     C                     MOVE *BLANK    SRFL01
     C                     MOVE *BLANK    SRFL03           列印碼
     C                     ENDIF
     C*
9908AC*                    MOVE UDATE     SRCFDT           確認日期
9908AC           *DATE     SUB  19000000  SRCFDT           確認日期
     C                     MOVE U#USID    SRCFUS           確認人員
     C*
     C                     UPDATSRREC
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2400    BEGSR
     C*****************************************************************
     C*確認時先進行兌換損失與奇零尾數的調整
     C*再進行發票金額的轉換
     C*然後計算繳款金額與沖銷金額的關係
     C*此動作只進行一次,如果計算錯誤則以手動修改之
     C                     MOVE *OFF      *IN97            計算燈號
     C                     MOVE *ALL'9'   ARY              初始陣列
     C*先將主檔內容紀錄於ARY中
     C                     EXSR SR2410
     C*由ARY將資訊讀出並做整理
     C  N97                EXSR SR2420
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2410    BEGSR
     C*****************************************************************
     C                     MOVE S#RVNO    SRRVNO
     C                     Z-ADD0         SRITEM
     C           K#SARC    SETLLSRREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SRREC                    90
     C*
     C           *IN90     IFEQ *ON
     C           SRRVNO    ORNE S#RVNO                     繳款單號
     C                     LEAVE
     C                     ENDIF
     C*
     C                     Z-ADDSRITEM    W#ITEM  20       最後一項
     C*先將主檔內容紀錄於ARY中
     C                     MOVE SRACTP    D#ACTP           借貸別
0109AC                     MOVELSRACNO    D#ACNO           會計科目
     C                     Z-ADDSRRAMT    D#RAMT           收款金額
     C                     Z-ADDSREXC2    D#EXC2           銀行匯率
     C                     MOVE SRCURY    D#CURY           幣別
     C                     Z-ADDSRITEM    I       20        INDEX
0109AC                     MOVELD#ARY     ARY,I
     C*紀錄SR2420所需資訊
     C                     Z-ADDSREXC1    W#EXC1  84       財會匯率
     C                     Z-ADDSREXC3    W#EXC3  84       報關匯率
     C*有以下三種科目則代表已經試算,不做第二次試算
     C           SRACNO    IFEQ '8212'                     兌換損失
     C           SRACNO    OREQ '8112'                     兌換收益
     C           SRACNO    OREQ '8246'                     奇零尾差
0109AC           SRACNO    OREQ '820201'
0109AC           SRACNO    OREQ '810201'
0109AC           SRACNO    OREQ '821204'
     C                     MOVE *ON       *IN97
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2420    BEGSR
     C*****************************************************************
     C*初始各計算值
9202 C                     Z-ADD0         W#2159 120       暫收其他
9202 C                     Z-ADD0         W#1138 120       暫收其他
     C                     Z-ADD0         W#1114 120       外幣存款
9203 C                     Z-ADD0         W#1111 120       庫存現金
     C                     Z-ADD0         W#7119 120       出口費用
9406 C                     Z-ADD0         W#7142 120       郵電費用
     C                     Z-ADD0         W#8211 120       出押息
     C                     Z-ADD0         W#AMTX 122       美金總額
0106AC                     Z-ADD0         W#8149 120       其他收益
0305AC                     Z-ADD0         W#1112 120       銀行存款
     C*由ARY將資訊讀出並做整理
     C                     SORTAARY
     C           1         DO   20        I
     C                     MOVE ARY,I     D#ARY
     C*
     C           D#ACTP    IFEQ '9'                        初始項次
     C                     LEAVE
     C                     ENDIF
     C*
     C                     SELEC
9203 C           D#ACNO    WHEQ '1111'                     庫存現金
0109AC           D#ACNO    OREQ '110101'
9203 C           D#CURY    IFEQ 'USD'
9203 C           D#RAMT    MULT W#EXC1    W#AMT  120H      台幣
9203 C                     ADD  W#AMT     W#1111
9203 C                     ADD  D#RAMT    W#AMTX
9203 C                     ELSE
9203 C                     ADD  D#RAMT    W#AMTX
9203 C                     ENDIF
0205AC           D#ACTP    IFEQ 'C'
0205AC           D#CURY    ANDEQ'USD'
0205AC           D#RAMT    MULT -1        W#AMTX
0205AC           W#AMT     MULT -1        W#1111
0205AC                     ENDIF
0205AC*
0205BC           D#ACTP    IFEQ 'C'
0205BC           D#CURY    ANDEQ'NTD'
0205BC                     Z-ADD0         W#AMTX
0205BC           D#RAMT    MULT -1        W#1111
0205BC                     ENDIF
     C*
     C           D#ACNO    WHEQ '1114'                     外幣存款
0109AC           D#ACNO    OREQ '110104'
     C           D#RAMT    MULT W#EXC1    W#AMT  120H      台幣
     C                     ADD  W#AMT     W#1114
     C                     ADD  D#RAMT    W#AMTX
9202 C           D#ACNO    WHEQ '2159'                     外幣存款
0109AC           D#ACNO    OREQ '211301'
9202 C           D#RAMT    MULT W#EXC1    W#AMT  120H      台幣
9406 C           D#ACTP    IFEQ 'C'
9406 C           W#AMT     MULT -1        W#AMT
9406 C           D#RAMT    MULT -1        D#RAMT
9406 C                     ENDIF
9202 C                     ADD  W#AMT     W#2159
9202 C                     ADD  D#RAMT    W#AMTX
9202 C           D#ACNO    WHEQ '1138'                     外幣存款
0109AC           D#ACNO    OREQ '111202'
9202 C           D#RAMT    MULT W#EXC1    W#AMT  120H      台幣
9202 C                     ADD  W#AMT     W#1138
9202 C                     ADD  D#RAMT    W#AMTX
0106AC           D#ACNO    WHEQ '8149'                     其他收益
0109AC           D#ACNO    OREQ '811210'
0106AC           D#RAMT    MULT W#EXC1    W#AMT            台幣
0106AC           D#ACTP    IFEQ 'C'
0106AC           W#AMT     MULT -1        W#AMT
0106AC           D#RAMT    MULT -1        D#RAMT
0106AC                     ENDIF
0106AC                     ADD  W#AMT     W#8149
0106AC                     ADD  D#RAMT    W#AMTX
0305AC           D#ACNO    WHEQ '110102'                   銀行存款
0305AC           D#CURY    IFEQ 'USD'
0305AC           D#RAMT    MULT W#EXC1    W#AMT  120H      台幣
0305AC                     ADD  W#AMT     W#1112
0305AC                     ADD  D#RAMT    W#AMTX
0305AC                     ELSE
0704AC                     ADD  D#RAMT    W#1112
0305AC*0704A               ADD  D#RAMT    W#AMTX
0305AC                     ENDIF
0305AC           D#ACTP    IFEQ 'C'
0305AC           D#CURY    ANDEQ'USD'
0305AC           D#RAMT    MULT -1        W#AMTX
0305AC           W#AMT     MULT -1        W#1112
0305AC                     ENDIF
0305AC           D#ACTP    IFEQ 'C'
0305AC           D#CURY    ANDEQ'NTD'
0305AC*0704A               Z-ADD0         W#AMTX
0305AC           D#RAMT    MULT -1        W#1112
0305AC                     ENDIF
     C*
     C*出口費用如為台幣,須先依銀行匯率轉成美金,再依財會匯率轉成台
     C*幣;如為美金則直接以財會匯率轉換
     C           D#ACNO    WHEQ '7119'                     出口費用
0109AC           D#ACNO    OREQ '710109'
     C           D#CURY    IFEQ 'NTD'                      台幣
     C           D#CURY    OREQ *BLANK                     台幣
     C           D#RAMT    DIV  D#EXC2    W#AMT1 133H
     C                     ADD  W#AMT1    W#AMTX
     C           W#AMT1    MULT W#EXC1    W#AMT     H
     C                     ADD  W#AMT     W#7119
     C                     ELSE
     C           D#RAMT    MULT W#EXC1    W#AMT     H
     C                     ADD  W#AMT     W#7119
     C                     ADD  D#RAMT    W#AMTX
     C                     ENDIF
     C*
     C*郵電費用如為台幣,須先依銀行匯率轉成美金,再依財會匯率轉成台
     C*幣;如為美金則直接以財會匯率轉換
     C           D#ACNO    WHEQ '7142'                     郵電費用
0109AC           D#ACNO    OREQ '710402'
     C           D#CURY    IFEQ 'NTD'                      台幣
     C           D#CURY    OREQ *BLANK                     台幣
     C           D#RAMT    DIV  D#EXC2    W#AMT1 133H
     C                     ADD  W#AMT1    W#AMTX
     C           W#AMT1    MULT W#EXC1    W#AMT     H
     C                     ADD  W#AMT     W#7142
     C                     ELSE
     C           D#RAMT    MULT W#EXC1    W#AMT     H
     C                     ADD  W#AMT     W#7142
     C                     ADD  D#RAMT    W#AMTX
     C                     ENDIF
     C*計算方式同上
     C           D#ACNO    WHEQ '8211'                     出押息
0109AC           D#ACNO    OREQ '820101'
     C           D#CURY    IFEQ 'NTD'                      台幣
     C           D#CURY    OREQ *BLANK                     台幣
     C           D#RAMT    DIV  D#EXC2    W#AMT1    H
     C                     ADD  W#AMT1    W#AMTX
     C           W#AMT1    MULT W#EXC1    W#AMT     H
     C                     ADD  W#AMT     W#8211
     C                     ELSE
     C           D#RAMT    MULT W#EXC1    W#AMT     H
     C                     ADD  W#AMT     W#8211
     C                     ADD  D#RAMT    W#AMTX
     C                     ENDIF
     C*
     C                     ENDSL
     C*
     C                     ENDDO
     C*寫入8212(兌換損失)或8112(兌換收益),與8246(奇零尾差)
     C                     EXSR SR2421
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2421    BEGSR
     C*****************************************************************
     C*初始各項值
     C                     Z-ADD0         W#8212 120       損失暫存
     C                     Z-ADD0         W#8112 120       收益暫存
     C                     Z-ADD0         W#8246 120       尾數暫存
     C                     Z-ADD0         W#CONT  30       須寫入筆數
     C                     Z-ADD0         W#CONX  30       已寫入筆數
     C*若財會匯率大於報關匯率則為收益,反之則為損失
     C           W#EXC1    IFGE W#EXC3
     C           W#EXC1    SUB  W#EXC3    W#EXCG  84
     C           W#AMTX    MULT W#EXCG    W#8112    H
     C                     ELSE
     C           W#EXC3    SUB  W#EXC1    W#EXCG
     C           W#AMTX    MULT W#EXCG    W#8212    H
     C                     ENDIF
     C*計算奇零尾差,以台幣表達(公式如下)
     C*沖銷總額=繳款總額
9202 C*繳款總額=1114+7119+8211+8212-8112+8246+2159+1138+7142+8149+
0305AC*           110102
     C* => 8246=沖銷總額-(1114+7119+8211+8212-8112+2159+1138+7142+
     C*                     8149+110102)
     C           S#XAMT    SUB  W#1114    W#8246
     C                     SUB  W#1111    W#8246
     C                     SUB  W#7119    W#8246
     C                     SUB  W#8211    W#8246
     C                     SUB  W#8212    W#8246
     C                     ADD  W#8112    W#8246
9202 C                     SUB  W#2159    W#8246
9202 C                     SUB  W#1138    W#8246
9406 C                     SUB  W#7142    W#8246
0106AC                     SUB  W#8149    W#8246
0305AC                     SUB  W#1112    W#8246
     C*計算須寫入筆數
     C           W#8112    IFNE 0
     C                     ADD  1         W#CONT
     C                     ENDIF
     C           W#8212    IFNE 0
     C                     ADD  1         W#CONT
     C                     ENDIF
     C           W#8246    IFNE 0
     C                     ADD  1         W#CONT
     C                     ENDIF
     C*先更新此繳款書的繳款金額然後再寫入
     C                     MOVE S#RVNO    SRRVNO
     C                     Z-ADD0         SRITEM
     C           K#SARC    SETLLSRREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SRREC                    90
     C*
     C           *IN90     IFEQ *ON
     C           SRRVNO    ORNE S#RVNO                     繳款單號
     C                     LEAVE
     C                     ENDIF
     C                     Z-ADDSRXAMT    SRNAMT
     C                     UPDATSRREC
     C*寫入新項次
     C           SRITEM    IFEQ W#ITEM                     已到最末筆
     C                     MOVE *BLANK    SRUSTP           款項別
     C                     MOVE *BLANK    SRNTTP           票據別
     C                     MOVE *BLANK    SRPBID           付款銀行
     C                     MOVE *BLANK    SRPANO           付款帳號
     C                     MOVE *BLANK    SRPLAC           付款地
     C                     MOVE *BLANK    SRNTNO           票據號碼
     C                     MOVE *BLANK    SRSBID           存入銀行
     C                     MOVE *BLANK    SRSANO           存入帳號
     C                     Z-ADD0         SRDUDT           到期日
     C                     MOVE *BLANK    SRRLNO           相關號碼
     C                     MOVEL*BLANK    SRRESV           保留碼
     C                     MOVE '1'       SRMKTP           外銷
     C                     MOVEL'NTD'     SRCURY           幣別
     C                     Z-ADDS#EXC1    SREXC1           財會匯率
     C                     Z-ADD0         SREXC2           銀行匯率
     C                     Z-ADDS#EXC3    SREXC3           報關匯率
9905AC                     MOVEL*BLANKS   SRPANO           付款帳號
9905AC                     MOVEL*BLANKS   SRPLAC           付款地
     C*
     C           W#8112    IFNE 0                          兌換收益
     C                     ADD  1         W#ITEM
     C                     Z-ADDW#ITEM    SRITEM           項次
     C                     MOVEL'C'       SRACTP           借貸別
0109AC   78                MOVEL'8112'    SRACNO           會計科目
0109AC  N78                MOVEL'810201'  SRACNO
     C                     Z-ADDW#8112    SRRAMT           繳款金額
     C                     WRITESRREC
     C                     ADD  1         W#CONX           已寫入筆數
     C                     ENDIF
     C*
     C           W#8212    IFNE 0                          兌換損失
     C                     ADD  1         W#ITEM
     C                     Z-ADDW#ITEM    SRITEM           項次
     C                     MOVEL'D'       SRACTP           借貸別
0109AC   78                MOVEL'8212'    SRACNO           會計科目
0109AC  N78                MOVEL'820201'  SRACNO
     C                     Z-ADDW#8212    SRRAMT           繳款金額
     C                     WRITESRREC
     C                     ADD  1         W#CONX           已寫入筆數
     C                     ENDIF
     C*
     C           W#8246    IFNE 0                          奇零尾數
     C                     ADD  1         W#ITEM
     C                     Z-ADDW#ITEM    SRITEM           項次
     C           W#8246    IFGT 0
     C                     MOVEL'D'       SRACTP           借貸別
0109AC   78                MOVEL'8246'    SRACNO           會計科目
0109AC  N78                MOVEL'821204'  SRACNO
     C                     Z-ADDW#8246    SRRAMT           繳款金額
     C                     ELSE
     C                     MOVEL'C'       SRACTP           借貸別
0109AC   78                MOVEL'8246'    SRACNO           會計科目
0109AC  N78                MOVEL'821204'  SRACNO
     C                     Z-SUBW#8246    SRRAMT           繳款金額
     C                     ENDIF
     C                     WRITESRREC
     C                     ADD  1         W#CONX           已寫入筆數
     C                     ENDIF
     C*
     C                     ENDIF                            W#ITEM
     C*
     C           W#CONT    IFEQ W#CONX
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ENDDO
     C                     ENDSR
     C*****************************************************************
     C           SR3000    BEGSR
     C*****************************************************************
     C*W#PRID='01'--->ARE037SC-2畫面
     C           W#FLAG    IFEQ ' '
     C                     Z-ADD0         RRN2    40
     C*
     C                     MOVE *ON       *IN73             SFLCLR
     C                     WRITEAR037F2C
     C                     MOVE *OFF      *IN73
     C*
     C           W#OPT     IFEQ 5                          查詢
     C                     MOVE *ON       *IN33
     C                     ELSE
     C                     MOVE *OFF      *IN33
     C                     ENDIF
     C*
     C                     MOVE *OFF      *IN61             PROTECT
     C*
     C                     SELEC
     C           W#OPT     WHEQ 1
     C                     MOVE '新增'  S#SF2T
     C           W#OPT     WHEQ 2
     C                     MOVE '修改'  S#SF2T
     C           W#OPT     WHEQ 5
     C                     MOVE '查詢'  S#SF2T
     C                     ENDSL
     C*
     C           W#OPT     IFNE 1                          新增
     C                     EXSR SR3100                     讀入檔頭
     C                     ELSE
     C                     MOVE *BLANK    S#RVNO           繳款單號
     C                     MOVE *BLANK    S#DPNO           部門別
     C                     MOVE *BLANK    S#RVID           收款業務
     C                     MOVE *BLANK    S#CUNO           客戶編號
1009AC                     MOVEL*BLANKS   S#CUNM
     C                     Z-ADD0         S#RVDT           繳款日期
     C                     Z-ADD0         S#NAMT           繳款總額
     C                     Z-ADD0         S#XAMT           沖銷總額
     C                     Z-ADD0         S#EXC1           財會匯率
     C                     Z-ADD0         S#EXC3           報關匯率
     C                     ENDIF
     C*
     C                     Z-ADD1         S#NBR2            SFLRCDNBR
     C                     EXSR SR3200                     初始SFL
     C                     ENDIF
     C*迴圈開始
     C  N33                MOVE *ON       *IN34            DSPATR(PC)
     C           W#PRID    DOWEQ'03'
     C*
     C           RRN2      IFEQ 0
     C                     MOVE *ON       *IN72             SFLDSP OFF
     C                     MOVELERR,1     S#ERR1
     C                     ELSE
     C                     MOVE *OFF      *IN72
     C                     ENDIF
     C*
     C                     MOVE *OFF      *IN74
     C                     WRITEAR037F2M
     C                     EXFMTAR037F2C
     C*
     C                     SETOF                     3499
     C                     MOVEA'00000'   *IN,53           DSPATR(PC)
     C                     MOVE *OFF      *IN62
     C                     MOVE *BLANK    S#ERR2
     C                     MOVE *BLANK    W#FLAG
     C*
     C           S#CRN2    IFNE 0
     C                     Z-ADDS#CRN2    S#NBR2           記錄位置
     C                     ENDIF
     C*IN03
     C           *IN03     IFEQ *ON
     C                     MOVE '00'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*IN12
     C           *IN12     IFEQ *ON
     C           RRN1      IFNE 0
     C                     MOVE '02'      W#PRID
     C                     ELSE
     C                     MOVE '01'      W#PRID
     C                     MOVE *OFF      *IN99            畫面一INI
     C                     ENDIF
     C                     LEAVE
     C                     ENDIF
     C*說明
     C           *IN01     IFEQ *ON
     C           S#CRN2    IFNE 0
     C                     Z-ADDS#CRN2    RRN2
     C           RRN2      CHAINAR037F2              38
     C           S#NTTP    IFEQ 'J'
     C           W#OPT     ANDNE5
     C                     EXSR SR5100
     C                     MOVEL'05'      W#PRID
     C                     GOTO EN3000
     C                     ENDIF
0109AC   78                EXFMTAR037F2W
0109AC  N78                EXFMTAR037F4W
     C                     ITER
     C                     ENDIF
     C                     ENDIF
     C*檢核
     C  N33                EXSR SR3300
     C*存檔
     C  N33N99   *IN10     IFEQ *ON
     C                     EXSR SR3400
     C                     MOVE '04'      W#PRID           沖銷畫面
     C                     LEAVE
     C                     ENDIF
     C*
     C   33                MOVE '04'      W#PRID
     C                     ENDDO
     C*
     C           EN3000    ENDSR
     C*****************************************************************
     C           SR3100    BEGSR
     C*****************************************************************
     C*讀入檔頭
     C                     MOVE S#RVNO    SRRVNO
     C                     Z-ADD1         SRITEM
     C           K#SARC    CHAINSRREC                90
     C                     MOVELSRRVNO    S#RVNO           繳款單號
     C                     MOVELSRDPNO    S#DPNO           部門別
     C                     MOVELSRRVID    S#RVID           收款業務
     C                     MOVELSRCUNO    S#CUNO           客戶編號
     C                     Z-ADDSRRVDT    S#RVDT           繳款日期
     C                     Z-ADDSRNAMT    S#NAMT           繳款總額
     C                     Z-ADDSRXAMT    S#XAMT           沖銷總額
     C                     Z-ADDSREXC1    S#EXC1           財會匯率
     C                     Z-ADDSREXC3    S#EXC3           報關匯率
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR3200    BEGSR
     C*****************************************************************
     C*讀入明細
     C           RRN2      DOWLE19
     C                     ADD  1         RRN2
     C                     MOVE S#RVNO    SRRVNO
     C                     Z-ADDRRN2      SRITEM
     C           K#SARC    CHAINSRREC                90
     C                     Z-ADDRRN2      S#SFI2
     C           *IN90     IFEQ *OFF                       有資料
     C                     MOVELSRACTP    S#ACTP           借貸別
     C                     MOVELSRUSTP    S#USTP           款項別
     C                     MOVELSRNTTP    S#NTTP           票據別
     C                     MOVELSRACNO    S#ACNO           會計科目
     C                     MOVELSRPBID    S#PBID           付款銀行
     C                     MOVELSRNTNO    S#NTNO           票據號碼
     C                     Z-ADDSRRAMT    S#RAMT           收款金額
     C                     MOVELSRRLNO    S#RLNO           相關號碼
     C                     MOVELSRSANO    S#SANO           存入帳號
     C                     Z-ADDSRDUDT    S#DUDT           到期日
9905AC                     MOVELSRPANO    S#PANO           付款帳號
9905AC                     MOVELSRPLAC    S#PLAC           付款地
     C           SRCURY    IFEQ *BLANK
     C                     MOVE 'NTD'     S#CURY
     C                     ELSE
     C                     MOVELSRCURY    S#CURY           幣別
     C                     ENDIF
     C                     Z-ADDSREXC2    S#EXC2           銀行匯率
     C                     ELSE
     C                     MOVE *BLANK    S#ACTP           借貸別
     C                     MOVE *BLANK    S#USTP           款項別
     C                     MOVE *BLANK    S#NTTP           票據別
     C                     MOVE *BLANK    S#ACNO           會計科目
     C                     MOVE *BLANK    S#PBID           付款銀行
     C                     MOVE *BLANK    S#NTNO           票據號碼
     C                     Z-ADD0         S#RAMT           收款金額
     C                     MOVE *BLANK    S#RLNO           相關號碼
     C                     MOVE *BLANK    S#SANO           存入帳號
     C                     Z-ADD0         S#DUDT           到期日
     C                     MOVE *BLANK    S#CURY           幣別
     C                     Z-ADD0         S#EXC2           銀行匯率
9905AC                     MOVEL*BLANKS   S#PANO           付款帳號
9905AC                     MOVEL*BLANKS   S#PLAC           付款地
     C                     ENDIF
     C  N33
     CORN90                WRITEAR037F2
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR3300    BEGSR
     C*****************************************************************
     C*清空繳款金額,於SR3320重新合計
     C                     Z-ADD0         S#NAMT           繳款金額
     C*檢核檔頭
     C           S#CUNO    IFEQ *BLANK                     客戶編號
     C                     SETON                     345399
     C                     MOVELERR,6     S#ERR2
     C                     ENDIF
     C*
     C           S#CUNO    CHAINCBREC               N90
     C  N99 90             SETON                     345399
     C  N99 90             MOVELERR,6     S#ERR2
     C  N99N90             MOVELCBCUNM    S#CUNM
     C*
     C                     MOVE S#RVDT    P#DATE           繳款日期
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           P#ERR   1
     C  N99      P#ERR     IFNE '0'
     C                     SETON                     5499
     C                     MOVELERR,6     S#ERR2
     C                     ENDIF
     C*
     C  N99      S#EXC3    IFEQ 0                          報關匯率
     C                     SETON                     6299
     C                     MOVELERR,6     S#ERR2
     C                     ENDIF
     C*
     C  N99      S#DPNO    IFEQ *BLANK                     部門別
     C                     SETON                     5599
     C                     MOVELERR,6     S#ERR2
     C                     ENDIF
     C*
     C  N99      S#RVID    IFEQ *BLANK                     業務員
     C                     SETON                     5699
     C                     MOVELERR,6     S#ERR2
     C                     ENDIF
     C*
     C  N99      S#EXC1    IFEQ 0                          財會匯率
     C                     SETON                     5799
     C                     MOVELERR,6     S#ERR2
     C                     ENDIF
     C*檢核SFL
     C                     Z-ADD0         W#CONT
     C                     MOVE *OFF      *IN98            錯誤判斷
1009AC                     MOVEA'00000000'*IN,41
1009AC                     MOVEA'0000'    *IN,49
     C  N99      W#CONT    DOWLERRN2
     C                     ADD  1         W#CONT
     C           W#CONT    CHAINAR037F2              90
     C   90                LEAVE
     C*當借貸別為空白時,自動清空所有欄位
     C*而不為空白時才進行檢核
     C           S#ACTP    IFEQ *BLANK                     借貸別
     C                     MOVE *BLANK    S#ACTP           借貸別
     C                     MOVE *BLANK    S#USTP           款項別
     C                     MOVE *BLANK    S#NTTP           票據別
     C                     MOVE *BLANK    S#ACNO           會計科目
     C                     MOVE *BLANK    S#PBID           付款銀行
     C                     MOVE *BLANK    S#NTNO           票據號碼
     C                     Z-ADD0         S#RAMT           收款金額
     C                     MOVE *BLANK    S#RLNO           相關號碼
     C                     MOVE *BLANK    S#SANO           存入帳號
     C                     Z-ADD0         S#DUDT           到期日
     C                     MOVE *BLANK    S#CURY           幣別
     C                     Z-ADD0         S#EXC2           銀行匯率
9905AC                     MOVEL*BLANKS   S#PANO           付款帳號
9905AC                     MOVEL*BLANKS   S#PLAC           付款地
     C                     MOVEA'00000000'*IN,41
     C                     MOVEA'0000'    *IN,49
9905AC                     MOVEL*OFF      *IN55
     C                     ELSE
     C                     EXSR SR3310                     明細檢核
     C                     EXSR SR3320                     計算繳款額
     C                     ENDIF
     C*
     C                     UPDATAR037F2
     C                     ENDDO
     C*
     C  N99 98             MOVE ERR,6     S#ERR2
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR3310    BEGSR
     C*****************************************************************
     C*檢核SFL
     C           S#USTP    IFNE 'A1'                       款項別
     C           S#USTP    ANDNE'A2'
     C           S#USTP    ANDNE'A3'
     C           S#USTP    ANDNE'A4'
     C           S#USTP    ANDNE'A5'
     C           S#USTP    ANDNE'Z5'
     C           S#USTP    ANDNE*BLANK
     C                     SETON                     4298
     C                     ENDIF
     C*
     C           S#NTTP    IFNE 'A'                        票款別
     C           S#NTTP    ANDNE'B'
     C           S#NTTP    ANDNE'C'
     C           S#NTTP    ANDNE'D'
     C           S#NTTP    ANDNE'E'
     C           S#NTTP    ANDNE'F'
     C           S#NTTP    ANDNE'G'
     C           S#NTTP    ANDNE'H'
     C           S#NTTP    ANDNE'I'
     C           S#NTTP    ANDNE'J'
     C           S#NTTP    ANDNE*BLANK
     C                     SETON                     4398
     C                     ENDIF
     C*
1009AC*檢核相關號碼
1009AC           S#RLNO    IFEQ *BLANKS
1009AC           S#ACNO    IFEQ '211301'
1009AC           S#ACNO    OREQ '710109'
1009AC                     MOVELERR,19    S#ERR2
1009AC                     SETON                     4999
1009AC                     GOTO END331
1009AC                     ENDIF
1009AC                     ENDIF
1009AC*
1009AC           S#RLNO    IFNE *BLANKS
1009AC                     MOVELS#RLNO    W1ORNO
1009AC*          ' ':'0'   XLATEW1ORNO:2  W1ORNO
1009AC*          S1OREA    IFNE U#AREA
1009AC*                    Z-ADD0         S1ORNO
1009AC*                    ENDIF
1009AC           KEY01     CHAINHSMAST              N69
1009AC           *IN69     IFEQ *OFF
1009AC           D#CUNO    ANDNES#CUNO
1009AC                     MOVELERR,20    S#ERR2
1009AC                     SETON                     4999
1009AC                     GOTO END331
1009AC                     ENDIF
1009AC           *IN69     IFEQ *ON
1009AC                     MOVELERR,7     S#ERR2
1009AC                     SETON                     4999
1009AC                     GOTO END331
1009AC                     ENDIF
1009AC*
1009AC                     ENDIF
     C*
 9203C           S#ACNO    IFNE '1111'                     會計科目
     C           S#ACNO    ANDNE'1114'
     C           S#ACNO    ANDNE'7119'
     C           S#ACNO    ANDNE'8212'
     C           S#ACNO    ANDNE'8211'
     C           S#ACNO    ANDNE'8112'
     C           S#ACNO    ANDNE'8246'
 9203C           S#ACNO    ANDNE'2159'
 9204C           S#ACNO    ANDNE'1138'
 9406C           S#ACNO    ANDNE'7142'
9905AC           S#ACNO    ANDNE'1131'
0106AC           S#ACNO    ANDNE'8149'
0109AC           S#ACNO    ANDNE'110101'                    1111
0109AC           S#ACNO    ANDNE'110104'                    1114
0109AC           S#ACNO    ANDNE'710109'                    7119
0109AC           S#ACNO    ANDNE'820201'                    8212
0109AC           S#ACNO    ANDNE'820101'                    8211
0109AC           S#ACNO    ANDNE'810201'                    8112
0109AC           S#ACNO    ANDNE'821204'                    8246
0109AC           S#ACNO    ANDNE'211301'                    2159
0109AC           S#ACNO    ANDNE'111202'                    1138
0109AC           S#ACNO    ANDNE'710402'                    7142
0109AC           S#ACNO    ANDNE'111001'                    1131
0109AC           S#ACNO    ANDNE'811210'                    8149
0305AC           S#ACNO    ANDNE'110102'
     C                     SETON                     4498
     C                     ENDIF
     C*
     C           S#ACNO    IFEQ '8212'
     C           S#ACTP    ANDNE'D'
0109AC           S#ACNO    OREQ '820201'
0109AC           S#ACTP    ANDNE'D'
     C                     SETON                     414498
     C                     ENDIF
     C*
     C           S#ACNO    IFEQ '8112'
     C           S#ACTP    ANDNE'C'
0109AC           S#ACNO    OREQ '810201'
0109AC           S#ACTP    ANDNE'C'
     C                     SETON                     414498
     C                     ENDIF
     C*
     C           S#NTNO    IFNE *BLANK                     票據號碼
     C           S#DUDT    ANDEQ0                          到期日
     C                     SETON                     4798
     C                     ENDIF
     C*
     C           S#DUDT    IFNE 0                          到期日
     C                     MOVE S#DUDT    P#DATE           繳款日期
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           P#ERR   1
     C           P#ERR     IFNE '0'
     C                     SETON                     4798
     C                     ENDIF
     C                     ENDIF
     C*
     C           S#CURY    IFNE *BLANK                     幣別
     C           S#CURY    ANDNE'NTD'
     C           S#CURY    ANDNE'USD'
9706 C           S#CURY    ANDNE'EUR'
     C                     SETON                     5098
     C                     ENDIF
     C*
     C           S#ACNO    IFEQ '1114'                     幣別
     C           S#CURY    ANDNE'USD'
9706 C           S#CURY    ANDNE'EUR'
0109AC           S#ACNO    OREQ '110104'
0109AC           S#CURY    ANDNE'USD'
0109AC           S#CURY    ANDNE'EUR'
     C                     SETON                     5098
     C                     ENDIF
     C*
     C           S#RAMT    IFEQ 0                          金額
     C                     SETON                     5198
     C                     ENDIF
     C*
     C           S#EXC2    IFEQ 0                          銀行匯率
     C                     SETON                     5298
     C                     ENDIF
     C*
9905AC           S#ACNO    IFEQ '1131'
9905AC           S#PLAC    ANDEQ*BLANKS
0109AC           S#ACNO    OREQ '111001'
0109AC           S#PLAC    ANDEQ*BLANKS
9905AC                     SETON                     5598
9905AC                     ENDIF
9905AC*
1009AC           END331    ENDSR
     C*****************************************************************
     C           SR3320    BEGSR
     C*****************************************************************
     C                     Z-ADD0         W#TMNA 120
     C*
     C           S#CURY    IFEQ 'NTD'                      台幣繳款
     C           S#CURY    OREQ *BLANK                     台幣繳款
     C*
     C           S#ACTP    IFEQ 'D'                        借方
     C                     Z-ADDS#RAMT    W#TMNA
     C                     ELSE                            貸方
     C                     Z-SUBS#RAMT    W#TMNA
     C                     ENDIF
     C*
     C                     ELSE                            外幣繳款
     C*
9905AC           S#ACNO    IFEQ '1131'
0109AC           S#ACNO    OREQ '111001'
9905AC           S#RAMT    MULT S#EXC2    W#TMNB    H
9905AC                     ELSE
     C           S#RAMT    MULT S#EXC1    W#TMNB 120H      幣值轉換
9905AC                     ENDIF
     C           S#ACTP    IFEQ 'D'                        借方
     C                     Z-ADDW#TMNB    W#TMNA
     C                     ELSE                            貸方
     C                     Z-SUBW#TMNB    W#TMNA
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     C                     ADD  W#TMNA    S#NAMT           合計繳款額
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR3400    BEGSR
     C*****************************************************************
     C*如為新增時,必須取得新的繳款單號
     C           W#OPT     IFEQ 1                          新增
     C                     MOVEL'04'      GEKIND
     C                     MOVEL'H'       GEPRIN
     C*
     C           K#NSEQ    CHAINGEREC                90
     C                     Z-ADDGECUNO    W#GENO  50
     C           W#GENO    IFEQ 99999
     C                     Z-ADD1         W#GENO
     C                     ELSE
     C                     ADD  1         W#GENO
     C                     ENDIF
     C*
     C                     MOVELGEPRIN    D#RVN1           設繳款單號
     C                     MOVE W#GENO    D#RVN2
     C                     MOVE D#RVNX    S#RVNO
     C*
     C                     Z-ADDW#GENO    GECUNO
TEST C                     UPDATGEREC
     C                     ENDIF
     C*
     C*先將原有之所有資訊刪除(SARCVF)
     C*
     C                     MOVE S#RVNO    SRRVNO           繳款單號
     C                     Z-ADD0         SRITEM           繳款項次
     C           K#SARC    SETLLSRREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SRREC                    90
     C   90                LEAVE
     C*
     C           SRRVNO    IFNE S#RVNO
     C                     LEAVE
     C                     ENDIF
     C*
     C                     DELETSRREC
     C                     ENDDO
     C*
     C*
     C*計算明細檔之沖銷總額
     C                     Z-ADD0         S#XAMT
     C*
     C                     MOVE S#RVNO    SIRVNO
     C                     Z-ADD0         SIITEM
     C           K#RVIN    SETLLSIREC
     C                     MOVE *OFF      *IN95
     C           *IN95     DOWEQ*OFF                       有資料
     C                     READ SIREC                    95
     C*
     C           *IN95     IFEQ *ON
     C           S#RVNO    ORNE SIRVNO
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ADD  SIINXA    S#XAMT           沖銷金額
     C                     ENDDO
     C*存檔
     C                     Z-ADD0         W#CONT
     C                     Z-ADD0         SRITEM           繳款項次
     C           W#CONT    DOWLE19
     C                     ADD  1         W#CONT
     C           W#CONT    CHAINAR037F2              90
     C   90                LEAVE
     C*
     C           S#ACTP    IFEQ *BLANK                     借貸別
     C                     ITER
     C                     ENDIF
     C*
     C                     EXSR SR3410                      MOVE DATA
     C*
     C           K#SARC    CHAINSRREC                90
     C   90                WRITESRREC
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR3410    BEGSR
     C*****************************************************************
     C* MOVE DATA
     C                     MOVELS#RVNO    SRRVNO           繳款編號
     C                     Z-ADDS#NAMT    SRNAMT           合計金額
     C                     Z-ADDS#RVDT    SRRVDT           繳款日
     C                     MOVELS#CUNO    SRCUNO           客戶編號
     C                     MOVELS#DPNO    SRDPNO           部門別
     C                     MOVELS#RVID    SRRVID           收款業務
     C                     Z-ADDS#XAMT    SRXAMT           沖銷總額
     C                     MOVE *BLANK    SRFL01
     C                     MOVE *BLANK    SRFL02
     C                     MOVE *BLANK    SRFL03
     C*
     C                     ADD  1         SRITEM           項次
     C                     MOVELS#ACTP    SRACTP           借貸別
     C                     MOVELS#ACNO    SRACNO           會計科目
     C                     MOVELS#USTP    SRUSTP           款項別
     C                     MOVELS#NTTP    SRNTTP           票據別
     C                     MOVELS#PBID    SRPBID           付款銀行
9905AC*                    MOVE *BLANK    SRPANO           付款帳號
9905AC*                    MOVE *BLANK    SRPLAC           付款地
9905AC                     MOVELS#PANO    SRPANO           付款銀行
9905AC                     MOVELS#PLAC    SRPLAC           付款地
     C                     MOVELS#NTNO    SRNTNO           票據號碼
     C                     Z-ADDS#RAMT    SRRAMT           收款金額
     C                     MOVEL*BLANK    SRSBID           存入銀行
     C                     MOVELS#SANO    SRSANO           存入帳號
     C                     Z-ADDS#DUDT    SRDUDT           到期日
     C                     MOVELS#RLNO    SRRLNO           相關號碼
     C                     MOVEL*BLANK    SRRESV           保留碼
     C                     MOVE '1'       SRMKTP           外銷
     C                     MOVELS#CURY    SRCURY           幣別
     C                     Z-ADDS#EXC1    SREXC1           財會匯率
     C                     Z-ADDS#EXC2    SREXC2           銀行匯率
     C                     Z-ADDS#EXC3    SREXC3           報關匯率
     C*
9908AC*                    Z-ADDUDATE     SRTXDT           登錄日期
9908AC           *DATE     SUB  19000000  SRTXDT           登錄日期
     C                     MOVELU#USID    SRTXUS           登錄人員
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4000    BEGSR
     C*****************************************************************
     C*W#PRID='01'--->ARE037SC-3畫面
     C*
     C                     Z-ADD0         RRN3    40
     C                     Z-ADD1         S#NBR3            SFLRCDNBR
     C*
     C                     MOVE *ON       *IN73             SFLCLR
     C                     WRITEAR037F3C
     C                     MOVE *OFF      *IN73
     C*
     C           W#OPT     IFEQ 5                          查詢
     C                     MOVE *ON       *IN33
     C                     MOVEL'查詢'  S#SF2T
     C                     ELSE
     C                     MOVE *OFF      *IN33
     C                     MOVEL'沖銷'  S#SF2T
     C                     ENDIF
     C*
     C                     MOVE *ON       *IN61             PROTECT
     C*
     C                     EXSR SR3100                     讀入檔頭
     C*
     C                     EXSR SR4100                     初始SFL
     C*
     C*迴圈開始
     C           W#PRID    DOWEQ'04'
9906AC   33      RRN3      IFEQ 0
9906AC                     MOVE *ON       *IN72
9906AC                     MOVELERR,1     S#ERR3
9906AC                     ENDIF
     C*
     C                     WRITEAR037F3M
     C                     EXFMTAR037F3C
     C*
     C                     SETOF                     3499
     C                     MOVE *BLANK    S#ERR3
     C*IN03
     C           *IN03     IFEQ *ON
     C                     MOVE '00'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*IN12
     C           *IN12     IFEQ *ON
     C                     MOVE '03'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*
     C*載入
     C*
     C  N33      *IN09     IFEQ *ON
     C                     EXSR SR4200
     C                     ITER
     C                     ENDIF
     C*
     C*檢核
     C*
     C  N33                EXSR SR4300
     C*
     C*存檔
     C*
     C  N33N99   *IN10     IFEQ *ON
     C                     EXSR SR4400
     C                     MOVE '01'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*
     C   33                MOVE '03'      W#PRID
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4100    BEGSR
     C*****************************************************************
     C*讀入明細
     C                     Z-ADD0         W#RRN3
9906AC                     MOVEL*BLANKS   W#SHOW
     C*
     C           RRN3      DOWLE699
     C                     ADD  1         RRN3
     C*
     C                     MOVE S#RVNO    SORVNO
     C                     Z-ADDRRN3      SOITEM
     C           K#RVOR    CHAINSOREC                95
9906AC   33 95   W#SHOW    IFNE 'Y'
9906AC                     Z-ADD0         RRN3
9906AC                     GOTO END410
9906AC                     ENDIF
     C                     Z-ADDRRN3      S#SFI3
     C*
     C           *IN95     IFEQ *OFF                       有資料
     C                     MOVELSOSTNO    S#STNO           磅單號碼
     C                     MOVELSOORNO    S#INOR           訂單號碼
     C                     MOVELSOCODE    S#CODE           全部沖銷
     C                     Z-ADDSONOXA    S#INXA           沖銷金額
     C                     Z-ADDSONOXA    S#HNXA           原沖銷金額
     C                     Z-ADDSONOAM    S#INAM           磅單金額
     C                     Z-ADDSONOBA    S#INBA           磅單餘額
     C                     Z-ADDSOSTIT    S#STIT           項次
9906AC                     MOVE 'Y'       W#SHOW  1        有資料
     C*
     C           S#STNO    CHAINSORECL1              90
     C           *IN90     DOWEQ*OFF
     C           W1RVNO    IFNE S#RVNO
     C           W1STNO    ANDEQS#STNO
     C           W1STIT    ANDEQS#STIT
     C                     ADD  W1NOXA    S#INRA           未過已沖
     C                     ENDIF
     C           S#STNO    READESORECL1                  90
     C                     ENDDO
     C*
     C                     Z-ADDRRN3      W#RRN3  40       最後一筆
     C*
     C                     ELSE
     C                     Z-ADD0         S#INXA           沖銷金額
     C                     Z-ADD0         S#HNXA           沖銷金額
     C                     Z-ADD0         S#INAM           磅單金額
     C                     Z-ADD0         S#INBA           磅單餘額
     C                     Z-ADD0         S#STIT           項次
     C                     MOVEL*BLANK    S#STNO           磅單號碼
     C                     MOVEL*BLANK    S#INOR           訂單號碼
     C                     MOVEL*BLANK    S#CODE           全部沖銷
     C                     Z-ADD0         S#INRA           未過已沖
     C                     ENDIF
     C*
     C  N33
     CORN95                WRITEAR037F3
     C                     ENDDO
     C*
9906AC*                    ENDSR
9906AC           END410    ENDSR
     C*****************************************************************
     C           SR4200    BEGSR
     C*****************************************************************
     C*載入明細
     C*
     C                     MOVE 'Y'       W#LOOP  1
     C                     CLEARAR037F3W
     C*
     C           W#LOOP    DOWEQ'Y'
     C                     EXFMTAR037F3W
     C                     MOVE *BLANK    S#ERRW
     C                     MOVE *OFF      *IN60
     C*
     C           *IN03     IFEQ *ON
     C           *IN12     OREQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C                     EXSR SR4210                     檢核
     C*
     C  N60                EXSR SR4220                     載入
     C  N60                MOVE 'N'       W#LOOP
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4210    BEGSR
     C*****************************************************************
     C*檢核
     C*
     C           S#ORNO    CHAINRHSCINV              90
     C*
     C*訂單號碼
     C*
     C           *IN90     IFEQ *ON
     C           C1ORNO    ORNE S#ORNO
     C                     MOVELERR,7     S#ERRW
     C                     MOVE *ON       *IN60
     C                     ENDIF
     C*
     C*客戶代號
     C*
     C  N60      C1OCUS    IFNE S#CUNO
     C                     MOVELERR,8     S#ERRW
     C                     MOVE *ON       *IN60
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4220    BEGSR
     C*****************************************************************
     C*載入
     C                     Z-ADD0         RRN3
     C*
     C                     MOVE *OFF      *IN90
     C           S#ORNO    SETLLTXRECL4
     C           *IN90     DOWEQ*OFF
     C           S#ORNO    READETXRECL4                  90
     C*
     C           *IN90     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C           TXFLAG    IFEQ 'D'
     C           TXORN5    ORNE S#ORNO
     C           D#IVN1    ORNE '#'
     C                     ITER
     C                     ENDIF
     C*
0312AC           TXORN5    IFEQ 'H16098'
0312AC           TXCUNO    ANDNE'EH150'
0312AC                     ITER
0312AC                     ENDIF
0312AC*
     C                     MOVELD#ORNO    K3ORNO           訂單編號
     C                     MOVELTXNO      K3STNO           磅單編號
0408AC                     Z-ADDTXITEM    K3STIT           磅單項次
     C                     MOVEL*OFF      *IN40
     C           K#SOL3    CHAINSORECL3              40
     C           *IN40     IFEQ *OFF
     C           W3CODE    ANDEQ'Y'
     C                     ITER
     C                     ENDIF
     C*
     C           RRN3      IFEQ 700                        最多70筆
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ADD  1         RRN3
     C           RRN3      CHAINAR037F3              77
     C*
     C  N40      W3CODE    IFEQ 'N'
     C           TXQTY     SUB  W3QTY     TXQTY
     C                     ENDIF
     C*
     C                     MOVELTXNO      S#STNO           磅單號碼
     C                     MOVELTXORNO    S#INOR           訂單號碼
     C                     MOVELTXPDNM    S#PDNM           品名
     C                     Z-ADDTXITEM    S#STIT           訂單項次
0707AC                     Z-ADDTXQTY     S#QTY            數量
     C*
     C           S#STNO    IFNE *BLANK
     C                     MOVEL'Y'       S#CODE           全部沖銷
     C                     ELSE
     C                     MOVEL*BLANK    S#CODE
     C                     ENDIF
     C*
     C                     Z-ADD0         W#USAT 133
     C                     Z-ADD0         W#NOXA 110
     C*
0707AC           D#ORNO    CHAINTREXPL1              58
0707AC           *IN58     IFEQ *ON
     C                     MOVELD#ORNO    C1ORNO           訂單編號
     C                     Z-ADD1         C1OSEQ           訂單流水號
     C                     Z-ADDD#OITM    C1OITM           訂單項次
     C           K#CINV    CHAINRHSCINV              40
     C*
     C                     SELEC
     C           C1ORNO    WHEQ 'H10001'
     C           C1ORNO    OREQ 'H10002'
     C           C1ORNO    OREQ 'H10003'
     C           C1ORNO    OREQ 'H10005'
     C           C1ORNO    OREQ 'H10006'
     C           D#AREA    IFEQ 'K'
     C           C1UPRC    MULT C1RRAT    S#UPRC    H
     C                     ELSE
     C           C1UPRC    MULT C1TRAT    S#UPRC    H
     C                     ENDIF
     C           S#UPRC    MULT TXQTY     S#INAM    H      磅單金額
     C*
     C                     OTHER
9804AC*          C1UPRC    MULT C1TRAT    S#UPRC    H
9804AC           C1UPRC    MULT S#EXC3    S#UPRC    H
     C           S#UPRC    MULT TXQTY     S#INAM    H      磅單金額
     C                     ENDSL
0707AC                     ELSE
0707AC                     Z-ADDTXAMT     S#INAM
0707AC                     ENDIF
     C*
     C                     MOVELTXORNO    K#ORNO
     C                     MOVELTXNO      K#STNO
     C           K#SOL2    CHAINSORECL2              40
     C           *IN40     DOWEQ'0'
     C           W2RVNO    IFNE S#RVNO
     C           W2STNO    ANDEQS#STNO
     C           W2STIT    ANDEQS#STIT
     C                     ADD  W2NOXA    W#NOXA           沖銷金額累
     C                     ENDIF
     C           K#SOL2    READESORECL2                  40
     C                     ENDDO
     C*
     C           S#INAM    SUB  W#NOXA    S#INBA           磅單餘額
     C*
     C           S#STNO    CHAINSORECL1              91
     C           *IN91     DOWEQ'0'
     C           W1RVNO    IFNE S#RVNO
     C           W1STNO    ANDEQS#STNO
     C           W1STIT    ANDEQS#STIT
     C                     ADD  W1NOXA    S#INRA           未過已沖
     C                     ENDIF
     C           S#STNO    READESORECL1                  91
     C                     ENDDO
     C*
     C           S#INAM    SUB  S#INRA    S#INXA           沖銷金額
     C                     Z-ADDS#INXA    S#HNXA           原沖銷金額
     C                     ADD  S#INXA    S#XAMT           總沖銷金額
     C*
     C  N77                UPDATAR037F3
     C                     Z-ADDRRN3      W#RRN3
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4300    BEGSR
     C*****************************************************************
     C*先計算沖銷總額
     C*
     C                     Z-ADD0         S#XAMT           沖銷總額
     C                     Z-ADD0         W#CONT
     C*
     C           W#CONT    DOWLE699
     C                     ADD  1         W#CONT
     C*
     C           W#CONT    CHAINAR037F3              90
     C   90                LEAVE
     C*
     C           S#STNO    IFEQ *BLANK                     磅單號碼
     C                     ITER
     C                     ENDIF
     C*
     C                     ADD  S#INXA    S#XAMT
     C                     ENDDO
     C*AR037F3的檢核
     C                     MOVE *BLANK    W#ORNO  6
     C                     MOVE *OFF      *IN98
     C*
     C                     Z-ADD0         W#CONT
     C           W#CONT    DOWLE699
     C                     ADD  1         W#CONT
     C*
     C           W#CONT    CHAINAR037F3              90
     C   90                LEAVE
     C*
     C           S#STNO    IFEQ *BLANK                     磅單號碼
     C                     Z-ADD0         S#INXA           沖銷金額
     C                     Z-ADD0         S#INAM           磅單金額
     C                     Z-ADD0         S#INBA           磅單餘額
     C*                    Z-ADD0         S#QTY            數量
     C                     MOVEL*BLANK    S#PDNM           品名
     C                     MOVEL*BLANK    S#INOR           訂單號碼
     C                     Z-ADD0         S#INRA           未過已沖
     C                     ITER
     C                     ENDIF
     C*
     C*僅紀錄一次訂單號碼
     C  N98                MOVELS#INOR    W#ORNO
     C  N98                MOVE *ON       *IN98
     C*
     C*以此訂單號碼檢核報關匯率
     C                     MOVE W#ORNO    C1ORNO
     C                     Z-ADD0         C1OSEQ
     C                     Z-ADD0         C1CITM
9804AC*          K#CINV    SETLLRHSCINV
9804AC*                    READ RHSCINV                  90
9804AC*          *IN90     IFNE *OFF
9804AC*          C1TRAT    ORNE S#EXC3
9804AC*          D#ORNO    SETLLRARCINV
0106AC           S#INOR    SETLLRARCINV
9804AC                     MOVEL*OFF      *IN90
9804AC                     MOVEL*BLANK    W#CHK   1
9804AC           *IN90     DOWEQ*OFF
9804AC*          D#ORNO    READERARCINV                  90
0106AC           S#INOR    READERARCINV                  90
9804AC   90                LEAVE
9804AC           R1TRAT    IFEQ S#EXC3
9804AC                     MOVEL'Y'       W#CHK
9804AC                     LEAVE
9804AC                     ENDIF
9804AC                     ENDDO
9804AC           W#CHK     IFNE 'Y'
     C                     MOVELERR,15    S#ERR3
     C                     SETON                     5899
     C                     ENDIF
     C*
     C*磅單錯誤
     C                     MOVEL'SA04'    K#CODE           單據代號
     C                     MOVELS#STNO    K#STNO           磅單編號
     C  N99      K#TRND    SETLLTXREC
     C           *IN99     DOWEQ*OFF
     C           K#TRND    READETXREC                    90
     C           *IN90     IFEQ *ON
     C                     MOVELERR,9     S#ERR3
     C                     SETON                     5899
     C                     ELSE
     C*
     C           TXDATE    IFLT 960101
     C                     ITER
     C                     ENDIF
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C*
     C*客戶不同
0312AC           S#RVNO    IFNE 'H69613'
     C  N99      TXCUNO    IFNE S#CUNO
     C                     MOVELERR,10    S#ERR3
     C                     SETON                     5899
     C                     ENDIF
0312AC                     ENDIF
     C*
     C*訂單不同
     C                     MOVELTXORNO    W#ORN1  6
     C  N99      W#ORNO    IFNE W#ORN1
     C                     MOVELERR,11    S#ERR3
     C                     SETON                     5899
     C                     ENDIF
     C*
     C*沖銷金額大於發票餘額
     C* N99      S#INXA    IFGT S#INBA
     C*                    MOVELERR,12    S#ERR3
     C*                    SETON                     5999
     C*                    ENDIF
     C*
     C                     Z-ADD0         W#INXA 110
     C*
     C  N99                SELEC
     C           S#CODE    WHEQ *BLANK
     C           S#STNO    ANDNE*BLANK
     C                     MOVELERR,17    S#ERR3
     C                     SETON                     6399
     C*
     C           S#CODE    WHEQ 'Y'
     C           S#INXA    SUB  S#HNXA    W#INXA
     C           W#INXA    IFGT 60
     C                     MOVELERR,12    S#ERR3
     C                     SETON                     5999
     C                     ENDIF
     C*
     C           S#CODE    WHEQ 'N'
     C           S#INXA    ANDGTS#INBA
     C                     MOVELERR,12    S#ERR3
     C                     SETON                     5999
     C                     ENDSL
     C*
     C  N99                SETOF                     585963無錯誤
     C*
     C                     UPDATAR037F3
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4400    BEGSR
     C*****************************************************************
     C*先將原有之所有資訊刪除
     C*
     C                     MOVE S#RVNO    SIRVNO           繳款單號
     C                     Z-ADD0         SIITEM           繳款項次
     C           K#RVIN    SETLLSIREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SIREC                    90
     C   90                LEAVE
     C*
     C           SIRVNO    IFNE S#RVNO
     C                     LEAVE
     C                     ENDIF
     C*
     C                     DELETSIREC
     C                     ENDDO
     C* (DELETE SARVOR)
     C                     MOVE S#RVNO    SORVNO           繳款單號
     C                     Z-ADD0         SOITEM           繳款項次
     C           K#RVOR    SETLLSOREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SOREC                    90
     C   90                LEAVE
     C*
     C           SORVNO    IFNE S#RVNO
     C                     LEAVE
     C                     ENDIF
     C*
     C                     DELETSOREC
     C                     ENDDO
     C*
     C                     Z-ADD0         WOINAM 110
     C                     Z-ADD0         WONOXA 110
     C                     Z-ADD0         WONOBA 110
     C*
     C*存檔(WRITE SRVROR)
     C                     Z-ADD0         W#CONT
     C                     Z-ADD0         SOITEM           繳款項次
     C           W#CONT    DOWLE699
     C                     ADD  1         W#CONT
     C           W#CONT    CHAINAR037F3              90
     C   90                LEAVE
     C*
     C           S#STNO    IFEQ *BLANK                     借貸別
     C           S#INXA    OREQ 0
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVE S#RVNO    SORVNO           繳款編號
     C                     ADD  1         SOITEM           繳款項次
     C           K#RVOR    CHAINSOREC                90
     C*
     C                     EXSR SR4410                      MOVE DATA
     C   90                WRITESOREC
     C*
     C                     ENDDO
     C*
     C*存檔(WRITE SRVRIN)
     C           WOINAM    IFNE 0
     C                     Z-ADD0         W#CONT
     C                     Z-ADD0         SIITEM           繳款項次
     C*
     C                     MOVE S#RVNO    SIRVNO
     C                     ADD  1         SIITEM
     C           K#RVIN    CHAINSIREC                90
     C*
     C                     EXSR SR4420
     C   90                WRITESIREC
     C                     ENDIF
     C*
     C*更新SARCVF沖銷金額合計
     C*
     C                     MOVE S#RVNO    SRRVNO           繳款單號
     C                     Z-ADD0         SRITEM           繳款項次
     C           K#SARC    SETLLSRREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SRREC                    90
     C   90                LEAVE
     C*
     C           SRRVNO    IFNE S#RVNO
     C                     LEAVE
     C                     ENDIF
     C*
     C                     Z-ADDS#XAMT    SRXAMT
     C                     UPDATSRREC
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4410    BEGSR
     C*****************************************************************
0707AC           S#ORNO    CHAINTREXPL1              58
     C                     MOVELS#ORNO    SOORNO           訂單編號
     C                     MOVELS#STNO    SOSTNO           磅單編號
     C                     Z-ADDS#STIT    SOSTIT           磅單項次
     C                     Z-ADDS#INXA    SONOXA           沖銷金額
     C                     MOVELS#PDNM    SOPDNM           品名代號
     C                     MOVELS#CODE    SOCODE           全部沖銷
     C                     MOVEL*BLANK    SIFL02           過入碼
0707AC           *IN58     IFEQ *ON
     C           S#INXA    DIV  S#UPRC    SOQTY     H      數量
0707AC                     ELSE
0707AC                     Z-ADDS#QTY     SOQTY
0707AC                     ENDIF
     C*
     C           S#CODE    IFEQ 'Y'
     C                     Z-ADDS#INXA    SONOAM           磅單金額
     C                     Z-ADDS#INXA    SONOBA           磅單餘額
     C                     ELSE
     C                     Z-ADDS#INAM    SONOAM           磅單金額
     C                     Z-ADDS#INBA    SONOBA           磅單餘額
     C                     ENDIF
     C*
     C                     ADD  SONOAM    WOINAM
     C                     ADD  SONOXA    WONOXA
     C                     ADD  SONOBA    WONOBA
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4420    BEGSR
     C*****************************************************************
     C                     MOVEL*BLANK    SIINNO
     C                     MOVELS#ORNO    SIINNO           發票號碼
     C                     Z-ADDWOINAM    SIINAM           訂單金額
     C                     Z-ADDWONOBA    SIINBA           訂單餘額
     C                     Z-ADDWONOXA    SIINXA           沖銷金額
     C                     MOVE *BLANK    SIFL02           過入碼
     C*
     C                     MOVE *ON       *IN35
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR5000    BEGSR
     C*****************************************************************
     C*選擇信用狀載入
     C*
     C           S#SFN5    IFEQ 0
     C                     MOVE *ON       *IN72
     C                     ELSE
     C                     MOVE *OFF      *IN72
     C                     ENDIF
     C*
     C                     MOVE *OFF      *IN74
     C                     WRITEAR037F5C
     C                     WRITEAR037F5M
     C                     EXFMTAR037F5C                   螢幕輸入
     C*
     C           S#CRN5    IFNE 0
     C                     Z-ADDS#CRN5    S#NBR5           記錄位置
     C                     ENDIF
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C                     MOVEL'00'      W#PRID
     C           *IN12     WHEQ '1'
     C                     MOVEL'03'      W#PRID
     C                     OTHER
     C                     EXSR SR5200
     C                     ENDSL
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR5100    BEGSR
     C*****************************************************************
     C                     MOVE *ON       *IN73
     C                     WRITEAR037F5C
     C                     MOVE *OFF      *IN73
     C*
     C                     Z-ADD0         S#SFN5
     C                     Z-ADD0         RRN5    40
     C*
     C           S#CUNO    SETGTLLCMSTL3
     C           1         DOWEQ1
     C                     READPLLCMSTL3                 38
     C*
     C           *IN38     IFEQ '1'
     C           S#CUNO    ORNE LLCUNO
     C                     LEAVE
     C                     ENDIF
     C*
9908AC           *DATE     SUB  19000000  U#DATE  80
9908AC*          LLENDT    IFLT UDATE
9908AC           LLENDT    IFLT U#DATE
     C                     ITER
     C                     ENDIF
     C*
     C                     ADD  1         S#SFN5
     C                     ADD  1         RRN5
     C                     CLEARAR037F5
     C                     MOVELLLNO      S#LLNO           信用狀
     C                     MOVELLLBANK    S#BANK           銀行
     C                     Z-ADDLLENDT    S#ENDT           到期日
     C                     WRITEAR037F5
     C                     ENDDO
     C*
     C                     Z-ADD1         S#NBR5
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR5200    BEGSR
     C*****************************************************************
     C           S#CRN5    IFNE 0
     C                     Z-ADDS#CRN5    RRN5
     C*
     C           RRN5      CHAINAR037F5              38
     C           RRN2      CHAINAR037F2              38
     C                     MOVELS#LLNO    S#NTNO
     C                     MOVELS#BANK    S#PBID
     C                     MOVE D#OF      S#PBID
     C                     UPDATAR037F2
     C                     MOVEL'X'       W#FLAG  1
     C                     ENDIF
     C*
     C                     MOVEL'03'      W#PRID
     C*
     C                     ENDSR
     C*****************************************************************
**  ERR
01-無任何資料！
02-該繳款書已過入財會,如需異動請通知財會還原之！
03-該繳款書已確認,如需異動請還原之！
04-該繳款書尚未確認,無法還原！
05-資料存檔完畢！
06-欄位輸入錯誤！
07-無此訂單！
08-客戶不同！
09-發票號碼不存在！
10-客戶代號不同！
11-訂單號碼不同！
12-沖銷金額不得大於磅單餘額！
13-繳款總額與沖銷總額不平,請修改或試算後再行確認！
14-試算完成！
15-報關匯率錯誤!
16-試算完畢,若結果無誤請逕行確認！
17-沖銷選項必須輸入！
18-僅可使用查詢功能!
19-本類會計科目之相關號碼必須輸入訂單編號!
20-訂單客戶與繳款單客戶不同，請檢核!!
