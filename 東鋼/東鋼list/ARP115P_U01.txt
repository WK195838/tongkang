    /*****************************************************************/
    /*                                                               */
    /*        >>  PROGRAM INTRODUCTION  <<                           */
    /*                                                               */
    /*        1.PROGRAM-ID      ARP115P                              */
    /*        2.PROGRAM-TYPE    CLP                                  */
    /*        3.AUTHOR          S02LBU                               */
    /*        4.FUNCTION       非營業繳款代開傳票還原              */
    /*        5.DATE-WRITTEN    96/09/28                             */
    /*        6.UPDATE  DATE   100/12/05  2011AR634 S00WCJ (0012A)   */
    /*                          配合IFRS修改                     */
    /*                         101/12/14  2012AR704 S00WCJ (0112A)   */
    /*                          配合IFRS正式轉換修改             */
    /*                                                               */
    /*****************************************************************/
             PGM
             DCL        VAR(&P#YYMM) TYPE(*CHAR) LEN(6)
             DCL        VAR(&D#YYMM) TYPE(*CHAR) LEN(6)
             DCL        VAR(&P#CODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&INT)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&WHOAMI) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DATEX)  TYPE(*CHAR) LEN(8) /*輸入日期*/
             DCL        VAR(&DATEE)  TYPE(*CHAR) LEN(8)
             DCL        VAR(&ENDD)   TYPE(*CHAR) LEN(2) /*輸入止日*/
             DCL        VAR(&AREA)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&P#AREA) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DTARA)  TYPE(*CHAR) LEN(11)
             DCL        VAR(&YM)     TYPE(*CHAR) LEN(6)
             DCL        VAR(&WSID)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&CNTT)   TYPE(*CHAR) LEN(6) /*改繳款數*/
 /*0012A*/   DCL        VAR(&D#VNO1) TYPE(*CHAR) LEN(6)
 /*0012A*/   DCL        VAR(&D#VNO2) TYPE(*CHAR) LEN(6)
             DCL        VAR(&CNTA)   TYPE(*CHAR) LEN(6) /*刪傳票數*/
 /*0112A*/   DCL        VAR(&IFRSD)  TYPE(*CHAR) LEN(8)

             RTVDTAARA  DTAARA(*LDA (1021 1)) RTNVAR(&AREA)
             CHGVAR     VAR(&DTARA) VALUE('ACCTLDTA' *CAT &AREA)

             RTVJOBA    TYPE(&INT) USER(&WHOAMI) JOB(&WSID)
             IF         COND(&INT *EQ '0')  THEN(GOTO BATCH)

                        /* 傳進已代開日期回還原日期 */
 DISPLY:     CALL       PGM(ARP115RS) PARM(&IN03 &DATEX &P#AREA &ENDD)

             IF         COND(&IN03 *EQ '0') THEN(DO)
             CHGVAR     VAR(&DATEE)  VALUE(&DATEX)
             CHGVAR     VAR(%SST(&DATEE 7 2)) VALUE(&ENDD)
             CHGDTAARA  DTAARA(*LDA (301 8)) VALUE(&DATEX)
             CHGDTAARA  DTAARA(*LDA (311 8)) VALUE(&DATEE)
             RTVDTAARA  DTAARA(*LDA (301 6)) RTNVAR(&D#YYMM)
             RTVDTAARA  DTAARA(*LDA (301 6)) RTNVAR(&P#YYMM)
             CHGDTAARA  DTAARA(*LDA (310 1)) VALUE(&P#AREA)

             CALL    PGM(ACP101R) PARM(&P#YYMM  &P#CODE)
             IF         COND(&P#CODE *EQ '1') THEN(DO)
             SNDBRKMSG  MSG('關帳日期已過，請與相關人員聯絡！') +
                          TOMSGQ(&WSID)
             GOTO       CMDLBL(DISPLY)
             ENDDO

             SBMJOB     JOB(ARP115P) JOBD(ARJOBD)  +
                        RQSDTA('CALL ARP115P')
             ENDDO
             RETURN

 BATCH:
             RTVDTAARA  DTAARA(*LDA (301  8)) RTNVAR(&DATEX)
             RTVDTAARA  DTAARA(*LDA (310  1)) RTNVAR(&P#AREA)
             RTVDTAARA  DTAARA(*LDA (311  8)) RTNVAR(&DATEE)
             RTVDTAARA  DTAARA(*LDA (1021 1)) RTNVAR(&AREA)
 /*0112A*/   RTVDTAARA  DTAARA(ARIFRSCTL (1  8))  RTNVAR(&IFRSD)

             CHGVAR     VAR(&YM)  VALUE(%SST(&DATEX 1 6))

             OVRDBF     FILE(RCVDTL) TOFILE(DALIB/RCVDTL) SHARE(*YES)
             IF         COND(&P#AREA *EQ 'P') THEN(DO)
             OPNQRYF    FILE((RCVDTL)) OPTION(*INP *UPD) +
                        QRYSLT('RVDATE *GE ' || &DATEX || ' *AND +
                                RVDATE *LE ' || &DATEE || ' *AND +
                                RVTYPE *EQ "3" *AND +
                                RVDECD *NE "D" *AND +
                           %SST(RVNO 1 1) *EQ %VALUES("P" "M")')
             ENDDO
             ELSE       CMD(DO)
             OPNQRYF    FILE((RCVDTL)) OPTION(*INP *UPD) +
                        QRYSLT('RVDATE *GE ' || &DATEX || ' *AND +
                                RVDATE *LE ' || &DATEE || ' *AND +
                                RVTYPE *EQ "3" *AND +
                                RVDECD *NE "D" *AND +
                           %SST(RVNO 1 1) *EQ "' || &P#AREA || '"')
             ENDDO


             OVRDBF     FILE(ACTRAN) TOFILE('AC' || &P#AREA || +
                          'R') LVLCHK(*NO)
             CPYF       FROMFILE(ACTRAN) TOFILE(QTEMP/ACTRANW) +
                        MBROPT(*ADD) CRTFILE(*YES) +
                        INCREL((*IF  ACVRNO *GE 'J00000' ) +
                               (*AND ACVRNO *LE 'J99999' ) +
                               (*AND ACAREA *EQ &AREA)     +
                               (*AND ACYM   *EQ &YM)       +
                               (*AND ACYMD  *GE &DATEX)    +
                               (*AND ACYMD  *LE &DATEE))
             OVRDBF     FILE(ACTRANW) TOFILE(QTEMP/ACTRANW) LVLCHK(*NO)

                        /* 還原代開繳款並刪除已代開傳票 */
             CALL       PGM(ARP115R) PARM(&CNTT &CNTA)

             DLTOVR     FILE(*ALL)

 /*0112A*/  IF    COND(&DATEX  *LT  &IFRSD)  THEN(DO)
 /*0012A STRAT */

             RTVDTAARA  DTAARA(*LDA (201 6)) RTNVAR(&D#VNO1)
             RTVDTAARA  DTAARA(*LDA (211 6)) RTNVAR(&D#VNO2)

             CALL       PGM(ARS888P) PARM(&AREA &YM &D#VNO1 &D#VNO2)
 /*0012A  END */
 /*0112A*/  ENDDO

             SNDMSG     MSG('繳款代開傳票還原作業已完成，合計改+
                             繳款明細' || &CNTT || '筆，刪傳票' +
                              || &CNTA || '筆。' || &DATEX) +
                        TOUSR(&WHOAMI)

             ENDPGM
