     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP072P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02YSH                                  */
     /*     4.FUNCTION     銷貨收入每月差異代開傳票               */
     /*     5.DATE-WRITTEN  89/05/20                                */
     /*     6.UPDATE  DATE  100/06/10  2011AR607 S00WCJ (0006A)     */
     /*                      增加庫別代號寫入總帳摘要三欄位       */
     /*                     100/12/02  2011AR634 S00WCJ (0012A)     */
     /*                       配合IFRS修改                      */
     /*                     101/12/14 2012AR704 S00WCJ (0112A)      */
     /*                       配合IFRS正式轉換修改              */
     /*                                                             */
     /*  <說明:> 由於銷貨收入由銷貨明細代開而非發票明細代開     */
     /*            因此當隨訂發票愈多時，便會有差異出現。         */
     /*            本作業每月進行一次，先刪除該月份之傳票資料     */
     /*            再進行代開整月的動作。（可重複代開）           */
     /*            傳票編號：P00001 -- P00499                     */
     /*                                                             */
     /***************************************************************/
             PGM
             DCLF       FILE(ARP072S)
             DCL        VAR(&P#YYMM) TYPE(*CHAR) LEN(6)
             DCL        VAR(&P#CODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&INT)   TYPE(*CHAR) LEN(1)   /*執行狀態*/
             DCL        VAR(&OUTQ)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03)  TYPE(*LGL)  LEN(1)
             DCL        VAR(&AREA)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&YYMM)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&YY)    TYPE(*CHAR) LEN(4)
             DCL        VAR(&MM)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&FNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C#SDAT) TYPE(*CHAR) LEN(8)
             DCL        VAR(&C#EDAT) TYPE(*CHAR) LEN(8)
 /*0011A*/   DCL        VAR(&D#VNO1) TYPE(*CHAR) LEN(6)
 /*0011A*/   DCL        VAR(&D#VNO2) TYPE(*CHAR) LEN(6)
 /*0112A*/   DCL        VAR(&IFRSD)  TYPE(*CHAR) LEN(6)

             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT) USER(&USER)
             IF         COND(&INT *EQ '0') THEN(GOTO BATCH)

             RTVDTAARA  DTAARA(*LDA (1021 1)) RTNVAR(&AREA)
             RTVDTAARA  DTAARA(*LDA (951 35)) RTNVAR(&S#COMP)
             RTVDTAARA  DTAARA(*LDA (1011 10)) RTNVAR(&S#DEVI)
             IF         COND(&AREA *EQ 'P')  THEN(DO)
             CHGVAR     VAR(&AREA) VALUE('M')
             ENDDO
             CHGVAR     VAR(&S#AREA) VALUE(&AREA)
 DISPLAY:
             SNDRCVF    RCDFMT(AR072F1)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)

             IF         COND(&S#AREA *EQ 'P') THEN(DO)
             CHGVAR     VAR(&S#ERR) +
                        VALUE('銷貨差異代開傳票作業，不得在台北進行')
                        GOTO DISPLAY
                        ENDDO

             CHGVAR     VAR(&YYMM) VALUE(&S#YYMM)
             CHGVAR     VAR(&P#YYMM) VALUE(&S#YYMM)
             CHGVAR     VAR(&YY) VALUE(%SST(&YYMM 1 4))
             CHGVAR     VAR(&MM) VALUE(%SST(&YYMM 5 2))
             CHGVAR     VAR(&FNAME) +
                        VALUE('AC' || &AREA || 'R')
             CHGVAR     VAR(&MNAME) VALUE('VR' || &MM)
             CHKOBJ     OBJ(&FNAME) OBJTYPE(*FILE)
             MONMSG     CPF0000     EXEC(DO)
             CHGVAR     VAR(&S#ERR) +
                        VALUE('銷貨差異代開傳票作業，無法找到指定+
                          的傳票檔：' || &FNAME)
                        GOTO DISPLAY
             ENDDO

             CALL    PGM(ACP101R) PARM(&P#YYMM &P#CODE)
             IF         COND(&P#CODE *EQ '1') THEN(DO)
             CHGVAR     VAR(&S#ERR) VALUE('關帳日期已過')
             GOTO       CMDLBL(DISPLAY)
             ENDDO

             CHGDTAARA  DTAARA(*LDA (1 6)) VALUE(&YYMM)
             CHGDTAARA  DTAARA(*LDA (7 1)) VALUE(&AREA)
             SBMJOB     JOB(ARP072P) JOBD(ARJOBD) OUTQ(&OUTQ)  +
                        RQSDTA('CALL ARP072P')
             RETURN

BATCH:
             RTVDTAARA  DTAARA(*LDA (1 6)) RTNVAR(&YYMM)
             RTVDTAARA  DTAARA(*LDA (1 4)) RTNVAR(&YY)
             RTVDTAARA  DTAARA(*LDA (5 2)) RTNVAR(&MM)
             RTVDTAARA  DTAARA(*LDA (7 1)) RTNVAR(&AREA)
 /*0112A*/   RTVDTAARA  DTAARA(ARIFRSCTL (1  6))  RTNVAR(&IFRSD)

             CHGVAR     VAR(&FNAME) +
                        VALUE('AC' || &AREA || 'R')
             CHGVAR     VAR(&MNAME) VALUE('VR' || &MM)


STEP1:       /* 刪除已代開過的資料 */

             OVRDBF     FILE(ACTRAN) TOFILE(&FNAME) SHARE(*YES)
             OPNQRYF    FILE(ACTRAN) OPTION(*ALL) +
                        QRYSLT('ACVRNO *GE "P00001" *AND +
                                ACVRNO *LE "P00499" *AND +
                                ACAREA *EQ "' || &AREA || '" *AND +
                                YM     *EQ "' || &YYMM || '"')    +
                        MAPFLD((YM  ACYM  *CHAR 6))

             CALL       PGM(ARP072R1) /*刪除輸入日期後的資料*/
             CLOF       OPNID(ACTRAN)
             DLTOVR     *ALL

 /*0112A*/  IF   COND(&YYMM  *LT  &IFRSD)  THEN(DO)
 /*0011A STRAT */

             CHGVAR   VAR(&D#VNO1)  VALUE('P00001')
             CHGVAR   VAR(&D#VNO2)  VALUE('P00499')

             CALL       PGM(ARS888P) PARM(&AREA &YYMM &D#VNO1 &D#VNO2)
 /*0011A  END */
 /*0112A*/  ENDDO

STEP2:       /* 先前處理 */
             CLRPFM     ARP072F
             OVRDBF     FILE(INVMST) TOFILE(INVMST) SHARE(*YES)

             CHGVAR     VAR(&C#SDAT) VALUE(&YYMM || '01')
             CHGVAR     VAR(&C#EDAT) VALUE(&YYMM || '31')

             OPNQRYF    FILE(INVMST) OPTION(*ALL) +
                        QRYSLT('+
                              (INDT *GE "' || &C#SDAT || '") *AND +
                              (INDT *LE "' || &C#EDAT || '") *AND +
                              (INNO *NE "JW32774325")  *AND +
                              (INNO *NE "JW32774497")  *AND +
                              (INORNO *NE "M01744")    *AND +
                              (INORNO *NE "M01762")    *AND +
                              (INORNO *NE "M01763")    *AND +
                              (INORNO *NE "M01764")    *AND +
                              (INORNO *NE "M01765")    *AND +
                              (INORNO *NE "M01766")    *AND +
                              (INORNO *NE "M01767")    *AND +
                              (INORNO *NE "M01781")    *AND +
                              (INDECD *NE "D")') +
                        MAPFLD((INDT ININDT *CHAR 8))

             CALL       PGM(ARP072R2) /*先前處理*/
             CLOF       OPNID(INVMST)
             DLTOVR     *ALL

             IF         COND((&AREA *EQ 'H')  *OR +
                             (&AREA *EQ 'K')) THEN(DO)
             CLRPFM     ARP072F1
             OVRDBF     FILE(HSCINV) TOFILE(HSCINV) SHARE(*YES)
             OPNQRYF    FILE(HSCINV) OPTION(*ALL) +
                        QRYSLT('+
                              (CDTE *GE "' || &C#SDAT || '") *AND +
                              (CDTE *LE "' || &C#EDAT || '") *AND +
                              (C1FLAG *NE "D")') +
                        MAPFLD((CDTE C1CDTE *CHAR 8))
             CALL       PGM(ARP072R3) /*先前處理*/
             CLOF       OPNID(HSCINV)
             DLTOVR     FILE(*ALL)
             ENDDO


STEP3:       /* 代開開始*/   /*------------------*/

             OVRDBF     FILE(ACTRAN) TOFILE(&FNAME) SHARE(*YES)
   /*0006A   OVRPRTF    FILE(ARP072T) TOFILE(ARP072T) PAGESIZE(*N +
                          132) CPI(10) HOLD(*YES) USRDTA('發銷差異') */
   /*0006A*/ OVRPRTF    FILE(ARP072T) TOFILE(ARP072T) PAGESIZE(*N +
                          156) CPI(12) HOLD(*YES) USRDTA('發銷差異')

             OVRDBF     FILE(ARP072F) TOFILE(ARP072F) SHARE(*YES)
  /*0006A    OPNQRYF    FILE(ARP072F) +
                        KEYFLD((A1ORNO) (A1IVNO) (A1PDNM)) */
  /*0006A*/  OPNQRYF    FILE(ARP072F) +
                        KEYFLD((A1ORNO) (A1IVNO) (A1PDNM) (A1STNO))

             CALL       PGM(ARP072R)  /*傳票代開及列表*/
             CLOF       OPNID(ARP072F)

 /*0112A*/  IF      COND(&YYMM  *LT  &IFRSD)  THEN(DO)
 /*0011A STRAT */

             RTVDTAARA  DTAARA(*LDA (201 6)) RTNVAR(&D#VNO1)
             RTVDTAARA  DTAARA(*LDA (211 6)) RTNVAR(&D#VNO2)

             CALL       PGM(ARS888P) PARM(&AREA &YYMM &D#VNO1 &D#VNO2)
 /*0011A  END */
 /*0112A*/  ENDDO

             IF         COND((&AREA *EQ 'H')  *OR +
                             (&AREA *EQ 'K')) THEN(DO)
             OVRPRTF    FILE(ARP072T) TOFILE(ARP072TA) PAGESIZE(*N +
                          158) CPI(12) HOLD(*YES) USRDTA('外銷差異')

             OVRDBF     FILE(ARP072F1) TOFILE(ARP072F1) SHARE(*YES)
             OPNQRYF    FILE(ARP072F1) +
                        KEYFLD((A1ORNO) (A1IVNO) (A1PDNM))

             CALL       PGM(ARP072RA)  /*傳票代開及列表*/
             CLOF       OPNID(ARP072F1)
             DLTOVR     FILE(*ALL)

 /*0112A*/  IF    COND(&YYMM  *LT &IFRSD) THEN(DO)
 /*0011A STRAT */

             RTVDTAARA  DTAARA(*LDA (201 6)) RTNVAR(&D#VNO1)
             RTVDTAARA  DTAARA(*LDA (211 6)) RTNVAR(&D#VNO2)

             CALL       PGM(ARS888P) PARM(&AREA &YYMM &D#VNO1 &D#VNO2)
 /*0011A  END */
 /*0112A*/  ENDDO
             ENDDO

             ENDPGM
