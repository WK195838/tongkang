     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARR147P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S00WCJ                                  */
     /*     4.FUNCTION     訂單別出貨統計表─交貨地點             */
     /*     5.DATE-WRITTEN  113/04/25 2024AR00015                   */
     /*     6.UPDATE  DATE                                          */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&INT)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             DCL        VAR(&IN03) TYPE(*LGL)  LEN(1)
             DCL        VAR(&DATS) TYPE(*CHAR) LEN(8)
             DCL        VAR(&DATE) TYPE(*CHAR) LEN(8)
             DCL        VAR(&CUNS) TYPE(*CHAR) LEN(6)
             DCL        VAR(&CUNE) TYPE(*CHAR) LEN(6)
             DCL        VAR(&CUNO) TYPE(*CHAR) LEN(6)
             DCL        VAR(&ORNS) TYPE(*CHAR) LEN(6)
             DCL        VAR(&ORNE) TYPE(*CHAR) LEN(6)
             DCL        VAR(&ORNO) TYPE(*CHAR) LEN(6)
             DCL        VAR(&MSMN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&AREA) TYPE(*CHAR) LEN(1)
             DCL        VAR(&INVT) TYPE(*CHAR) LEN(1)

             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT)
             IF         COND(&INT *EQ '0') THEN(GOTO BATCH)

             CALL       PGM(ARR147RS) PARM(&IN03)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)

             SBMJOB     JOB(ARR147P) JOBD(ARJOBD) OUTQ(&OUTQ) +
                          RQSDTA('CALL ARR147P')
             RETURN

 BATCH:      RTVDTAARA  DTAARA(*LDA (401 8))      RTNVAR(&DATS)
             RTVDTAARA  DTAARA(*LDA (411 8))      RTNVAR(&DATE)
             RTVDTAARA  DTAARA(*LDA (421 6))      RTNVAR(&CUNO)
             RTVDTAARA  DTAARA(*LDA (431 6))      RTNVAR(&ORNO)
             RTVDTAARA  DTAARA(*LDA (441 10))     RTNVAR(&MSMN)
             RTVDTAARA  DTAARA(*LDA (451 1))      RTNVAR(&AREA)
             RTVDTAARA  DTAARA(*LDA (452 1))      RTNVAR(&INVT)

             IF         COND(&CUNO *EQ '      ') THEN(DO)
                 CHGVAR      VAR(&CUNS)   VALUE('      ')
                 CHGVAR      VAR(&CUNE)   VALUE('999999')
             ENDDO

             IF         COND(&CUNO *NE '      ') THEN(DO)
                 CHGVAR      VAR(&CUNS)   VALUE(&CUNO)
                 CHGVAR      VAR(&CUNE)   VALUE(&CUNO)
             ENDDO

             IF         COND(&ORNO *EQ '      ') THEN(DO)
                 CHGVAR      VAR(&ORNS)   VALUE('      ')
                 CHGVAR      VAR(&ORNE)   VALUE('999999')
             ENDDO

             IF         COND(&ORNO *NE '      ') THEN(DO)
                 CHGVAR      VAR(&ORNS)   VALUE(&ORNO)
                 CHGVAR      VAR(&ORNE)   VALUE(&ORNO)
             ENDDO


             DLTF       FILE(QTEMP/ARR147F)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(ARR147F) FROMLIB(DALIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF0000)
             OVRDBF   FILE(ARR147F) TOFILE(QTEMP/ARR147F) SHARE(*YES)

 STEP1:                 /* 找出磅單 */
             OVRDBF   FILE(TRNDTL) TOFILE(TRNDTL) SHARE(*YES)
             OPNQRYF  FILE(TRNDTL) +
                        QRYSLT( +
                           'TXFLAG *NE "D" *AND +
                            TXCODE *EQ "SA04" *AND +
                            TXDATE *GE ' || &DATS || ' *AND +
                            TXDATE *LE ' || &DATE || ' *AND +
                            ORNO   *GE "' || &ORNS || '" *AND +
                            ORNO   *LE "' || &ORNE || '" *AND +
                            TXCUNO *GE "' || &CUNS || '" *AND +
                            TXCUNO *LE "' || &CUNE || '"  +
                                     ')     +
                        MAPFLD((ORNO   '%SST(TXORNO 1 6)')) +
                        KEYFLD((TXCUNO) (TXORNO) (TXDATE))

             CALL       PGM(ARR147R)
             CLOF       OPNID(TRNDTL)
             DLTOVR     *ALL


 STEP3:                 /* 列印報表 */
             OVRPRTF    FILE(ARR147T) TOFILE(ARR147T) +
                        PAGESIZE(*N 158) CPI(12) HOLD(*YES) +
                        USRDTA('訂貨交彙')
             OVRPRTF    FILE(ARR147T1) TOFILE(ARR147T1) +
                        PAGESIZE(*N 158) CPI(12) HOLD(*YES)   +
                        USRDTA('訂貨交明')

             OVRDBF   FILE(ARR147F) TOFILE(QTEMP/ARR147F) SHARE(*YES)

             CALL       PGM(ARR147R1)

             DLTOVR     FILE(*ALL)

             ENDPGM
