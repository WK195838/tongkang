     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARR117P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02CSF                                  */
     /*     4.FUNCTION     未過入財會營業退款明細表               */
     /*     5.DATE-WRITTEN  93/07/28                                */
     /*     6.UPDATE  DATE  99/05/06  2010AR476 S00WCJ (9905A)      */
     /*                     99/08/18  2010AR517 S00WCJ (9908A)      */
     /*                                                             */
     /***************************************************************/
             PGM
             DCLF       FILE(ARR117S)
             DCL        VAR(&INT)     TYPE(*CHAR)   LEN(1)
             DCL        VAR(&OUTQ)    TYPE(*CHAR)   LEN(10)
             DCL        VAR(&W#FLAG)  TYPE(*CHAR)   LEN(1)
    /*9908A  DCL        VAR(&W#DATE)  TYPE(*DEC)    LEN(6 0) */
    /*9908A*/DCL        VAR(&W#DATE)  TYPE(*DEC)    LEN(8 0)
    /*9908A  DCL        VAR(&W#UDATE) TYPE(*CHAR)   LEN(6)  */
    /*9908A*/DCL        VAR(&W#UDATE) TYPE(*CHAR)   LEN(8)
             DCL        VAR(&W#RTDT)  TYPE(*CHAR)   LEN(8)

             DCL        VAR(&P#PDAT) TYPE(*CHAR) LEN(8)  /*輸入日期*/
             DCL        VAR(&P#MODE) TYPE(*CHAR) LEN(1)  /*年份別*/
             DCL        VAR(&P#MTL)  TYPE(*CHAR) LEN(24) /*月日數*/
             DCL        VAR(&P#LEAP) TYPE(*CHAR) LEN(1)  /*閏年碼*/



             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT)
             IF         COND(&INT *EQ '0') THEN(GOTO BATCH)


             RTVDTAARA  DTAARA(*LDA ( 951 35)) RTNVAR(&S#COMP)
             RTVDTAARA  DTAARA(*LDA (1011 10)) RTNVAR(&S#DEVN)
    /*9908A  START */
          /* RTVSYSVAL  SYSVAL(QDATE) RTNVAR(&W#UDATE) */

             RTVJOBA    CYMDDATE(&W#UDATE)
             CHGVAR     VAR(&W#UDATE)  VALUE('0' *CAT &W#UDATE)
    /*9908A  END */
             CHGVAR     VAR(&S#RTDT)  VALUE(&W#UDATE)
 INPUT:
             SNDRCVF    RCDFMT(AR117F1)
             IF         COND(&IN03 *EQ '1' *OR &IN12 *EQ '1') +
                        THEN(RETURN)

             CHGVAR     VAR(&IN51) VALUE('0')
             CHGVAR     VAR(&IN52) VALUE('0')

             IF         COND(&S#RTDP *EQ ' ') THEN(DO)
                CHGVAR  VAR(&IN51) VALUE('1')
                CHGVAR  VAR(&S#ERR) VALUE('退款廠區不可空白')
                GOTO INPUT
             ENDDO

             CHGVAR     VAR(&P#PDAT) VALUE(&S#RTDT)
             CHGVAR     VAR(&P#MODE) VALUE('1')
             CALL       PGM(UTS102R) PARM(&P#PDAT &P#MODE +
                                          &P#MTL &P#LEAP &W#FLAG)
             IF         COND(&W#FLAG *NE '0') THEN(DO)
                CHGVAR  VAR(&IN52) VALUE('1')
                CHGVAR  VAR(&S#ERR) VALUE('日期格式錯誤！')
                 GOTO    INPUT
             ENDDO

             CHGVAR     VAR(&W#DATE)  VALUE(&W#UDATE)
             IF         COND(&S#RTDT *GT &W#DATE) THEN(DO)
                CHGVAR  VAR(&IN52) VALUE('1')
                CHGVAR  VAR(&S#ERR) VALUE('日期不可超過系統日期')
             ENDDO

             CHGDTAARA  DTAARA(*LDA (1 1)) VALUE(&S#RTDP)
             CHGVAR     VAR(&W#RTDT)       VALUE(&S#RTDT)
             CHGDTAARA  DTAARA(*LDA (2 8)) VALUE(&W#RTDT)
        /*   SBMJOB     JOB(ARR117P) JOBD(ARJOBD) OUTQ(&OUTQ) +
                        RQSDTA('CALL ARR117P')   */
    /*9905A  START */
       /*    CHGDTAARA  DTAARA(*LDA (801 70)) +
             VALUE('未過入財會營業退款明細表已匯入排程中。') */
             CHGDTAARA  DTAARA(*LDA (601 70)) +
             VALUE('未過入財會營業退款明細表已匯入排程中。')
    /*9905A  END */
        /*   RETURN   */


 BATCH:
             RTVDTAARA  DTAARA(*LDA (1 1)) RTNVAR(&S#RTDP)
             RTVDTAARA  DTAARA(*LDA (2 8)) RTNVAR(&W#RTDT)

                        /* 已確認未過入（且未列印者） */
             OVRPRTF    FILE(ARR117T) TOFILE(ARR117T) +
                        PAGESIZE(*N 132) CPI(10) +
                        OVRFLW(57) HOLD(*YES) USRDTA('未過已確')
             OVRDBF     FILE(ARRETN) TOFILE(DALIB/ARRETN) SHARE(*YES)
             OPNQRYF    FILE(ARRETN) +
                        OPTION(*ALL) +
                        QRYSLT( +
                         'ANFL02 *EQ " " *AND   +
                          ANFL03 *EQ " " *AND   +
                          ANFL01 *EQ "Y" *AND   +
                     %SST(ANRTNO 1 1) *EQ "' || &S#RTDP || '" *AND +
                          ANRTDT *EQ ' || &W#RTDT ) +
                        KEYFLD((ANRTDT) (ANRTNO))

             CALL       PGM(ARR117R)
             CLOF       OPNID(ARRETN)
             DLTOVR     FILE(*ALL)

             ENDPGM
