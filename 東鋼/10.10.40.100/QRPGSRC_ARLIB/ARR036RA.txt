     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR036RA
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     未開立發票明細表（先期處理）
     H*            5.DATE-WRITTEN  87/07/30
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTL  IP  E           K        DISK
     FTRNDTLW O   E           K        DISK
     F            TXREC                             KRENAMETXW
     FARCUCT  IF  E           K        DISK
     FARODCT  IF  E           K        DISK
     I*****************************************************************
     I            DS
     I                                        1  10 TXIVNO
     I                                        1   1 D#TEMP
     C*****************************************************************
     C*        搜尋關鍵
     C*****************************************************************
     C           K#A1      KLIST
     C                     KFLD           A1CUNO
     C                     KFLD           A1CTKD
     C*
     C           K#A2      KLIST
     C                     KFLD           A2ORNO
     C                     KFLD           A2CTKD
     C*
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C                     EXSR SR1000
     C                     MOVELW#MTHD    TXFLAG
     C*
     C           D#TEMP    IFEQ '*'                        暫不開立
     C                     MOVEL'6'       TXFLAG           另行印出
     C                     ENDIF
     C*
     C                     WRITETXW
     C*
     C*==========================================
     C*  客戶訂單管控資料查詢發票之開立方式
     C*==========================================
     CSR         SR1000    BEGSR
     C                     MOVEL'0'       W#MTHD  1        開立方式
     C*
     C                     MOVELTXCUNO    A1CUNO
     C                     MOVEL'04'      A1CTKD
     C           K#A1      CHAINRARCUCT              69
     C           *IN69     IFEQ '0'
     C                     MOVE A1MTHD    W#MTHD
     C           A1RFOR    IFEQ ' '                        採用客戶
     C                     GOTO ES1000
     C                     ENDIF
     C                     ENDIF
     C*
     C                     MOVELTXORNO    A2ORNO
     C                     MOVEL'04'      A2CTKD
     C           K#A2      CHAINRARODCT              69
     C           *IN69     IFEQ '0'
     C                     MOVE A2MTHD    W#MTHD           取1,2..5
     C                     ENDIF
     CSR         ES1000    ENDSR
     C*
