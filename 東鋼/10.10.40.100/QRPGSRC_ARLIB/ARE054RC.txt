     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARE054RC
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     營業退款輸入作業（依日期排序）
     H*            5.DATE-WRITTEN  94/05/25
     H*            6.DATE-MODIFY  101/09/19  2012AR682  S00WCJ (0109A)
     H*
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARE054SCCF  E                    WORKSTN
     F                                        RRN1  KSFILE AR054F1
     F                                        RRN2  KSFILE AR054F2
     FARRETNL1IF  E           K        DISK
     F            RARRETN                           KRENAMEARETNL1
     FARRETN  IF  E           K        DISK
     FCBCUST  IF  E           K        DISK
     E*************************************************************
     E                    ERR     1   1 70
     I*************************************************************
     IARETNL1
     I              ANRTNO                          F#RTNO
     I              ANITEM                          F#ITEM
     I              ANCUNO                          F#CUNO
     I              ANCUNM                          F#CUNM
     I              ANORNO                          F#ORNO
     I              ANAREA                          F#AREA
     I              ANDECD                          F#DECD
     I              ANDEDT                          F#DEDT
     I              ANDC                            F#DC
     I              ANACNO                          F#ACNO
     I              ANDPNO                          F#DPNO
     I              ANRLNO                          F#RLNO
     I              ANDUDT                          F#DUDT
     I              ANCSNM                          F#CSNM
     I              ANDSC1                          F#DSC1
     I              ANDSC2                          F#DSC2
     I              ANRTUS                          F#RTUS
     I              ANRTDT                          F#RTDT
     I              ANRTTM                          F#RTTM
     I              ANAAMT                          F#AAMT
     I              ANFL01                          F#FL01
     I              ANCFUS                          F#CFUS
     I              ANCFDT                          F#CFDT
     I              ANFL02                          F#FL02
     I              ANFL03                          F#FL03
     I              ANTRUS                          F#TRUS
     I              ANTRDT                          F#TRDT
     I              ANGLNO                          F#GLNO
     I              ANRESV                          F#RESV
     I*
     I           UDS
     I                                     10111020 S#DEVN
     I                                     10011003 U#USDP
     I                                     10011010 S#USID
     I                                     10211021 U#AREA
     I            DS
     I                                        1   6 D#RTNO
     I                                        1   1 D#RTN1
     I                                        2   60D#RTN2
0109AIIFRS       UDS                             50
0109AI                                        1   80D#IFRS
     C**************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P#IN12  1
     C**************************************************************
     C*   檔案搜尋欄位組合
     C**************************************************************
     C*FILE==>ARRETN(營業退款檔)
     C           K#RETN    KLIST
     C                     KFLD           ANRTNO
     C                     KFLD           ANITEM
     C**************************************************************
     C*   主程式開始
     C**************************************************************
0109AC           *DATE     SUB  19000000  U#SYSD  80
0109AC           *NAMVAR   DEFN ARIFRSCTL IFRS
0109AC           U#SYSD    IFLT D#IFRS
0109AC                     MOVEL*ON       *IN77
0109AC                     ELSE
0109AC                     MOVEL*OFF      *IN77
0109AC                     ENDIF
0109AC                     UNLCKIFRS
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C           W#PRID    CASEQ'02'      SR2000            READC
     C           W#PRID    CASEQ'03'      SR3000           畫面三
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C           SR0000    BEGSR
     C**************************************************************
     C                     MOVEL'01'      W#PRID  2        函式代號
     C                     Z-ADD0         S#KEY1
     C                     MOVE *ALL'='   S#LIN1
     C                     MOVE *ALL'='   S#LIN2
     C                     MOVE *ALL'='   S#LIN3
     C                     MOVE *ALL'='   S#LIN4
     C                     MOVE *ALL'='   S#LIN5
     C                     MOVE *ALL'='   S#LIN6
     C                     MOVE *OFF      *IN99
     C*
     C                     ENDSR
     C**************************************************************
     C           SR1000    BEGSR
     C**************************************************************
     C*W#PRID='01'--->ARR054S-1 畫面
     C*
     C                     Z-SUB11        S#NBR1
     C                     Z-ADD0         RRN1    50
     C*
     C* CLEAR SUBFILE
     C                     MOVE *ON       *IN73            SFLCLR
     C                     WRITEAR054F1C
     C                     MOVE *OFF      *IN73
     C*
     C*                    MOVE '0'       *IN,65
     C           S#KEY1    SETLLARETNL1
     C                     EXSR SR1100
     C*
     C           RRN1      IFEQ 0                          SFLDSP OFF
     C                     MOVE *ON       *IN72
     C                     MOVELERR,1     S#ERR1
     C                     ELSE
     C                     MOVE *OFF      *IN72
     C                     ENDIF
     C*
     C                     Z-ADD0         S#KEY1
     C*顯示畫面
     C           W#PRID    DOWEQ'01'
     C*
     C                     WRITEAR054H
     C                     WRITEAR054F1M
     C                     EXFMTAR054F1C
     C                     MOVEL*BLANK    S#ERR1
     C*結束作業
     C*
     C           *IN03     IFEQ *ON
     C                     MOVE '00'      W#PRID
     C                     MOVEL'0'       P#IN12
     C                     LEAVE
     C                     ENDIF
     C*回上頁
     C           *IN12     IFEQ *ON
     C                     MOVE '00'      W#PRID
     C                     MOVEL'1'       P#IN12
     C                     LEAVE
     C                     ENDIF
     C*
     C*REPOSITION
     C           S#KEY1    IFNE 0
     C                     LEAVE
     C                     ENDIF
     C*READC
     C                     MOVE '02'      W#PRID
     C*
     C                     ENDDO
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         SR1100    BEGSR                           讀入資料
     C**************************************************************
     C                     MOVE *BLANK    W#ORNO  6
     C                     MOVE *OFF      *IN40
     C           *IN40     DOWEQ*OFF
     C                     READ ARETNL1                  40
     C   40                LEAVE
     C*
     C           F#DC      IFEQ 'D'                        只要一筆
     C           F#DECD    OREQ 'D'                        已作廢
     C           F#ITEM    IFEQ 1
     C                     MOVELF#ORNO    W#ORNO
     C                     ENDIF
     C                     ITER
     C                     ENDIF
     C*
     C                     CLEARAR054F1
     C*
     C                     MOVELW#ORNO    S#ORNO           訂單編號
     C                     Z-ADDF#RTDT    S#RTDT           退款日期
     C                     MOVELF#RTNO    S#RTNO           退款編號
     C                     MOVELF#CUNO    S#CUNO           客戶編號
     C                     MOVELF#CUNM    S#CUNM           客戶名稱
     C                     Z-ADDF#AAMT    S#AAMT           退款金額
     C                     MOVELF#FL01    S#FL01           營業確認碼
     C                     MOVELF#FL02    S#FL02           列印碼
     C                     MOVELF#FL03    S#FL03           過入碼
     C*
     C                     ADD  1         RRN1
     C                     WRITEAR054F1
     C*
     C                     ENDDO
     C*
     C           RRN1      IFGT 0
     C                     ADD  12        S#NBR1
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           SR2000    BEGSR
     C*****************************************************************
     C*READC-->AR054F1
     C                     Z-ADD1         RRN1
     C                     MOVE *OFF      *IN99
     C*
     C           W#PRID    DOWEQ'02'
     C*
     C                     READCAR054F1                  63
     C   63                MOVE '01'      W#PRID
     C   63                LEAVE
     C*
     C           S#OPT1    IFEQ *BLANK
     C                     ITER
     C                     ENDIF
     C*
     C           S#CRN1    IFNE 0
     C                     Z-ADDS#CRN1    S#NBR1
     C                     ENDIF
     C*
     C           S#OPT1    IFEQ '5'
     C                     MOVEL'03'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C**************************************************************
     C           SR3000    BEGSR
     C**************************************************************
     C*W#PRID='03'--->ARE054S-2 畫面
     C*
     C                     Z-SUB4         S#NBR2
     C                     Z-ADD0         RRN2    50
     C*
     C* CLEAR SUBFILE
     C                     MOVE *ON       *IN83            SFLCLR
     C                     WRITEAR054F2C
     C                     MOVE *OFF      *IN83
     C*
     C                     MOVE '查詢'  S#SF1T
     C*
     C                     EXSR SR3100                     讀入檔頭
     C                     EXSR SR3200                     初始SFL
     C*顯示畫面
     C           W#PRID    DOWEQ'03'
     C*
     C                     WRITEAR054H
     C                     WRITEAR054F2M
     C                     EXFMTAR054F2C
     C                     MOVEL*BLANK    S#ERR2
     C*結束作業
     C           *IN03     IFEQ *ON
     C                     MOVEL'00'      W#PRID
     C                     MOVEL'0'       P#IN12
     C                     LEAVE
     C                     ENDIF
     C*回上頁
     C           *IN12     IFEQ *ON
     C                     MOVE '01'      W#PRID
     C                     MOVE 'X'       W#FLAG  1
     C                     LEAVE
     C                     ENDIF
     C*說明
     C           *IN01     IFEQ *ON
0109AC   77                EXFMTAR054F2W
0109AC  N77                EXFMTAR054F3W
     C                     ITER
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     CSR                   ENDSR
     C**************************************************************
     C           SR3100    BEGSR
     C**************************************************************
     C                     MOVELS#RTNO    ANRTNO
     C           ANRTNO    CHAINRARRETN              42
     C           *IN42     IFEQ *OFF
     C                     MOVELANRTDT    S#RTDT           退款日期
     C                     MOVELANRTNO    S#RTNO           退款單號
     C                     MOVELANDPNO    S#DPNO           退款部門
     C                     MOVELANCUNO    S#CUNO           客戶名稱
     C                     MOVELANSAID    S#SAID           領款人
     C           ANCUNO    CHAINCBREC                43
     C  N43                MOVELCBFNAM    S#FNAM           客戶代號
     C                     ENDIF
     C*
     C                     ENDSR
     C**************************************************************
     C           SR3200    BEGSR
     C**************************************************************
     C                     Z-ADD0         S#OAMT
     C           RRN2      DOWLT5
     C                     ADD  1         RRN2
     C                     MOVE S#RTNO    ANRTNO           退款單號
     C                     Z-ADDRRN2      ANITEM           退款項次
     C           K#RETN    CHAINRARRETN             N44
     C           *IN44     IFEQ *OFF                       新增或沒有
     C                     MOVELANDC      S#DC             借貸別
     C                     MOVELANUSTP    S#USTP           款項別
     C                     MOVELANACNO    S#ACNO           會計科目
     C                     MOVELANRLNO    S#RLNO           相關號碼
     C                     Z-ADDANDUDT    S#DUDT           到期日期
     C                     MOVELANCSNM    S#CSNM           對象別
     C                     Z-ADDANAAMT    S#AAMT           退款金額
     C                     MOVELANDSC1    S#DSC1           摘要
     C                     MOVELANDSC2    S#DSC2           輔助摘要
     C           ANDC      IFEQ 'C'
     C                     ADD  ANAAMT    S#OAMT           退款金額
     C                     ENDIF
     C                     ELSE
     C                     CLEARAR054F2
     C                     ENDIF
     C*
     C                     Z-ADDRRN2      S#SFI2           項次
     C*
     C                     WRITEAR054F2
     C*
     C                     ENDDO
     C*
     C                     Z-ADD1         S#NBR2            SFLRCDNBR
     C*
     C                     ENDSR
     C**************************************************************
** ERR
０１－無資料！
