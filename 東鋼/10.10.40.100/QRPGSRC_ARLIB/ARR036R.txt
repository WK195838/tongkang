     H        1   Y                                     1                 BR003
     FTRNDTL  IP  E                    DISK
     FQPRINT  O   F     132            PRINTER
     E*************************************************************
     E                    T#TXT   1   7 16
     I*************************************************************
     ITXREC
     I                                              TXFL01L5
     I                                              TXFLAGL4
     I                                              TXCUNOL3
     I                                              TXPCNOL2
     I                                              TXNO  L1
     I****
     IAADS        DS
     I              'も未開立發票明細表-C         CON3                       X
     I              'も　　　'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     I           UDS
     I                                        1   1 AREAF
     I                                        2   2 AREAT
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L4                EXSR DL#L4
     C   L3                EXSR DL#L3
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     CL3                   EXSR TL#L3
     CL4                   EXSR TL#L4
     CLR                   EXSR TL#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
      *
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD55        TCNT    30
     C                     Z-ADD0         QTY5    80
     C                     Z-ADD0         AMT5   100
     C                     Z-ADDTCNT      LCNT
     C                     MOVE *ALL'='   ALS   132
     C                     MOVE *ALL'-'   ALQ   132
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD8         LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L4                             **
     C**----------------------------------------------**
     CSR         DL#L4     BEGSR
     C                     Z-ADD0         QTY4    70
     C                     Z-ADD0         AMT4    90
     C                     Z-ADDTCNT      LCNT
     C                     SELEC
     C           TXFL01    WHEQ 'P'
     C                     MOVEL'台北'  AREAC   6
     C           TXFL01    WHEQ 'T'
     C                     MOVEL'桃園'  AREAC   6
     C           TXFL01    WHEQ 'K'
     C                     MOVEL'高雄'  AREAC   6
     C           TXFL01    WHEQ 'L'
     C                     MOVEL'林口'  AREAC   6
     C           TXFL01    WHEQ 'M'
     C                     MOVEL'台中'  AREAC   6
     C           TXFL01    WHEQ 'H'
     C                     MOVEL'苗栗'  AREAC   6
     C                     OTHER
     C                     MOVEL'　　'  AREAC
     C                     ENDSL
     C*
     C                     MOVE TXFLAG    I       10
     C                     ADD  1         I
     C                     MOVELT#TXT,I   CTTYPE 16
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L3                             **
     C**----------------------------------------------**
     CSR         DL#L3     BEGSR
     C                     Z-ADD0         QTY3    70
     C                     Z-ADD0         AMT3    90
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         QTY2    70
     C                     Z-ADD0         AMT2    90
     CSR                   ENDSR
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         QTY1    70
     C                     Z-ADD0         AMT1    90
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     Z-ADDTXAMT     RAMT    90
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#01
     C                     ADD  TXQTY     QTY1
     C                     ADD  RAMT      AMT1
     C**
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C                     EXCPTET#L1
     C                     ADD  QTY1      QTY2
     C                     ADD  AMT1      AMT2
     C                     ADD  1         LCNT
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C                     EXCPTET#L2
     C                     ADD  QTY2      QTY3
     C                     ADD  AMT2      AMT3
     C                     ADD  1         LCNT
     CSR         TL#L2Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L3     BEGSR
     C                     EXCPTET#L3
     C                     ADD  QTY3      QTY4
     C                     ADD  AMT3      AMT4
     C                     ADD  1         LCNT
     CSR         TL#L3Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L4     BEGSR
     C                     EXCPTET#L4
     C                     ADD  QTY4      QTY5
     C                     ADD  AMT4      AMT5
     CSR         TL#L4Z    ENDSR
     C******
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     CSR         TL#LRZ    ENDSR
     OQPRINT  E   02           EH#01
     O                                   17 '報表編號: AR036'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
     O                         UDATE Y  132
     O        E 1              EH#01
     O                         CON3      79                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 2              EH#01
     O                                   11 '　地區別:'
     O                         TXFL01  +  1
     O                         AREAC   +  1
     O                         CTTYPE    70
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              EH#01
     O                                   10 '客戶編號'
     O                                   20 '客戶名稱'
     O                                   30 '訂單號碼'
     O                                   40 '磅單號碼'
     O                                   50 '出貨日期'
     O                                   60 '品　名'
     O                                   70 '單　價'
     O                                   80 '數　量'
     O                                   95 '金　額'
     O                                  105 '傳票號碼'
     O                                  115 '業務員'
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              ED#01
     O                 75      TXCUNO     8
     O                 L3      TXCUNO     8
     O                 75      TXCUNM    20
     O                 L3      TXCUNM    20
     O                 75      TXPCNO    28
     O                 L2      TXPCNO    28
     O                 75      TXNO      39
     O                 L1      TXNO      39
     O                 75      TXDATE    49 '  /  /  '
     O                 L1      TXDATE    49 '  /  /  '
     O                         TXPDNM    57
     O                         TXUPRCN   69
     O                         TXQTY N   79
     O                         RAMT  N   94
     O                         TXVUNO   103
     O                         TXSALE   113
     O        E 1              ET#L1
     O                                   67 '磅單小計：'
     O                         QTY1  N   79
     O                         AMT1  N   94
     O        E 1              ET#L2
     O                                   67 '訂單合計：'
     O                         QTY2  N   79
     O                         AMT2  N   94
     O        E 1              ET#L3
     O                                   67 '客戶合計：'
     O                         QTY3  N   79
     O                         AMT3  N   94
     O        E 1              ET#L4
     O                                   67 '廠區合計：'
     O                         QTY4  N   79
     O                         AMT4  N   94
     O        E 1              ET#LR
     O                                   67 '　總合計：'
     O                         QTY5  N   79
     O                         AMT5  N   94
** T#TXT
每日隨車開立類
每日隨訂開立類
每月隨訂開立類
區間隨訂開立類
指定銷貨開立類
人工發票開立類
發票暫不開立類
