     H*****************************************************************
     H*
     H*            *****   PROGRAM INTRODUCTION   *****
     H*
     H*            1.PROGRAM-ID    ARP084R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02YSH
     H*            4.FUNCTION     承購發票預支試算作業--輸入
     H*            5.DATE-WRITTEN  90/11/10
     H*            6.UPDATE
     H*                            99/08/31  2010AR517  S00WCJ (9908A)
     H*****************************************************************
     H        1   Y                                     1
     FARP084SACF  E                    WORKSTN
     FARBANK  IF  E           K        DISK
     E                    ERR     1   3 70               *錯誤訊息
     I           UDS
     I                                      301 306 D#DATE
     I                                      307 309 D#BKID
     I                                      310 321 D#PAMT
     I                                      322 327 D#PRAT
     I                                     10011010 S#USER
     I                                     10111020 S#DEVI
     C*****************************************************************
     C*          Key       List
     C*****************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P#IN03  1
     C**************************************************************
     C*                  MAIN  PROGRAM
     C**************************************************************
9908AC           *DATE     SUB  19000000  U#SYSD  80
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C           SR0000    BEGSR
     C**************************************************************
9908AC                     MOVE U#SYSD    S#DATE
     C                     MOVE '01'      W#PRID  2
     C                     ENDSR
     C**************************************************************
     C           SR1000    BEGSR
     C**************************************************************
     C           W#PRID    DOWEQ'01'
     C                     EXFMTAR084F1
     C                     MOVEA'000'     *IN,30
     C                     MOVE *OFF      *IN99
     C                     MOVE *BLANK    S#ERR
     C*
     C           *IN03     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     MOVEL'1'       P#IN03
     C                     MOVEL'00'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*
     C                     EXSR SR1100                     畫面檢核
     C*
     C  N99                MOVELERR,3     S#ERR
     C  N99      *IN10     IFEQ *ON
     C                     EXSR SR1200                     紀錄資料
     C                     MOVEL'00'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C                     ENDSR
     C**************************************************************
     C           SR1100    BEGSR
     C**************************************************************
     C*資料檢核
     C           S#BKID    IFEQ *BLANK
     C                     MOVELERR,1     S#ERR
     C                     SETON                     3099
     C                     ENDIF
     C*
     C           S#BKID    CHAINRARBANK              40
     C  N99      *IN40     IFEQ *ON
     C                     MOVELERR,2     S#ERR
     C                     SETON                     3099
     C                     ENDIF
     C*
     C  N99      S#PAMT    IFEQ 0
     C                     MOVELERR,1     S#ERR
     C                     SETON                     3199
     C                     ENDIF
     C*
     C  N99      S#PRAT    IFEQ 0
     C                     MOVELERR,1     S#ERR
     C                     SETON                     3299
     C                     ENDIF
     C*
     C*
     C                     ENDSR
     C**************************************************************
     C           SR1200    BEGSR
     C**************************************************************
     C*紀錄資料
     C                     MOVE S#DATE    D#DATE
     C                     MOVE S#BKID    D#BKID
     C                     MOVE S#PAMT    D#PAMT
     C           S#PRAT    MULT 1000      W#PRAT  60
     C                     MOVE W#PRAT    D#PRAT
     C*
     C                     ENDSR
**  ERR
01-欄位值不得空白或為零！
02-銀行代號未建檔！             　
03-請按下Ｆ１０確認試算！
