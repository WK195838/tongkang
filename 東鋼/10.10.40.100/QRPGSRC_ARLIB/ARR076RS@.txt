     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR076RS
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     銷貨帳款帳齡分析彙總表（畫面）
     H*            5.DATE-WRITTEN  88/06/11
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     F*************************************************************
     FARR076S CF  E                    WORKSTN
     E*************************************************************
     E                    T#MSG   1   2 70
     I*************************************************************
     IAXDTA      UDS                             28
     I                                        1   4 X#DAT1
     I                                        5   8 X#DAT2
     I                                        9  12 X#DAT3
     I                                       21  28 X#ETXD
     I           UDS
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 S#USID
     I                                     10211021 U#AREA
     C**************************************************************
     C*   程式參數接收區
     C**************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P#IN03  1        結束旗標
     C**************************************************************
     C*   主程式開始
     C**************************************************************
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C*   副程式開始
     C**************************************************************
     C*----------------------------------------
     C*  宣告及初始變數
     C*----------------------------------------
     CSR         SR0000    BEGSR
     C           *NAMVAR   DEFN ARXCTLDTA AXDTA  28
     C                     MOVE X#DAT1    S#DAT1
     C                     MOVE X#DAT2    S#DAT2
     C                     MOVE X#DAT3    S#DAT3
     C                     MOVE X#ETXD    S#ETXD
     C*
     C                     MOVEL'01'      W#PRID  2
     CSR                   ENDSR
     C*
     C************************************
     C*  畫面一:
     C************************************
     CSR         SR1000    BEGSR
     C                     EXFMTAR076F1
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C           *IN12     OREQ '1'
     C                     MOVEL'1'       P#IN03
     C                     MOVEL'00'      W#PRID
     C*
     C                     OTHER                           執行鍵
     C           S#DAT2    IFLE S#DAT1
     C                     MOVELT#MSG,1   S#MSG1
     C                     GOTO ES1000
     C                     ENDIF
     C           S#DAT3    IFLE S#DAT2
     C                     MOVELT#MSG,2   S#MSG1
     C                     GOTO ES1000
     C                     ENDIF
     C*
     C                     MOVEL'0'       P#IN03
     C                     MOVE S#DAT1    X#DAT1
     C                     MOVE S#DAT2    X#DAT2
     C                     MOVE S#DAT3    X#DAT3
     C           S#ETXD    IFEQ 0
     C                     MOVEL*ALL'0'   X#ETXD
     C                     MOVE UDATE     X#ETXD
     C                     ELSE
     C                     MOVE S#ETXD    X#ETXD
     C                     ENDIF
     C                     MOVEL'00'      W#PRID
     C                     ENDSL
     C*
     CSR         ES1000    ENDSR
     C**************************************************************
** T#MSG
０１列入逾期之日數必須大於列入領款之日數。
０２列入催收之日數必須大於列入逾期之日數。
