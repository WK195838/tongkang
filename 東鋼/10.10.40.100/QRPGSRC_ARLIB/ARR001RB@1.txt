     H        1   Y                                     1                 BR003
     FINVMSTL1IF  E           K        DISK
     FTRNDTLL3IF  E           K        DISK
     FINVDTL  IF  E           K        DISK
     FHSFXMD  IF  E           K        DISK
     FARR001PFIP  E           K        DISK
     FQPRINT  O   F     106            PRINTER
     E                    DTA     1   1 14
     I*************************************************************
     I              '一．承蒙　貴客戶惠-C         CON1
     I              '顧，不勝感激，玆檢-
     I              '送　貴客戶本期應付-
     I              '帳款明細、出貨單（-
     I              '磅單）、及發票（如-
     I              '附）'
     I              '敬請查收並惠予核查-C         CON2
     I              '，若發現帳款內容與-
     I              '實際不符時，請即連-
     I              '絡本公司營業部經辦-
     I              '人員予以更正．'
     I              '二．核查無誤後，本-C         CON3
     I              '公司業務員將依貴我-
     I              '雙方約定之領款日（-
     I              '　年　月　日）前往-
     I              '收款．'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     I              '三．本公司帳款處理-C         CON5
     I              '均已電腦化，敬請貴-
     I              '客戶依本帳單所列金-
     I              '額核實付款．'
     I            DS
     I                                        1  15 FMNBR
     I                                        1   2 FMNBR1
     I                                        3   3 FMNBR2
     I                                        4   6 FMNBR3
     I                                        7  10 FMNBR4
     I                                       11  15 FMNBR5
     I            DS
     I                                        1 100 FAXDTA
     I                                        1   3 DSTART
     I                                        4  18 DTOTP
     I                                       19  32 DTOMN
     I                                       33  33 DPGWD
     I                                       34  35 DKIND
     I                                       36  43 DPPNB
     I                                       44  51 DBGNM
     I                                       52  59 DEDNM
     I                                       60  60 DWDTP
     I                                       61 100 DMARK
     I                                      100 100 DEND
     I                                      101 115 DFMTP
     I                                      116 129 DFMMN
     I                                      130 130 PGWD
     I                                      131 1320DSQNB
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           INKEY     KLIST
     C                     KFLD           INNO             *發票號碼
     C*發票主檔
     C           IN1KEY    KLIST
     C                     KFLD           INCUNO           *客戶編號
     C                     KFLD           INAPNO           *請款單號
     C                     KFLD           ININDT           *發票日期
     C                     KFLD           INNO             *發票號碼
     C*
     C           IN2KEY    KLIST
     C                     KFLD           INCUNO
     C                     KFLD           INAPNO
     C*銷貨明細檔
     C           TXKEY     KLIST
     C                     KFLD           TXIVNO           *發票號碼
     C*發票明細檔
     C           IVKEY     KLIST
     C                     KFLD           IVNO             *發票號碼
     C                     KFLD           IVACNT           *類別
     C                     KFLD           IVITEM           *項次
     C*FILE => HSFXMD
     C           KFXMD     KLIST
     C                     KFLD           KFXMD1  6        *客戶編號
     C                     KFLD           KFXMD2  20       *序號
     C*FILE => ARR001PF
     C           K#ARPF    KLIST
     C                     KFLD           ARCUNO           *客戶編號
     C                     KFLD           ARAPNO           *請款單號
     C**************************************************************
     C*          *ENTRY    PLIST
     C*                    PARM           CUNO    6
     C*                    PARM           APNO    6
     C*****************************************************************
     C*          Main      Program
     C*****************************************************************
     C                     EXSR INISR
     C*
     C                     EXSR DTLSR
     CLR                   RETRN
     C**************************************************************
     C           INISR     BEGSR
     C**************************************************************
     C* SET INITIAL
     C                     Z-ADD0         PAG     30
     C                     Z-ADD21        LCNT    20
     C                     Z-ADD20        TCNT    20
     C                     Z-ADD50        FCNT    20
     C                     Z-ADD0         CNT
     C                     Z-ADD0         QTYT    70
     C                     Z-ADD0         AAMTT   80
     C                     Z-ADD0         BAMTT   70
     C                     Z-ADD0         CAMTT   80
     C                     Z-ADD0         DAMTT   70
     C                     Z-ADD0         EAMTT   80
     C                     Z-ADD0         TTLAMT  80
     C                     MOVE *ALL'='   ALS    98
     C                     SETOF                     22
     C                     SETOF                     23
     C*
     C                     ENDSR
     C**************************************************************
     C           DTLSR     BEGSR
     C**************************************************************
     C                     EXSR CKFXMD
     C                     EXSR MVAREA
     C                     EXCPTFX#01
     C                     EXSR DP#01
     C*
     C                     ENDSR
     C**************************************************************
     C           MVAREA    BEGSR
     C**************************************************************
     C                     MOVE DTOTP     TOTP   15
     C                     MOVE DTOMN     TOMN   14
     C                     MOVE DFMTP     FMTP   15
     C                     MOVE DFMMN     FMMN   14
     C                     MOVE DPPNB     PPNB    8
     C                     MOVE DMARK     MARK   40
     C*
     C                     ENDSR
     C*****************************************************************
     C           CKFXMD    BEGSR
     C*****************************************************************
     C                     MOVE ARCUNO    KFXMD1
     C                     Z-ADD1         KFXMD2
     C           KFXMD     CHAINRHSFXMD              61
     C           *IN61     IFEQ '0'
     C                     MOVE '#$%'     DSTART  3
     C                     MOVE '%$#'     PFXEND  3
     C                     MOVE '02'      FMNBR1
     C                     MOVE '-'       FMNBR2
     C                     MOVE '562'     FMNBR3
     C                     MOVE '6620'    FMNBR4
     C                     MOVE *BLANK    FMNBR5
     C                     MOVE FMNBR     FMTP
     C                     MOVE HXTELP    DTOTP
     C                     Z-ADDHXSQNB    DSQNB
     C                     MOVE HXRVMN    DTOMN
     C                     MOVE HXPPNB    DPPNB
     C                     MOVE HXBGNM    DBGNM
     C                     MOVE HXEDNM    DEDNM
     C                     MOVE HXMARK    DMARK
     C                     MOVE '1'       DPGWD            *  80
     C                     MOVE '1'       DEND
     C                     MOVE HXKIND    DKIND
     C                     MOVE HXWDTP    DWDTP
     C                     MOVE FMTP      DFMTP
     C                     MOVE DTA,1     DFMMN
     C                     ELSE
     C                     MOVE *BLANK    FAXDTA
     C                     MOVE 'ERR'     DSTART
     C                     MOVE *BLANK    PFXEND
     C                     SETON                     61
     C                     END
     C*
     C                     ENDSR
     C**************************************************************
     CSR         DP#01     BEGSR
     C**************************************************************
     C                     MOVE ARCUNO    INCUNO           *客戶編號
     C                     MOVE ARAPNO    INAPNO           *請款單號
     C                     Z-ADD0         ININDT           *發票日期
     C                     MOVE *BLANK    INNO             *發票號碼
     C           IN1KEY    SETLLINVMSTL1             97
     C  N97      IN2KEY    READEINVMSTL1                 97
     C           *IN97     DOWEQ'0'
     C           INDECD    IFNE 'D'                        *作廢碼
     C                     MOVELINNO      INVNO  10        *發票號碼
9008 C                     MOVELINORNO    ORNO    6        *訂單號碼
LYW  C                     Z-ADDININDT    DATE    60       *發票日期
     C                     Z-ADDINAAMT    AAMT    80       *出貨金額
     C                     Z-ADDINCAMT    BAMT    70       *折讓銷退
     C                     Z-ADDINBAMT    CAMT    80       *扣預收款
     C                     ADD  INDAMT    CAMT             *
     C                     Z-ADDINATAX    DAMT    70
     C                     ADD  INCTAX    DAMT
     C                     ADD  INDTAX    DAMT
     C                     Z-ADDINEAMT    EAMT    80
     C                     ADD  INFAMT    EAMT
     C                     EXSR @WEI
     C                     EXSR @GET
     C                     Z-ADDAAMT      NBAL    80
     C                     ADD  BAMT      NBAL
     C                     ADD  CAMT      NBAL
     C                     ADD  DAMT      NBAL
     C                     ADD  EAMT      NBAL
     C                     ADD  NBAL      TTLAMT  80
     C                     ADD  AAMT      AAMTT   80
     C                     ADD  BAMT      BAMTT   70
     C                     ADD  CAMT      CAMTT   80
     C                     ADD  DAMT      DAMTT   70
     C                     ADD  EAMT      EAMTT   80
     C                     EXSR PR#01
     C                     ENDIF
     C           IN2KEY    READEINVMSTL1                 97
     C                     ENDDO
     C*
     C                     EXCPTET#01
     C*
     C                     SETON                     23
     C                     EXCPTEF#01
     C                     EXCPTFX#02
     C*
     CSR         DP#01Z    ENDSR
     C**************************************************************
     CSR         PR#01     BEGSR
     C**************************************************************
     C           LCNT      COMP TCNT                 75  75
     C   75 22             EXCPTEF#01
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD0         LCNT
     C  N22                SETON                     22
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     C                     ADD  1         CNT     30
     C  N22                SETON                     22
     C*
     CSR         PR#01Z    ENDSR
     C**************************************************************
     C* GET DISCOUNT AMT
     CSR         @GET      BEGSR
     C**************************************************************
     C                     MOVELINNO      TXIVNO
     C           TXKEY     SETLLTRNDTLL3             97
     C           *IN97     DOWEQ'0'
     C           TXIVNO    READETRNDTLL3                 97
     C   97                LEAVE
     C           TXFL01    IFEQ 'Y'
     C           TXFLAG    OREQ 'D'
     C                     ITER
     C                     ENDIF
     C           INTYPE    IFEQ '2'
     C                     SUB  TXAMT     BAMT
     C                     ELSE
     C           TXACNT    IFNE '4'
     C                     SUB  TXAMT     BAMT
     C                     ELSE
     C                     SUB  TXAMT     CAMT
     C                     ENDIF
     C                     ENDIF
     C                     SUB  TXTAX     DAMT
     C                     ENDDO
     CSR         @GETZ     ENDSR
     C**************************************************************
     C*GET WEIGHT
     CSR         @WEI      BEGSR
     C**************************************************************
     C                     Z-ADD0         QTY     70
     C                     MOVE *BLANK    PNO     8
     C                     MOVELINNO      IVNO
     C                     MOVEL*BLANK    IVACNT
     C                     Z-ADD0         IVITEM
     C           IVKEY     SETLLINVDTL               97
     C           *IN97     DOWEQ'0'
     C           IVNO      READEINVDTL                   97
     C   97                LEAVE
     C           IVACNT    IFNE '1'
     C                     LEAVE
     C                     ENDIF
     C           IVFLAG    IFEQ 'D'
     C           IVDECD    OREQ 'D'
     C                     ITER
     C                     ENDIF
     C                     MOVELIVAPNO    PNO
     C                     ADD  IVQTY     QTY
     C                     ENDDO
     C                     ADD  QTY       QTYT
     C*
     CSR         @WEIZ     ENDSR
     C**************************************************************
     OQPRINT  E   02           FX#01
     O                         FAXDTA   100
     O        E 1              FX#01
     O                                   15 '收件人號碼:'
     O                         TOTP      30
     O                                   52 '收件人姓名:'
     O                         TOMN      66
     O        E 1              FX#01
     O                                   15 '送件人號碼:'
     O                         FMTP      30
     O                                   52 '送件人姓名:'
     O                         FMMN      66
     O        E 1              FX#01
     O                                   13 '文件編號:'
     O                         PPNB      21
     O                                   34 '文件備註:'
     O                         MARK      75
     O*QPRINT  E   02           EH#01
     O        E 2              EH#01
     O                                   17 '報表編號: AR001'
     O                         CON4      62                                    X
     O                                   87 '製表日期'
     O                                   88 ':'
     O                         UDATE Y   96
     O        E 1              EH#01
     O                                   61 '　請　款　通　知　單　'         X
     O                                   87 '頁　　次'
     O                                   88 ':'
     O                         PAG   Z   91
     O        E 2              EH#01
     O                                   11 '客戶編號:'
     O                         ARCUNO    18
     O                                   42 '訂單編號:'
     O                         INORNO    49
     O                                   69 '請款日期:'
     O                         INAPDTY   78
     O        E 1              EH#01
     O                                   11 '客戶名稱:'
     O                         INCUNM    21
     O                                   42 '收款業務員:'
     O                         INRVID    45
     O                                   69 '請款單號:'
     O                         ARAPNO    77
     O        E 1              EH#01
     O                         ALS       98
     O        E 1              EH#01
     O                                   10 '發票日期'
     O                                   20 '發票號碼'
     O                                   30 '磅單號碼'
     O                                   40 '出貨數量'
     O                                   52 '出貨金額'
     O                                   64 '折讓／銷退'
     O                                   75 '預收貨款'
     O                                   86 '銷項稅額'
     O                                   98 '請款金額'
     O        E 1              EH#01
     O                         ALS       98
     O        E 11             ED#01
     O                         DATE  Y    9
     O                         INVNO     20
     O                         PNO       30
     O                         QTY   J   40
     O                         AAMT  J   52
     O                         BAMT  J   63
     O                         CAMT  J   75
     O                         DAMT  J   86
     O                         NBAL  J   98
     O        E 1              ET#01
     O                         ALS       98
     O        E 1              ET#01
     O                                   20 '合　　計：'
     O                         QTYT  J   40
     O                         AAMTT J   52
     O                         BAMTT J   63
     O                         CAMTT J   75
     O                         DAMTT J   86
     O                         TTLAMT    98 '   ,   ,$0 -'
     O        E 2              ET#01
     O                                   94 '發票總張數：'
     O                         CNT   J   98
     O*       E   51   23      EF#01
     O*                                  20 '客戶簽收：'
     O*                                  40 '簽收日期：'
     O*                                  60 '主管：'
     O*                                  80 '業務員：'
     O*       E 3 44   23      EF#01
     O*                        ARDSP1    70
     O*       E 1 45   23      EF#01
     O*                        ARDSP2    70
     O*       E 1 46   23      EF#01
     O*                        ARDSP3    70
     O*       E 1 47   23      EF#01
     O*                        ARDSP4    70
     O*       E 1 48   23      EF#01
     O*                        ARDSP5    70
     O        E 3      23      EF#01
     O                         ARDSP1    70
     O        E 1      23      EF#01
     O                         ARDSP2    70
     O        E 1      23      EF#01
     O                         ARDSP3    70
     O        E 1      23      EF#01
     O                         ARDSP4    70
     O        E 1      23      EF#01
     O                         ARDSP5    70
     O*
     O        E 1      23      EF#01
     O                         ALS       98
     O        E 1     N23      EF#01
     O                                   24 '========================'
     O                                   44 '===================='
     O                                   56 '接　下　頁'
     O                                   72 '================'
     O                                   96 '========================'
     O                                   98 '=='
     O        E 2              EF#01
     O                         CON1      98
     O        E 1              EF#01
     O                         CON2      94
     O        E 2              EF#01
     O                         CON3      82
     O        E 2              EF#01
     O                         CON5      70
     O        E 2              FX#02
     O                         PFXEND     3
**  DTA
東和鋼鐵
