     H        1   Y                                     1                 BR003
     FPRSTAT  IP  E                    DISK
     FQPRINT  O   F     100            PRINTER
      *************************************************************
     E                    ARR     1   5 12
     IPRREC
     I                                              PRCUNOL2
     I                                              PRORNOL1
     I****
     IAADS        DS
     I              'も預收貨款減項明細-C         CON3                       X
     I              '表（扣預收除外）も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
      *
     I           UDS
     I                                        1   40YMA
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L1                EXSR DL#L1
     C   L2                EXSR DL#L2
     C                     EXSR DP#01
     C*
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     CLR                   EXSR TL#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD0         HAMT1  110
     C                     Z-ADD59        OVRFLW  30
     C                     MOVE *ALL'='   ALS   100
     C                     MOVE *ALL'-'   ALQ    50
     C                     MOVE *ALL'*'   ALP    50
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP OVRFLW               75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD10        LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         GAMT1  110
     C                     Z-ADD0         CNT2    20
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         CNT1    20
     C                     Z-ADD0         FAMT1  110
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
      *
     C                     MOVEL*BLANK    TYPE   12
     C                     SELEC
     C           PRFL03    WHEQ 'E'
     C                     MOVELARR,1     TYPE
     C           PRFL03    WHEQ 'F'
     C                     MOVELARR,2     TYPE
     C           PRFL03    WHEQ 'G'
     C                     MOVELARR,3     TYPE
     C           PRFL03    WHEQ 'H'
     C                     MOVELARR,4     TYPE
     C                     OTHER
     C                     MOVELARR,5     TYPE
     C                     ENDSL
     C***
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#01
      *
     C                     ADD  PRAAMT    FAMT1
     C                     ADD  PRAAMT    GAMT1
     C                     ADD  PRAAMT    HAMT1
     C                     ADD  1         CNT1
     C                     ADD  1         CNT2
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C           CNT2      IFGE 2
     C                     EXCPTET#L2
     C                     ADD  2         LCNT
     C                     ENDIF
     CSR         TL#L2Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C           CNT1      IFGE 2
     C                     EXCPTET#L1
     C                     ADD  3         LCNT
     C                     ENDIF
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME LR PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     CSR         TL#LRZ    ENDSR
      ***********************************************************
      ****   DEFINE OUPUT FORMAT                             ****
      ***********************************************************
     OQPRINT  E   03           EH#01
     O                                   17 '報表編號: AR057'
     O                         CON4      53                                    X
     O                                   75 '製表日期'
     O                                   76 ':'
     O                         UDATE Y   84
     O        E 1              EH#01
     O                         CON3      60                                    X
     O                                   75 '頁　　次'
     O                                   76 ':'
     O                         PAG   Z   79
     O        E 2              EH#01
     O                                   11 '起迄期間:'
     O                                 +  1 '82/01/01 - 82/05/31'
     O        E 1              EH#01
     O                         ALS      100
     O        E 1              EH#01
     O                                   10 '客戶編號'
     O                                   24 '客戶名稱'
     O                                   38 '訂單號碼'
     O                                   52 '發票號碼'
     O                                   64 '出貨日期'
     O                                   78 '減項種類'
     O                                  100 '金　　額'
     O        E 1              EH#01
     O                         ALS      100
     O        E 1              ED#01
     O                 L2      PRCUNO     7
     O                 75      PRCUNO     7
     O                 L2      PRCUNM    24
     O                 75      PRCUNM    24
     O                 L1      PRORNO    34
     O                 75      PRORNO    34
     O                         PRIVNO    52
     O                         PRACDTY   63
     O                         TYPE      78
     O                         PRAAMTN  100
     O        E 1              ET#L1
     O                         ALP       84
     O        E 1              ET#L1
     O                                   70 '訂單小計：'
     O                         FAMT1 N  100
     O        E 1              ET#L1
     O                         ALP       84
     O        E 1              ET#L2
     O                                   70 '客戶小計：'
     O                         GAMT1 N  100
     O        E 1              ET#L2
     O                         ALP       84
     O        E 1              ET#LR
     O                                   70 '　總合計：'
     O                         HAMT1 N  100
     O        E 1              ET#LR
     O                         ALQ       84
** ARR
扣預收貨款
收預收貨款
退預收貨款
扣預收沖回
其　　　他
