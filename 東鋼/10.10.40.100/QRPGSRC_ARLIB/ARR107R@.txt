     H*****************************************************************
     H*
     H*          >>  PROGRAM INTRODUCTION  <<
     H*          1.PROGRAM-ID    ARR107R1
     H*          2.PROGRAM-TYPE  RPG/400
     H*          3.AUTHOR        S02CSF
     H*          4.FUNCTION     應收帳款承購(E-FACTOTING)託收票據明細表
     H*                          (出表)
     H*          5.DATE-WRITTEN  92/02/16
     H*          6.DATE-MODIFY
     H*
     H*****************************************************************
     H        1   Y                                     1
     FARR107F IP  E           K        DISK
     FARR107T O   E             66     PRINTER
     IRAR107F
     I                                              AFBKIDL2
     I                                              AFDUDTL1
     I           UDS
     I                                     10011010 U#USID
     I                                      317 319 S#BKN1
     I                                      320 324 S#CUN1
     I*****************************************************************
     C*         主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C   L2                EXSR L2CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CL2 99                EXSR L2SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C*          初始程式
     C*****************************************************************
     CSR         INISR     BEGSR
     C                     SETON                     6699
     C                     MOVE *ALL'='   T#LIN1
     C                     MOVE *ALL'='   T#LIN2
     C                     MOVE *ALL'-'   T#LIN3
     C                     MOVE *OFF      *IN30
     C*HEAD
     C           S#CUN1    IFEQ *BLANK                     客戶代號
     C                     MOVEL'全部'  T#CUN1
     C                     ELSE
     C                     MOVELAFCUNM    T#CUN1
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           L2CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR107T1A
     C                     MOVE *ON       *IN66
     C*HEAD
     C                     MOVELAFBKNM    T#BKN1           銀行代號
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR107T1S
     C                     MOVEL*BLANK    W#CUNO  5
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     CLEARAR107T1D
     C                     MOVELAFCUNO    T#CUNO           客戶代號
     C                     MOVELAFCUNM    T#CUNM           客戶姓名
     C                     MOVELAFRCNO    T#RCNO           繳款單號
     C                     Z-ADDAFACDT    T#ACDT           繳款日期
     C                     Z-ADDAFDUDT    T#DUDT           票據到期日
     C                     MOVELAFRLNO    T#RLNO           票據號碼
     C                     Z-ADDAFRAMT    T#RAMT           票據金額
     C                     MOVELAFPLAC    T#PLAC           付款地
     C                     MOVELAFBANM    T#BANM           付款銀行
     C                     MOVELAFPANO    T#PANO           付款帳號
     C                     MOVELAFNTCD    T#NTCD           兌現碼
     C*
     C*客戶代號，客戶名稱相同省略不印
     C           T#CUNO    IFEQ W#CUNO
     C                     MOVE *ON       *IN50
     C                     ELSE
     C                     MOVE *OFF      *IN50
     C                     MOVELT#CUNO    W#CUNO
     C                     ENDIF
     C*
     C   66                WRITEAR107T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR107T1D
     C*
     C                     ADD  AFRAMT    T#RAMS           票據金額小計
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C   66                WRITEAR107T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR107T1S
     C                     WRITEAR107T1L
     C*
     C                     ADD  T#RAMS    T#RAMA           票據金額合計
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           L2SR      BEGSR
     C*****************************************************************
     C   66                WRITEAR107T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR107T1A
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C*****************************************************************
     C   66                WRITEAR107T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR107T1M
     C*
     CSR                   ENDSR
