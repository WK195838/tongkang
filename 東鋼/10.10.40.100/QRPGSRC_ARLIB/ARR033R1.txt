     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP033R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION     折讓單依鋼種尺寸長度分攤金額檢核表
     H*            5.DATE-WRITTEN  104/08/21  2015AR
     H*            6.UDPTAE  DATE  104/10/02  S00WCJ (0410A)
     H*                             增加數量小計及合計
     H*                            106/05/02  2017AR    S00WCJ (0605A)
     H*                             增加T1
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARDSDT  IP  E           K        DISK
     FARR033T O   E             69     PRINTER
     E                    ARY        50 12               類別數量金額
     IRARDSDT     01
     I                                              ASINNOL1
0605AI            DS
0605AI                                        1  10 ASRESV
0605AI                                        8  10 D#SPE3
     I           UDS
     I                                      301 306 T#DXNO
     I                                     10011010 T#USID
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C   01                EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C* KEY LIST
     C*初始報表
     C                     SETON                     6999
     C                     MOVE *BLANK    W#INNO 10
     C                     CLEARAR033T3
     C                     MOVE *ALL'='   T#LIN2
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR033T2
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C*
     C           ASINNO    IFNE W#INNO
     C                     MOVE *OFF      *IN60
     C                     MOVELASINNO    W#INNO
     C                     ELSE
     C                     MOVE *ON       *IN60
     C                     ENDIF
     C*
     C                     MOVELASORNO    T#ORNO           訂單編號
     C                     MOVELASCUNO    T#CUNO           客戶編號
     C                     MOVELASCUNM    T#CUNM           客戶名稱
     C                     MOVELASINNO    T#INNO           發票編號
     C                     MOVELASTXNO    T#TXNO           磅單編號
     C                     Z-ADDASTXIT    T#TXIT           磅單項次
     C                     MOVELASMETL    T#METL           鋼種
     C                     MOVELASSPE1    T#SPE1            H/W/D
     C                     MOVELASSPE2    T#SPE2            B/T
0605AC                     MOVELD#SPE3    T#SPE3            T1
     C                     MOVELASSPE5    T#SPE5           長度
     C                     Z-ADDASQTY1    T#QTY1           銷貨數量
     C                     Z-ADDASAMT4    T#AMT4           折讓單價
     C                     Z-ADDASDMT1    T#DMT1           折讓金額
     C                     Z-ADDASDMT2    T#DMT2           折讓稅額
     C           ASDMT1    ADD  ASDMT2    T#TAM1           折讓總額
     C*
0410AC                     ADD  T#QTY1    T2QTY1
0410AC                     ADD  T#QTY1    T1QTY1
     C                     ADD  T#DMT1    T2DMT1
     C                     ADD  T#DMT1    T1DMT1
     C                     ADD  T#DMT2    T2DMT2
     C                     ADD  T#DMT2    T1DMT2
     C                     ADD  T#TAM1    T#DMTA
     C                     ADD  T#TAM1    T1DMTA
     C*
     C   69                WRITEAR033TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR033T1
     C*
     C           ENDDL     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C   69                WRITEAR033TH
     C                     MOVE *OFF      *IN69
     C                     MOVEL*ALL'-'   T#LIN1
     C                     WRITEAR033T2
     C*
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C   69                WRITEAR033TH
     C                     MOVE *ALL'='   T#LIN1
     C                     WRITEAR033T3
     C                     MOVE *OFF      *IN69
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C   69                WRITEAR033TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR033TR
     C*
     C                     ENDSR
