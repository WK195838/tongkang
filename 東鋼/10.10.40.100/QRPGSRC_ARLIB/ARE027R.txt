     H*****************************************************************
     H*
     H*            1.PROGRAM-ID    ARE027R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S01LYC
     H*            4.FUNCTION     預收貨款輸入與查詢
     H*            5.DATE-WRITTEN  85/05/22
     H*            6.LAST UPDATE
     H*
     H*****************************************************************
     H            Y
     FARE027S CF  E                    WORKSTN
     F                                        RRN   KSFILE MP01F2
     FAFCSLC  UF  E           K        DISK                      A
     FINVMST  IF  E           K        DISK
     FRCVDTL  IF  E           K        DISK
     F            RVREC                             KRENAMERVRECL0
     F*NVMSTL3IF  E           K        DISK
     FRCVDTLL7IF  E           K        DISK
     F            RVREC                             KRENAMERVRECL7
     E                    TAB1    5   5  1 0 TAB2   10   *維護名稱
     E                    TAB3    6   6  1   TAB4    6   *地區名稱
     E                    SCR     3   3 10               *螢幕代號
     E                    ERR     1  12 70               *錯誤訊息
     I           UDS
     I                                      951 985 S#COMP
     I                                     10011010 W#USER
     I                                     10111020 S#DEVN
     I                                     10211021 U#LOC
     I            DS
     I                                        1  140W#SYST
     I                                        1   60W#UPDT
     C*****************************************************************
     C*          Main      Program
     C*****************************************************************
     C*SR0000 => Initialize Screen Number
     C*SR2000 => Screen Number = '02' Process
     C*SR3000 => Screen Number = '02' Process Read Subfile Screen
     C*SR4000 => Screen Number = '03' Process
     C*
     C                     EXSR SR0000
     C           PRCID     DOUEQ'00'
     C           PRCID     CASEQ'02'      SR2000
     C           PRCID     CASEQ'03'      SR3000
     C           PRCID     CASEQ'04'      SR4000
     C                     ENDCS
     C                     ENDDO
     C                     MOVE *ON       *INLR
     C*****************************************************************
     C           SR0000    BEGSR
     C*****************************************************************
     C*讀取廠區名稱
     C*
     C                     MOVE U#LOC     S#AREA
     C           S#AREA    LOKUPTAB3      TAB4           50*EQ
     C  N50                MOVE *ALL'*'   S#ARNM
     C   50                MOVE TAB4      S#ARNM
     C*
     C                     MOVE *LOVAL    S#INN
     C                     MOVE '02'      PRCID   2
     C                     ENDSR
     C*****************************************************************
     C           SR2000    BEGSR
     C*****************************************************************
     C*Screen Number = '02' Process
     C*
     C                     MOVE *BLANK    S#ERR
     C                     MOVEASCR,2     S#SCRN
     C                     EXSR #LOCAT                     *定起點
     C*====================
     C           PRCID     DOWEQ'02'
     C*
     C*Check Write Subfile Record Found ?
     C*
     C                     MOVE *OFF      *IN72            *SFLDSP ON
     C           RRN       IFEQ *ZEROS
     C                     MOVEAERR,2     S#ERR
     C                     MOVE *ON       *IN72            *SFLDSP OFF
     C                     ENDIF
     C                     WRITEHEAD
     C                     WRITEMP01F2MG
     C                     EXFMTMP01F2CL
     C                     MOVE *BLANK    S#ERR
     C*
     C*PF3=Exit
     C*
     C           *IN03     IFEQ *ON
     C                     MOVE '00'      PRCID
     C                     LEAVE
     C                     ENDIF
     C*
     C*PF12=Cancel
     C*
     C           *IN12     IFEQ *ON
     C                     MOVE '00'      PRCID
     C                     LEAVE
     C                     ENDIF
     C*
     C*PF6= Add Record
     C*
     C           *IN06     IFEQ *ON                        *PF6=ADD
     C                     MOVE 1         W#OPT
     C                     EXSR SR3100
     C                     MOVE '04'      PRCID
     C                     LEAVE
     C                     ENDIF
     C*
     C* PA1 =次頁
     C*
     C           *IN91     IFEQ *ON
     C                     Z-ADDTOTRRN    RRN
     C                     Z-ADDLSTPNT    POINT
     C  N50                EXSR #READ
     C                     ITER
     C                     ENDIF
     C*
     C*Check Screen Filed
     C*
     C                     MOVE '03'      PRCID
     C                     LEAVE
     C                     ENDDO
     C                     ENDSR
     C*****************************************************************
     C           #LOCAT    BEGSR
     C*****************************************************************
     C*Initialize Subfile Screen => MP01F2
     C*
     C                     MOVE *ON       *IN73            *SFLCLR
     C                     WRITEMP01F2CL
     C                     MOVE *OFF      *IN73            *SFLCLR
     C                     CLEARMP01F2
     C                     Z-ADD*ZEROS    RRN     40
     C                     Z-SUB11        POINT
     C*--------------------
     C           S#INN     SETLLAXREC                51    *NR
     C* Read data into subfile
     C                     EXSR #READ
     C                     ENDSR
     C*****************************************************************
     C           #READ     BEGSR
     C*****************************************************************
     C*Read File AFCSLC Write Subfile Screen
     C*
     C                     Z-ADD*ZERO     W#CNT   20
     C           W#CNT     DOWLT12
     C                     READ AXREC               N    50*EOF
     C   50                LEAVE
     C                     ADD  1         RRN
     C                     ADD  1         W#CNT
     C*
     C                     MOVELAXINNO    S#INNO
     C                     MOVELAXINDT    S#INDT
     C                     MOVELAXORNO    S#ORNO
     C                     MOVELAXCUNO    S#CUNO
     C                     MOVELAXCUNM    S#CUNM
     C                     MOVELAXFAMT    S#FAMT
     C                     MOVELAXRCNO    S#RCNO
     C*
     C                     WRITEMP01F2
     C                     ENDDO
     C*
     C           W#CNT     IFGT *ZERO                      *至少一筆
     C                     ADD  12        POINT
     C                     Z-ADDRRN       TOTRRN  40
     C                     Z-ADDPOINT     LSTPNT  40
     C                     ENDIF
     C                     ENDSR
     C*****************************************************************
     C           SR3000    BEGSR
     C*****************************************************************
     C*Screen Number = '02' Process Read Subfile Screen
     C*
     C           PRCID     DOWEQ'03'
     C                     READCMP01F2                 5353*ER*EOF
     C   53                MOVE '02'      PRCID
     C   53                LEAVE
     C*
     C*If S#OPT = " " Iter
     C*
     C           S#OPT     IFEQ *ZEROS
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVELS#OPT     W#OPT   10
     C                     Z-ADD*ZEROS    S#OPT
     C                     EXSR SR3100
     C                     UPDATMP01F2
     C                     Z-ADDRRN       POINT
     C                     MOVE '04'      PRCID
     C                     ENDDO
     C                     ENDSR
     C*****************************************************************
     C           SR3100    BEGSR
     C*****************************************************************
     C*Read File AFCSLC Detail Record Write Screen
     C*1=Add 2=Change 3=Copy 4=Delete 5=Display
     C*
     C                     MOVEA'00'      *IN,60
     C                     SELEC
     C*          W#OPT     WHEQ 2
     C*                    MOVE *ON       *IN60
     C           W#OPT     WHEQ 4
     C*          W#OPT     OREQ 5
     C                     MOVE *ON       *IN61
     C                     ENDSL
     C*
     C*If W#OPT = 1 Add Record Initialize Screen => MP01F3
     C*
     C           W#OPT     IFEQ 1
     C                     CLEARMP01F3
     C                     ENDIF
     C*
     C                     MOVE S#AREA    W#AREA
     C                     MOVE S#ARNM    W#ARNM
     C*判定維護種類
     C*
     C           W#OPT     IFEQ 4
     C                     MOVE *OFF      *IN28
     C                     ELSE
     C                     MOVE *ON       *IN28
     C                     ENDIF
     C           W#OPT     LOKUPTAB1      TAB2           50*EQ
     C  N50                MOVE *ALL'*'   S#MODE
     C   50                MOVE TAB2      S#MODE
     C*
     C*Read File AFCSLC Detail Record Write Screen
     C*
     C           S#INNO    CHAINAXREC                54    *NR
     C           *IN54     IFEQ *OFF
     C                     MOVELAXINNO    S#INNO
     C                     MOVELAXINDT    S#INDT
     C                     MOVELAXORNO    S#ORNO
     C                     MOVELAXCUNO    S#CUNO
     C                     MOVELAXFAMT    S#FAMT
     C                     MOVELAXRCNO    S#RCNO
     C*
     C                     ENDIF
     C                     ENDSR
     C*****************************************************************
     C           SR4000    BEGSR
     C*****************************************************************
     C*Read Subfile Screen Number = '03'
     C*
     C                     MOVEASCR,3     S#SCRN
     C                     MOVE *BLANK    S#ERR
     C           PRCID     DOWEQ'04'
     C                     WRITEHEAD
     C                     EXFMTMP01F3
     C                     MOVE *BLANK    DELFLG  1
     C                     MOVE *BLANK    SAVFLG  1
     C                     MOVEA'000'     *IN,30
     C*
     C*PF3=Exit
     C*
     C           *IN03     IFEQ *ON
     C                     MOVE '00'      PRCID
     C                     LEAVE
     C                     ENDIF
     C*
     C*PF12=Cancel
     C*
     C           *IN12     IFEQ *ON
     C                     MOVE '02'      PRCID
     C                     LEAVE
     C                     ENDIF
     C*
     C*Enter = Check Screen Option 5= Display Pass
     C*
     C*          W#OPT     IFEQ 5
     C*                    MOVE '03'      PRCID
     C*                    LEAVE
     C*                    ENDIF
     C*
     C                     EXSR SR4100
     C           SAVFLG    COMP 'N'                      55*EQ
     C   55                ITER
     C*
     C*PF10=Save File
     C*
     C           *IN10     IFEQ *ON
     C*Read Subfile Screen Record Write File => AFCSLC
     C                     EXSR SR4200
     C           W#OPT     IFEQ 1
     C                     EXSR SR3100
     C                     MOVE '04'      PRCID
     C                     ELSE
     C                     MOVE '03'      PRCID
     C                     ENDIF
     C                     ENDIF
     C*
     C                     ENDDO
     C                     ENDSR
     C*****************************************************************
     C           SR4100    BEGSR
     C*****************************************************************
     C*Read File INVMST Detail Record Write Screen
     C*Check Screen Filed 1=Add 2=Change 3=Copy 4=Delete 5=Display
     C*
     C           W#OPT     IFEQ 1
     C*          W#OPT     OREQ 2
     C*          W#OPT     OREQ 3
     C*
     C           S#INNO    IFEQ *BLANK
     C                     MOVE *ON       *IN30
     C                     MOVEAERR,3     S#ERR
     C                     MOVE 'N'       SAVFLG
     C                     GOTO ENDA
     C                     ENDIF
     C*Read File INVMST Detail Record Write Screen
     C*
     C           S#INNO    CHAININREC                54    *NR
     C           *IN54     IFEQ *OFF
     C           INDECD    IFEQ 'D'                        已作廢
     C                     MOVEAERR,7     S#ERR
     C                     MOVE *ON       *IN30
     C                     MOVE 'N'       SAVFLG
     C                     GOTO ENDA
     C                     ENDIF
     C           INTYPE    IFNE '2'                        非預收
     C                     MOVEAERR,6     S#ERR
     C                     MOVE *ON       *IN30
     C                     MOVE 'N'       SAVFLG
     C                     GOTO ENDA
     C                     ENDIF
     C           INAREA    IFNE W#AREA                     開立廠區
     C                     MOVEAERR,11    S#ERR
     C                     MOVE *ON       *IN30
     C                     MOVE 'N'       SAVFLG
     C                     GOTO ENDA
     C                     ENDIF
     C                     MOVELININDT    S#INDT
     C                     MOVELINORNO    S#ORNO
     C                     MOVELINCUNO    S#CUNO
     C                     MOVELINCUNM    S#CUNM
     C           0         SUB  INEAMT    S#FAMT           *取正值
     C                     MOVELINRCNO    S#RCNO
     C           S#FAMT    IFGT INAAMT                     預收＞出貨
     C                     MOVELINAAMT    S#FAMT           預收＝出貨
     C                     ELSE
     C           S#FAMT    SUB  INCTAX    S#FAMT
     C*預收金額＝繳款金額－銷貨稅額
     C                     ENDIF
     C*
     C                     ELSE
     C*
     C                     MOVEAERR,2     S#ERR
     C                     MOVE *ON       *IN30
     C                     MOVE 'N'       SAVFLG
     C                     GOTO ENDA
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     C           W#OPT     IFEQ 1
     C           S#RCNO    IFEQ *BLANK
     C                     MOVEAERR,5     S#ERR
     C                     MOVE *ON       *IN30
     C                     MOVE 'N'       SAVFLG
     C                     GOTO ENDA
     C                     ENDIF
     C           S#RCNO    CHAINRVRECL0              55    *NR
     C           *IN55     IFEQ *OFF
     C           RVDECD    IFEQ 'D'                        已作廢
     C           S#RCNO    CHAINRVRECL7              56    *NR
     C   56                MOVEAERR,8     S#ERR
     C   56                MOVE *ON       *IN30
     C   56                MOVE 'N'       SAVFLG
     C   56                GOTO ENDA
     C                     ENDIF
     C           RVUSTP    IFEQ 'A2'                       款項不合
     C                     MOVEAERR,9     S#ERR
     C                     MOVE *ON       *IN30
     C                     MOVE 'N'       SAVFLG
     C                     GOTO ENDA
     C                     ENDIF
     C           RVNTCD    IFNE '1'                        未兌現
     C           S#RCNO    CHAINRVRECL7              56    *NR
     C  N56      RVRAMT    IFLT S#FAMT                     收款＜預收
     C                     MOVEAERR,12    S#ERR
     C                     MOVE *ON       *IN30
     C                     MOVE 'N'       SAVFLG
     C                     GOTO ENDA
     C                     ENDIF
     C   56                MOVEAERR,10    S#ERR
     C   56                MOVE *ON       *IN30
     C   56                MOVE 'N'       SAVFLG
     C   56                GOTO ENDA
     C                     ENDIF
     C                     ELSE
     C                     MOVEAERR,5     S#ERR
     C                     MOVE *ON       *IN30
     C                     MOVE 'N'       SAVFLG
     C                     GOTO ENDA
     C                     ENDIF
     C                     ENDIF
     C*
     C           W#OPT     IFEQ 1
     C*          W#OPT     OREQ 3
     C           S#INNO    CHAINAXREC                55    *NR
     C  N55                MOVE *ON       *IN30
     C  N55                MOVEAERR,1     S#ERR
     C  N55                MOVE 'N'       SAVFLG
     C  N55                GOTO ENDA
     C                     ENDIF
     C*
     C*刪除確認用
     C*
     C           W#OPT     IFEQ 4
     C           DELFLG    ANDEQ*BLANK
     C                     MOVE 'Y'       DELFLG
     C                     MOVEAERR,4     S#ERR
     C                     ENDIF
     C*
     C*存檔確認用
     C*
     C           W#OPT     IFEQ 2
     C           W#OPT     OREQ 1
     C           SAVFLG    ANDEQ*BLANK
     C                     MOVE 'Y'       SAVFLG
     C                     MOVEAERR,4     S#ERR
     C                     ENDIF
     C*
     C           ENDA      TAG
     C                     ENDSR
     C*****************************************************************
     C           SR4200    BEGSR
     C*****************************************************************
     C*Read Screen Record Write File => AFCSLC
     C*1=Add 2=Change 3=Copy 4=Delete 5=Display
     C*
     C           S#INNO    CHAINAXREC                57    *NR
     C                     SELEC
     C           W#OPT     WHEQ 1
     C*          W#OPT     OREQ 3
     C                     EXSR SR4210
     C   57                WRITEAXREC
     C*          W#OPT     WHEQ 2
     C*                    EXSR SR4210
     C*  57                WRITEAXREC
     C* N57                UPDATAXREC
     C           W#OPT     WHEQ 4
     C  N57                DELETAXREC
     C                     ENDSL
     C                     ENDSR
     C*****************************************************************
     C           SR4210    BEGSR
     C*****************************************************************
     C*Move Screen Record To File Record
     C*
     C                     MOVE UDATE     W#UPDD
     C                     TIME           W#SYST
     C*
     C*          W#OPT     IFEQ 1
     C*          W#OPT     OREQ 3
     C                     CLEARAXREC
     C*                    MOVELW#USER    A3INPU
     C*                    MOVELW#UPDD    A3INPD
     C                     MOVELW#UPDT    AXCHDT
     C*                    ENDIF
     C*
     C                     MOVELS#INNO    AXINNO
     C                     MOVELS#INDT    AXINDT
     C                     MOVELS#ORNO    AXORNO
     C                     MOVELS#CUNO    AXCUNO
     C                     MOVELS#CUNM    AXCUNM
     C                     MOVELS#FAMT    AXFAMT
     C                     MOVELS#RCNO    AXRCNO
     C                     MOVELW#UPDT    AXCHDT
     C                     ENDSR
**  TAB1 TAB2
1新　　增2修　　改3複　　製4刪　　除5查　　詢
**  TAB3 TAB4
P台北T桃園M台中K高雄H苗栗L林口
**  SCR
ARE027S-1 ARE027S-2 ARE027S-3
**  ERR
01-資料已存在！
02-資料不存在！
03-欄位不可空白！
04-請按＜ＰＦ１０＞確認。
05-此發票未繳款！！
06-此發票非預收貨款發票！！
07-發票已作廢！！
08-繳款書已作廢！！
09-繳款書款項別不合！！
10-繳款書未兌現！！
11-開立廠區非本廠區！！
12-兌現金額不等於預收貨款金額，請查明！！
