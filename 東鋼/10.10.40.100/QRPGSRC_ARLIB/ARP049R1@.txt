     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP049R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     鋼筋地磅重與銷貨重差異分析（地磅重）
     H*            5.DATE-WRITTEN  86/04/26
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FWTMASPJ1IP  E           K        DISK
     FARP049F O   E           K        DISK
     FCBCUST  IF  E           K        DISK
     F*****************************************************************
     IRWTMASPJ
     I                                              W6CUNOL2
     I                                              W6ORNOL1
     I*------------------------------------------------------
     I           UDS
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     I            DS
     I                                        1   2 W6TRCD
     I                                        1   1 W#IO
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*
     C*********************************
     C*  初始程式
     C*********************************
     CSR         INISR     BEGSR
     C                     Z-ADD0         W#PDWT 120       地磅重量
     C                     MOVEL*BLANK    W#ORNO  5
     C                     MOVEL*BLANK    W#CUNO  6
     C                     MOVEL*BLANK    W#CUNM 10
     CSR                   ENDSR
     C*
     C*********************************
     C*  L1清除函式
     C*********************************
     CSR         L1CLR     BEGSR
     C                     Z-ADD0         W#PDWT 120       地磅重量
     C                     MOVELW6ORNO    W#ORNO
     C                     MOVELW6CUNO    W#CUNO
     C           W#CUNO    CHAINCBCUST               69
     C  N69                MOVELCBCUNM    W#CUNM
     C   69                MOVEL*BLANK    W#CUNM
     CSR                   ENDSR
     C*
     C*********************************
     C*  DETAIL TIME 函式
     C*********************************
     CSR         DTLSR     BEGSR
     C           W#IO      IFEQ '1'                        出貨類
     C           W6TYPE    IFEQ '0'
     C                     ADD  W7STDW    W#PDWT           理論重
     C                     ELSE
     C                     ADD  W7NWGT    W#PDWT           實重
     C                     ENDIF
     C*
     C                     ELSE                            退貨類
     C           W6TYPE    IFEQ '0'
     C                     SUB  W7STDW    W#PDWT           理論重
     C                     ELSE
     C                     SUB  W7NWGT    W#PDWT           實重
     C                     ENDIF
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*********************************
     C*  L1 函式  存檔
     C*********************************
     CSR         L1SR      BEGSR
     C                     CLEARWDREC
     C                     MOVELW#CUNO    WDCUNO
     C                     MOVELW#CUNM    WDCUNM
     C                     MOVELW#ORNO    WDORNO
     C                     Z-ADDW#PDWT    WDPDWT
     C                     WRITEWDREC
     CSR                   ENDSR
     C*
     C*********************************
     C*  LR 函式  印核覆
     C*********************************
     CSR         LRSR      BEGSR
     CSR                   ENDSR
     C*
     C*********************************
     C*  查無資料函式
     C*********************************
     CSR         MSGSR     BEGSR
     C*                    WRITEAR049T1H
     C*                    WRITEAR049T1M
     CSR                   ENDSR
     C*
     C*********************************
     C*  子函式集
     C*********************************
