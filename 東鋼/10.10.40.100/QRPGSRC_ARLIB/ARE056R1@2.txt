     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARE056R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     營業發票作廢申請作業－出表
     H*            5.DATE-WRITTEN  93/12/27
     H*            6.DATE-MODIFY
     H*
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FDELMSTL1IP  E           K        DISK
     FDELDTL  IF  E           K        DISK
     FINVMST  IF  E           K        DISK
     FARE056T O   E             66     PRINTER
     I*************************************************************
     IRDELMST
     I                                              DMINDTL2
     I                                              DMINNOL1
     I           UDS
     I                                      951 985 U#COMP
     I                                     10011010 U#USID
     I                                     10111020 U#DEVI
     I                                     10211021 U#AREA
     C*****************************************************************
     C*FILE==>DELDTL
     C           K#DELD    KLIST
     C                     KFLD           DLIVNO
     C                     KFLD           DLWTNO
     C                     KFLD           DLITEM
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C   L2                EXSR L2CLR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CL2 99                EXSR L2SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     MOVE *ON       *IN99
     C                     MOVE *ON       *IN66
     C*                    Z-ADD40        N#TCNT  30       頁內總筆數
     C*                    Z-ADD0         W#LCNT  30       已印筆數
     C*
     C                     MOVE *ALL'='   T#LIN1
     C                     MOVE *ALL'='   T#LIN2
     C                     MOVE *ALL'='   T#LIN3
     C                     MOVEL*BLANK    W#WTNO  8
     C*
     C                     MOVELDMAREA    T#AREA
     C                     Z-ADDDMAPDT    T#APDT
     C                     MOVELDMCUN1    T#CUNO
     C                     MOVELDMCUM1    T#CUNM
     C                     MOVELDMORN1    T#ORNO
     C                     MOVELDMDLNO    T#DLNO
     C*
     C                     ENDSR
     C*****************************************************************
     C           L2CLR     BEGSR
     C*****************************************************************
     C                     MOVE *OFF      *IN50
     C                     Z-ADD0         W#INDT  80
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     MOVE *OFF      *IN51
     C                     MOVEL*BLANK    W#INNO 10
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     CLEARAR056T1D
     C                     Z-ADDDMINDT    T#INDT           發票日期
     C                     MOVELDMINNO    T#INNO           發票號碼
     C                     MOVELDMCODE    T#CODE
     C                     MOVELDMDESP    T#DESP
     C*
     C           DMINNO    CHAININREC                45
     C  N45      INTYPE    IFEQ '1'
     C                     MOVELDMINNO    DLIVNO
     C                     MOVEL*BLANK    DLWTNO
     C                     Z-ADD0         DLITEM
     C           K#DELD    SETLLRDELDTL
     C                     MOVE *OFF      *IN40
     C           *IN40     DOWEQ*OFF
     C                     READ RDELDTL                  40
     C   40                LEAVE
     C*
     C           DLIVNO    IFNE DMINNO
     C                     LEAVE
     C                     ENDIF
     C*
     C                     MOVELDLWTNO    T#WTNO           磅單編號
     C                     MOVELDLPDN1    T#PDN1           品名
     C                     Z-ADDDLQTY1    T#QTY1           原出貨數量
     C                     Z-ADDDLPRC1    T#PRC1           原出貨單價
     C                     Z-ADDDLAMT1    T#AMT1           原出貨金額
     C                     Z-ADDDLQTY2    T#QTY2           新出貨數量
     C                     Z-ADDDLPRC2    T#PRC2           新出貨單價
     C                     Z-ADDDLAMT2    T#AMT2           新出貨數量
     C                     Z-ADDDLTAX2    T#TAX2           新出貨稅額
     C                     Z-ADDDLAAMT    T#AAMT           新應收金額
     C                     Z-ADDDLQTY3    T#QTY3           差異數量
     C                     Z-ADDDLPRC3    T#PRC3           差異單價
     C                     Z-ADDDLAMT3    T#AMT3           差異金額
     C           T#INDT    IFNE W#INDT
     C                     Z-ADDT#INDT    W#INDT
     C                     MOVE *OFF      *IN50
     C                     ELSE
     C                     MOVE *ON       *IN50
     C                     ENDIF
     C*
     C           T#INNO    IFNE W#INNO
     C                     MOVELT#INNO    W#INNO
     C                     MOVE *OFF      *IN51
     C                     ELSE
     C                     MOVE *ON       *IN51
     C                     ENDIF
     C*
     C           T#WTNO    IFNE W#WTNO
     C                     MOVELT#WTNO    W#WTNO
     C                     MOVE *OFF      *IN52
     C                     ELSE
     C                     MOVE *ON       *IN52
     C                     ENDIF
     C*
     C   66                WRITEAR056T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR056T1D
     C*
     C                     ENDDO
     C                     ELSE
     C                     Z-ADDDMAAMT    T#AMT1
     C   66                WRITEAR056T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR056T1D
     C*
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C*
     C                     ENDSR
     C*****************************************************************
     C           L2SR      BEGSR
     C*****************************************************************
     C*
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C   66                WRITEAR056T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR056T1Y
     C                     WRITEAR056T1X
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C   66                WRITEAR056T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR056T1M
     C*
     C                     ENDSR
