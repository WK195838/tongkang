     H*****************************************************************
     H*
     H*         >>  PROGRAM INTRODUCTION  <<
     H*
     H*         1.PROGRAM-ID      ARR100R
     H*         2.PROGRAM-TYPE    RPG
     H*         3.AUTHOR          S02LYW
     H*         4.FUNCTION       應收帳款承購已承購未結案發票明細
     H*         5.DATE-WRITTEN    91/10/17
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARINVM  IP  E           K        DISK
     FINVMST  IF  E           K        DISK
     FARR100T O   E             66     PRINTER
     IRARINVM     01
     I                                              AMORNOL1
     I*----------------------------------------------------------------
     I           UDS
     I                                      301 306 R#CUNO
     I                                      307 312 R#ORNS
     I                                      313 318 R#ORNE
     I*          UDS
     I                                     10011010 U#USID
     I                                     10111020 S#DEVN
     C*****************************************************************
     C*        MAIN  PROGRAM
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*****************************************************************
     CSR         INISR     BEGSR
     C*****************************************************************
     C*初始報表
     C                     MOVE *ALL'='   R#LIN1
     C                     MOVE *ALL'='   R#LIN2
     C                     MOVE *ALL'-'   R#LIN3
     C                     MOVE *ALL'-'   R#LIN4
     C                     MOVE *ALL'*'   R#LIN5
     C                     MOVE *ALL'*'   R#LIN6
     C                     SETON                     66
     C*
     C                     MOVE *BLANK    W#CUNM 10
     C                     MOVE *BLANK    W#CUNO  6
     C                     MOVE *BLANK    W#ORNO  6
     C                     Z-ADD0         W#EAT  140
     C                     Z-ADD0         W#FAT  140
     C                     Z-ADD0         W#TAT  140
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     MOVE *BLANK    W#RVID  2
     C                     Z-ADD0         W#EAM  140
     C                     Z-ADD0         W#FAM  140
     C                     Z-ADD0         W#TAM  140
     C                     CLEARAR100TD
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C*
     C           AMINNO    CHAININREC                41
     C  N41                MOVELINRVID    R#RVID           業務員
     C   41                MOVE *BLANK    R#RVID
     C*
     C                     MOVELAMCUNO    R#CUN1           客戶編號
     C                     MOVELAMINDT    R#INDT           發票日期
     C                     MOVELAMINNO    R#INNO           發票號碼
     C                     MOVELAMCUNM    R#CUNM           客戶名稱
     C                     MOVELAMORNO    R#ORNO           訂單號碼
     C                     Z-ADDAMAAMT    R#AAMT           貨款
     C                     Z-ADDAMCAMT    R#CAMT           扣預收款
     C                     Z-ADDAMATAX    R#ATAX           銷項稅額
     C                     Z-ADDAMDAMT    R#DAMT           發票金額
     C                     Z-ADDAMBAMT    R#BAMT           銷／折金額
     C                     Z-ADDAMDAMT    R#EAMT           應收貨款
     C                     Z-ADDAMFAMT    R#FAMT           己收金額
     C           R#EAMT    SUB  R#FAMT    R#AMTT           應收帳款
     C*小計
     C                     ADD  R#EAMT    W#EAM            應收貨款訂
     C                     ADD  R#FAMT    W#FAM            己收金額訂
     C                     ADD  R#AMTT    W#TAM            應收帳款訂
     C*
     C                     ADD  R#EAMT    W#EAT            應收貨款客
     C                     ADD  R#FAMT    W#FAT            己收金額訂
     C                     ADD  R#AMTT    W#TAT            應收帳款客
     C*
     C           R#CUNM    IFEQ W#CUNM
     C                     MOVE *ON       *IN60
     C                     ELSE
     C                     MOVELR#CUNM    W#CUNM
     C                     MOVE *OFF      *IN60
     C                     ENDIF
     C*
     C           R#CUNO    IFEQ W#CUNO
     C                     MOVE *ON       *IN61
     C                     ELSE
     C                     MOVELR#CUNO    W#CUNO
     C                     MOVE *OFF      *IN61
     C                     ENDIF
     C*
     C           R#ORNO    IFEQ W#ORNO
     C                     MOVE *ON       *IN62
     C                     ELSE
     C                     MOVELR#ORNO    W#ORNO
     C                     MOVE *OFF      *IN62
     C                     ENDIF
     C*
     C           R#RVID    IFEQ W#RVID
     C                     MOVE *ON       *IN63
     C                     ELSE
     C                     MOVELR#RVID    W#RVID
     C                     MOVE *OFF      *IN63
     C                     ENDIF
     C*
     C   66                WRITEAR100TH
     C                     SETOF                     66
     C                     WRITEAR100TD
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C                     Z-ADDW#EAM     R#SOET
     C                     Z-ADDW#FAM     R#SOFT
     C                     Z-ADDW#TAM     R#SOAT
     C*
     C   66                WRITEAR100TH
     C                     MOVE *OFF      *IN66
     C                     WRITEAR100TS
     C*
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C                     Z-ADDW#EAT     R#SCET
     C                     Z-ADDW#FAT     R#SCFT
     C                     Z-ADDW#TAT     R#SCAT
     C*
     C   66                WRITEAR100TH
     C                     MOVE *OFF      *IN66
     C                     WRITEAR100TX
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C*無資料
     C                     EXSR INISR
     C*
     C   66                WRITEAR100TH
     C                     MOVE *OFF      *IN66
     C                     WRITEAR100TM
     C*
     C                     ENDSR
     C*****************************************************************
