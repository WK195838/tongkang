     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR001R2
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02LYW
     H*            4.FUNCTION     請款單列印作業（出表）FOR應收承購
     H*            5.DATE-WRITTEN  91/01/08
     H*            6.DATE-UPDATED  89/05/10 加入類別合計
     H*                            89/07/18  DOWNLOAD  TO NOTES
     H*                            91/01/24  FOR承購
     H*
     H*  <說明> 本程式從原請款單列印作業 ARR001R複製再加以修改。
     H*           本程式將原來之全手動改成RPG CYCLE 的寫法，並且
     H*           依訂單別不同而換頁，同一張發票可有多筆磅單。
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR001F IP  E           K        DISK
     FARINVM  UF  E           K        DISK                      A
     FARINVD  IF  E           K        DISK
     FARR001N IF  E           K        DISK
     FARR001T O   E             66     PRINTER
     E                    PDN     1   6 14
     E                    ARY        10 18
     I*************************************************************
     IARREC
     I                                              ARORNOL2
     I                                              ARINNOL1
     I*------------------------------------------------------------
     I            DS
     I                                        1  18 D#ARY
     I                                        1   1 D#PDN
     I                                        1   3 D#PDN1
     I                                        4  100D#QTY
     I                                       11  180D#AMT
     I            DS
     I                                        1   80D#DAT1
     I                                        1   40D#DAT2
     I                                        5   60D#DAT3
     I                                        7   80D#DAT4
     I            DS
     I                                        1  10 D#DAT5
     I                                        1   4 D#DAT6
     I                                        5   5 D#DAT7
     I                                        6   7 D#DAT8
     I                                        8   8 D#DAT9
     I                                        9  10 D#DAT0
     I           UDS
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 U#USID
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C   L2                EXSR L2CLR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CL2 99                EXSR L2SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*
     C*********************************
     C*  初始程式
     C*********************************
     CSR         INISR     BEGSR
     C                     Z-ADD40        N#TCNT  30       頁內總筆數
     C                     Z-ADD0         W#LCNT  30       已印筆數
     C                     MOVE *ALL'9'   ARY
     C                     Z-ADD9999999   W#QTY   70
     C                     Z-ADD99999999  W#AMT   80
     C           K#INVD    KLIST
     C                     KFLD           AVINNO
     C                     KFLD           AVACNT
     C                     KFLD           AVITEM
     C           K#01RN    KLIST
     C                     KFLD           ANAPNO
     C                     KFLD           ANAPIT
     C                     Z-ADD0         ANAPIT
     C                     MOVE *ALL'9'   W#ORNO  6
     CSR                   ENDSR
     C*
     C*********************************
     C*  L2清除函式（換訂單）
     C*********************************
     CSR         L2CLR     BEGSR
     C           AVINNO    CHAINRARINVM              69
     C                     MOVELAMCUNO    R#CUNO
     C                     MOVELAMORNO    R#ORNO
     C                     Z-ADDAMAPD2    R#APDT
     C                     MOVELAMCUNM    R#CUNM
     C*                    MOVELAMRVID    R#RVID
     C                     MOVELAMAPN2    R#APNO
     C*
     C           W#LCNT    IFEQ 0
     C                     WRITEAR001T1H                   印出表頭
     C                     Z-ADD0         W#LCNT  30       已印筆數
     C                     ELSE
     C                     EXSR PR0001                     接下一頁
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*********************************
     C*  L1清除函式（換發票）
     C*********************************
     CSR         L1CLR     BEGSR
     C                     SETON                     51
     C                     ADD  ARQTY     R#QTYT           合計
     C                     ADD  ARAAMT    R#TAAM
     C                     ADD  ARBAMT    R#TBAM
     C                     ADD  ARCAMT    R#TCAM
     C                     ADD  ARDAMT    R#TDAM
     C                     ADD  AREAMT    R#TEAM
     C                     ADD  1         R#NUMS
     C*
     C           *INL2     IFEQ '1'                        第一筆不用
     C                     GOTO L1ECL
     C                     ENDIF
     C*
     C           W#LCNT    IFLT N#TCNT
     C                     WRITEAR001T1L
     C                     ADD  1         W#LCNT           已印一筆
     C                     ELSE
     C                     EXSR PR0001                     接下一頁
     C                     ENDIF
     CSR         L1ECL     ENDSR
     C*
     C*********************************
     C*  DETAIL TIME
     C*********************************
     CSR         DTLSR     BEGSR
     C*          ARQTY     IFNE 0
     C           ARAAMT    IFNE 0
     C                     Z-ADDARINDT    R#INDT
     C                     MOVELARINNO    R#INNO
     C                     MOVELARWTNO    R#WTNO
     C                     Z-ADDARQTY     R#QTY
     C                     Z-ADDARAAMT    R#AAMT
     C                     Z-ADDARBAMT    R#BAMT
     C                     Z-ADDARCAMT    R#CAMT
     C                     Z-ADDARDAMT    R#DAMT
     C                     Z-ADDAREAMT    R#EAMT
     C*
     C           W#LCNT    IFGE N#TCNT
     C                     EXSR PR0001                     接下一頁
     C                     ENDIF
     C*
     C                     WRITEAR001T1D
     C                     ADD  1         W#LCNT
     C**  8906  START ----------------
     C                     MOVE R#APNO    ANAPNO           請款單號
     C                     ADD  1         ANAPIT           請款單項次
     C           K#01RN    CHAINRARR001N             50
     C                     EXSR SR1000
     C                     MOVELANORNO    W#NO    1
     C           W#NO      IFEQ 'H'                        型鋼
     C   50                WRITERARINVM
     C  N50                UPDATRARINVM
     C                     ENDIF
     C**  8906  END   ----------------
     C*
     C                     SETOF                     51
     C**  8905  START ----------------分類別合計
     C                     MOVE ARINNO    AVINNO           發票號碼
     C                     MOVE '1'       AVACNT           類別
     C                     Z-ADD0         AVITEM           項次
     C*
     C           K#INVD    SETLLRARINVD
     C                     READ RARINVD                  40
     C           *IN40     DOWEQ*OFF
     C*
     C           AVINNO    IFNE ARINNO
     C                     LEAVE
     C                     ENDIF
     C*將類別分別合計至ARY中
     C           AVACNT    IFEQ '1'                        出貨
     C                     SORTAARY                        排序
     C           1         DO   10        I
     C                     MOVE ARY,I     D#ARY
     C*
     C           D#PDN1    IFEQ '999'                      第一次初始
     C                     MOVE AVPDCD    D#PDN1
     C                     Z-ADDAVQTY     D#QTY
     C                     Z-ADDAVAMT     D#AMT
     C                     MOVE D#ARY     ARY,I
     C                     LEAVE
     C                     ELSE
     C           AVPDCD    IFEQ D#PDN1                     類別
     C                     ADD  AVQTY     D#QTY            數量
     C                     ADD  AVAMT     D#AMT            金額
     C                     MOVE D#ARY     ARY,I
     C                     LEAVE
     C                     ENDIF                           類別
     C                     ENDIF                            D#PDN1
     C*
     C                     ENDDO
     C                     ENDIF                            IVACTP
     C*
     C                     READ RARINVD                  40
     C*
     C                     ENDDO
     C**  8905  END   ----------------
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*********************************
     C*  L1 函式
     C*********************************
     CSR         L1SR      BEGSR
     CSR                   ENDSR
     C*
     C*********************************
     C*  L2 函式
     C*********************************
     CSR         L2SR      BEGSR
     CSR                   ENDSR
     C*
     C*********************************
     C*  LR 函式  印核覆
     C*********************************
     CSR         LRSR      BEGSR
     C           N#TCNT    SUB  3         W#TMP1  30
     C           N#TCNT    ADD  1         W#TMP2  30
     C*
     C           W#LCNT    IFGT W#TMP1
     C                     EXSR PR0001
     C                     ENDIF
     C*
     C           W#LCNT    IFNE 0
     C                     WRITEAR001T1M
     C                     ADD  1         W#LCNT
     C                     ENDIF
     C*
     C                     WRITEAR001T1T
     C                     ADD  3         W#LCNT
     C* 8905 列印類別分類合計 START------------
     C                     SORTAARY
     C           1         DO   10        I
     C                     MOVE ARY,I     D#ARY
     C*型鋼與鋼板不印分類合計
     C           D#PDN     IFEQ '3'                        型鋼
     C           D#PDN     OREQ '4'                        鋼板
     C                     MOVE *ON       *IN41
     C                     ENDIF
     C*
     C           D#PDN1    IFNE '999'                      未經合計
     C                     MOVE D#PDN1    R#PDN1           類別
     C                     SELEC
     C           D#PDN1    WHEQ '212'
     C                     MOVE PDN,1     R#NAME           類別名稱
     C           D#PDN1    WHEQ '213'
     C                     MOVE PDN,2     R#NAME           類別名稱
     C           D#PDN1    WHEQ '218'
     C                     MOVE PDN,3     R#NAME           類別名稱
     C           D#PDN1    WHEQ '252'
     C                     MOVE PDN,4     R#NAME           類別名稱
     C           D#PDN1    WHEQ '253'
     C                     MOVE PDN,5     R#NAME           類別名稱
     C           D#PDN1    WHEQ '258'
     C                     MOVE PDN,6     R#NAME           類別名稱
     C                     ENDSL
     C*
     C                     Z-ADDD#QTY     R#QTYP           數量
     C                     Z-ADDD#AMT     R#TAAP           金額
     C*
     C                     ADD  D#QTY     R#QTYQ           數量
     C                     ADD  D#AMT     R#TAAQ           金額
     C*
     C  N41                WRITEAR001T1P
     C  N41                ADD  1         W#LCNT
     C*
     C                     ELSE
     C*
     C  N41                WRITEAR001T1Q
     C  N41                ADD  1         W#LCNT
     C                     LEAVE
     C*
     C                     ENDIF
     C                     ENDDO
     C* 8905 列印類別分類合計 END  ------------
     C           W#TMP2    SUB  W#LCNT    W#TMP1
     C           1         DO   W#TMP1    I       20
     C                     WRITEAR001T1L
     C                     ENDDO
     C*
     C                     WRITEAR001T1Y
     C                     WRITEAR001T1Z
     CSR                   ENDSR
     C*
     C*********************************
     C*  查無資料函式
     C*********************************
     CSR         MSGSR     BEGSR
     CSR                   ENDSR
     C*
     C*****************************************************************
     C*  子函式集
     C*****************************************************************
     C*    接下一頁（印表尾再印表頭）
     CSR         PR0001    BEGSR
     C           N#TCNT    ADD  2         W#TMP3  30
     C*
     C           W#TMP3    SUB  W#LCNT    W#TMP4  30
     C           1         DO   W#TMP4    I       20
     C                     WRITEAR001T1L
     C                     ENDDO
     C*
     C                     WRITEAR001T1X
     C                     WRITEAR001T1Z
     C*
     C                     WRITEAR001T1H
     C                     Z-ADD0         W#LCNT
     CSR                   ENDSR
     C*
     CSR         SR1000    BEGSR
     C                     MOVE *BLANK    ANFLAG
     C   50                MOVE 'A'       ANCODE
     C  N50                MOVE 'C'       ANCODE
     C                     MOVE R#CUNO    ANCUNO           客戶編號
     C                     MOVELR#ORNO    ANORNO           訂單編號
     C                     Z-ADDR#APDT    D#DAT1
     C                     ADD  1911      D#DAT2
     C                     MOVE D#DAT2    D#DAT6
     C                     MOVE '/'       D#DAT7
     C                     MOVE D#DAT3    D#DAT8
     C                     MOVE '/'       D#DAT9
     C                     MOVE D#DAT4    D#DAT0
     C                     MOVE D#DAT5    ANAPDT           請款日期
     C                     MOVE R#RVID    ANSALE           收款業務
     C   51                MOVE R#INNO    ANINNO           發票號碼
     C  N51                MOVE *BLANK    ANINNO           發票號碼
     C                     Z-ADDR#INDT    D#DAT1
     C                     ADD  1911      D#DAT2
     C                     MOVE D#DAT2    D#DAT6
     C                     MOVE '/'       D#DAT7
     C                     MOVE D#DAT3    D#DAT8
     C                     MOVE '/'       D#DAT9
     C                     MOVE D#DAT4    D#DAT0
     C   51                MOVE D#DAT5    ANINDT           發票日期
     C  N51                MOVE *BLANK    ANINDT
     C  N51                MOVEL'　'    ANINDT           發票日期
     C                     MOVE R#WTNO    ANWTNO           磅單號碼
     C                     Z-ADDR#QTY     ANQTY            出貨數量
     C                     Z-ADDR#AAMT    ANAAMT           出貨金額
     C   51                Z-ADDR#BAMT    ANBAMT           折退金額
     C  N51                Z-ADD0         ANBAMT           折退金額
     C   51                Z-ADDR#CAMT    ANCAMT           預收貨款
     C  N51                Z-ADD0         ANCAMT           預收貨款
     C   51                Z-ADDR#DAMT    ANDAMT           銷貨稅額
     C  N51                Z-ADD0         ANDAMT           銷貨稅額
     C   51                Z-ADDR#EAMT    ANEAMT           請款金額
     C  N51                Z-ADD0         ANEAMT           請款金額
     C           ANORNO    IFEQ W#ORNO
     C                     MOVE *BLANK    ANORN1
     C                     ELSE
     C                     MOVELANORNO    ANORN1
     C                     MOVELANORNO    W#ORNO
     C                     ENDIF
     CSR                   ENDSR
     C*****************************************************************
**  PDN
中級鋼筋
高拉力鋼筋
低合金鋼筋
中級定尺清
高拉力定尺清
低合金定尺清
