     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP072R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02YSH
     H*            4.FUNCTION     發票與銷貨金額差異代開
     H*            5.DATE-WRITTEN  89/05/24
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTL  IP  E           K        DISK
     FINVMST  IF  E           K        DISK
     FINVDTL  IF  E           K        DISK
     FTRNDTLL2IF  E           K        DISK
     F            TXREC                             KRENAMETXRECL
     FACTRAN  O   E                    DISK
     FARP072T O   E             69     PRINTER
     E                    ARY     1   8 14
     I*****************************************************************
     ITXREC
     I*                                             TXRESVL2
     I                                              TXIVNOL1
     I                                              TXPDNML1
     ITXRECL
     I              TXIVNO                          F#IVNO
     I              TXAMT                           F#AMT
     I              TXPDNM                          F#PDNM
     I              TXRESV                          F#RESV
     I*------------------------------------------------------
TEST I            DS
 .   I                                        1  10 TXIVNO
 .   I                                        3  10 D#IVNO
     I            DS
     I                                        1   4 T#NO
     I                                        1   1 D#TNO1
     I            DS
     I                                        1   4 D#VRNO
     I                                        1   1 D#VRN1
     I                                        2   40D#VRN2
     I            DS
     I                                        1   3 D#DSC
     I                                        1   1 D#DSC1
     I                                        2   2 D#DSC2
     I            DS
     I                                        1   6 D#NO
     I                                        1   1 D#NO1
     I                                        2   2 D#NO2
     I                                        3   6 D#NO3
     I           UDS
     I                                        1   40U#YYMM
     I                                        3   40U#MM
     I                                        7   7 U#AREA
     I                                     10011010 U#USID
     C*****************************************************************
     C*         KEY  LIST
     C*****************************************************************
     C           K#VDTL    KLIST
     C                     KFLD           IVNO
     C                     KFLD           IVACNT
     C                     KFLD           IVITEM
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*
     C*********************************
     C*  初始程式
     C*********************************
     CSR         INISR     BEGSR
     C*初始傳票編號
     C                     MOVE 'P'       D#VRN1
     C                     Z-ADD0         D#VRN2
     C                     Z-ADD0         W#VRIT  20
     C                     MOVE *OFF      *IN98            項次燈號
     C*
     C                     EXSR DATSR                      傳票日期
     C*
     C                     Z-ADDD#YMD     R#DATE           入帳日期
     C                     MOVE *ALL'-'   T#LINE
     C                     MOVE *ON       *IN69
     C                     MOVE *OFF      *IN71
     CSR                   ENDSR
     C*
     C*********************************
     C*  L1清除函式換發票
     C*********************************
     CSR         L1CLR     BEGSR
     C                     Z-ADD0         W#AMTA
     C                     Z-ADD0         W#AMTB
     C                     Z-ADD0         W#AMTC
     CSR                   ENDSR
     C*
     C*********************************
     C*  DETAIL TIME 函式
     C*********************************
     CSR         DTLSR     BEGSR
     C                     ADD  TXAMT     W#AMTB 120
     CSR         ESDTL     ENDSR
     C*
     C*********************************
     C*  L1 函式  發票明細
     C*********************************
     CSR         L1SR      BEGSR
TEST C           D#IVNO    IFEQ '87871711'
 .   C                     Z-ADD0         W#TEST  10
 .   C                     ENDIF
     C           TXIVNO    CHAININREC                50
     C                     MOVELINCUNO    D#CUNO  1
     C*
     C           INKIND    IFEQ '2'                        二聯式
     C           W#AMTB    MULT 1.05      W#AMTB    H
     C                     ENDIF
     C*
     C*  50                Z-ADD0         W#AMTA 120
     C* N50                Z-ADDINAAMT    W#AMTA           出貨金額
     C*自發票明細檔中計算該品名之發票金額
     C                     MOVE *OFF      *IN70
     C*
     C                     MOVE INNO      IVNO
     C                     MOVE *BLANK    IVACNT
     C                     Z-ADD0         IVITEM
     C           K#VDTL    SETLLIVREC
     C           *IN70     DOWEQ*OFF
     C                     READ IVREC                    70
     C*
     C           *IN70     IFEQ *ON
     C           IVNO      ORNE INNO
     C                     LEAVE
     C                     ENDIF
     C*
     C           TXPDNM    IFEQ IVPDCD                     品名代號
     C           IVACNT    ANDEQ'1'                        出貨類
     C                     ADD  IVAMT     W#AMTA 120
     C                     ENDIF
     C*
     C                     ENDDO
     C           W#AMTA    SUB  W#AMTB    W#AMTC 120       差額
     C*差額一旦大於十, 需重新檢核.以防止人工開立跨月銷貨
     C           W#AMTC    IFLT -10
     C           W#AMTC    ORGT 10
     C           W#AMTC    ANDNE0
     C*
     C           TXIVNO    SETLLTXRECL                      TRNDTLL2
     C                     READ TXRECL                   40
     C                     Z-ADD0         W#AMTD 120
     C           *IN40     DOWEQ*OFF
     C*
     C           TXIVNO    IFNE F#IVNO                     發票號碼
     C                     LEAVE
     C                     ENDIF
     C*
     C           INKIND    IFEQ '2'                        二聯式
     C           D#CUNO    ANDNE'E'                        外銷客戶
     C           INCUNO    ANDNE'DH0101'
     C           F#AMT     MULT 1.05      F#AMT     H
     C                     ENDIF
     C*
     C           TXPDNM    IFEQ F#PDNM                     品名代號
     C                     ADD  F#AMT     W#AMTD           出貨金額
     C                     ENDIF
     C                     READ TXRECL                   40
     C                     ENDDO
     C*
     C           W#AMTA    SUB  W#AMTD    W#AMTC           差額
     C*
     C                     ENDIF
     C*
8912 C*                    ADD  W#AMTC    W#AMTX  90       總計
     C*寫入傳票檔
     C           W#AMTC    IFNE 0
     C                     EXSR SR1000
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*********************************
     C*  LR 函式 （印出合計）
     C*********************************
     CSR         LRSR      BEGSR
     C           *IN98     IFEQ *ON
     C                     ADD  1         W#VRIT
     C                     EXSR SR2100                     借方總計
     C                     EXSR WTRSR
     C*
     C                     EXSR SR2200                     貸方總計
     C                     EXSR WTRSR
     C                     ENDIF
     C*
     C   69                WRITEAR072TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR072T2
     C*
     C   69                WRITEAR072TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR072TX
     C*
     CSR                   ENDSR
     C*
     C*********************************
     C*  查無資料函式
     C*********************************
     CSR         MSGSR     BEGSR
     C*
     C   69                WRITEAR072TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR072TM
     C*
     CSR                   ENDSR
     C*
     C*********************************
     C*  子函式集
     C*********************************
     CSR         SR1000    BEGSR
     C                     MOVELINCUNO    D#CUNO  1
     C  N98                ADD  1         D#VRN2
     C  N98                MOVE D#VRNO    W#VRND  4        借方號碼
     C  N98                ADD  1         D#VRN2
     C  N98                MOVE D#VRNO    W#VRNC  4        貸方號碼
     C  N98                MOVE *ON       *IN98            項次燈號
     C                     ADD  1         W#VRIT
     C*
     C                     EXSR SR1100                     寫入借方
     C                     EXSR WTRSR                      報表
     C*
     C                     EXSR SR1200                     寫入貸方
     C                     EXSR WTRSR                      報表
     C*
8912 C                     ADD  W#AMTC    W#AMTX  90       總計
     C*
     C           W#VRIT    IFEQ 98
     C                     ADD  1         W#VRIT
     C                     EXSR SR2100                     借方總計
     C                     EXSR WTRSR
     C*
     C                     EXSR SR2200                     貸方總計
     C                     EXSR WTRSR
     C*
     C                     MOVE *OFF      *IN98
     C                     Z-ADD0         W#VRIT
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*寫入借方
     CSR         SR1100    BEGSR
     C*
     C                     CLEARRACVOUR
     C                     MOVE U#AREA    ACDM03            ACDM03
     C                     MOVE 'P'       ACCARD           廠別
     C                     MOVE 'A'       ACKIND           類別
     C                     Z-ADDD#YMD     ACYMD            入帳日期
     C                     MOVE W#VRND    ACVRNO           傳票號碼
     C                     Z-ADDW#VRIT    ACITEM           項目
     C                     MOVE 'D'       ACDC             借貸
     C                     MOVE '1134'    ACNO             會計科目
     C                     MOVE 'G'       ACTYCO           方式要件
     C                     MOVELTXRESV    ACRLNO           相關號碼
     C                     MOVE INCUNM    ACCSNM           客戶簡稱
     C*
     C           W#AMTC    IFLT 0
     C                     Z-ADD99        ACCORT           更正代號
     C                     Z-SUBW#AMTC    ACAMT            金額
     C                     ELSE
     C                     Z-ADD0         ACCORT
     C                     Z-ADDW#AMTC    ACAMT            金額
     C                     ENDIF
     C*
     C                     MOVE 'A'       ACSEPT
     C                     MOVELINNO      ACDM04           發票號碼
     C                     WRITERACVOUR
     CSR                   ENDSR
     C*寫入貸方
     CSR         SR1200    BEGSR
     C*
     C                     CLEARRACVOUR
     C                     MOVE U#AREA    ACDM03            ACDM03
     C                     MOVE U#AREA    ACCARD           廠別
     C                     MOVE 'A'       ACKIND           類別
     C                     Z-ADDD#YMD     ACYMD            入帳日期
     C                     MOVE W#VRNC    ACVRNO           傳票號碼
     C                     Z-ADDW#VRIT    ACITEM           項目
     C                     MOVE 'C'       ACDC             借貸
     C           D#CUNO    IFEQ 'E'
     C                     MOVE '4114'    ACNO             會計科目
     C                     ELSE
     C                     MOVE '4111'    ACNO             會計科目
     C                     ENDIF
     C                     MOVE 'L'       ACTYCO           方式要件
     C*
     C                     SELEC
     C           U#AREA    WHEQ 'K'
     C                     MOVE 'K210'    ACDPNO           部門代號
     C           U#AREA    WHEQ 'T'
     C                     MOVE 'T380'    ACDPNO           部門代號
     C           U#AREA    WHEQ 'H'
     C                     MOVE 'H800'    ACDPNO           部門代號
     C           U#AREA    WHEQ 'M'
     C                     MOVE 'W040'    ACDPNO           部門代號
     C                     ENDSL
     C*
     C                     MOVELTXRESV    ACRLNO           相關號碼
     C                     MOVE INCUNM    ACCSNM           客戶簡稱
     C*
     C           W#AMTC    IFLT 0
     C                     Z-ADD99        ACCORT           更正代號
     C                     Z-SUBW#AMTC    ACAMT            金額
     C                     ELSE
     C                     Z-ADD0         ACCORT
     C                     Z-ADDW#AMTC    ACAMT            金額
     C                     ENDIF
     C*
     C                     MOVELTXPDNM    ACDSC1           輔助摘要
     C                     MOVE *ALL'0'   ACDSC2
     C                     MOVE 'A'       ACSEPT
     C                     MOVELINNO      ACDM04           發票號碼
     C                     WRITERACVOUR
     CSR                   ENDSR
     C*寫入借方總計
     CSR         SR2100    BEGSR
     C*
     C                     CLEARRACVOUR
     C                     MOVE U#AREA    ACDM03            ACDM03
     C                     MOVE 'P'       ACCARD           廠別
     C                     MOVE 'A'       ACKIND           類別
     C                     Z-ADDD#YMD     ACYMD            入帳日期
     C                     MOVE W#VRND    ACVRNO           傳票號碼
     C                     Z-ADDW#VRIT    ACITEM           項目
     C                     MOVE 'C'       ACDC             借貸
     C*
     C                     SELEC
     C           U#AREA    WHEQ 'K'
     C                     MOVE '3512'    ACNO             會計科目
     C           U#AREA    WHEQ 'T'
     C                     MOVE '3513'    ACNO             會計科目
     C           U#AREA    WHEQ 'H'
     C                     MOVE '3514'    ACNO             會計科目
     C           U#AREA    WHEQ 'M'
     C                     MOVE '3515'    ACNO             會計科目
     C                     ENDSL
     C*
     C                     MOVE 'A'       ACTYCO           方式要件
     C*                    MOVELTXRESV    ACRLNO           相關號碼
     C*                    MOVE INCUNM    ACCSNM           客戶簡稱
     C*
     C           W#AMTX    IFLT 0
     C                     Z-ADD99        ACCORT           更正代號
     C                     Z-SUBW#AMTX    ACAMT            金額
     C                     ELSE
     C                     Z-ADD0         ACCORT
     C                     Z-ADDW#AMTX    ACAMT            金額
     C                     ENDIF
     C*
     C                     MOVE 'A'       ACSEPT
     C                     WRITERACVOUR
     CSR                   ENDSR
     C*寫入貸方總計
     CSR         SR2200    BEGSR
     C*
     C                     CLEARRACVOUR
     C                     MOVE U#AREA    ACDM03            ACDM03
     C                     MOVE U#AREA    ACCARD           廠別
     C                     MOVE 'A'       ACKIND           類別
     C                     Z-ADDD#YMD     ACYMD            入帳日期
     C                     MOVE W#VRNC    ACVRNO           傳票號碼
     C                     Z-ADDW#VRIT    ACITEM           項目
     C                     MOVE 'D'       ACDC             借貸
     C*
     C                     MOVE '3511'    ACNO             會計科目
     C*
     C                     MOVE 'A'       ACTYCO           方式要件
     C*                    MOVELTXRESV    ACRLNO           相關號碼
     C*                    MOVE INCUNM    ACCSNM           客戶簡稱
     C*
     C           W#AMTX    IFLT 0
     C                     Z-ADD99        ACCORT           更正代號
     C                     Z-SUBW#AMTX    ACAMT            金額
     C                     ELSE
     C                     Z-ADD0         ACCORT
     C                     Z-ADDW#AMTX    ACAMT            金額
     C                     ENDIF
     C*
     C                     MOVE 'A'       ACSEPT
     C                     WRITERACVOUR
     C                     Z-ADD0         W#AMTX
     CSR                   ENDSR
     C*取得該月之最後一日
     CSR         DATSR     BEGSR
     C           U#YYMM    MULT 100       D#YMD   80
     C                     ADD  1         D#YMD
     C*
     C                     MOVE D#YMD     P#PDAT  8        第一天
     C                     MOVE '1'       P#MODE  1        中元年
     C                     MOVE *BLANK    P#MT1   2
     C                     MOVE *BLANK    P#MT2   2
     C                     MOVE *BLANK    P#MT3   2
     C                     MOVE *BLANK    P#MT4   2
     C                     MOVE *BLANK    P#MT5   2
     C                     MOVE *BLANK    P#MT6   2
     C                     MOVE *BLANK    P#MT7   2
     C                     MOVE *BLANK    P#MT8   2
     C                     MOVE *BLANK    P#MT9   2
     C                     MOVE *BLANK    P#MT10  2
     C                     MOVE *BLANK    P#MT11  2
     C                     MOVE *BLANK    P#MT12  2
     C                     MOVE *BLANK    P#LEAP  1        閏年碼
     C                     MOVE '0'       P#ERR   1        錯誤碼
     C                     CALL 'UTS101R'
     C                     PARM           P#PDAT
     C                     PARM           P#MODE
     C                     PARM           P#MT1
     C                     PARM           P#MT2
     C                     PARM           P#MT3
     C                     PARM           P#MT4
     C                     PARM           P#MT5
     C                     PARM           P#MT6
     C                     PARM           P#MT7
     C                     PARM           P#MT8
     C                     PARM           P#MT9
     C                     PARM           P#MT10
     C                     PARM           P#MT11
     C                     PARM           P#MT12
     C                     PARM           P#LEAP
     C                     PARM           P#ERR
     C*
     C                     SUB  1         D#YMD
     C                     SELEC
     C           U#MM      WHEQ 1
     C                     MOVE P#MT1     D#MT    20
     C                     ADD  D#MT      D#YMD
     C           U#MM      WHEQ 2
     C                     MOVE P#MT2     D#MT
     C                     ADD  D#MT      D#YMD
     C           U#MM      WHEQ 3
     C                     MOVE P#MT3     D#MT
     C                     ADD  D#MT      D#YMD
     C           U#MM      WHEQ 4
     C                     MOVE P#MT4     D#MT
     C                     ADD  D#MT      D#YMD
     C           U#MM      WHEQ 5
     C                     MOVE P#MT5     D#MT
     C                     ADD  D#MT      D#YMD
     C           U#MM      WHEQ 6
     C                     MOVE P#MT6     D#MT
     C                     ADD  D#MT      D#YMD
     C           U#MM      WHEQ 7
     C                     MOVE P#MT7     D#MT
     C                     ADD  D#MT      D#YMD
     C           U#MM      WHEQ 8
     C                     MOVE P#MT8     D#MT
     C                     ADD  D#MT      D#YMD
     C           U#MM      WHEQ 9
     C                     MOVE P#MT9     D#MT
     C                     ADD  D#MT      D#YMD
     C           U#MM      WHEQ 10
     C                     MOVE P#MT10    D#MT
     C                     ADD  D#MT      D#YMD
     C           U#MM      WHEQ 11
     C                     MOVE P#MT11    D#MT
     C                     ADD  D#MT      D#YMD
     C           U#MM      WHEQ 12
     C                     MOVE P#MT12    D#MT
     C                     ADD  D#MT      D#YMD
     C                     ENDSL
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         WTRSR     BEGSR
     C                     CLEARAR072T1
     C*
     C                     MOVELACDM04    T#IVNO           發票號碼
     C                     MOVELACRLNO    T#ORNO           訂單號碼
     C                     MOVE ACCSNM    T#CUNM           客戶簡稱
     C                     MOVE ACDC      T#DC             借貸別
     C                     MOVE ACNO      T#NO             會計科目
     C*
     C                     SELEC
     C           T#NO      WHEQ '1134'
     C                     MOVELARY,1     T#NONM           科目名稱
     C                     MOVE *OFF      *IN71
     C           T#NO      WHEQ '4111'
     C                     MOVELARY,2     T#NONM
     C                     MOVE *OFF      *IN71
     C           T#NO      WHEQ '4114'
     C                     MOVELARY,8     T#NONM
     C                     MOVE *OFF      *IN71
     C           T#NO      WHEQ '3511'
     C                     MOVELARY,3     T#NONM
     C                     MOVE *ON       *IN71
     C           T#NO      WHEQ '3513'
     C                     MOVELARY,4     T#NONM
     C                     MOVE *ON       *IN71
     C           T#NO      WHEQ '3514'
     C                     MOVELARY,5     T#NONM
     C                     MOVE *ON       *IN71
     C           T#NO      WHEQ '3515'
     C                     MOVELARY,6     T#NONM
     C                     MOVE *ON       *IN71
     C           T#NO      WHEQ '3512'
     C                     MOVELARY,7     T#NONM
     C                     MOVE *ON       *IN71
     C                     ENDSL
     C*
     C                     MOVE ACTYCO    T#TYCO           方式要件
     C                     MOVELACDSC1    T#DSC2           類別
     C                     MOVELT#DSC2    D#DSC
     C*
     C                     SELEC
     C           D#DSC1    WHEQ '3'
     C                     MOVEL'H型鋼' T#DSCN           類別名稱
     C           D#DSC1    WHEQ '4'
     C                     MOVEL'鋼　板'T#DSCN
     C           D#DSC1    WHEQ '2'
     C                     MOVEL'鋼　筋'T#DSCN
     C                     ENDSL
     C*
     C                     MOVE ACCORT    T#CORT           更正代號
     C*
     C           T#DC      IFEQ 'D'                        借方
     C  N71                Z-ADDACAMT     T#DAMT
     C   71                Z-ADDACAMT     T#CAMT
     C                     ELSE
     C  N71                Z-ADDACAMT     T#CAMT
     C   71                Z-ADDACAMT     T#DAMT
     C                     ENDIF
     C*
     C                     MOVE ACDPNO    T#DPNO           部門代號
     C                     MOVE ACDM03    D#NO1            開立廠區
     C                     MOVE ACCARD    D#NO2            歸屬廠區
     C                     MOVE ACVRNO    D#NO3            傳票號碼
     C                     MOVE D#NO      T#VRNO
     C                     MOVE ACITEM    T#ITEM           項次
     C*合計
     C           ACDC      IFEQ 'D'
     C           D#TNO1    IFNE '3'
     C  N71      ACCORT    IFEQ 0
     C                     ADD  ACAMT     T#CNTD
     C                     ELSE
     C                     SUB  ACAMT     T#CNTD
     C                     ENDIF
     C                     ENDIF
     C                     ELSE
     C           D#TNO1    IFNE '3'
     C  N71      ACCORT    IFEQ 0
     C                     ADD  ACAMT     T#CNTC
     C                     ELSE
     C                     SUB  ACAMT     T#CNTC
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C*
     C                     ADD  1         T#RCNT
     C*
     C   69                WRITEAR072TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR072T1
     CSR                   ENDSR
**  ARY
應收帳款
內銷銷貨收入
總公司往來
桃園往來
苗栗往來
台中往來
高雄往來
外銷銷貨收入
