     H*****************************************************************
     H*
     H*            >>  PROGRAM INTRODUCTION  <<
     H*
     H*            1.PROGRAM-ID      ARE012R
     H*            2.PROGRAM-TYPE    RPG
     H*            3.AUTHOR
     H*            4.FUNCTION       兌現處理作業
     H*            5.DATE-WRITTEN
     H*            6.UPDATE-WRITTEN  98/08/19
     H*
     H*修改說明：１、９８０８新增財會人工選取兌現該票據時，兌現碼上Ｙ
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARE012S CF  E                    WORKSTN
     F                                        RRN   KSFILE SF1
     FRCVDTLL2UF  E           K        DISK
     F*ANKID  IF  E           K        DISK
     FBANKAC  IF  E           K        DISK
     E*************************************************************
     E                    ERR     1  13 70
     E                    TAB1    1   6  1   TABR1  10
     I*************************************************************
     IAADS        DS
     I                                       21  260YMD
     I                                       21  220YY
     I                                       23  240MM
     I                                       25  260DD
     I            DS
     I                                        1   7 H05
     I                                        3   3 W#VN
     IBBDS        DS
     I*                                       1  10 W01
     I*                                       1   3 BAID1
     I*                                       4   7 BAID2
     I                                       11  20 W02
     I                                       11  11 W02TT1
     I                                       12  12 W02TT2
     I                                       13  20 W02TT3
     I                                       21  30 W22
     I                                       21  21 W22TT1
     I                                       22  22 W22TT2
     I                                       23  30 W22TT3
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           RVKEY     KLIST
     C                     KFLD           RVDUDT
     C                     KFLD           RVNTNO
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C*          BKKEY     KLIST
     C*                    KFLD           BAID1
     C*                    KFLD           BAID2
     C           ACKEY     KLIST
     C                     KFLD           ACTYPE
     C                     KFLD           ACCURR
     C                     KFLD           ACACNO
     C********************************
     C*****                     ******
     C*****  INITIAL VALUES     ******
     C*****                     ******
     C********************************
     C  N90                Z-ADDUDATE     DATE0
     C  N90                Z-ADDUDATE     DATE1
     C  N90                Z-ADDUDATE     DATE2
     C  N90                MOVE '1'       SCRN    1
     C  N90                Z-ADD990       R#L01   30
     C  N90                MOVE '1'       *IN,90
     C********************************
     C*****                     ******
     C*****     MAIN LOGIC      ******
     C*****                     ******
     C********************************
     C           *IN03     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C********************************
     C*****                     ******
     C***** SCREEN 01 MAIN CTRL ******
     C*****                     ******
     C********************************
     CSR         SR#01     BEGSR
     C           TXAR      IFEQ 'P'
     C                     MOVE TXAR      S#LOC
     C                     ENDIF
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C   KC                GOTO SR#01Z
     C                     EXSR CK#01
     C  N99                EXSR PR#02
     C  N99                MOVE '2'       SCRN
     C   99                MOVE '1'       SCRN
     CSR         SR#01Z    ENDSR
     C********************************
     C*****                     ******
     C***** SCREEN 02 MAIN CTRL ******
     C*****                     ******
     C********************************
     CSR         SR#02     BEGSR
     C                     WRITEUND01
     C                     EXFMTSFCTL1
     C   KC                EXSR KC#01
     C   KC                GOTO SR#02Z
     C   KJ                EXSR KJ#02
     C   KJ                GOTO SR#02Z
     C   KL                EXSR KL#02
     C   KL                GOTO SR#02Z
     C                     EXSR CK#02
     CSR         SR#02Z    ENDSR
     C********************************
     C*****                     ******
     C*****   CF03 EXIT         ******
     C*****                     ******
     C********************************
     CSR         KC#01     BEGSR
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C********************************
     C*****                     ******
     C*****   CF10 SAVE         ******
     C*****                     ******
     C********************************
     CSR         KJ#02     BEGSR
     C                     EXSR CK#02
     C  N99                EXSR FL#02
     C  N99                EXSR KC#01
     CSR         KJ#02Z    ENDSR
     C********************************
     C*****                     ******
     C*****   CF12 PREVIOUS     ******
     C*****                     ******
     C********************************
     CSR         KL#02     BEGSR
     C                     MOVE *BLANK    ERRMSG
     C                     MOVE '1'       SCRN
     CSR         KL#02Z    ENDSR
     C********************************
     C*****                     ******
     C*****   SCREEN 01 CKECK   ******
     C*****                     ******
     C********************************
     CSR         CK#01     BEGSR
     C                     SETOF                     414299
     C                     SETOF                     434440
     C                     MOVE *BLANK    ERRMSG
      **
     C           DATE1     IFGT DATE2
     C                     SETON                     9941
     C                     MOVELERR,2     ERRMSG
     C                     ENDIF
      **
     C           DATE1     IFNE 0
     C*                    CALL 'C01'
     C*                    PARM DATE1     APDT    60
     C*                    PARM ' '       FLAG    1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DATE1     P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM ' '       FLAG    1
     C           FLAG      IFNE '0'
     C                     SETON                     9941
     C                     MOVELERR,1     ERRMSG
     C                     ENDIF
     C                     ENDIF
      **
     C*                    CALL 'C01'
     C*                    PARM DATE2     APDT    60
     C*                    PARM ' '       FLAG    1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DATE2     P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE
     C                     PARM '1'       P#MODE
     C                     PARM           P#MTL
     C                     PARM           P#LY
     C                     PARM ' '       FLAG
     C           FLAG      IFNE '0'
     C                     SETON                     9942
     C                     MOVELERR,1     ERRMSG
     C                     ENDIF
     CSR         CK#01Z    ENDSR
     C********************************
     C*****                      *****
     C*****   SCREEN 02 PREPARE  *****
     C*****                      *****
     C********************************
     CSR         PR#02     BEGSR
     C                     Z-ADD1         DSPREC
     C                     MOVE *BLANK    ERRMSG
     C                     SETON                     80
     C                     WRITESFCTL1
     C                     SETOF                     801051
     C                     SETOF                     97
     C                     Z-ADD0         RRN     30
     C                     Z-ADDDATE1     RVDUDT
     C                     MOVE *BLANK    RVNTNO
     C                     SETOF                     212223
     C           RVKEY     SETLLRCVDTLL2             97
     C           *IN97     DOWEQ'0'
     C                     READ RCVDTLL2            N    97
     C  N97      RVDUDT    COMP DATE2                97
     C   97                LEAVE
     C           RVACNO    COMP '1131'               3131
     C  N31      RVVNO2    COMP *BLANK               3131
     C  N31      RVNTNO    COMP *BLANK                   31
     C   31                ITER
     C                     MOVELRVNO      TXARW   1
     C*          TXAR      COMP TXARW                3131
     C           TXAR      COMP RVAREA               3131
      * 901221
     C  N31      RVTXAR    IFNE ' '
     C           S#LOC     COMP RVTXAR               3131
     C                     ENDIF
      *
     C*          RVTYPE    COMP '3'                      21
     C*  21      TXAR      COMP 'H'                  2222
     C*  21      TXAR      COMP 'M'                  2323
     C   31                ITER
     C*R 22 23             ITER
     C           RVNTCD    COMP *BLANK               3131
     C   31      RVNTCD    COMP '1'                  3131
     C   31                ITER
     C                     MOVELRVTBID    H01
     C                     MOVELRVTANO    H02
     C                     MOVELRVNO      H03
     C                     MOVELRVITEM    H04
     C                     MOVELRVVNO1    H05
     C                     MOVE *BLANK    ACT
     C                     MOVELRVSBID    W01
     C                     MOVELRVSANO    W02
     C                     MOVELW02TT1    ACTYPE
     C                     MOVELW02TT2    ACCURR
     C                     MOVELW02TT3    ACACNO
     C           ACKEY     CHAINBANKAC               31
     C   31                MOVE *BLANK    ACBAID
     C   31                MOVE *BLANK    ACBNAM
     C                     MOVELACBAID    W01
     C                     MOVELACBNAM    W01X
     C                     MOVELRVNTNO    W03
     C                     MOVE *BLANK    W04
     C           RVNTTP    LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     W04
     C  N31                MOVEL'其他'  W04
     C                     Z-ADDRVDUDT    W05
     C                     Z-ADDRVRAMT    W06
     C           RVNTCD    COMP '1'                      51
     C   51                MOVEL'Y'       W07
     C  N51                MOVE *BLANK    W07
     C                     MOVELRVTBID    W21
     C                     MOVELRVTANO    W22
     C                     MOVELW22TT1    ACTYPE
     C                     MOVELW22TT2    ACCURR
     C                     MOVELW22TT3    ACACNO
     C           ACKEY     CHAINBANKAC               31
     C   31                MOVE *BLANK    ACBAID
     C   31                MOVE *BLANK    ACBNAM
     C                     MOVELACBAID    W21
     C                     MOVELACBNAM    W21X
     C                     MOVELRVNO      W23
     C                     MOVELRVCUNM    W24
     C                     MOVELRVDATE    W25
     C                     MOVE *BLANK    W26
     C           RVPLAC    COMP 'D'                      31
     C   31                MOVEL'本埠'  W26
     C           RVPLAC    COMP 'E'                      31
     C   31                MOVEL'外埠'  W26
     C                     MOVELRVUSTP    W27
     C                     ADD  1         RRN
     C                     WRITESF1
     C           RRN       IFGT R#L01
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C           PR#02Y    TAG
     C           RRN       IFEQ 0
     C                     CLEARSF1
     C                     MOVEL'END DATA'W01X
     C                     ADD  1         RRN
     C                     SETON                     51
     C                     WRITESF1
     C                     SETOF                     51
     C                     ENDIF
      *
     C                     SETON                     10
     CSR         PR#02Z    ENDSR
     C********************************
     C*****                      *****
     C*****   SCREEN 02 CHECK    *****
     C*****                      *****
     C********************************
     CSR         CK#02     BEGSR
     C                     Z-ADD1         DSPREC
     C                     SETOF                     31
     C           *IN31     DOWEQ'0'
     C                     READCSF1                      31
     C  N31                Z-ADDRRN       DSPREC
     C                     END
      *
     C                     SETOF                     994041
     C                     SETOF                     424344
     C                     MOVE *BLANK    ERRMSG
      **
     C*                    CALL 'C01'
     C*                    PARM DATE0     APDT    60
     C*                    PARM ' '       FLAG    1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DATE0     P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE
     C                     PARM '1'       P#MODE
     C                     PARM           P#MTL
     C                     PARM           P#LY
     C                     PARM ' '       FLAG
     C           FLAG      IFNE '0'
     C                     SETON                     9940
     C                     MOVELERR,1     ERRMSG
     C                     ENDIF
     C***
     C                     Z-ADD0         RRN
     C                     SETOF                     97
     C           *IN97     DOWEQ'0'
     C                     ADD  1         RRN
     C                     SETOF                     414243
     C                     SETOF                     51
     C           RRN       CHAINSF1                  97
     C   97                LEAVE
     C           ACT       COMP ' '                      31
     C  N31      ACT       COMP 'Y'                      31
     C  N31      ACT       COMP 'C'                      31
     C  N31                SETON                     4199
     C  N31                MOVELERR,3     ERRMSG
      *
     C           ACT       COMP 'Y'                      31
     C   31      W07       COMP 'Y'                      31
     C   31                SETON                     4199
     C   31                MOVELERR,6     ERRMSG
      *
     C           ACT       COMP 'Y'                  3131
     C   31      W07       COMP 'C'                      31
     C   31                SETON                     4199
     C   31                MOVELERR,7     ERRMSG
      *
     C           *IN99     IFEQ '0'
     C           ACT       ANDEQ'Y'
     C**         W01       ANDEQ*BLANK
     C           W02       ANDEQ*BLANK
     C                     MOVELW21       W01
     C                     MOVELW21X      W01X
     C                     MOVELW22       W02
     C                     ENDIF
      * 931013
     C                     MOVELW23       W#W23   1
     C           W#W23     IFEQ 'K'
     C           ACT       ANDEQ'Y'
     C           W#W23     OREQ 'T'
     C           ACT       ANDEQ'Y'
     C           W#W23     OREQ 'H'
     C           W#VN      ANDEQ'J'
     C           ACT       ANDEQ'Y'
     C                     SETON                     4199
     C                     MOVELERR,8     ERRMSG
     C                     ENDIF
      *
     C*          W01       IFNE *BLANK
     C*          ACT       OREQ 'Y'
     C*          BKKEY     CHAINBANKID               31
     C*  31                SETON                     4299
     C*  31                MOVELERR,4     ERRMSG
     C*                    ENDIF
      *
     C           W02       IFNE *BLANK
     C           ACT       OREQ 'Y'
     C                     MOVELW02TT1    ACTYPE
     C                     MOVELW02TT2    ACCURR
     C                     MOVELW02TT3    ACACNO
     C           ACKEY     CHAINBANKAC               31
     C   31                SETON                     4399
     C   31                MOVELERR,5     ERRMSG
     C   31                MOVE *BLANK    ACBAID
     C   31                MOVE *BLANK    ACBNAM
     C                     MOVELACBAID    W01
     C                     MOVELACBNAM    W01X
     C                     ENDIF
      *
9808 C  N99                SELEC
 .   C           ACT       WHEQ 'Y'
 .   C                     MOVEL'Y'       W07
 .   C           ACT       WHEQ 'C'
 .   C                     MOVEL' '       W07
9808 C                     ENDSL
      *
     C           W07       COMP 'Y'                      51
      *
     C                     UPDATSF1
     C                     ENDDO
     CSR         CK#02Z    ENDSR
     C********************************
     C*****                      *****
     C*****   WRITE DATA TO FILE *****
     C*****                      *****
     C********************************
     CSR         FL#02     BEGSR
     C                     Z-ADD0         RRN
     C                     SETOF                     97
     C           *IN97     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  97
     C   97                LEAVE
     C**         H01       IFNE W01
     C*          H02       IFNE W02
     C*          ACT       ORNE *BLANK
      *
     C                     Z-ADDW05       RVDUDT
     C                     MOVELW03       RVNTNO
     C                     MOVELH03       RVNO
     C                     MOVELH04       RVITEM
     C           RVKEY     CHAINRCVDTLL2             31
     C   31                ITER
      *
     C           ACT       COMP 'Y'                      51
     C           ACT       COMP 'C'                      52
     C   51                MOVEL'1'       RVNTCD
     C   52                MOVE *BLANK    RVNTCD
     C   52                MOVE *BLANK    W01
     C   52                MOVE *BLANK    W01X
     C   52                MOVE *BLANK    W02
      *
     C                     MOVELW01       RVSBID
     C                     MOVELW02       RVSANO
     C                     MOVEL'C'       RVFLAG
     C                     MOVELTXAR      RVTXAR
     C                     Z-ADDUDATE     RVTXDT
     C                     Z-ADDDATE0     RVNTDT
     C                     UPDATSF1
     C                     UPDATRVREC
     C*                    ENDIF
     C                     ENDDO
     CSR         FL#02Z    ENDSR
      ******************************* END PROGRAM *****************
** ERR
日期錯誤
日期範圍錯誤
處理代碼錯誤
銀行代號錯誤
銀行帳號錯誤
此筆資料已兌現不可再兌現
此筆資料未兌現不可取消
此筆資料請至所屬廠區兌現
** TAB1
A支票
B背書支票
C本票
D背書本票
E銀行本票
I信用狀
