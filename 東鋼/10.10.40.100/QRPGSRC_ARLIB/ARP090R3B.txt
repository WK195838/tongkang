     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP090R3B
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02YSH
     H*            4.FUNCTION     兌現預估明細處理
     H*            5.DATE-WRITTEN  91/03/11
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FINVMST  IP  E           K        DISK
     FRCVDTL  IF  E           K        DISK
     FHSCONT  IF  E           K        DISK
     FARR092F IF  E           K        DISK
     FARINVM  IF  E           K        DISK
     FARP090X O   E           K        DISK
     IINREC       01
     I                                              ININDTL1
     I                                              INNO  L1
     I            DS
     I                                        1   6 D#ORNO
     I                                        1   1 D#ORN1
     I                                        2   6 D#ORN2
     I            DS
     I                                        1   80D#INDT
     I                                        1   40D#YEAR
     I                                        5   60D#MONT
     I                                        7   80D#DATE
     I           UDS
     I                                      450 4550D#DAT1
     I                                      456 4610D#DAT2
     C*****************************************************************
     C*         KEY  LIST
     C*****************************************************************
     C*RCVDTL
     C           K#VDTL    KLIST
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C*HSCONT
     C           K#CONT    KLIST
     C                     KFLD           SBAREA
     C                     KFLD           SBCNUM
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                MOVE *ON       *IN99
     C   L1                EXSR L1CLR
     C   01                EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR                   RETRN
     C*********************************
     C*  L1CLR       函式
     C*********************************
     CSR         L1CLR     BEGSR
     C                     CLEARRARP090X
     C                     ENDSR
     C*********************************
     C*  DETAIL TIME 函式
     C*********************************
     CSR         DTLSR     BEGSR
     C                     ADD  INNBAL    A1AMT
     C                     ENDSR
     C*********************************
     C*  L1SR        函式
     C*********************************
     CSR         L1SR      BEGSR
TEST C                     MOVELINNO      W#INN1  2
 .   C                     MOVE INNO      W#INN2  8
 .   C           W#INN1    IFEQ 'CR'
 .   C           W#INN2    ANDEQ'97024877'
 .   C                     Z-ADD0         W#TEST  10
 .   C                     ENDIF
     C*
     C                     MOVELINORNO    W#ORNO  1
     C           INNBAL    IFNE 0
     C           INTYPE    ANDEQ'1'
     C           W#ORNO    IFNE 'H'
     C                     EXSR SR1000
     C                     ELSE
     C                     EXSR SR2000
     C                     ENDIF
     C                     ENDIF
     C                     ENDSR
     C*********************************
     C*  SR1000      函式  (鋼筋用)
     C*********************************
     CSR         SR1000    BEGSR
     C*檢核此發票之繳款書是否已經營業確認
     C                     MOVE INRCNO    RVNO
     C                     Z-ADD0         RVITEM
     C           K#VDTL    SETLLRVREC
     C                     READ RVREC                    40
     C           INRCNO    IFEQ RVNO
     C                     GOTO END1
     C                     ENDIF
     C*排除鋼筋承購
     C           INNO      CHAINRARINVM             N50
     C  N50                GOTO END1
     C*
     C                     MOVEL'IN'      A1CODE           單據代號
     C                     MOVELINNO      A1NO             單據號碼
     C                     MOVELINCUNO    A1CUNO           客戶代號
     C                     MOVELINRVID    A1SALE           業務代號
     C                     Z-ADDININDT    A1INDT           單據日期
     C                     MOVE '2'       A1PROD           產品大類
     C*計算發票到期日期
     C*
     C                     MOVELINORNO    D#ORNO
     C                     MOVELD#ORN1    SBAREA
     C                     MOVELD#ORN2    SBCNUM
     C           K#CONT    CHAINRHSCONT              40
     C*計算發票到期日
     C                     MOVE ININDT    D#INDT
     C*
     C                     Z-ADDSBRTD1    D#DATE
     C                     Z-ADDD#INDT    W#DAT1  80       請款一
     C*
     C                     MOVE ININDT    D#INDT
     C                     Z-ADDSBRTD2    D#DATE
     C           SBRTD2    IFLT SBRTD1                     次月
     C                     ADD  1         D#MONT
     C                     ENDIF
     C           D#MONT    IFEQ 13
     C                     Z-ADD1         D#MONT
     C                     ADD  1         D#YEAR
     C                     ENDIF
     C                     Z-ADDD#INDT    W#DAT2  80       請款二
     C*
     C                     MOVE ININDT    D#INDT
     C                     Z-ADDSBRTD3    D#DATE
     C           SBRTD3    IFLT SBRTD2                     次月
     C                     ADD  1         D#MONT
     C                     ENDIF
     C           D#MONT    IFEQ 13
     C                     Z-ADD1         D#MONT
     C                     ADD  1         D#YEAR
     C                     ENDIF
     C                     Z-ADDD#INDT    W#DAT3  80       請款三
     C*
     C                     MOVE ININDT    D#INDT
     C                     Z-ADDSBRTD1    D#DATE
     C                     ADD  1         D#MONT
     C           D#MONT    IFEQ 13
     C                     Z-ADD1         D#MONT
     C                     ADD  1         D#YEAR
     C                     ENDIF
     C                     Z-ADDD#INDT    W#DAT4  80       次月請款一
     C*
     C                     MOVE W#DAT1    P#DATE
     C                     CALL 'UTS104R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MOD1  1
     C                     PARM '1'       P#MOD2  1
     C                     PARM '0005'    P#DAYS  4
     C                     PARM *BLANK    P#RTND  8
     C                     PARM *BLANK    P#ERR   1
     C           P#ERR     IFEQ '0'
     C                     MOVE P#RTND    W#DAT1           理論請款一
     C                     ELSE
     C                     Z-ADD0         W#DAT1
     C                     ENDIF
     C*
     C                     MOVE W#DAT2    P#DATE
     C                     CALL 'UTS104R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MOD1  1
     C                     PARM '1'       P#MOD2  1
     C                     PARM '0005'    P#DAYS  4
     C                     PARM *BLANK    P#RTND  8
     C                     PARM *BLANK    P#ERR   1
     C           P#ERR     IFEQ '0'
     C                     MOVE P#RTND    W#DAT2           理論請款二
     C                     ELSE
     C                     Z-ADD0         W#DAT2
     C                     ENDIF
     C*
     C                     MOVE W#DAT3    P#DATE
     C                     CALL 'UTS104R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MOD1  1
     C                     PARM '1'       P#MOD2  1
     C                     PARM '0005'    P#DAYS  4
     C                     PARM *BLANK    P#RTND  8
     C                     PARM *BLANK    P#ERR   1
     C           P#ERR     IFEQ '0'
     C                     MOVE P#RTND    W#DAT3           理論請款三
     C                     ELSE
     C                     Z-ADD0         W#DAT3
     C                     ENDIF
     C*
     C           ININDT    IFLE W#DAT1                     以付款日一
     C                     Z-ADDW#DAT1    D#INDT           理論請款一
     C                     Z-ADDSBPDT1    D#DATE
     C                     ADD  1         D#MONT
     C           D#MONT    IFEQ 13
     C                     Z-ADD1         D#MONT
     C                     ADD  1         D#YEAR
     C                     ENDIF
     C                     Z-ADDD#INDT    A1DAT1           發票到期日
     C                     ENDIF
     C*
     C           ININDT    IFLE W#DAT2                     以付款日二
     C           ININDT    ANDGEW#DAT1
     C                     Z-ADDW#DAT2    D#INDT           理論請款二
     C                     Z-ADDSBPDT2    D#DATE
     C                     ADD  1         D#MONT
     C           D#MONT    IFEQ 13
     C                     Z-ADD1         D#MONT
     C                     ADD  1         D#YEAR
     C                     ENDIF
     C                     Z-ADDD#INDT    A1DAT1           發票到期日
     C                     ENDIF
     C*
     C           ININDT    IFLE W#DAT3                     以付款日三
     C           ININDT    ANDGEW#DAT2
     C           ININDT    ANDGEW#DAT1
     C                     Z-ADDW#DAT3    D#INDT           理論請款三
     C                     Z-ADDSBPDT3    D#DATE
     C                     ADD  1         D#MONT
     C           D#MONT    IFEQ 13
     C                     Z-ADD1         D#MONT
     C                     ADD  1         D#YEAR
     C                     ENDIF
     C                     Z-ADDD#INDT    A1DAT1           發票到期日
     C                     ENDIF
     C*
     C           ININDT    IFLE W#DAT4                     次月請款一
     C           ININDT    ANDGEW#DAT3
     C           ININDT    ANDGEW#DAT2
     C           ININDT    ANDGEW#DAT1
     C                     Z-ADDW#DAT4    D#INDT
     C                     Z-ADDSBPDT1    D#DATE
     C                     ADD  1         D#MONT
     C           D#MONT    IFEQ 13
     C                     Z-ADD1         D#MONT
     C                     ADD  1         D#YEAR
     C                     ENDIF
     C                     Z-ADDD#INDT    A1DAT1           發票到期日
     C                     ENDIF
     C*
     C           SBPYCK    IFNE ' '                        支票繳款
     C                     MOVE A1DAT1    P#DATE
     C                     Z-ADDSBPERD    W#PERD  40
     C                     MOVE W#PERD    P#DAYS
     C                     CALL 'UTS104R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MOD1  1
     C                     PARM '2'       P#MOD2  1
     C                     PARM           P#DAYS  4
     C                     PARM *BLANK    P#RTND  8
     C                     PARM *BLANK    P#ERR   1
     C           P#ERR     IFEQ '0'
     C                     MOVE P#RTND    A1DAT1           預估兌現日
     C                     ELSE
     C                     Z-ADD0         W#DAT3
     C                     ENDIF
     C                     ENDIF
     C*
     C           A1DAT1    IFGE D#DAT1
     C           A1DAT1    ANDLED#DAT2
     C                     WRITERARP090X
     C                     ENDIF
     C*
     C           END1      ENDSR
     C*********************************
     C*  SR2000      函式  (型鋼用)
     C*********************************
     CSR         SR2000    BEGSR
     C*檢核此發票之繳款書是否已經營業確認
     C                     MOVE INRCNO    RVNO
     C                     Z-ADD0         RVITEM
     C           K#VDTL    SETLLRVREC
     C                     READ RVREC                    40
     C           INRCNO    IFEQ RVNO
     C                     GOTO END2
     C                     ENDIF
     C*
     C           INCUNO    IFEQ 'DH043'
     C                     GOTO END2
     C                     ENDIF
     C*
     C                     MOVEL'IN'      A1CODE           單據代號
     C                     MOVELINNO      A1NO             單據號碼
     C                     MOVELINCUNO    A1CUNO           客戶代號
     C                     MOVELINRVID    A1SALE           業務代號
     C                     Z-ADDININDT    A1INDT           單據日期
     C                     MOVE '3'       A1PROD           產品大類
     C*計算發票到期日期
     C                     MOVE ININDT    P#PDAT
     C           A1CUNO    CHAINRARR092              40
     C  N40      A9DAT2    IFGE 0
     C                     MOVE '2'       P#OMOD
     C                     Z-ADDA9DAT2    W#DATS  40
     C                     ELSE
     C                     MOVE '1'       P#OMOD
     C                     Z-SUBA9DAT2    W#DATS
     C                     ENDIF
     C   40                MOVE '2'       P#OMOD
     C   40                Z-ADD45        W#DATS
     C                     MOVE W#DATS    P#DAY
     C*
     C                     CALL 'UTS104R'
     C                     PARM           P#PDAT  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#OMOD  1
     C                     PARM           P#DAY   4
     C                     PARM           P#EDAT  8
     C                     PARM *BLANK    P#ERR   1
     C*型鋼為週二或週四押匯,所以修正理論兌現日
     C****計算星期幾
     C                     MOVE P#EDAT    P#PDAT
     C                     CALL 'UTS105R'
     C                     PARM           P#PDAT
     C                     PARM           P#MODE
     C                     PARM           P#WKDY  1
     C                     PARM           P#ERR
     C****修正理論兌現日
     C                     SELEC
     C           P#WKDY    WHEQ '1'
     C                     MOVE '0001'    P#DAY
     C           P#WKDY    WHEQ '2'
     C                     MOVE '0000'    P#DAY
     C           P#WKDY    WHEQ '3'
     C                     MOVE '0001'    P#DAY
     C           P#WKDY    WHEQ '4'
     C                     MOVE '0000'    P#DAY
     C           P#WKDY    WHEQ '5'
     C                     MOVE '0004'    P#DAY
     C           P#WKDY    WHEQ '6'
     C                     MOVE '0003'    P#DAY
     C           P#WKDY    WHEQ '7'
     C                     MOVE '0002'    P#DAY
     C                     ENDSL
     C                     CALL 'UTS104R'
     C                     PARM           P#PDAT
     C                     PARM '1'       P#MODE
     C                     PARM '2'       P#OMOD
     C                     PARM           P#DAY
     C                     PARM           P#EDAT
     C                     PARM *BLANK    P#ERR
     C*
     C                     MOVE P#EDAT    A1DAT1
     C*
     C           A1DAT1    IFGE D#DAT1
     C           A1DAT1    ANDLED#DAT2
     C                     WRITERARP090X
     C                     ENDIF
     C*
     C           END2      ENDSR
