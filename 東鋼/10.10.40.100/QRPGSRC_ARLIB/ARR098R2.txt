     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR098R1
     H*            2.PROGRAM-TYPE  RPG
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     自製出貨分析表　　
     H*            5.DATE-WRITTEN  91/09/24
     H*            6.DATE-MODIFY
     H*
     H*            說明:950815新增扣除外購折扣部份
     H*
     H*            *將外購成品出貨明細表寫入ARR098F2,辨別碼=2
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTL  IP  E           K        DISK
     FWTMASPL4IF  E           K        DISK
9508 FINVDTL  IF  E           K        DISK
     FARR098F1O   E           K        DISK
     E*****************************************************************
     E                    ARY        30 10
     E*****************************************************************
     ITXREC
     I                                              TXPDNML1
     I*----------------------------------------------------------------
     I            DS
     I                                        1   8 TXNO
     I                                        1   1 W6AREA
     I                                        2   3 W6VNO1
     I                                        4   80W6VNO2
     I*
     I            DS
     I                                        1  10 D#ARY
     I                                        1   2 D#IV1
     I                                        3  10 D#NO1
     I*
     I            DS
     I                                        1  10 TXIVNO
     I                                        1   2 W#IV1
     I                                        3  10 W#NO1
     I*
     I           UDS
     I                                      410 410 D#AREA
     I                                      411 4180T#DATS
     I                                      419 4260T#DATE
     I                                      427 429 T#PDNS
     I                                      430 432 T#PDNE
     C*****************************************************************
     C*          Main      Program
     C*****************************************************************
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     SETON                       99
     C*
     C           K#MASP    KLIST
     C                     KFLD           W6ENTD
     C                     KFLD           W6AREA
     C                     KFLD           W6VNO1
     C                     KFLD           W6VNO2
9508 C*
 .   C           K#INVD    KLIST
 .   C                     KFLD           IVNO
 .   C                     KFLD           IVACNT
9508 C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     Z-ADD0         W#QTY            *出貨數量
     C                     Z-ADD0         W#AMT            *出貨金額
     C                     MOVE *ALL'9'   ARY
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     Z-ADDTXDATE    W6ENTD
     C           K#MASP    CHAINRWTMASP             N40
     C  N40      W6TRCD    IFEQ '1E'
     C*          W6WTNO    ANDNE'G'
     C*          W6WTNO    ANDNE'F'
 9111C                     ADD  TXQTY     W#QTY   90       數量
     C                     ADD  TXAMT     W#AMT  110       金額
     C*
     C                     EXSR SR2000                     折扣票ARY
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C                     EXSR SR2100
     C*
     C                     MOVELD#AREA    F1AREA           廠區
     C                     MOVELTXPDNM    F1PDNM           品名
     C                     MOVELTXPDNM    W#PDN1  2
     C           W#PDN1    IFEQ '37'
     C           W#PDN1    OREQ '3A'
     C                     MOVEL'B'       F1PDN1
     C                     ELSE
     C                     MOVEL'A'       F1PDN1
     C                     ENDIF
     C                     MOVEL'2'       F1CODE           辨別碼
     C                     Z-ADDW#QTY     F1QTY4           數量
     C                     Z-ADDW#AMT     F1AMT4           收入
     C                     Z-ADDW#QTY     F1QTY            銷貨淨數量
     C                     Z-ADDW#AMT     F1AMT            銷貨淨收入
     C                     WRITERAR098F1
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2000    BEGSR
     C*****************************************************************
     C*記錄折扣發票號碼於ARY中
     C*
     C                     MOVE *OFF      *IN42
     C                     MOVELTXIVNO    IVNO
     C                     MOVEL'3'       IVACNT
     C*
     C*將銷貨中包含折扣之發票號碼寫入陣列
     C*以便排除多張磅單同一發票號重覆計算
     C*
     C           K#INVD    CHAININVDTL               42
     C*
     C           *IN42     IFEQ *OFF
     C                     SORTAARY
     C           1         DO   30        I       20
     C                     MOVE ARY,I     D#ARY
     C*
     C           D#IV1     IFEQ '99'
     C                     MOVELTXIVNO    D#IV1
     C                     MOVE TXIVNO    D#NO1
     C                     MOVELD#ARY     ARY,I
     C                     LEAVE
     C                     ELSE
     C*
     C           D#IV1     IFEQ W#IV1
     C           D#NO1     ANDEQW#NO1
     C                     LEAVE
     C                     ENDIF
     C                     ENDIF
     C*
     C                     ENDDO
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2100    BEGSR
     C*****************************************************************
     C*扣除折扣發票金額
     C*
     C           1         DO   30        I
     C                     MOVE ARY,I     D#ARY
     C*
     C           D#IV1     IFEQ '99'
     C                     LEAVE
     C                     ELSE
     C*
     C                     MOVELD#IV1     IVNO             發票號碼
     C                     MOVE D#NO1     IVNO             發票號碼
     C                     MOVEL'3'       IVACNT           類別
     C*
     C                     MOVE *OFF      *IN41
     C           K#INVD    SETLLIVREC
     C           *IN40     DOWEQ*OFF
     C           K#INVD    READEIVREC                    41
     C*
     C   41                LEAVE
     C*
     C           IVAPNO    IFNE *BLANK
     C                     ITER
     C                     ENDIF
     C*
     C                     ADD  IVAMT     W#AMT            金額
IVDO C                     ENDDO
     C*
ARIF C                     ENDIF
ARDO C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
