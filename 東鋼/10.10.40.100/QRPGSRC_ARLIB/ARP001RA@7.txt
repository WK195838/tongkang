     H****************************************************************
     H*
     H*            UPDATE  DATE  99/08/25  2010AR517  S00WCJ (9908A)
     H*                         102/12/31  S00WCJ (0212A)
     H*                          將磅單項次寫入TXRESV第13.14位
     H*                         103/11/26  2014AR887  S00WCJ (0311A)
     H*                          配合營業鋼筋產品代碼擴檔
     H*                         104/09/22  2015AR953  S00WCJ (0409A)
     H*                          產品代碼3碼擴5碼
     H*                         108/08/23  2019AR00057 S00WCJ (0808A)
     H*                          改為過至磅單項次
     H*
     H****************************************************************
     H            Y                                     1
     FSAVOUR  UP  E                    DISK
     FTRNDTL  IF  E           K        DISK                      A
     FCBCUST  IF  E           K        DISK
     FSAMAST  IF  E           K        DISK
     FPRINT   O   F     132            PRINTER
     FPRINT1  O   F     132            PRINTER
     IRSAVOUR
     I                                              S4VREAL2
     I                                              S4VRNOL2
     I                                              S4ORNOL1
0808AI                                              S4VRTML1
     IAADS        DS
     I                                        1   6 W1CUNO
     I                                        1   6 CBKEY
     I                                        1   1 S1KIND
     I                                        2   2 S1CUN1
     I                                        3   5 S1CUN2
9008 I                                        6   6 S1CD01
LYW  I                                        7   7 S1CD02
 .   I                                        8   8 S1CD03
 .   I                                        9   9 S1CD04
 .   I                                       10  10 S1CD05
     I                                       11  18 S4KEY
     I                                       11  11 S4VREA
     I                                       12  13 S4PCOD
     I                                       12  13 SPEC
     I                                       14  180S4VRNO
     IBBDS        DS
9008 I                                        1   9 WORNO
LYW  I                                        1   1 WORNO1
 .   I                                        2   6 WORNO2
 .   I                                        7   9 WORNO3
CLJB I            DS
0409AI                                        1  30 TXRESV
CLJE I                                       10  10 D#TXRV
0212AI                                       13  140D#VRTM
0808AI                                       19  21 D#PDS1
     I           UDS
9710 I*9908A                                  1   80YMD
9908AI                                       51  580YMD
9710 I*9908A                                  9  160YMD1
9908AI                                        7  120YMD1
     I                                     10211021 TXAR
     C*----------------------------------------------------*
9908AC           *DATE     SUB  19000000  U#SYSD  80
     C  N81                EXSR INZ#01
     C  N81                SETON                         81
     C*----------------------------------------------------*
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     CLR                   EXSR TL#LR
     C*----------------------------------------------------*
     CSR         INZ#01    BEGSR
     C                     MOVE 'SA04'    CDE1    4
     C                     MOVE *ALL'='   ALS   108
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD55        TCNT    20
     C                     Z-ADD0         PAG     30
     C                     Z-ADD0         WITEM   20
     C*                    MOVEL'11'      SPEC             要寶？
     C*----------------------------------------------------*
     C           KEY       KLIST
     C                     KFLD           CDE1
     C                     KFLD           TXNO
     C                     KFLD           TXITEM
     C           KEYT      KLIST
     C                     KFLD           CDE1
     C                     KFLD           TXNO
     C           S1KEY     KLIST
     C                     KFLD           S1OREA
     C                     KFLD           S1ORNO
     C                     KFLD           S1ORTM
     CSR                   ENDSR
     C*----------------------------------------------------*
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         WAMTT   90
     C                     Z-ADD0         WAMTP   90
     C                     Z-ADD0         TWQTY   70
     CSR                   ENDSR
     C*----------------------------------------------------*
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         WQTY    90
     CSR                   ENDSR
     C*----------------------------------------------------*
     CSR         DP#01     BEGSR
9008 C                     MOVELS4ORNO    WORNO
     C                     MOVE WORNO1    S1OREA
9008 C                     MOVELWORNO2    S1ORNO
     C                     MOVE WORNO3    S1ORTM
     C           S1KEY     CHAINSAMAST               36
     C           *IN36     IFEQ '0'
     C           CBKEY     CHAINCBCUST               97
     C  N97                MOVELCBIVCO    S1CHK
     C           S1KIND    IFEQ 'D'
     C           S1KIND    OREQ 'G'
     C                     MOVE 'L'       WSK
     C                     ELSE
     C                     MOVE S1KIND    WSK     1
     C                     END
     C           S1UPRC    COMP 0                        35
     C                     END
     C           S4DATE    COMP YMD1                   37
     C                     SETOF                     90
     C   37
     COR 35
     COR 36                SETON                     90
     C   90                ADD  1         CNT0    40
     C   90      CNT0      COMP 1                        33
     C   90                EXCPT
     C                     SETOF                     333536
     C  N90                MOVE 'X'       S4CDE2
     C  N90                UPDATRSAVOUR
     C  N90                EXSR DP#02
     C                     ENDSR
     C*---------------------------------------------------*
     CSR         DP#02     BEGSR
     C                     ADD  S4NWGT    WQTY
     CSR                   ENDSR
     C*---------------------------------------------------*
     C*  TOTAL TIME  L1                                   *
     C*---------------------------------------------------*
     CSR         TL#L1     BEGSR
     C           WQTY      MULT S1UPRC    WAMT   123
     C                     Z-ADDWAMT      WAMTP     H
     C                     ADD  WAMTP     WAMTT
     C                     Z-ADDWAMTP     WAMTO  112     80
     C                     ADD  WQTY      TWQTY
     C                     ADD  WAMTP     WAMTTT  90
     C  N80                EXSR FITEM
     C  N80                EXSR WR#TN
     C  N80                EXSR OU#01
     CSR                   ENDSR
     C*---------------------------------------------------*
     C*  TOTAL TIME  L2                                   *
     C*---------------------------------------------------*
     CSR         TL#L2     BEGSR
     C           WAMTT     COMP 0                        17
     C  N17                EXCPTET#01
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C*---------------------------------------------------*
     C*  TOTAL TIME  LR                                   *
     C*---------------------------------------------------*
     CSR         TL#LR     BEGSR
     C                     Z-ADDWAMTTT    WAMTTR  90
     C                     EXCPTET#02
     CSR                   ENDSR
     C*---------------------------------------------------*
     C*  FIND LAST NUMBER                                 *
     C*---------------------------------------------------*
     CSR         FITEM     BEGSR
     C                     SETOF                     18
     C                     MOVE *BLANK    TXNO
     C                     MOVELS4KEY     TXNO
     C                     Z-ADD99        TXITEM
     C           KEY       SETGTTRNDTL
     C  N18      KEYT      REDPETRNDTL                   18
     C      18             Z-ADD1         WITEM
     C     N18   TXITEM    ADD  1         WITEM
     CSR                   ENDSR
     C*-----------------------------------------------------------*
     C** WRITE TO TRNDTL                                          *
     C*-----------------------------------------------------------*
     CSR         WR#TN     BEGSR
     C                     CLEARTXREC
     C                     MOVE 'A'       TXFLAG
     C                     MOVELCDE1      TXCODE
     C                     MOVE '1'       TXACNT
     C                     MOVE *BLANK    TXNO
     C                     MOVELS4KEY     TXNO
     C                     Z-ADDWITEM     TXITEM
     C                     Z-ADDS4DATE    TXDATE
     C                     Z-ADDS4DATE    TXACDT
9607 C           W1CUNO    CHAINCBCUST               69
 .   C  N69                MOVELCBCUNM    TXCUNM
9607 C   69                MOVELS1CUNO    TXCUNM
     C                     MOVELW1CUNO    TXCUNO
     C                     MOVELS4ORNO    TXORNO
0212AC                     Z-ADDS4VRTM    D#VRTM
0808AC                     MOVELS4PDS1    D#PDS1
     C                     MOVELS1RECV    TXRVID
     C                     MOVELS1SND     TXSALE
     C                     MOVELS1KIND    TXSATP
     C                     MOVELS1CHK     TXIVTP
     C                     MOVELS4PDNM    TXPDNM
     C                     Z-ADDWQTY      TXQTY
     C                     Z-ADDS1UPRC    TXUPRC
     C           S1UPRC    MULT 100000    TPRC    70
     C                     Z-ADDWAMTO     TXAMT
     C                     MOVELTXAR      TXTXAR
9908AC*                    Z-ADDUDATE     TXTXDT
9908AC                     Z-ADDU#SYSD    TXTXDT
     C                     MOVELTPRC      TXRESV
CLJ  C                     MOVEL'*'       D#TXRV           已出貨未傳
     C                     WRITETXREC
     CSR                   ENDSR
     C*-------------------------------------------------------*
     CSR         OU#01     BEGSR
     C           LCNT      IFGT TCNT
     C                     ADD  1         PAG
     C                     EXCPTEH#01
     C                     Z-ADD10        LCNT
     C                     END
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C*-------------------------------------------------------*
     OPRINT1  E  201           EH#01
     O                                   11 '報表編號:'
     O                                 +  1 'SAR04'
     O                                   56 '* 應收帳款過帳單 *'
     O                                   98 '印表日期'
     O                                   99 ':'
9908AO*                        UDATE Y  108
9908AO                         U#SYSD   110 '    /  /  '
     O        E  1             EH#01
     O                                   99 '頁　　次:'
     O                         PAG   Z +  1
     O        E  1             EH#01
     O                                   11 '截止日期:'
9710 O                         YMD     +  1 '    /  /  '
     O        E  1             EH#01
     O                         ALS      108
     O        E  1             EH#01
     O                                   10 '出貨日期'
     O                                   20 '磅單編號'
     O                                   30 '客戶代號'
     O                                   40 '客戶名稱'
     O                                   50 '訂單號碼'
     O                                   56 '品名'
     O                                   70 '重　量'
     O                                   72 'kg'
     O                                   80 '單價'
     O                                   94 '金　　額'
     O                                  108 '小　　計'
     O        E 1              EH#01
     O                         ALS      108
     O        E 1              ED#01
     O                         S4DATEY   10
     O                         S4VREA    11
     O                         SPEC      13
     O                         S4VRNO    18
     O                                   19 '-'
     O                         S4VRTM    20
     O                         W1CUNO    28
     O                         S4CUST    40
     O                         WORNO1    41
     O                         WORNO2    46
     O                                   47 '-'
     O                         WORNO3    50
0311AO                         S4PDNM  +  0
     O                         WQTY  K   70
     O                         S1UPRCK   80
     O                         WAMTP K   94
     O        E 01             ET#01
     O                         WAMTT K  108
     O        E  2             ET#02
     O                                   10 '銷貨合計'
     O                                   11 ':'
     O                         WAMTTT2   25
     O                                   92 '應收合計'
     O                                   93 ':'
     O                         WAMTTRK  108
     OPRINT   E  103   33
     O                                    6 'RPG :'
     O                                 +  1 'SAR04R-2'
     O                                   55 '*** 單價錯誤清單 ***'           X
     O                                   90 '印表日期'
     O                                   91 ':'
9908AO*                        UDATE Y  100
9908AO                         U#SYSD   102 '    /  /  '
     O        E  1     33
     O                                    8 '日　期'
     O                                   21 '磅單號碼'
     O                                   41 '訂單號碼'
     O                                   60 '數　　量'
     O                                   80 '備　　註'
     O        E  1     33
     O                                    8 '--------'
     O                                   21 '----------'
     O                                   41 '----------'
     O                                   60 '----------'
     O                                   80 '----------'
     O        E  1     90
     O                         S4DATEY   10
     O                         S4VREA    15
     O                         SPEC      17
     O                         S4VRNO    22
     O                         S4ORNO    40
     O                         S4NWGTK   60
     O                 35                80 '單價＝０'
     O                 36                80 '無此訂單'
     O                 37                80 '超過入帳日'
9710 O                 37      YMD1      90 '    /  /  '
