     H        1   Y                                     1                 BR003
     FARE009S CF  E                    WORKSTN
     F                                        RR1   KSFILE SF1
     F                                        RR3   KSFILE SF3
     FRCVDTL  UF  E           K        DISK                      A
     FRCADTL  UF  E           K        DISK                      A
     F*APTAL  IF  E           K        DISK
     FBANKAC  IF  E           K        DISK
     F*ENSEQ  UF  E           K        DISK                      A
     FBCNAME  IF  E           K        DISK
     FACDEPT  IF  E           K        DISK
     FACCNTL  IF  E           K        DISK
     E*************************************************************
     E                    ARRX    1  11  1
     E                    ARRY    1  20  2
     E                    ERR     1  46 70
     I*************************************************************
     IACREC
     I              ACTYPE                          ACTYP1
     I              ACNAME                          ACNAM1
     IAADS        DS
     I                                       21  30 SBID
     I                                       21  23 SBID1
     I                                       24  27 SBID2
     I                                       28  30 SBID3
     I                                       31  40 SANO
     I                                       31  31 SANO1
     I                                       32  32 SANO2
     I                                       33  40 SANO3
     IBBDS        DS
     I                                        1  10 GRP
     I                                        1   1 GE1
     I                                        2  10 GE2
     I                                       11  16 NOG
     I                                       11  11 NOA
     I                                       12  160NOB
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C*          GEKEY     KLIST
     C*                    KFLD           GEKIND
     C*                    KFLD           GEPRIN
     C           RVKEY     KLIST
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C           RAKEY     KLIST
     C                     KFLD           RANO
     C                     KFLD           RAITEM
     C*          BAKEY     KLIST
     C*                    KFLD           SBID1
     C*                    KFLD           SBID2
     C           ACKEY     KLIST
     C                     KFLD           SANO1
     C                     KFLD           SANO2
     C                     KFLD           SANO3
     C**************************************************************
     C  N90                MOVEL''      OE      1
     C  N90                MOVE ''      OF      1
     C  N90                MOVE '1'       SCRN    1
     C  N90                MOVE '1'       *IN,90
     C**************************************************************
     C           *INKC     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
     C           SCRN      CASEQ'3'       SR#03
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     CSR         SR#01     BEGSR
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C                     EXSR CK#01
     C  N99                EXSR PR#02
     C  N99                MOVE '2'       SCRN
     CSR         SR#01Z    ENDSR
     C******
     CSR         SR#02     BEGSR
     C                     WRITEUND01
     C                     EXFMTSFCTL1
     C   KC                EXSR KC#01
     C   KL                MOVE '1'       SCRN
     C   KL                Z-ADD0         RR1
     C   KL                Z-ADD0         RR3
     C   KL                MOVE *BLANK    ERRMSG
     C   KL                GOTO SR#02Z
     C   KJ                EXSR KJ#02
     C   KJ                GOTO SR#02Z
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C                     ELSE
     C                     MOVE '3'       SCRN
     C                     EXSR PR#03
     C                     ENDIF
     CSR         SR#02Z    ENDSR
     C*****
     CSR         SR#03     BEGSR
     C                     WRITEUND03
     C                     EXFMTSFCTL3
     C   KL                MOVE '2'       SCRN
     C   KL                MOVE *BLANK    ERRMSG
     C   KL                GOTO SR#03Z
     C   KJ                EXSR KJ#03
     C   KJ                GOTO SR#03Z
     C   KC                EXSR KC#01
     C   KC                GOTO SR#03Z
     C           CODE      IFEQ '4'
     C                     MOVE '1'       SCRN
     C                     Z-ADD0         RR1
     C                     Z-ADD0         RR3
     C                     ENDIF
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#03
     C                     ENDIF
     CSR         SR#03Z    ENDSR
     C*****
     CSR         KC#01     BEGSR
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C****
     CSR         KJ#02     BEGSR
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C                     ENDIF
     C  N99                MOVE '3'       SCRN
     C  N99                EXSR PR#03
     CSR         KJ#02Z    ENDSR
     C****
     CSR         KJ#03     BEGSR
     C           CODE      IFNE '3'
     C           CODE      ANDNE'4'
     C                     EXSR CK#03
     C                     ENDIF
     C  N99      CODE      IFNE '4'
     C                     EXSR FL#04
     C                     ENDIF
     C  N99                MOVE '1'       SCRN
     C  N99                Z-ADD0         RR1
     C  N99                Z-ADD0         RR3
     CSR         KJ#03Z    ENDSR
     C****
     CSR         CK#01     BEGSR
     C                     SETOF                     414299
     C                     SETOF                     7170
     C                     MOVE *BLANK    ERRMSG
     C                     SELEC
     C           CODE      WHEQ ' '
     C                     SETON                     4199
     C                     MOVE ERR,1     ERRMSG
     C           CODE      WHEQ '1'
     C                     MOVE '新增'  MOD
     C                     MOVE '0'       *IN71
     C           CODE      WHEQ '2'
     C                     MOVE '修改'  MOD
     C                     MOVE '0'       *IN71
     C           CODE      WHEQ '3'
     C                     MOVE '刪除'  MOD
     C                     MOVE '1'       *IN71
     C           CODE      WHEQ '4'
     C                     MOVE '查詢'  MOD
     C                     MOVE '1'       *IN71
     C                     OTHER
     C                     SETON                     4199
     C                     MOVE ERR,2     ERRMSG
     C                     ENDSL
     C******
     C*          CODE      IFEQ '2'
     C*          CODE      OREQ '3'
     C                     MOVELNO        NOAREA  1
     C           NOAREA    IFNE TXAR
     C                     SETON                     4299
     C                     MOVE ERR,34    ERRMSG
     C                     ENDIF
     C*                    ENDIF
     C**
     C*          CODE      IFEQ '1'
     C*          NO        ANDNE*BLANK
     C*                    SETON                     4299
     C*                    MOVE ERR,31    ERRMSG
     C*                    ENDIF
     C**
     C*          CODE      IFNE '1'
     C           NO        IFEQ *BLANK
     C                     SETON                     4299
     C                     MOVE ERR,3     ERRMSG
     C                     ENDIF
     C****
     C**
     C           *IN99     IFEQ '0'
     C                     MOVE NO        RVNO
     C                     Z-ADD1         RVITEM
     C           RVKEY     CHAINRCVDTL              N97
     C                     SELEC
     C           *IN97     WHEQ '0'
     C           RVTYPE    ANDNE'3'
     C                     SETON                     4299
     C                     MOVE ERR,41    ERRMSG
     C           CODE      WHEQ '1'
     C           *IN97     ANDEQ'0'
     C                     SETON                     4299
     C                     MOVE ERR,4     ERRMSG
     C           CODE      WHNE '1'
     C           *IN97     ANDEQ'1'
     C                     SETON                     4299
     C                     MOVE ERR,5     ERRMSG
     C*          CODE      WHEQ '2'
     C*          *IN97     ANDEQ'0'
     C*          RVDECD    ANDNE' '
     C*                    SETON                     4299
     C*                    MOVE ERR,6     ERRMSG
     C           CODE      WHEQ '3'
     C           *IN97     ANDEQ'0'
     C           RVDECD    ANDNE' '
     C                     SETON                     4299
     C                     MOVE ERR,7     ERRMSG
     C                     ENDSL
     C                     ENDIF
     CSR         CK#01Z    ENDSR
     C******
     CSR         PR#02     BEGSR
     C                     MOVE *BLANK    ERRMSG
     C                     SETON                     80
     C                     WRITESFCTL1
     C                     SETOF                     80
     C                     Z-ADD0         RR1     20
     C                     MOVEA'000000'  *IN,43
     C                     MOVEA'00000000'*IN,51
     C                     MOVEA'0000000' *IN,59
     C                     MOVE *BLANK    CUNM
     C                     MOVE *BLANK    DPNO
     C                     Z-ADDUDATE     DATE
     C                     Z-ADD0         TTLAMT
     C**
     C           CODE      CASEQ'1'       P2#ADD
     C                     CAS            P2#OTH
     C                     ENDCS
     CSR         PR#02Z    ENDSR
     C******
     CSR         P2#ADD    BEGSR
     C*                    EXSR @GETNO
     C                     CLEARSF1
     C           RR1       DOUGE90
     C                     ADD  1         RR1
     C                     Z-ADDRR1       ITEM
     C                     WRITESF1
     C                     ENDDO
     C                     SETON                     10
     CSR         P2#ADZ    ENDSR
     C******
     CSR         @GETNO    BEGSR
     C*                    MOVEL'04'      GEKIND
     C*                    MOVE *BLANK    GEPRIN
     C*                    MOVE *BLANK    GRP
     C*                    MOVELTXAR      GE1
     C*                    MOVELGRP       GEPRIN
     C*          GEKEY     CHAINGENSEQ              N97
     C*  97                Z-ADD10000     GECUNO
     C*                    MOVELTXAR      NOA
     C*          GECUNO    ADD  1         NOB
     C*                    MOVELNOG       NO
     CSR                   ENDSR
     C***
     CSR         P2#OTH    BEGSR
     C                     MOVELRVCUNM    CUNM
     C                     MOVELRVDPNO    DPNO
     C                     Z-ADDRVDATE    DATE
     C**
     C                     MOVE NO        RVNO
     C                     Z-ADD0         RVITEM
     C           RVKEY     SETLLRCVDTL               97
     C  N97      NO        READERCVDTL              N    97
     C           RR1       DOWLE39
     C           *IN97     ANDEQ'0'
     C                     CLEARSF1
     C   71                SETON                     70
     C                     ADD  1         RR1
     C           RR1       IFNE RVITEM
     C           RR1       DOWLTRVITEM
     C                     WRITESF1
     C                     ADD  1         RR1
     C                     ENDDO
     C                     ENDIF
     C           RVFLAG    IFNE 'D'
     C           RVACTP    IFEQ 'C'
     C                     SUB  RVRAMT    TTLAMT
     C                     ELSE
     C                     ADD  RVRAMT    TTLAMT
     C                     ENDIF
     C                     ENDIF
     ****
     C                     MOVELRVFLAG    FLAG
     C                     MOVELRVITEM    ITEM
     C                     MOVELRVNTTP    NTTP
     C                     MOVELRVUSTP    USTP
     C                     MOVELRVACTP    ACTP
     C                     MOVELRVACNO    ACNO
     C                     MOVELRVPBID    PBID
     C                     MOVELRVPANO    PANO
     C                     MOVELRVPLAC    PLAC
     C                     MOVELRVNTNO    NTNO
     C                     Z-ADDRVRAMT    RAMT
     C                     MOVELRVRLNO    RLNO
     C                     MOVELRVSBID    SBID
     C                     MOVELRVSANO    SANO
     C                     Z-ADDRVDUDT    DUDT
     C*                    MOVELRVAMCD    AMCD
     C                     WRITESF1
     C           NO        READERCVDTL              N    97
     C                     ENDDO
     C****
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C           RR1       OREQ 0
     C                     CLEARSF1
     C   71                SETON                     70
     C           RR1       DOWLE39
     C                     ADD  1         RR1
     C                     Z-ADDRR1       ITEM
     C                     WRITESF1
     C                     ENDDO
     C                     ENDIF
     C   71                SETON                     70
     C****
     C                     SETON                     10
     CSR         P2#OTZ    ENDSR
     C******
     CSR         CK#02     BEGSR
     C                     SETOF                     9996
     C                     MOVEA'000000'  *IN,43
     C                     MOVE *BLANK    ERRMSG
     C                     MOVE *BLANK    PBIDT  14
     C                     MOVE *BLANK    PANOT  10
     C                     MOVE *BLANK    PLACT   1
     C                     MOVE *BLANK    USTPT   2
     C                     MOVE *BLANK    NTTPT   1
     C                     Z-ADD0         TTLAMT
     C***
     C                     SELEC
     C           DATE      WHEQ 0
     C                     SETON                     9945
     C                     MOVELERR,18    ERRMSG
     C                     OTHER
     C*                    CALL 'C01'
     C*                    PARM           DATE    60
     C*                    PARM ' '       FLAG1   1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DATE      P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           FLAG1   1
     C           FLAG1     IFNE '0'
     C                     SETON                     9945
     C                     MOVELERR,18    ERRMSG
     C                     ENDIF
     C                     ENDSL
      **
     C                     Z-ADD0         RR1
     C           RR1       DOWLT90
     C                     MOVEA'00000000'*IN,51
     C                     MOVEA'0000000' *IN,59
     C                     ADD  1         RR1
     C           RR1       CHAINSF1                  97
     C   97                LEAVE
     C   21                MOVELPBIDT     PBID
     C                     MOVELPBID      PBIDT
     C   22                MOVELPANOT     PANO
     C                     MOVELPANO      PANOT
     C   23                MOVELPLACT     PLAC
     C                     MOVELPLAC      PLACT
     C   24                MOVELNTTPT     NTTP
     C                     MOVELNTTP      NTTPT
     C   25                MOVELUSTPT     USTP
     C                     MOVELUSTP      USTPT
     C           NTTP      IFEQ *BLANK
     C           USTP      ANDEQ*BLANK
     C           ACTP      ANDEQ*BLANK
     C           ACNO      ANDEQ*BLANK
     C           FLAG      OREQ 'D'
     C                     UPDATSF1
     C                     ITER
     C                     ENDIF
     C***
     C                     SETON                     96
     C                     SELEC
     C*          NTTP      WHEQ ' '
     C*                    SETON                     9951
     C*                    MOVE ERR,9     ERRMSG
     C           NTTP      WHNE ' '
     C           NTTP      LOKUPARRX                     97
     C  N97                SETON                     9951
     C  N97                MOVE ERR,10    ERRMSG
     C                     ENDSL
     C***
     C                     SELEC
     C*          USTP      WHEQ '  '
     C*                    SETON                     9952
     C*                    MOVE ERR,11    ERRMSG
     C           USTP      WHNE '  '
     C           USTP      LOKUPARRY                     97
     C  N97                SETON                     9952
     C  N97                MOVE ERR,12    ERRMSG
     C                     ENDSL
     C***
     C           ACTP      IFEQ ' '
     C                     MOVEL'D'       ACTP
     C                     ENDIF
     C           ACTP      IFNE 'C'
     C           ACTP      ANDNE'D'
     C                     SETON                     9953
     C                     MOVE ERR,19    ERRMSG
     C                     ENDIF
     C***
     C                     SELEC
     C           NTTP      WHGE 'A'
     C           NTTP      ANDLE'E'
     C                     MOVEL'1131'    ACNO
     C           NTTP      WHEQ 'I'
     C                     MOVEL'1131'    ACNO
     C*          NTTP      WHEQ 'F'
     C*          NTTP      WHEQ 'G'
     C*          NTTP      OREQ 'J'
     C*          NTTP      WHEQ 'J'
     C*                    MOVEL'1112'    ACNO
     C           NTTP      WHEQ 'H'
     C                     MOVEL'1111'    ACNO
     C                     ENDSL
     C****
     C                     SELEC
     C           ACNO      WHEQ *BLANK
     C                     SETON                     9954
     C                     MOVELERR,20    ERRMSG
     C           ACNO      WHNE *BLANK
     C           ACNO      CHAINBCNAME               95
     C   95                SETON                     9954
     C   95                MOVELERR,20    ERRMSG
     C                     ENDSL
     C***
     C           ACNO      IFEQ '2221'
     C           RLNO      ANDEQ*BLANK
     C                     SETON                     9960
     C                     MOVELERR,43    ERRMSG
     C                     ENDIF
     C****
     C           NTTP      IFGE 'A'
     C           NTTP      ANDLE'E'
     C           NTTP      OREQ 'I'
     C                     SELEC
     C           PLAC      WHNE 'D'
     C           PLAC      ANDNE'E'
     C                     SETON                     9957
     C                     MOVELERR,21    ERRMSG
     C           NTNO      WHEQ *BLANK
     C                     SETON                     9958
     C                     MOVELERR,22    ERRMSG
     C           DUDT      WHEQ 0
     C                     SETON                     9963
     C                     MOVELERR,23    ERRMSG
     C           DUDT      WHNE 0
     C*                    CALL 'C01'
     C*                    PARM           DUDT
     C*                    PARM ' '       FLAG1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DUDT      P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE
     C                     PARM '1'       P#MODE
     C                     PARM           P#MTL
     C                     PARM           P#LY
     C                     PARM           FLAG1
     C           FLAG1     IFNE '0'
     C                     SETON                     9963
     C                     MOVELERR,24    ERRMSG
     C                     ENDIF
     C                     ENDSL
     C                     ENDIF
     ***
     C           RAMT      IFEQ 0
     C                     SETON                     9959
     C                     MOVELERR,25    ERRMSG
     C                     ENDIF
     ***
     C*          SBID      IFNE *BLANK
     C*          BAKEY     CHAINBANKID               97
     C*  97                SETON                     9961
     C*  97                MOVELERR,27    ERRMSG
     C*                    ENDIF
     ****
     C                     MOVEL*BLANK    SBID
     C           SANO      IFNE *BLANK
     C           ACKEY     CHAINBANKAC               97
     C   97                SETON                     9962
     C   97                MOVELERR,28    ERRMSG
     C  N97                MOVELACBAID    SBID
     C* N97      ACCURR    IFNE 'N'
     C*          NTTP      ANDNE'F'
     C*                    MOVEL'1114'    ACNO
     C*                    ENDIF
      *
     C*          NTTP      IFEQ 'F'
     C*                    MOVEL'1131'    ACNO
     C*                    ENDIF
     C                     ENDIF
     ****:
     C*          AMCD      IFEQ *BLANK
     C*                    SETON                     9965
     C*                    MOVELERR,29    ERRMSG
     C*                    ELSE
     C*          AMCD      CHAINCAPTAL               97
     C*  97                SETON                     9965
     C*  97                MOVELERR,30    ERRMSG
     C*                    ENDIF
     C           ACTP      IFEQ 'C'
     C                     SUB  RAMT      TTLAMT
     C                     ELSE
     C                     ADD  RAMT      TTLAMT
     C                     ENDIF
     C                     UPDATSF1
     C***
     C                     ENDDO
     C           *IN96     IFEQ '0'
     C                     SETON                     99
     C                     MOVELERR,40    ERRMSG
     C                     ENDIF
     C******
     CSR         CK#02Z    ENDSR
     C******
     CSR         PR#03     BEGSR
     C                     MOVE *BLANK    ERRMSG
     C                     Z-ADD0         TAAMT
     C                     SETOF                     99
     C           RR3       IFNE 0
     C                     GOTO PR#03Z
     C                     ENDIF
     C**
     C                     SETON                     80
     C                     WRITESFCTL3
     C                     SETOF                     80
     C                     Z-ADD0         RR3     20
     C                     CLEARSF3
     C***
     C           CODE      CASEQ'1'       P3#ADD
     C                     CAS            P3#OTH
     C                     ENDCS
     ***
     CSR         PR#03Z    ENDSR
     C**
     C******
     CSR         P3#ADD    BEGSR
     C                     Z-ADD0         RR3     20
     C**
     C           RR3       DOWLE39
     C                     ADD  1         RR3
     C                     CLEARSF3
     C                     Z-ADDRR3       ITEM
     C                     WRITESF3
     C                     ENDDO
     C                     SETON                     10
     C**
     CSR         P3#ADZ    ENDSR
     C******
     CSR         P3#OTH    BEGSR
     C                     Z-ADD0         RR3     20
     C                     Z-ADD0         TAAMT
     C**
     C                     MOVELNO        RANO
     C                     Z-ADD0         RAITEM
     C           RAKEY     SETLLRCADTL               97
     C           *IN97     DOWEQ'0'
     C  N97      NO        READERCADTL              N    97
     C   97                LEAVE
     C                     CLEARSF3
     C   71                SETON                     70
     C                     ADD  1         RR3
     C                     Z-ADDRR3       ITEM
     C                     MOVELRAACTP    DCCD
     C                     MOVELRAACNO    GLNO
     C                     MOVELRADEPT    DEPT
     C                     MOVELRARLNO    RLNO
     C                     MOVELRAMUID    MUID
     C                     MOVELRAWAYC    WAYC
     C                     MOVELRACOMM    COMM
     C                     Z-ADDRAAAMT    AAMT
     C                     Z-ADDRADUDT    DUE1
     C                     MOVELRARESV    BNNM
     C*                    MOVELRAAMCD    AMCE
     C                     MOVELRAFLAG    FLAG
     C           FLAG      IFNE 'D'
     C           RAACTP    IFEQ 'C'
     C                     SUB  RAAAMT    TAAMT
     C                     ELSE
     C                     ADD  RAAAMT    TAAMT
     C                     ENDIF
     C                     ENDIF
     C                     WRITESF3
     C                     ENDDO
     C***
     C           CODE      IFEQ '2'
     C           RR3       OREQ 0
     C           RR3       DOWLE39
     C                     ADD  1         RR3
     C                     CLEARSF3
     C   71                SETON                     70
     C                     Z-ADDRR3       ITEM
     C                     WRITESF3
     C                     ENDDO
     C                     ENDIF
     C                     SETON                     10
     C   71                SETON                     70
     C*****
     CSR         P3#OTZ    ENDSR
     C******
     CSR         CK#03     BEGSR
     C                     SETOF                     996667
     C                     Z-ADD0         TAAMT
     C                     MOVE *BLANK    COMMT  30
     C                     MOVE *BLANK    ERRMSG
     C***
     C                     Z-ADD0         RR3
     C           RR3       DOWLE39
     C                     MOVEA'00000000'*IN,31
     C                     MOVEA'00'      *IN,39
     C                     ADD  1         RR3
     C           RR3       CHAINSF3                  97
     C   97                LEAVE
     C   22                MOVELCOMMT     COMM
     C                     MOVELCOMM      COMMT
     C           DCCD      IFEQ *BLANK
     C           GLNO      ANDEQ*BLANK
     C           FLAG      OREQ 'D'
     C                     UPDATSF3
     C                     ITER
     C                     UPDATSF3
     C                     ENDIF
     C                     SELEC
     C           DCCD      WHEQ ' '
     C                     SETON                     9931
     C                     MOVELERR,20    ERRMSG
     C           DCCD      WHNE 'C'
     C           DCCD      ANDNE'D'
     C                     SETON                     9931
     C                     MOVELERR,20    ERRMSG
     C                     ENDSL
     C***
     C           GLNO      CHAINBCNAME               95
     C   95                SETON                     9932
     C   95                MOVE ERR,20    ERRMSG
     C***
     C           WAYC      CHAINACCNTL               95
     C   95                SETON                     9933
     C   95                MOVE ERR,35    ERRMSG
     C  N95                EXSR CHKWAY
     ****
     C           AAMT      IFLE 0
     C                     SETON                     9937
     C                     MOVELERR,39    ERRMSG
     C                     ELSE
     C           DCCD      IFEQ 'C'
     C                     ADD  AAMT      TAAMT
     C                     ELSE
     C                     SUB  AAMT      TAAMT
     C                     ENDIF
     C                     ENDIF
     C***
     C*          AMCE      IFEQ *BLANK
     C*                    SETON                     9938
     C*                    MOVELERR,29    ERRMSG
     C*                    ELSE
     C*          AMCE      CHAINCAPTAL               97
     C*  97                SETON                     9938
     C*  97                MOVELERR,30    ERRMSG
     C*                    ENDIF
     C           BNNM      IFNE *BLANK
     C                     MOVELBNNM      SANO
     C           ACKEY     CHAINBANKAC               97
     C   97                SETON                     9938
     C   97                MOVELERR,28    ERRMSG
     C                     ENDIF
     C                     UPDATSF3
     C                     ENDDO
     C  N99      TAAMT     IFNE TTLAMT
     C                     SETON                     99
     C                     MOVELERR,14    ERRMSG
     C                     ENDIF
     CSR         CK#03Z    ENDSR
     C**-----------------------------------------------**
     C**   沖帳方式檢核                              **
     C**-----------------------------------------------**
     CSR         CHKWAY    BEGSR
     C           ACCODP    IFEQ '0'
     C           DEPT      COMP *BLANK               9090
     C   90                SETON                     9934
     C   90                MOVE ERR,36    ERRMSG
     C                     ELSE
     C           DEPT      COMP *BLANK                   90
     C   90                SETON                     3499
     C   90                MOVE ERR,36    ERRMSG
     C  N90      DEPT      CHAINACDEPT               95
     C  N90 95             SETON                     9934
     C  N90 95             MOVE ERR,36    ERRMSG
     C                     END
     ****
     C           ACCORL    IFEQ '1'
     C           RLNO      COMP *BLANK                   90
     C   90                SETON                     9935
     C   90                MOVE ERR,37    ERRMSG
     C                     ELSE
     C           RLNO      COMP *BLANK               9090
     C   90                SETON                     9935
     C   90                MOVE ERR,37    ERRMSG
     C  N90                MOVELRLNO      RLS     1
     C  N90                MOVE RLNO      RLE     1
     C  N90      RLS       IFEQ OE
     C           RLS       ANDNEOF
     C                     SETON                     9935
     C                     MOVE ERR,42    ERRMSG
     C                     END
     C                     END
     C***
     C           ACCOCU    IFEQ '1'
     C           MUID      COMP *BLANK                   90
     C   90                SETON                     9936
     C   90                MOVE ERR,38    ERRMSG
     C                     ELSE
     C           MUID      COMP *BLANK               9090
     C   90                SETON                     9936
     C   90                MOVE ERR,38    ERRMSG
     C                     END
     C***
     C           ACCOYD    IFEQ '1'
     C           DUE1      COMP 0                        90
     C   90                SETON                     9940
     C   90                MOVE ERR,44    ERRMSG
     C* N90                CALL 'C01'
     C*                    PARM           DUE1
     C*                    PARM ' '       FLAG1
     C* N90                FREE 'C01'
     C  N90                MOVE *ALL'0'   P#DATE
     C  N90                MOVE DUE1      P#DATE
     C  N90                CALL 'UTS102R'
     C                     PARM           P#DATE
     C                     PARM '1'       P#MODE
     C                     PARM           P#MTL
     C                     PARM           P#LY
     C                     PARM           FLAG1
     C  N90      FLAG1     IFNE '0'
     C                     SETON                     9940
     C                     MOVELERR,45    ERRMSG
     C                     ENDIF
     C                     ELSE
     C           DUE1      COMP 0                    9090
     C   90                SETON                     9940
     C   90                MOVE ERR,46    ERRMSG
     C                     ENDIF
     CSR         CHKTYZ    ENDSR
     C******
     CSR         FL#04     BEGSR
     C           CODE      CASEQ'3'       DL#04
     C           CODE      CASEQ'2'       UP#04
     C           CODE      CASEQ'1'       WR#04
     C                     ENDCS
     CSR         FL#04Z    ENDSR
     C****
     CSR         WR#04     BEGSR
     C***
     C*                    CLEARGEREC
     C*                    MOVEL'04'      GEKIND
     C*                    MOVE *BLANK    GEPRIN
     C*                    MOVE *BLANK    GRP
     C*                    MOVELTXAR      GE1
     C*                    MOVELGRP       GEPRIN
     C*          GEKEY     CHAINGENSEQ               97
     C*  97                Z-ADD10001     GECUNO
     C*  97                WRITEGEREC
     C* N97                ADD  1         GECUNO
     C* N97                UPDATGEREC
     C*                    MOVELTXAR      NOA
     C*                    Z-ADDGECUNO    NOB
     C*                    MOVELNOG       NO
     C***
     C                     Z-ADD0         RR1
     C                     SETOF                     95
     C           *IN95     DOWEQ'0'
     C                     ADD  1         RR1
     C           RR1       CHAINSF1                  95
     C   95                LEAVE
     C           NTTP      IFEQ *BLANK
     C           USTP      ANDEQ*BLANK
     C           ACTP      ANDEQ*BLANK
     C           ACNO      ANDEQ*BLANK
     C           FLAG      OREQ 'D'
     C                     ITER
     C                     ENDIF
     C***
     C                     EXSR WR#RV
     C******
     C                     ENDDO
     C******
     C                     Z-ADD0         RR3
     C                     SETOF                     95
     C           *IN95     DOWEQ'0'
     C                     ADD  1         RR3
     C           RR3       CHAINSF3                  95
     C   95                LEAVE
     C           DCCD      IFEQ *BLANK
     C           GLNO      ANDEQ*BLANK
     C           FLAG      OREQ 'D'
     C                     ITER
     C                     ENDIF
     C***
     C                     EXSR WR#RA
     C                     ENDDO
     C                     MOVELNO        NOG
     C                     ADD  1         NOB
     C                     MOVELNOG       NO
     CSR         WR#04Z    ENDSR
     C******
     CSR         DL#04     BEGSR
     C                     EXSR DL#RV
     C                     EXSR DL#RA
     C****
     CSR         DL#04Z    ENDSR
     *****
     CSR         UP#04     BEGSR
     C***
     C                     Z-ADD0         RR1
     C                     SETOF                     95
     C           *IN95     DOWEQ'0'
     C                     ADD  1         RR1
     C           RR1       CHAINSF1                  95
     C   95                LEAVE
     C                     MOVE NO        RVNO
     C                     Z-ADDRR1       RVITEM
     C           RVKEY     CHAINRCVDTL               96
     C           NTTP      IFEQ *BLANK
     C           USTP      ANDEQ*BLANK
     C           ACNO      ANDEQ*BLANK
     C           ACTP      ANDEQ*BLANK
     C           FLAG      OREQ 'D'
     C   96                ITER
     C                     MOVE 'D'       RVFLAG
     C                     MOVE 'D'       RVDECD
     C                     Z-ADDUDATE     RVDEDT
     C                     MOVE TXAR      RVTXAR
     C                     Z-ADDUDATE     RVTXDT
     C                     UPDATRVREC
     C                     ELSE
     C  N96                EXSR UP#RV
     C   96                EXSR WR#RV
     C                     ENDIF
     C***
     C                     ENDDO
     C******
     C                     Z-ADD0         RR3
     C                     SETOF                     95
     C           *IN95     DOWEQ'0'
     C                     ADD  1         RR3
     C           RR3       CHAINSF3                  95
     C   95                LEAVE
     C                     MOVELNO        RANO
     C                     Z-ADDRR3       RAITEM
     C           RAKEY     CHAINRCADTL               96
     C           DCCD      IFEQ *BLANK
     C           GLNO      ANDEQ*BLANK
     C           FLAG      OREQ 'D'
     C   96                ITER
     C                     MOVE 'D'       RAFLAG
     C                     MOVE TXAR      RATXAR
     C                     Z-ADDUDATE     RATXDT
     C                     UPDATRAREC
     C                     ELSE
     C  N96                EXSR UP#RA
     C   96                EXSR WR#RA
     C                     ENDIF
     C***
     C                     ENDDO
     C***
     CSR         UP#04Z    ENDSR
     C***
     CSR         WR#RV     BEGSR
     C                     CLEARRVREC
     C                     MOVE 'A'       RVFLAG
     C                     MOVE NO        RVNO
     C                     Z-ADDRR1       RVITEM
     C                     Z-ADDDATE      RVDATE
     C                     MOVELCUNM      RVCUNM
     C                     MOVE '3'       RVTYPE
     C                     MOVELDPNO      RVDPNO
     C                     MOVELUSTP      RVUSTP
     C                     MOVELNTTP      RVNTTP
     C                     MOVELPBID      RVPBID
     C                     MOVELPANO      RVPANO
     C                     MOVELACTP      RVACTP
     C                     MOVELACNO      RVACNO
     C                     MOVELPLAC      RVPLAC
     C                     MOVELNTNO      RVNTNO
     C                     Z-ADDDUDT      RVDUDT
     C                     Z-ADDRAMT      RVRAMT
     C                     MOVELSBID      RVSBID
     C                     MOVELSANO      RVSANO
     C*                    MOVELAMCD      RVAMCD
     C                     MOVELRLNO      RVRLNO
     C                     MOVELTXAR      RVTXAR
     C                     MOVELTXAR      RVAREA
     C                     Z-ADDUDATE     RVTXDT
     C                     WRITERVREC
     CSR         WR#RVZ    ENDSR
     C******
     CSR         WR#RA     BEGSR
     C                     CLEARRAREC
     C                     MOVE 'A'       RAFLAG
     C                     MOVELNO        RANO
     C                     Z-ADDRR3       RAITEM
     C                     MOVELDCCD      RAACTP
     C                     MOVELGLNO      RAACNO
     C                     MOVELWAYC      RAWAYC
     C                     MOVELDEPT      RADEPT
     C                     MOVELRLNO      RARLNO
     C                     MOVELMUID      RAMUID
     C                     MOVELCOMM      RACOMM
     C                     Z-ADDDUE1      RADUDT
     C                     MOVELBNNM      RARESV
     C*                    MOVELAMCE      RAAMCD
     C                     Z-ADDAAMT      RAAAMT
     C                     MOVELTXAR      RATXAR
     C                     Z-ADDUDATE     RATXDT
     C                     WRITERAREC
     CSR         WR#RAZ    ENDSR
     C******
     CSR         UP#RV     BEGSR
     C                     MOVE 'C'       RVFLAG
     C                     Z-ADDDATE      RVDATE
     C                     MOVELCUNM      RVCUNM
     C                     MOVE *BLANK    RVDECD
     C                     Z-ADD0         RVDEDT
     C                     MOVE '3'       RVTYPE
     C                     MOVELDPNO      RVDPNO
     C                     MOVELUSTP      RVUSTP
     C                     MOVELNTTP      RVNTTP
     C                     MOVELPBID      RVPBID
     C                     MOVELPANO      RVPANO
     C                     MOVELACTP      RVACTP
     C                     MOVELACNO      RVACNO
     C                     MOVELPLAC      RVPLAC
     C                     MOVELNTNO      RVNTNO
     C                     Z-ADDDUDT      RVDUDT
     C                     Z-ADDRAMT      RVRAMT
     C                     MOVELSBID      RVSBID
     C                     MOVELSANO      RVSANO
     C*                    MOVELAMCD      RVAMCD
     C                     MOVELRLNO      RVRLNO
     C                     MOVELTXAR      RVTXAR
     C                     Z-ADDUDATE     RVTXDT
     C                     UPDATRVREC
     CSR         UP#RVZ    ENDSR
     *****
     CSR         UP#RA     BEGSR
     C                     MOVEL'C'       RAFLAG
     C                     MOVELDCCD      RAACTP
     C                     MOVELGLNO      RAACNO
     C                     MOVELWAYC      RAWAYC
     C                     MOVELDEPT      RADEPT
     C                     MOVELRLNO      RARLNO
     C                     MOVELMUID      RAMUID
     C*                    MOVELAMCE      RAAMCD
     C                     MOVELCOMM      RACOMM
     C                     Z-ADDDUE1      RADUDT
     C                     MOVELBNNM      RARESV
     C                     Z-ADDAAMT      RAAAMT
     C                     MOVELTXAR      RATXAR
     C                     Z-ADDUDATE     RATXDT
     C                     UPDATRAREC
     CSR         UP#RAZ    ENDSR
     C***
     CSR         DL#RV     BEGSR
     C                     MOVE NO        RVNO
     C                     Z-ADD0         RVITEM
     C           RVKEY     SETLLRCVDTL               97
     C           *IN97     DOWEQ'0'
     C           NO        READERCVDTL                   97
     C   97                LEAVE
     C                     MOVE 'D'       RVDECD
     C                     Z-ADDUDATE     RVDEDT
     C                     MOVEL'D'       RVFLAG
     C                     MOVE TXAR      RVTXAR
     C                     Z-ADDUDATE     RVTXDT
     C                     UPDATRVREC
     C                     ENDDO
     C****
     CSR         DL#RVZ    ENDSR
     C****
     CSR         DL#RA     BEGSR
     C                     MOVE NO        RANO
     C                     Z-ADD0         RAITEM
     C           RAKEY     SETLLRCADTL               97
     C           *IN97     DOWEQ'0'
     C  N97      NO        READERCADTL                   97
     C   97                LEAVE
     C                     MOVEL'D'       RAFLAG
     C                     MOVE TXAR      RATXAR
     C                     Z-ADDUDATE     RATXDT
     C                     UPDATRAREC
     C                     ENDDO
     C****
     CSR         DL#RAZ    ENDSR
     C****
** ARRX
A
B
C
D
E
F
G
H
I
J
K
** ARRY
A1
A2
A3
A4
A5
B1
B2
C1
C2
C3
D1
D2
D3
D4
D5
Z1
Z2
Z3
Z4
Z5
** ERR
０１－功能代碼必須輸入．
０２－功能代碼必須為１，２，３，４
０３－繳款號碼必須輸入　　　　
０４－繳款號碼已存在，不可新增
０５－繳款號碼不存在
０６－此張繳款已作廢，不可再修改
０７－此張繳款已作廢，不可再作廢
０８－客戶編號不存在
０９－票款別必須輸入
１０－無此種票款別　　　　　
１１－款項別必須輸入
１２－無此種款項別　　　　　
１３－發票號碼不存在　　　　
１４－繳款總金額不等於沖銷總金額
１５－此張發票已繳完款，不可再沖銷
１６－發票號碼重複
１７－繳款日期必須輸入
１８－繳款日期錯誤
１９－借貸別錯誤，必須為Ｃ或Ｄ
２０－會計科目錯誤
２１－付款地必須為Ｄ，Ｅ
２２－支票號碼必須輸入
２３－到期日期必須輸入
２４－到期日期錯誤
２５－繳款金額不可為０
２６－訂單編號不存在
２７－銀行代號不存在
２８－帳戶編號不存在
２９－資金代號必須輸入
３０－資金代號不存在
３１－新增時，繳款號碼不可輸入　　　　
３２－此張發票未收餘額比沖銷金額還小，不可沖銷
３３－此張發票之客戶編號不等於繳款客戶編號
３４－此張繳款單非屬本廠區．
３５－沖帳方式錯誤
３６－部門代號錯誤
３７－相關號碼錯誤
３８－對象別　錯誤
３９－金額　　錯誤
４０－明細項至少必須輸入一筆
４１－此張繳款單非本程式輸入，請查核
４２－相關號碼若為中文，則必須輸滿
４３－會計科目為存入保證時，相關號碼必須為訂單編號．
４４－此控制方式需輸入到期日期，請檢核．
４５－此控制方式需輸入到期日期，但到期日期不合邏輯，請查核
４６－此控制方式不需輸入到期日期，請檢核．
