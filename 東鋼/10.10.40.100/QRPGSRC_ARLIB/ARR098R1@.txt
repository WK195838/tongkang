     H*****************************************************************
     H*
     H*            >>   INTRODUCTION PROGRAM  <<
     H*
     H*            1.PROGRAM-ID     ARR098R1
     H*            2.PROGAM-TYPE    RPG
     H*            3.AUTHOR         S02CSF
     H*            4.FUNCTION      自製出貨分析表
     H*            5.DATE-WRITTEN   91/09/24
     H*            6.DATE-MODIFY
     H*
     H*            *將銷貨彙總表的資料寫入ARR098F1,辨識碼=1
     H*****************************************************************
     H        1   Y                                     1
     FAC255PF IP  E           K        DISK
     FARR098F1O   E           K        DISK
     IRAC255PF    01
     I                                              A1PRNOL1
     C*****************************************************************
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     MOVE *ON       *IN99
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     CLEARRAR098F1
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     ADD  A1SQTY    F1QTY4           數量
     C                     ADD  A1SAMT    F1AMT4           金額
     C                     ADD  A1RQTY    F1QTY2           退貨數量
     C                     ADD  A1RAMT    F1AMT2           退貨金額
     C                     ADD  A1FAMT    F1AMT3           折讓金額
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C                     MOVEL'1'       F1CODE           辨別碼
     C                     MOVELA1AREA    F1AREA           廠區
     C                     MOVELA1PRNO    F1PDNM           品名
     C*
     C           F1QTY4    SUB  F1QTY2    F1QTY            銷貨淨數量
     C           F1AMT4    SUB  F1AMT2    W#AMT  110
     C           W#AMT     SUB  F1AMT3    F1AMT            銷貨淨收入
     C*
     C                     WRITERAR098F1
     C*
     C                     ENDSR
