     A*****************************************************************
     A*
     A*            *****   PROGRAM INTRODUCTION   *****
     A*
     A*            1.PROGRAM-ID    ARP139RC
     A*            2.PROGRAM-TYPE  RPG
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     台北INEDINT-->桃園、苗栗、高雄
     A*            5.DATE-WRITTEN  107/12/07 2018AR00031
     A*            6.UPDATE  DATE  108/05/09 S00WCJ (0805A)
     A*                             增加寫入LOG檔
     A*
     A*****************************************************************
     H            Y                                     1                 BR100
     FINEDINT UP  E           K        DISK
     F            RINEDIN                           KRENAMEDINECD
     FINEDIN  UF  E           K        DISK                      A
0805AFINEDING O   E           K        DISK
0805AF            RINEDIN                           KRENAMEDING
     IDINECD      01
     I              DNFLAG                          FLAG
     I              DNAREA                          AREA
     I              DNMSTE                          MSTE
     I              DNNO                            NO
     I              DNINDT                          INDT
     I              DNBYID                          BYID
     I              DNSLID                          SLID
     I              DNDEDT                          DEDT
     I              DNDETM                          DETM
     I              DNCLRN                          CLRN
     I              DNRTDN                          RTDN
     I              DNREMK                          REMK
     I              DNTMDT                          TMDT
     I              DNTMTM                          TMTM
     I              DNTMOP                          TMOP
     I              DNTAWS                          TAWS
     I              DNSTCL                          STCL
     I              DNXPDD                          XPDD
     I              DNXPDT                          XPDT
     I              DNXUSR                          XUSR
     I              DNMAR1                          MAR1
     I              DNPTYN                          PTYN
     I           UDS
     I                                        6   6 AREA
0805AI                                     10011010 U#USID
     C           KEY       KLIST
     C                     KFLD           NO
     C*
     C                     MOVEA'0000'    *IN,11
     C           FLAG      COMP 'A'                      11
     C           FLAG      COMP 'C'                      12
     C           FLAG      COMP 'D'                      13
     C*
     C           KEY       CHAINRINEDIN              31
     C                     EXSR MOVE1
     C           *IN31     IFEQ '1'
     C           *IN11     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     WRITERINEDIN
0805AC                     EXSR MOVE2
0805AC                     WRITEDING
     C                     END
     C                     END
     C           *IN31     IFEQ '0'
     C           *IN11     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     UPDATRINEDIN
0805AC                     EXSR MOVE2
0805AC                     WRITEDING
     C                     END
     C                     END
     C*
     C                     DELETDINECD
     C*
     C****************************************************************
     C           MOVE1     BEGSR
     C****************************************************************
     C                     Z-ADDTMDT      DNTMDT
     C                     Z-ADDTMTM      DNTMTM
     C                     MOVELTMOP      DNTMOP
     C                     MOVELTAWS      DNTAWS
     C                     MOVELSTCL      DNSTCL
     C                     ENDSR
     C**************************************************************
0805AC           MOVE2     BEGSR
0805AC**************************************************************
0805AC                     MOVELFLAG      DNFLAG
0805AC                     MOVELAREA      DNAREA
0805AC                     MOVELMSTE      DNMSTE
0805AC                     MOVELNO        DNNO
0805AC                     MOVELINDT      DNINDT
0805AC                     MOVELBYID      DNBYID
0805AC                     MOVELSLID      DNSLID
0805AC                     MOVELDEDT      DNDEDT
0805AC                     MOVELDETM      DNDETM
0805AC                     MOVELCLRN      DNCLRN
0805AC                     MOVELRTDN      DNRTDN
0805AC                     MOVELREMK      DNREMK
0805AC                     Z-ADDTMDT      DNTMDT
0805AC                     Z-ADDTMTM      DNTMTM
0805AC                     MOVELTMOP      DNTMOP
0805AC                     MOVELTAWS      DNTAWS
0805AC                     MOVELSTCL      DNSTCL
0805AC                     Z-ADDXPDD      DNXPDD
0805AC                     Z-ADDXPDT      DNXPDT
0805AC                     MOVELXUSR      DNXUSR
0805AC                     MOVELMAR1      DNMAR1
0805AC                     MOVELPTYN      DNPTYN
0805AC                     MOVEL'ARP139RC'DNAPNM
0805AC           *DATE     SUB  19000000  DNCHDD
0805AC                     TIME           DNCHDT
0805AC                     MOVELU#USID    DNCUSR
0805AC                     ENDSR
