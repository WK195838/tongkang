     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR001R5
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION     請款單記錄磅單日期區間
     H*            5.DATE-WRITTEN  100/03/15
     H*            6.DATE-UPDATED  104/03/25 S00WCJ (0403A)
     H*                             避免磅單重覆抓錯日期，增加訂單檢
     H*                             核
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR001F IP  E           K        DISK
     FTRNDTL  IF  E           K        DISK
     I*
     ILOCAL      UDS
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 U#USID
     I                                      300 300 U#OPT
     I                                      311 3180D#DATS
     I                                      321 3280D#DATE
     C**************************************************************
     C*          KEY     LIST
     C*****************************************************************
     C           KEY01     KLIST
     C                     KFLD           W#CODE  4
     C                     KFLD           W#TXNO  8
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C*
     C                     MOVEL'SA04'    W#CODE
     C                     MOVELARWTNO    W#TXNO
     C           *IN90     IFEQ *OFF
     C           *NAMVAR   DEFN *LDA      LOCAL
     C                     IN   LOCAL
     C                     Z-ADD0         D#DATS
     C                     Z-ADD0         D#DATE
     C                     MOVEL*ON       *IN90
     C                     ENDIF
     C                     MOVEL*OFF      *IN40
     C           KEY01     SETLLTRNDTL
     C           *IN40     DOWEQ*OFF
     C           KEY01     READETRNDTL                   40
     C   40                LEAVE
0403AC           ARORNO    IFNE TXORNO
0403AC                     ITER
0403AC                     ENDIF
     C           D#DATS    IFEQ 0
     C           D#DATE    ANDEQ0
     C                     Z-ADDTXDATE    D#DATS
     C                     Z-ADDTXDATE    D#DATE
     C                     ENDIF
     C           TXDATE    IFLT D#DATS
     C                     Z-ADDTXDATE    D#DATS
     C                     ENDIF
     C           TXDATE    IFGT D#DATE
     C                     Z-ADDTXDATE    D#DATE
     C                     ENDIF
     C                     ENDDO
     C                     OUT  LOCAL
