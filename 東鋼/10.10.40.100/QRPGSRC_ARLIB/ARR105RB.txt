     H*****************************************************************
     H*
     H*          >>  PROGRAM INTRODUCTION  <<
     H*          1.PROGRAM-ID    ARR105RB
     H*          2.PROGRAM-TYPE  RPG/400
     H*          3.AUTHOR        S02CSF
     H*          4.FUNCTION     應收帳款承購狀況彙總表（營業）
     H*          5.DATE-WRITTEN  92/06/06
     H*          6.UPDATE  DATE  99/09/03  2010AR517  S00WCJ (9909A)
     H*
     H*****************************************************************
     H        1   Y                                     1
     FARCUDT  IP  E           K        DISK
     FARR105TBO   E             66     PRINTER
     E*****************************************************************
     IRARCUDT
     I                                              ACBKIDL1
     I           UDS
     I                                     10011010 U#USID
     I                                     10111020 S#DEVN
     I                                      316 320 U#CUNO
     I                                      321 323 U#BKID
     I*****************************************************************
     C*         主程式開始
     C*****************************************************************
9909AC           *DATE     SUB  19000000  U#SYSD  80
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     MOVE *ON       *IN99
     C                     MOVE *ON       *IN66
     C*
     C                     MOVEL*ALL'='   T#LIN1
     C                     MOVEL*ALL'='   T#LIN2
     C                     MOVEL*ALL'-'   T#LIN3
     C*
     C                     SELEC
     C           U#BKID    WHEQ *BLANK
     C                     MOVEL'全部'  T#BKNM
     C           U#BKID    WHEQ '807'
     C                     MOVEL'建華'  T#BKNM
     C           U#BKID    WHEQ '814'
     C                     MOVEL'大眾'  T#BKNM
     C                     ENDSL
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR105T1S
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     CLEARAR105T1D
     C*
     C*承購止日等於０，或承購止日大於當日
     C*
     C           ACDAT2    IFEQ 0
9909AC           ACDAT2    ORGT U#SYSD
     C*
     C                     MOVELACCUNO    T#CUNO           客戶代號
     C                     MOVELACCUNM    T#CUNM           客戶名稱
     C*                    MOVELACBKNM    T#BKNM           承購銀行
     C                     Z-ADDACAAMT    T#AAMT           承購額度（Ａ）
     C                     Z-ADDACBAMT    T#BAMT           融資額度
     C                     Z-ADDACDAT2    T#DAT2           合約到期日
     C                     Z-ADDACAAM1    T#AAM1           已承購金額（Ｂ）
     C           T#AAMT    SUB  T#AAM1    T#KAMT           尚可承購額度
     C                     Z-ADDACCAMT    T#CAMT           已融資金額（Ｄ）
     C                     Z-ADDACHAMT    T#HAMT           逾期未兌現金額
     C                     Z-ADDACGAMT    T#GAMT           可動撥餘額
     C                     Z-ADDACIAMT    T#IAMT           帳上餘額
     C*
     C   66                WRITEAR105T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR105T1D
     C*小計
     C                     ADD  T#AAMT    T#SAMT           承購額度
     C                     ADD  T#BAMT    T#BAMS           融資額度
     C                     ADD  T#AAM1    T#AAMS           已承購金額
     C                     ADD  T#KAMT    T#KAMS           尚可承購額度
     C                     ADD  T#CAMT    T#CAMS           已融資金額
     C                     ADD  T#HAMT    T#HAMS           逾期未兌現金額
     C                     ADD  T#GAMT    T#GAMS           可動撥餘額
     C                     ADD  T#IAMT    T#IAMS           帳上餘額
     C*
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C   66                WRITEAR105T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR105T1L
     C                     WRITEAR105T1S
     C*總計
     C                     ADD  T#SAMT    T#AATT           承購額度
     C                     ADD  T#BAMS    T#BAMA           融資額度
     C                     ADD  T#AAMS    T#AAMA           已承購金額
     C                     ADD  T#KAMS    T#KAMA           尚可承購額度
     C                     ADD  T#CAMS    T#CAMA           已融資金額
     C                     ADD  T#HAMS    T#HAMA           逾期未兌現金額
     C                     ADD  T#GAMS    T#GAMA           可動撥餘額
     C                     ADD  T#IAMS    T#IAMA           帳上餘額
     C*
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C   66                WRITEAR105T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR105T1A
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C*
     C   66                WRITEAR105T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR105T1M
     C*
     C                     ENDSR
