     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARE035R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     發票維護作業異常處理版
     H*            5.DATE-WRITTEN  86/03/29
     H*            6.DATE-MODUFIED 90/06/01 BY S02YSH
     H*            P.S. 1外銷發票不檢核單價與金額之間的關係900601
     H*                            99/08/23 2010AR517 S00WCJ (9908A)
     H*                           100/05/04 S00WCJ (0005A)
     H*                            異動日期改為8碼
     H*                           101/05/16 S00WCJ (0105A)
     H*                            請款日期改為8碼
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARE035S CF  E                    WORKSTN
     F                                        RRN1  KSFILE AR035F1
     F                                        RRN2  KSFILE AR035F2
     FINVMSTL4IF  E           K        DISK
     F            INREC                             KRENAMEINRECW
     FINVMST  UF  E           K        DISK
     FINVDTL  UF  E           K        DISK
     FARALOG  O   E           K        DISK
     E*************************************************************
     E                    T#ERR   1  11 70
     I*************************************************************
     I           UDS
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     I                                     10011001 D#USID
     I            DS
     I                                        1  140W#SYST
     I                                        1   60D#ST
     I                                        7  100D#SY
     I                                       11  140D#SMD
     C**************************************************************
     C*   檔案搜尋欄位組合
     C**************************************************************
     C           K#IN      KLIST                           發票主檔
9009 C                     KFLD           F#INDT  80
     C                     KFLD           F#INNO 10
     C           K#IV      KLIST                           發票明細
     C                     KFLD           F#IVNO 10
     C                     KFLD           F#IVAC  1
     C                     KFLD           F#IVIT  20
     C**************************************************************
     C*   主程式開始
     C**************************************************************
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C           W#PRID    CASEQ'02'      SR2000           畫面二
     C           W#PRID    CASEQ'03'      SR3000           畫面三
     C           W#PRID    CASEQ'04'      SR4000           畫面四
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C*   副程式開始
     C**************************************************************
     C*----------------------------------------
     C*  宣告及初始變數
     C*----------------------------------------
     CSR         SR0000    BEGSR
     C                     MOVEL'01'      W#PRID  2        函式代號
     C                     MOVEL'F'       W#RTNV  1        函式返回值
     C                     Z-ADD13        N#PAG1  20       SF1筆數
     C                     MOVEL'F'       W#SFE1  1        SF1結束
     C                     MOVEL'F'       W#SFB1  1        SF1開頭
     C                     Z-ADD0         W#TMPA 110       金額暫
     C                     Z-ADD0         RRN1    40
     C                     Z-ADD0         RRN2    40
     C*
     C                     SELEC                           設定檢核度
     C           D#USID    WHEQ 'S'
     C                     Z-ADD0         W#CKLV  10
 9303C                     MOVE *OFF      *IN62
     C           D#USID    WHEQ 'B'                        營業課
     C                     Z-ADD2         W#CKLV
 9303C                     MOVE *ON       *IN62
     C                     OTHER                           其他財會課
     C                     Z-ADD1         W#CKLV
 9303C                     MOVE *ON       *IN62
     C                     ENDSL
     C*
     C                     MOVEL*BLANK    S#INK1 10        發票號碼
9009 C                     Z-ADD0         S#INK2  80       發票日期
     C                     MOVEL*BLANK    S#INK3  1        發票廠區
     C                     EXSR SR1100                     初始畫面一
     CSR                   ENDSR
     C*
     C************************************
     C*  畫面一:選擇發票號碼
     C************************************
     CSR         SR1000    BEGSR
     C                     WRITEAR035F1M
     C           S#SFN1    IFEQ 0
     C                     SETOF                     7273
     C                     SETON                     71
     C                     ELSE                            有資料
     C                     SETOF                     73
     C                     SETON                     7172
     C                     ENDIF
     C           W#SFE1    IFEQ 'T'
     C                     SETON                     74
     C                     ELSE
     C                     SETOF                     74
     C                     ENDIF
     C                     EXFMTAR035F1C
     C*
     C                     SETOF                     81
     C                     MOVEL*BLANK    S#MSG1
     C           S#CRN1    IFNE 0
     C                     Z-ADDS#CRN1    S#NBR1
     C                     ENDIF
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C           *IN12     OREQ '1'
     C                     MOVEL'00'      W#PRID
     C           *IN91     WHEQ '1'                        向下翻頁
     C                     EXSR SR1200
     C           *IN92     WHEQ '1'                        向上翻頁
     C                     EXSR SR1300
     C           *IN05     WHEQ '1'                        重新顯示
     C                     EXSR SR1400
     C*
     C                     OTHER                           執行鍵
     C           S#INK1    IFNE *BLANK                     指定記錄
     C           S#INK2    ORNE 0
     C           S#INK3    ORNE *BLANK
     C                     EXSR SR1100
     C*
     C                     ELSE                            執行選擇
     C                     EXSR SR1500
     C           W#SCN1    IFEQ 'F'
     C                     SETON                     81
     C                     ENDIF
     C                     ENDIF
     C                     ENDSL
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  初始畫面一(指向指定的記錄)
     C*----------------------------------------
     CSR         SR1100    BEGSR
     C                     Z-ADD0         S#SFN1
     C                     MOVELT#ERR,1   S#MSG1           查無資料
     C                     SETON                     51
     C*
     C           S#INK1    IFEQ *BLANK                     初始值找
     C           S#INK2    ANDEQ0
     C           S#INK3    ANDEQ*BLANK
     C                     SETON                     81     PC
     C           *LOVAL    SETLLINRECW               69
     C           *IN69     IFEQ '1'
     C                     GOTO ES1100
     C                     ENDIF
     C*
     C                     ELSE
     C           S#INK1    IFNE *BLANK                     輸入號碼找
     C           S#INK1    CHAININREC               N69
     C           *IN69     IFEQ '1'
     C                     GOTO ES1100
     C                     ENDIF
     C                     Z-ADDININDT    F#INDT
     C                     MOVELINNO      F#INNO
     C           K#IN      SETLLINRECW               69
     C*
     C                     ELSE                            輸入日期找
     C           S#INK2    IFNE 0
     C                     Z-ADDS#INK2    W#INK2  80
     C           W#INK2    SETLLINRECW               69
     C           *IN69     IFEQ '1'
     C                     GOTO ES1100
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C*
     C           S#INK1    IFEQ *BLANK                     非號碼
     C           S#INK3    ANDNE*BLANK                     找廠區
     C                     READ INRECW                   69
     C           *IN69     DOWEQ'0'
     C           INAREA    IFEQ S#INK3
     C                     LEAVE
     C                     ENDIF
     C                     READ INRECW                   69
     C                     ENDDO
     C           *IN69     IFEQ '1'
     C                     GOTO ES1100
     C                     ENDIF
     C                     READPINRECW                   69
     C                     ENDIF
     C*          -------------------------------
     C                     SETOF                     51    成功
     C                     MOVEL*BLANK    S#MSG1
     C                     MOVEL*BLANK    S#INK1
     C                     Z-ADD0         S#INK2
     C                     MOVEL*BLANK    S#INK3
     C*
     C                     EXSR SR1010                     清除SF
     C                     EXSR SR1020                     讀入SF
     C                     Z-ADD1         S#NBR1           指向第一筆
     CSR         ES1100    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一清除SF
     C*----------------------------------------
     CSR         SR1010    BEGSR
     C                     MOVEL*BLANK    S#MSG1
     C                     SETOF                     717251
     C                     SETON                     73    清除 SF
     C                     WRITEAR035F1C
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一向下讀入SF (從目前的檔案位置)
     C*----------------------------------------
     CSR         SR1020    BEGSR
     C                     Z-ADD0         S#SFN1
     C                     Z-ADD0         RRN1
     C*
     C           1         DOWEQ1                          讀取迴圈
     C                     READ INRECW                   69
     C           *IN69     IFEQ '1'
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ADD  1         S#SFN1
     C           S#SFN1    IFGT N#PAG1                     已滿一頁
     C                     Z-ADDN#PAG1    S#SFN1
     C                     LEAVE
     C                     ENDIF
     C                     ADD  1         RRN1
     C                     CLEARAR035F1
     C                     MOVELINAREA    S#INAR           搬移資料
     C                     MOVELINTYPE    S#INTP
     C                     MOVELINNO      S#INNO
     C                     Z-ADDININDT    S#INDT
     C                     MOVELINKIND    S#INKD
     C                     MOVELINDECD    S#INDE
     C                     Z-ADDINAAMT    S#INAM
     C                     Z-ADDINATAX    S#INTX
     C                     WRITEAR035F1
     C                     ENDDO                           讀取迴圈
     C*
     C           *IN69     IFEQ '1'
     C                     MOVEL'T'       W#SFE1           已達檔底
     C                     ELSE
     C                     MOVEL'F'       W#SFE1           未達檔底
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一向上移動記錄(從目前的檔案位置)
     C*----------------------------------------
     CSR         SR1030    BEGSR
     C                     Z-ADD0         S#SFN1
     C           1         DOWEQ1
     C                     READPINRECW                   69
     C           *IN69     IFEQ '1'
     C                     LEAVE
     C                     ENDIF
     C                     ADD  1         S#SFN1
     C           S#SFN1    IFGT N#PAG1                     多一筆
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C*
     C           *IN69     IFEQ '1'
     C           *LOVAL    SETLLINRECW               69
     C                     MOVEL'T'       W#SFB1           已達檔頭
     C                     ELSE
     C                     MOVEL'F'       W#SFB1           未達檔頭
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一向下翻一頁
     C*----------------------------------------
     CSR         SR1200    BEGSR
     C           W#SFE1    IFEQ 'T'
     C                     MOVELT#ERR,2   S#MSG1           已達檔底
     C                     GOTO ES1200
     C                     ENDIF
     C*
     C           S#SFN1    IFNE 0                          本頁有資料
     C                     Z-ADDS#SFN1    RRN1
     C           RRN1      CHAINAR035F1              69
     C                     Z-ADDS#INDT    F#INDT
     C                     MOVELS#INNO    F#INNO
     C           K#IN      CHAININRECW               69    移至本頁尾
     C                     ENDIF                           本頁有資料
     C*
     C                     EXSR SR1010                     清除SF
     C                     EXSR SR1020                     讀入SF
     C           S#NBR1    IFGT S#SFN1
     C                     Z-ADD1         S#NBR1
     C                     ENDIF
     CSR         ES1200    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一向上翻一頁
     C*----------------------------------------
     CSR         SR1300    BEGSR
     C           S#SFN1    IFNE 0                          本頁有資料
     C                     Z-ADD1         RRN1
     C           RRN1      CHAINAR035F1              69
     C                     Z-ADDS#INDT    F#INDT
     C                     MOVELS#INNO    F#INNO
     C           K#IN      CHAININRECW               69    移至本頁頭
     C                     ENDIF                           本頁有資料
     C*
     C                     EXSR SR1030                     向前移動
     C                     EXSR SR1010                     清除SF
     C                     EXSR SR1020                     讀入SF
     C*
     C           W#SFB1    IFEQ 'T'
     C                     MOVELT#ERR,3   S#MSG1           己達檔頭
     C                     ENDIF
     CSR         ES1300    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一重新顯示
     C*----------------------------------------
     CSR         SR1400    BEGSR
     C           S#SFN1    IFEQ 0                          本頁無資料
     C                     GOTO ES1400
     C                     ENDIF
     C*
     C                     Z-ADD1         RRN1
     C           RRN1      CHAINAR035F1              69
     C                     Z-ADDS#INDT    F#INDT
     C                     MOVELS#INNO    F#INNO
     C           K#IN      CHAININRECW               69    移至本頁頭
     C*
     C                     READPINRECW                   69前移一筆
     C           *IN69     IFEQ '1'
     C           *LOVAL    SETLLINRECW
     C                     ENDIF
     C*
     C                     EXSR SR1010                     清除SF
     C                     EXSR SR1020                     讀入SF
     CSR         ES1400    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一執行選擇
     C*----------------------------------------
     CSR         SR1500    BEGSR
     C                     MOVEL'F'       W#SCN1  1
     C                     Z-ADD0         RRN1             指向選擇的
     C                     DO   S#SFN1
     C                     ADD  1         RRN1
     C           RRN1      CHAINAR035F1              69
     C                     SELEC
     C*
     C           S#OPT1    WHEQ ' '
     C                     SETOF                     52
     C                     UPDATAR035F1
     C*
     C           S#OPT1    WHEQ '1'
     C                     MOVEL'T'       W#SCN1
     C                     MOVEL*BLANK    S#OPT1           清除選項
     C                     SETOF                     52
     C                     UPDATAR035F1
     C                     EXSR SR2100                     初始畫面二
     C                     MOVEL'02'      W#PRID
     C                     Z-ADDRRN1      S#NBR1
     C                     LEAVE
     C*
     C           S#OPT1    WHEQ '2'
     C                     MOVEL'T'       W#SCN1
     C*
     C           W#CKLV    IFGT 0                          非系統工程
     C           S#INAR    ANDNEU#AREA                     且非本廠
     C*
     C           W#CKLV    IFEQ 1                          台北財務可
     C           U#AREA    ANDEQ'P'
     C                     MOVEL*BLANK    S#OPT1           清除選項
     C                     SETOF                     52
     C                     UPDATAR035F1
     C                     EXSR SR3100                     初始畫面三
     C                     MOVEL'03'      W#PRID
     C*
     C                     ELSE
     C                     MOVELT#ERR,4   S#MSG1           非本廠區
     C                     SETON                     52
     C                     UPDATAR035F1
     C                     ENDIF
     C*
     C                     ELSE                            本廠或系工
     C                     MOVEL*BLANK    S#OPT1           清除選項
     C                     SETOF                     52
     C                     UPDATAR035F1
     C                     EXSR SR3100                     初始畫面三
     C                     MOVEL'03'      W#PRID
     C                     ENDIF
     C*
     C                     Z-ADDRRN1      S#NBR1
     C                     LEAVE
     C                     ENDSL
     C                     ENDDO
     CSR                   ENDSR
     C*
     C************************************
     C*  畫面二:發票明細選擇主函式
     C************************************
     CSR         SR2000    BEGSR
     C                     WRITEAR035F2M
     C           S#SFN2    IFEQ 0
     C                     SETOF                     7273
     C                     SETON                     7174
     C                     ELSE                            有資料
     C                     SETOF                     73
     C                     SETON                     717274
     C                     ENDIF
     C                     EXFMTAR035F2C                   螢幕輸入
     C*
     C                     MOVEL*BLANK    S#MSG2
     C           S#CRN2    IFNE 0
     C                     Z-ADDS#CRN2    S#NBR2           記錄位置
     C                     ENDIF
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C                     MOVEL'00'      W#PRID           結束
     C           *IN12     WHEQ '1'
     C                     MOVEL'01'      W#PRID           回前畫面
     C*
     C                     OTHER                           執行鍵
     C                     Z-ADD0         RRN2             指向選擇的
     C                     DO   S#SFN2
     C                     ADD  1         RRN2
     C           RRN2      CHAINAR035F2              69
     C*
     C                     SELEC
     C           S#OPT2    WHEQ ' '
     C                     SETOF                     52
     C                     UPDATAR035F2
     C           S#OPT2    WHEQ '2'
     C           W#CKLV    IFGT 0
     C           S#INAR    ANDNEU#AREA
     C           W#CKLV    IFEQ 1                          台北財務可
     C           U#AREA    ANDEQ'P'
     C                     MOVEL*BLANK    S#OPT2           清除選項
     C                     SETOF                     52
     C                     UPDATAR035F2
     C                     EXSR SR4100                     初始畫面四
     C                     MOVEL'04'      W#PRID
     C                     ELSE
     C                     MOVELT#ERR,4   S#MSG2           非本廠區
     C                     SETON                     52
     C                     UPDATAR035F2
     C                     ENDIF
     C                     ELSE
     C                     MOVEL*BLANK    S#OPT2           清除選項
     C                     SETOF                     52
     C                     UPDATAR035F2
     C                     EXSR SR4100                     初始畫面四
     C                     MOVEL'04'      W#PRID
     C                     ENDIF
     C                     Z-ADDRRN2      S#NBR2
     C                     LEAVE
     C                     ENDSL
     C*
     C                     ENDDO
     C                     ENDSL
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  初始畫面二
     C*----------------------------------------
     CSR         SR2100    BEGSR
     C           S#INNO    CHAININREC               N69
     C                     MOVELINCUNO    S#CUNO           客戶編號
     C                     MOVELINCUNM    S#CUNM           簡稱
     C                     MOVELINORNO    S#ORNO           訂單
     C                     MOVELINTYPE    S#TYPE           發票類別
     C                     MOVELININDT    S#INDT           發票日期
     C                     MOVELINTXTP    S#TXTP           課稅別
     C                     MOVELINKIND    S#KIND           發票聯式
     C                     MOVELINSATP    S#SATP           銷售別
     C                     MOVELINSALE    S#SALE           出貨員
     C                     MOVELINRVID    S#RVID           收款員
     C                     MOVELINAREA    S#AREA           開立廠區
     C*
     C                     MOVEL*BLANK    S#MSG2
     C                     Z-ADD1         S#NBR2
     C                     SETOF                     7172
     C                     SETON                     73    清除 SF
     C                     WRITEAR035F2C
     C*          -------------------------------讀入畫面
     C                     Z-ADD0         S#SFN2
     C                     Z-ADD0         RRN2
     C           S#INNO    SETLLIVREC                69
     C*
     C           *IN69     DOWNE'1'
     C           S#INNO    READEIVREC               N    69
     C           *IN69     IFEQ '1'
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ADD  1         RRN2
     C                     CLEARAR035F2
     C                     MOVELIVDECD    S#DECD           作廢
     C                     MOVELIVACNT    S#ACNT           類別
     C                     Z-ADDIVITEM    S#ITEM           項次
     C                     MOVELIVPDCD    S#PDNM           品名
     C                     Z-ADDIVQTY     S#QTY            數量
     C                     Z-ADDIVUPRC    S#UPRC           單價
     C                     Z-ADDIVAMT     S#AMT            金額
     C                     MOVELIVAPNO    S#APNO           憑証編號
     C                     WRITEAR035F2
     C                     ADD  1         S#SFN2
     C                     ENDDO
     CSR                   ENDSR
     C*
     C************************************
     C*  畫面三:發票主檔編輯主函式
     C************************************
     CSR         SR3000    BEGSR
     C                     WRITEAR035F3M
     C                     EXFMTAR035F3H                   螢幕輸入
     C*
     C                     MOVEL*BLANK    S#MSG3
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C                     MOVEL'00'      W#PRID           結束
     C           *IN12     WHEQ '1'
     C                     MOVEL'01'      W#PRID           回前畫面
     C           *IN10     WHEQ '1'
     C                     EXSR SR3300                     存檔
     C*
     C           *IN91     WHEQ '1'                        向下翻頁
     C           W#CKLV    ANDEQ0
     C                     Z-ADDININDT    F#INDT
     C                     MOVELINNO      F#INNO
     C           K#IN      CHAININRECW               69
     C                     READ INRECW                   69
     C           *IN69     IFEQ '1'
     C           K#IN      CHAININRECW               69    還原
     C                     MOVELT#ERR,2   S#MSG3
     C                     ENDIF
     C                     MOVELINNO      F#INNO
     C           F#INNO    CHAININREC               N69
     C                     EXSR SR3110                     計算折扣
     C*
     C           *IN92     WHEQ '1'                        向上翻頁
     C           W#CKLV    ANDEQ0
     C                     Z-ADDININDT    F#INDT
     C                     MOVELINNO      F#INNO
     C           K#IN      CHAININRECW               69
     C                     READPINRECW                   69
     C           *IN69     IFEQ '1'
     C           K#IN      CHAININRECW               69    還原
     C                     MOVELT#ERR,3   S#MSG3
     C                     ENDIF
     C                     MOVELINNO      F#INNO
     C           F#INNO    CHAININREC               N69
     C                     EXSR SR3110                     計算折扣
     C*
     C                     OTHER
     C                     EXSR SR3200                     試算
     C                     ENDSL
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  初始畫面三
     C*----------------------------------------
     CSR         SR3100    BEGSR
     C*                    SETOF                     6162
     C*          W#CKLV    IFEQ 1                          財會
     C*                    SETON                     62
     C*                    ENDIF
     C*          W#CKLV    IFEQ 2                          營業
     C*                    SETON                     6162
     C*                    ENDIF
     C*
     C           S#INNO    CHAININREC               N69
     C                     EXSR SR3110                     計算折扣
     CSR                   ENDSR
     C*
     C*----------------------------------------
     CSR         SR3110    BEGSR
     C*
     C                     Z-ADD0         S#GAMT           折扣金額
     C           W#CKLV    IFEQ 0
     C                     MOVELINNO      F#IVNO           發票號碼
     C                     ELSE
     C           W#CKLV    IFEQ 1
     C                     MOVELS#INNO    F#IVNO
     C                     ENDIF
     C                     ENDIF
     C                     MOVEL*BLANK    F#IVAC           發票類別
     C                     Z-ADD0         F#IVIT           發票項次
     C           K#IV      SETLLIVREC
     C                     MOVE *OFF      *IN50
     C           *IN50     DOWEQ*OFF
     C                     READ IVREC                    50
     C   50                LEAVE
     C*
     C           W#CKLV    IFEQ 0
     C           IVNO      IFNE INNO
     C                     LEAVE
     C                     ENDIF
     C                     ELSE
     C           IVNO      IFNE S#INNO
     C                     LEAVE
     C                     ENDIF
     C                     ENDIF
     C*
     C           IVACNT    IFEQ '3'
     C           IVFL03    ANDEQ'K'
     C                     ADD  IVAMT     S#GAMT           折扣金額
     C                     ENDIF
     C*
     C                     ENDDO
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  試算畫面三
     C*----------------------------------------
     CSR         SR3200    BEGSR
     C                     MOVEL'F'       W#ISTX  1        是否應稅
     C                     Z-ADDINAAMT    W#AAMT 110       暫存金額
     C                     Z-ADDINATAX    W#ATAX 110
     C                     Z-ADDINBAMT    W#BAMT 110
     C                     Z-ADDINCAMT    W#CAMT 110
     C                     Z-ADDINCTAX    W#CTAX 110
     C                     Z-ADDINDAMT    W#DAMT 110
     C                     Z-ADDINDTAX    W#DTAX 110
     C                     Z-ADDINEAMT    W#EAMT 110
     C                     Z-ADDINFAMT    W#FAMT 110
     C                     Z-ADDS#GAMT    W#GAMT 110       折扣金額
     C*
     C                     Z-ADD0         INAAMT           出貨金額
     C                     Z-ADD0         INATAX           出貨稅額
     C                     Z-ADD0         INBAMT           扣預收
     C                     Z-ADD0         INCAMT           折退金額
     C                     Z-ADD0         INCTAX           折退稅額
     C                     Z-ADD0         INDAMT           扣預金沖回
     C                     Z-ADD0         INDTAX           扣預稅沖回
     C                     Z-ADD0         INEAMT           繳款金額
     C                     Z-ADD0         INFAMT           退票金額
     C                     Z-ADD0         INNBAL           未收餘額
     C                     Z-ADD0         S#GAMT           折扣金額
     C*
     C           INNO      CHAINIVREC               N69
     C           *IN69     DOWEQ'0'
     C           INDECD    IFEQ 'D'                        已作廢全計
     C           IVFLAG    ORNE 'D'                        刪除者不計
     C           IVDECD    ANDNE'D'
     C                     SELEC
     C*
     C           IVACNT    WHEQ '1'                        出貨
     C                     ADD  IVAMT     INAAMT
     C           IVACNT    WHEQ '2'                        退貨
     C                     ADD  IVAMT     INCAMT
     C           IVACNT    WHEQ '3'                        折讓
     C           IVFL03    ANDNE'K'
     C                     ADD  IVAMT     INCAMT
     C           IVACNT    WHEQ '3'                        折扣
     C           IVFL03    ANDEQ'K'
     C                     ADD  IVAMT     S#GAMT
     C           IVACNT    WHEQ '4'
     C           IVAMT     IFLT 0                          負數
     C           INTYPE    IFEQ '2'                        預收發票
     C                     ADD  IVAMT     INCAMT           折預收
     C                     ELSE
     C                     ADD  IVAMT     INBAMT           扣預收
     C                     ENDIF
     C                     ELSE                            正數
     C           INTYPE    IFEQ '2'                        預收發票
     C                     ADD  IVAMT     INAAMT           預收金額
     C                     ELSE
     C                     ADD  IVAMT     INDAMT           扣預收沖回
     C                     ENDIF
     C                     ENDIF
     C           IVACNT    WHEQ '5'
     C                     MOVEL'T'       W#ISTX           含有稅項
     C                     SELEC
     C           IVPDCD    WHEQ '   '                      出貨稅額
     C                     ADD  IVAMT     INATAX
     C           IVPDCD    WHEQ 'A  '                      預收沖回稅
     C                     ADD  IVAMT     INDTAX
     C           IVPDCD    WHEQ 'B  '                      退貨稅額
     C                     ADD  IVAMT     INCTAX
     C           IVPDCD    WHEQ 'C  '                      折讓稅額
     C                     ADD  IVAMT     INCTAX
     C                     OTHER                           打錯
     C                     ADD  IVAMT     INATAX
     C                     ENDSL
     C           IVACNT    WHEQ '6'                        繳款
     C                     ADD  IVAMT     INEAMT
     C           IVACNT    WHEQ '7'                        退票轉出
     C                     ADD  IVAMT     INFAMT
     C           IVACNT    WHEQ '8'                        勞務
     C                     ADD  IVAMT     INAAMT
     C           IVACNT    WHEQ '9'                        其他
     C                     ADD  IVAMT     INAAMT
     C*
     C                     ENDSL
     C                     ENDIF
     C           INNO      READEIVREC               N    69
     C                     ENDDO
     C*
     C           INAAMT    ADD  S#GAMT    INAAMT
     C*
     C           INAAMT    ADD  INATAX    INNBAL           彙總未收
     C                     ADD  INBAMT    INNBAL
     C                     ADD  INCAMT    INNBAL
     C                     ADD  INCTAX    INNBAL
     C                     ADD  INDAMT    INNBAL
     C                     ADD  INDTAX    INNBAL
     C                     ADD  INEAMT    INNBAL
     C                     ADD  INFAMT    INNBAL
     C*                    ADD  S#GAMT    INNBAL
     C*
     C           W#ISTX    IFEQ 'T'
     C           INTXTP    ANDNE'1'
     C                     MOVELT#ERR,10  S#MSG3
     C                     ENDIF
     C*
     C           INAAMT    IFNE W#AAMT
     C           INATAX    ORNE W#ATAX
     C           INBAMT    ORNE W#BAMT
     C           INCAMT    ORNE W#CAMT
     C           INCTAX    ORNE W#CTAX
     C           INDAMT    ORNE W#DAMT
     C           INDTAX    ORNE W#DTAX
     C           INEAMT    ORNE W#EAMT
     C           INFAMT    ORNE W#FAMT
     C           S#GAMT    ORNE W#GAMT
     C                     MOVELT#ERR,9   S#MSG3           試算不同
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  存檔畫面三
     C*----------------------------------------
     CSR         SR3300    BEGSR
     C                     EXFMTAR035F5H                   詢問確認
     C*
     C           *IN03     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     GOTO ES3300
     C                     ENDIF
     C*
     C           W#CKLV    IFGT 0
     C                     CLEARRARALOG                    異動登錄
9908AC*                    Z-ADDUDATE     AGDATE           日期
9908AC           *DATE     SUB  19000000  AGDATE           日期
     C                     TIME           W#SYST
     C                     Z-ADDD#ST      AGTIME           時間
     C                     MOVELU#USID    AGURID           使用者
     C                     MOVEL'AI6'     AGTRID           異動代號
     C                     MOVELINNO      AGRENO           相關號碼
     C                     MOVELS#CGCA    AGCGCA           修改原因
     C                     WRITERARALOG
     C*
     C                     MOVELU#AREA    INTXAR
9908AC*                    Z-ADDUDATE     INTXDT
9908AC           *DATE     SUB  19000000  INTXDT
     C                     MOVEL'C'       INFLAG
     C                     ENDIF
     C*
     C                     EXSR SR3310                     移出資料
     C           INNO      CHAININREC                69    銷住
     C                     EXSR SR3320                     移進資料
     C                     UPDATINREC                      修改發票
     C*
     C                     MOVELT#ERR,6   S#MSG3           已存檔
     CSR         ES3300    ENDSR
     C*
     C*----------------------------------------
     C*  存檔畫面三之移出資料
     C*----------------------------------------
     CSR         SR3310    BEGSR
     C                     MOVELINFLAG    X#FLAG  1
     C                     MOVELINTYPE    X#TYPE  1
     C                     MOVELINDECD    X#DECD  1
9009 C                     Z-ADDINDEDT    X#DEDT  80
     C                     MOVELINNO      X#NO   10
     C                     MOVELINCUNO    X#CUNO  6
     C                     MOVELINCUNM    X#CUNM 10
9009 C                     MOVELINORNO    X#ORNO  6
9009 C                     Z-ADDININDT    X#INDT  80
     C                     MOVELINKIND    X#KIND  1
     C                     MOVELINRVID    X#RVID  2
     C                     MOVELINSALE    X#SALE  2
     C                     MOVELINSATP    X#SATP  1
     C                     MOVELINAREA    X#AREA  1
     C                     MOVELINTXTP    X#TXTP  1
     C                     Z-ADDINAAMT    X#AAMT 110
     C                     Z-ADDINATAX    X#ATAX 110
     C                     Z-ADDINBAMT    X#BAMT 110
     C                     Z-ADDINCAMT    X#CAMT 110
     C                     Z-ADDINCTAX    X#CTAX 110
     C                     Z-ADDINDAMT    X#DAMT 110
     C                     Z-ADDINDTAX    X#DTAX 110
     C                     Z-ADDINEAMT    X#EAMT 110
     C                     Z-ADDINFAMT    X#FAMT 110
     C                     Z-ADDINNBAL    X#NBAL 110
     C                     Z-ADDINMAMT    X#MAMT 110
     C                     MOVELINAPNO    X#APNO  7
0105AC                     Z-ADDINAPDT    X#APDT  80
     C                     MOVELINRCNO    X#RCNO  6
0105AC                     Z-ADDINRCDT    X#RCDT  80
     C                     MOVELINFL01    X#FL01  1
     C                     MOVELINTXAR    X#TXAR  1
0005AC*                    Z-ADDINTXDT    X#TXDT  60
0005AC                     Z-ADDINTXDT    X#TXDT  80
     C                     MOVELINPRTC    X#PRTC  1
     C                     MOVELINRESV    X#RESV  6
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  存檔畫面三之移進資料
     C*----------------------------------------
     CSR         SR3320    BEGSR
     C                     MOVELX#FLAG    INFLAG
     C                     MOVELX#TYPE    INTYPE
     C                     MOVELX#DECD    INDECD
     C                     Z-ADDX#DEDT    INDEDT
     C                     MOVELX#NO      INNO
     C                     MOVELX#CUNO    INCUNO
     C                     MOVELX#CUNM    INCUNM
     C                     MOVELX#ORNO    INORNO
     C                     Z-ADDX#INDT    ININDT
     C                     MOVELX#KIND    INKIND
     C                     MOVELX#RVID    INRVID
     C                     MOVELX#SALE    INSALE
     C                     MOVELX#SATP    INSATP
     C                     MOVELX#AREA    INAREA
     C                     MOVELX#TXTP    INTXTP
     C                     Z-ADDX#AAMT    INAAMT
     C                     Z-ADDX#ATAX    INATAX
     C                     Z-ADDX#BAMT    INBAMT
     C                     Z-ADDX#CAMT    INCAMT
     C                     Z-ADDX#CTAX    INCTAX
     C                     Z-ADDX#DAMT    INDAMT
     C                     Z-ADDX#DTAX    INDTAX
     C                     Z-ADDX#EAMT    INEAMT
     C                     Z-ADDX#FAMT    INFAMT
     C                     Z-ADDX#NBAL    INNBAL
     C                     Z-ADDX#MAMT    INMAMT
     C                     MOVELX#APNO    INAPNO
     C                     Z-ADDX#APDT    INAPDT
     C                     MOVELX#RCNO    INRCNO
     C                     Z-ADDX#RCDT    INRCDT
     C                     MOVELX#FL01    INFL01
     C                     MOVELX#TXAR    INTXAR
     C                     Z-ADDX#TXDT    INTXDT
     C                     MOVELX#PRTC    INPRTC
     C                     MOVELX#RESV    INRESV
     CSR                   ENDSR
     C*
     C************************************
     C*  畫面四:發票明細編輯主函式
     C************************************
     CSR         SR4000    BEGSR
     C                     WRITEAR035F4M
     C                     EXFMTAR035F4H                   螢幕輸入
     C*
     C                     MOVEL*BLANK    S#MSG4
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C                     MOVEL'00'      W#PRID           結束
     C           *IN12     WHEQ '1'
     C           W#SVIV    IFEQ 'T'                        有存檔
     C                     EXSR SR2100                     修改畫面二
     C                     ENDIF
     C                     MOVEL'02'      W#PRID           回前畫面
     C           *IN10     WHEQ '1'
     C                     EXSR SR4300                     存檔
     C*
     C           *IN91     WHEQ '1'                        向下翻頁
     C                     MOVELIVNO      F#IVNO
     C                     MOVELIVACNT    F#IVAC
     C                     Z-ADDIVITEM    F#IVIT
     C                     READ IVREC               N    69
     C           *IN69     IFEQ '1'
     C           F#IVNO    ORNE IVNO
     C           K#IV      CHAINIVREC               N69    還原
     C                     MOVELT#ERR,7   S#MSG4
     C                     ENDIF
     C*
     C           *IN92     WHEQ '1'                        向上翻頁
     C                     MOVELIVNO      F#IVNO
     C                     MOVELIVACNT    F#IVAC
     C                     Z-ADDIVITEM    F#IVIT
     C                     READPIVREC               N    69
     C           *IN69     IFEQ '1'
     C           F#IVNO    ORNE IVNO
     C           K#IV      CHAINIVREC               N69    還原
     C                     MOVELT#ERR,8   S#MSG4
     C                     ENDIF
     C*
     C                     OTHER
     C                     EXSR SR4200                     試算
     C                     ENDSL
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  初始畫面四
     C*----------------------------------------
     CSR         SR4100    BEGSR
     C                     SETOF                     6162
     C           W#CKLV    IFEQ 1                          財會
     C                     SETON                     62
     C                     ENDIF
     C           W#CKLV    IFEQ 2                          營業
     C                     SETON                     6162
     C                     ENDIF
     C*
     C                     MOVEL'F'       W#SVIV  1        存檔旗標
     C*
     C                     MOVELS#INNO    F#IVNO
     C                     MOVELS#ACNT    F#IVAC
     C                     Z-ADDS#ITEM    F#IVIT
     C           K#IV      CHAINIVREC               N69    不鎖住
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  試算畫面四
     C*----------------------------------------
     CSR         SR4200    BEGSR
9006 C                     MOVELS#CUNO    W#CUNO  1
  .  C           W#CUNO    IFEQ 'E'
  .  C                     MOVE *ON       *IN98
  .  C                     ELSE
  .  C                     MOVE *OFF      *IN98
  .  C                     ENDIF
  .  C*
     C           IVQTY     IFNE 0
     C           IVUPRC    ANDNE0
     C           IVAMT     ANDNE0
     C           IVQTY     MULT IVUPRC    W#TMPA    H
     C           IVAMT     IFNE W#TMPA
9006 C  N98                MOVELT#ERR,5   S#MSG4           乘法有誤
     C                     ENDIF
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  存檔畫面四
     C*----------------------------------------
     CSR         SR4300    BEGSR
     C           W#CKLV    IFGT 1                          限制營業
     C           IVACNO    ANDNE*BLANK
     C                     MOVELT#ERR,11  S#MSG4
     C                     GOTO ES4300
     C                     ENDIF
     C*
     C                     EXFMTAR035F6H                   詢問確認
     C*
     C           *IN03     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     GOTO ES4300
     C                     ENDIF
     C*
     C           W#CKLV    IFGT 0                          修改異動
     C                     CLEARRARALOG                    異動登錄
9908AC*                    Z-ADDUDATE     AGDATE           日期
9908AC           *DATE     SUB  19000000  AGDATE           日期
     C                     TIME           W#SYST
     C                     Z-ADDD#ST      AGTIME           時間
     C                     MOVELU#USID    AGURID           使用者
     C                     MOVEL'AI7'     AGTRID           異動代號
     C                     MOVELIVNO      AGRENO           相關號碼
     C                     MOVELS#CGCA    AGCGCA           修改原因
     C                     WRITERARALOG
     C*
     C                     MOVELU#AREA    IVTXAR
9908AC*                    Z-ADDUDATE     IVTXDT
9908AC           *DATE     SUB  19000000  IVTXDT
     C                     MOVEL'C'       IVFLAG
     C                     ENDIF
     C*
     C                     EXSR SR4310                     移出資料
     C                     MOVELIVNO      F#IVNO
     C                     MOVELIVACNT    F#IVAC
     C                     Z-ADDIVITEM    F#IVIT
     C           K#IV      CHAINIVREC                69    銷住
     C                     EXSR SR4320                     移進資料
     C                     UPDATIVREC                      修改明細
     C*
     C                     MOVEL'T'       W#SVIV
     C                     MOVELT#ERR,6   S#MSG4           已存檔
     CSR         ES4300    ENDSR
     C*
     C*----------------------------------------
     C*  存檔畫面四之移出資料
     C*----------------------------------------
     CSR         SR4310    BEGSR
     C                     MOVELIVFLAG    Y#FLAG  1
     C                     MOVELIVNO      Y#NO   10
     C                     MOVELIVACNT    Y#ACNT  1
     C                     Z-ADDIVITEM    Y#ITEM  20
9009 C                     Z-ADDIVACDT    Y#ACDT  80
LYW  C                     MOVELIVORNO    Y#ORNO  9
     C                     MOVELIVPDCD    Y#PDCD  3
     C                     Z-ADDIVQTY     Y#QTY   70
     C                     Z-ADDIVUPRC    Y#UPRC  83
     C                     Z-ADDIVAMT     Y#AMT  110
     C                     MOVELIVDECD    Y#DECD  1
9009 C                     Z-ADDIVDEDT    Y#DEDT  80
     C                     MOVELIVAPNO    Y#APNO  8
     C                     MOVELIVACNO    Y#ACNO  7
     C                     MOVELIVFL01    Y#FL01  1
     C                     MOVELIVFL02    Y#FL02  1
     C                     MOVELIVFL03    Y#FL03  1
     C                     MOVELIVTXAR    Y#TXAR  1
9009 C                     Z-ADDIVTXDT    Y#TXDT  80
     C                     MOVELIVRESV    Y#RESV  5
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  存檔畫面四之移進資料
     C*----------------------------------------
     CSR         SR4320    BEGSR
     C                     MOVELY#FLAG    IVFLAG
     C                     MOVELY#NO      IVNO
     C                     MOVELY#ACNT    IVACNT
     C                     Z-ADDY#ITEM    IVITEM
     C                     Z-ADDY#ACDT    IVACDT
     C                     MOVELY#ORNO    IVORNO
     C                     MOVELY#PDCD    IVPDCD
     C                     Z-ADDY#QTY     IVQTY
     C                     Z-ADDY#UPRC    IVUPRC
     C                     Z-ADDY#AMT     IVAMT
     C                     MOVELY#DECD    IVDECD
     C                     Z-ADDY#DEDT    IVDEDT
     C                     MOVELY#APNO    IVAPNO
     C                     MOVELY#ACNO    IVACNO
     C                     MOVELY#FL01    IVFL01
     C                     MOVELY#FL02    IVFL02
     C                     MOVELY#FL03    IVFL03
     C                     MOVELY#TXAR    IVTXAR
     C                     Z-ADDY#TXDT    IVTXDT
     C                     MOVELY#RESV    IVRESV
     CSR                   ENDSR
     C*
     C**************************************************************
** T#ERR
０１－找不到資料
０２－已達檔底
０３－已達檔頭
０４－本張發票非本廠區所開立，不可編修
０５－本發票明細之數量及單價相乘之結果不等於金額，請查核
０６－資料已經存檔完畢
０７－已到本發票號碼的最後一筆
０８－已到本發票號碼的第一筆
０９－發票金額彙總試算之結果與原先有異，查核無誤後請存檔
１０－本發票之課稅別為不含稅，但卻有稅項明細資料，請查核
１１－本發票明細已代開傳票，不得存檔，請找財會人員處理
