     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR116R
     H*            2.PROGRAM-TYPE  RPG
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     請款別出貨及發票明細表─ＦＯＲ型鋼（出表）
     H*            5.DATE-WRITTEN  93/06/10
     H*            6.DATE-MODIFY
     H*
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR116F IP  E           K        DISK
     FARR116T O   E             69     PRINTER
     E*****************************************************************
     IRAR116F
     I                                              A6INNOL2
     I                                              A6PDNML1
     I            DS
     I                                        1   5 A6SPE1
     I                                        1   40D#SPE1
     I            DS
     I                                        1   5 A6SPE2
     I                                        1   40D#SPE2
     I           UDS
     I                                     10011010 U#USID
     I                                      300 306 T#APNO
     I*****************************************************************
     C  N99                EXSR INISR
     C   L2                EXSR L2CLR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CL2 99                EXSR L2SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     MOVE *ON       *IN99
     C                     MOVE *ON       *IN69
     C*
     C                     MOVEL*ALL'='   T#LIN1
     C                     MOVEL*ALL'='   T#LIN2
     C*
     C                     ENDSR
     C*****************************************************************
     C           L2CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR116T1T
     C                     MOVE *BLANK    W#INNO 10
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR116T1S
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     CLEARAR116T1D
     C*
     C           W#INNO    IFNE A6INNO
     C                     MOVELA6INNO    W#INNO
     C                     MOVE *OFF      *IN50
     C                     ELSE
     C                     MOVE *ON       *IN50
     C                     ENDIF
     C*
     C                     MOVELA6INNO    T#INNO           發票號碼
     C                     MOVELA6ORNO    T#ORNO           訂單號碼
     C                     Z-ADDA6ORTM    T#ORTM           訂單項次
     C                     MOVELA6VRNO    T#VRNO           磅單編號
     C                     MOVELA6VRTM    T#VRTM           磅單項次
     C                     MOVELA6STND    T#STND           規範別
     C                     MOVELA6QULT    T#QULT           鋼種
     C                     MOVELA6PDNM    T#PDNM           品名
     C                     Z-ADDD#SPE1    T#SPE1            H/T/D
     C           A6SPE2    IFEQ *BLANK
     C           A6SPE2    OREQ '00000'
     C                     MOVE *ON       *IN53
     C                     ELSE
     C                     MOVE *OFF      *IN53
     C                     ENDIF
     C                     Z-ADDD#SPE2    T#SPE2            B/W
     C           A6SPE3    IFEQ *BLANK
     C           A6SPE3    OREQ '000  '
     C                     MOVE *ON       *IN51
     C                     ELSE
     C                     MOVE *OFF      *IN51
     C                     ENDIF
     C                     MOVE A6SPE3    T#SPE3            T1
     C           A6SPE4    IFEQ *BLANK
     C           A6SPE4    OREQ '000  '
     C                     MOVE *ON       *IN52
     C                     ELSE
     C                     MOVE *OFF      *IN52
     C                     ENDIF
     C                     MOVE A6SPE4    T#SPE4            T2
     C                     Z-ADDA6SPE5    T#SPE5           長度
     C                     Z-ADDA6PNUM    T#PNUM           支數
     C                     Z-ADDA6WGHT    T#QTY            重量
     C                     Z-ADDA6UPRC    T#UPRC           單價
     C                     Z-ADDA6AMT     T#AMT            金額
     C                     MOVELA6PLE1    T#PLE1           交貨地點
     C                     MOVELA6NOTE    T#NOTE           備註
     C*
     C   69                WRITEAR116T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR116T1D
     C*品名小計
     C                     ADD  T#QTY     T#QTYS
     C                     ADD  T#AMT     T#AMTS
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C   69                WRITEAR116T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR116T1S
     C*發票合計
     C                     ADD  T#QTYS    T#QTYT
     C                     ADD  T#AMTS    T#AMTT
     C*
     C                     ENDSR
     C*****************************************************************
     C           L2SR      BEGSR
     C*****************************************************************
     C   69                WRITEAR116T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR116T1T
     C*發票合計
     C                     ADD  T#QTYT    T#QTYA
     C                     ADD  T#AMTT    T#AMTA
     C*
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C   69                WRITEAR116T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR116T1A
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C*
     C   69                WRITEAR116T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR116T1M
     C*
     C                     ENDSR
