     H****************************************************************
     H*
     H*            UPDATE  DATE  99/08/31  2010AR517  S00WCJ (9908A)
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FRCVDTL  IP  E                    DISK
     FBANKID  IF  E           K        DISK
     FQPRINT  O   F     176            PRINTER
      *************************************************************
     E                    TAB1    1   6  1   TABR1  10
     I*************************************************************
     IRVREC
     I                                              RVDUDTL2
     I                                              RVTANOL1
     I****
     IAADS        DS
     I              'も到期日應收票據明-C         CON3                       X
     I              '細表も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     IBBDS        DS
     I                                        1  10 XXXX
     I                                        1   3 BAID1
     I                                        4   7 BAID2
      *
     I                                       21  280YMD
     I                                       21  260YM
     I                                       21  240YY
     I                                       25  260MM
     I                                       27  280DD
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
9908AC           *DATE     SUB  19000000  U#SYSD  80
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     CLR                   EXSR TL#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C           *ENTRY    PLIST
     C                     PARM           BDATE1  80
     C                     PARM           EDATE2  80
     C           BKKEY     KLIST
     C                     KFLD           BAID1
     C                     KFLD           BAID2
      *
     C                     Z-ADDBDATE1    YMD
     C                     Z-ADDYM        DATE1   60
     C                     Z-ADDEDATE2    YMD
     C                     Z-ADDYM        DATE2   60
      *
     C                     Z-ADD0         BAMT   110
     C                     Z-ADD0         BQTY    50
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD55        TCNT    30
     C                     MOVE *ALL'='   ALS   162
     C                     MOVE *ALL'-'   ALQ   106
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD8         LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         AAMT   110
     C                     Z-ADD0         AQTY    50
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         CAMT   110
     C                     Z-ADD0         CQTY    50
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     MOVE *BLANK    CHAR10 10
     C           RVNTTP    LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     CHAR10
     C  N31                MOVEL'其他'  CHAR10
      *
     C           RVPLAC    COMP 'D'                      31
     C   31                MOVEL'本埠'  CHAR6   6
     C  N31                MOVEL'外埠'  CHAR6
      *
     C                     MOVE *BLANK    CHAR    1
9908AC           RVDUDT    COMP U#SYSD               31  31
     C   31                MOVEL'*'       CHAR
      *
     C                     MOVELRVTBID    XXXX
     C           BKKEY     CHAINBANKID               31
     C  N31                MOVELBABNAM    CHARAX 10
     C   31                MOVEL*BLANK    CHARAX 10
      *
     C                     Z-ADDRVRAMT    DEC9    90
      *
     C                     MOVEL*BLANK    R#NTNO 20
     C                     MOVELRVNTNO    R#NTNO
      *
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#01
     C                     ADD  1         AQTY
     C                     ADD  RVRAMT    AAMT
     C                     ADD  1         CQTY
     C                     ADD  RVRAMT    CAMT
     C                     ADD  1         BQTY
     C                     ADD  RVRAMT    BAMT
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C                     EXCPTET#L1
     C                     ADD  2         LCNT
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C                     EXCPTET#L2
     C                     ADD  2         LCNT
     CSR         TL#L2Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME LR PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     CSR         TL#LRZ    ENDSR
      ***********************************************************
      ****   DEFINE OUPUT FORMAT                             ****
      ***********************************************************
     OQPRINT  E   02           EH#01
     O                                   17 '報表編號: AR017'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
9908AO                         U#SYSDZ  132
     O        E 1              EH#01
     O                         CON3      75                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 2              EH#01
     O                                   11 '收款年月:'
     O                         DATE1   +  1 '    /  '
     O                                 +  1 '-'
     O                         DATE2   +  1 '    /  '
     O        E 1              EH#01
     O                         ALS      162
     O        E 1              EH#01
     O                                   11 '到期日期'
     O                                   21 '客戶編號'
     O                                   31 '客戶名稱'
     O                                   42 '付款銀行'
     O                                   56 '付款帳號'
     O                                   67 '票據號碼'
     O                                   84 '款項'
     O                                   92 '票據別'
     O                                  102 '付款地'
     O                                  112 '繳款單號'
     O                                  124 '金　額'
     O                                  135 '託收日期'
     O                                  145 '託收銀行'
     O                                  155 '託收帳號'
     O                                  162 '註記'
     O        E 1              EH#01
     O                         ALS      162
     O        E 1              ED#01
     O                 L2      RVDUDT    10 '    /  /  '
     O                 75      RVDUDT    10 '    /  /  '
     O                         RVCUNO    19
     O                         RVCUNM    31
     O                         RVPBID    46
     O                         RVPANO    57
     O                         R#NTNO    78
     O                         RVUSTP    82
     O                         CHAR10    94
     O                         CHAR6    101
     O                         RVNO     110
     O                         DEC9  J  124
     O                         RVPRDT   134 '    /  /  '
     O                         CHARAX   145
     O                         RVTANO   156
     O                         CHAR     159
     O        E 1              ET#L1
     O                                   68 '記帳帳號小計：　共'
     O                         CQTY  J   76
     O                                   80 '張'
     O                                   97 '總金額:'
     O                         CAMT  J  124
     O        E 1              ET#L1
     O                         ALQ      162
     O        E 1              ET#L2
     O                                   66 '到期日合計：　共'
     O                         AQTY  J   76
     O                                   80 '張'
     O                                   97 '總金額:'
     O                         AAMT  J  124
     O        E 1              ET#L2
     O                         ALQ      162
     O        E 1              ET#LR
     O                                   66 '　總合計：　共'
     O                         BQTY  J   76
     O                                   80 '張'
     O                                   97 '總金額:'
     O                         BAMT  J  124
     O*****************************************************************
** TAB1
A支票
B背書支票
C本票
D背書本票
E銀行本票
I信用狀　
