     H****************************************************************
     H*
     H*            UPDATE  DATE  99/08/20  2010AR517  S00WCJ (9908A)
     H*                         100/01/03  2011ARXXX  S00WCJ (0001A)
     H*                         100/10/25  2011AR629  S00WCJ (0010A)
     H*                         預收貨款發票列印名稱修改
     H*                         101/11/05  2012AR697  S00WCJ (0111A)
     H*                         增加發票品名檢核
     H*                         103/01/19  2014AR796  S00WCJ (0301A)
     H*                          發票作廢時，ARDSDT上作廢碼
     H*                         104/08/07  2015AR949  S00WCJ (0408A)
     H*                          9其他發票增加日期檢核
     H*                         104/09/18  2015AR953  S00WCJ (0409A)
     H*                          產品代碼3碼擴5碼
     H*                         104/11/05  S00WCJ (0411A)
     H*                          修正2015AR949，僅做同月份之日期檢
     H*                          核
     H*                         106/12/01  2017AR1112  S00WCJ (0612A)
     H*                          作廢時寫入電子發票介面檔
     H*                         107/03/29  2017AR1112  S00WCJ (0703A)
     H*                          檢核空白發票若已上送介面檔，則不可
     H*                          再開立
     H*                         107/06/15  2018AR1162  S00WCJ (0706A)
     H*                          電子發票介面檔增加處理代號及廠區
     H*                         107/10/31  2018AR00025 S00WCJ (0710A)
     H*                          二聯式發票作廢訊息代號為C0501
     H*                         107/11/30  S00WCJ (0711A)
     H*                          增加電子發票上線日檢核
     H*                         108/01/23  S00WCJ (0801A)
     H*                          增加檢核發票未列印不可作廢
     H*                         108/05/06  S00WCJ (0805A)
     H*                          修正寫入INEDIN作廢原因亂碼之BUG
     H*                         108/05/09  S00WCJ (0805B)
     H*                          電子發票介面LOG檔增加異動時間資訊
     H*                         108/12/12 2019AR00078 S00WCJ (0812A)
     H*                          開放銷貨可修改金額，最多可修改8元
     H*                         110/04/07 S00WCJ (1004A)
     H*                          修改銷貨修改金額最多修改8元檢核之
     H*                           BUG
     H*                         110/06/04 2021AR00019 S00WCJ (1006A)
     H*                          增加檢核訂單上結案碼則不可開立預收發
     H*                          票
     H*                         110/12/14 2021AR00049 S00WCJ (1012A)
     H*                          訂單若有未結案項次，即可開立預收發票
     H*                         111/03/21 2022AR00008 S00WCJ (1103A)
     H*                          增加判斷若為運費折扣品名欄位反白顯示
     H*                         111/08/01 S00WCJ (1108A)
     H*                          檢核訂單時，原程式用第一項去CHAIN
     H*                          訂單檔，訂單H26142項次由37開始，
     H*                          詢問金蘭後，金蘭告知訂單檔確實有可能
     H*                          第一項資料會被刪掉，因此修改為只用訂
     H*                          單編號去檢核
     H*                         111/08/01 S00WCJ (1108B)
     H*                          增加檢核發票類別2種類只能輸入4.5
     H*                          發票類別9，種類只能輸入9.5
     H*                         111/11/24 S00WCJ (1111A)
     H*                          修正發票聯式檢核BUG
     H*                         112/04/10 2023AR00018 S00WCJ (1204A)
     H*                          跨月發票異動日由3日改為1日
     H*                         112/05/18 2023AR00027 S00WCJ (1205A)
     H*                          調整發票作廢時一併刪除調整單
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FARE001S CF  E                    WORKSTN
     F                                        RRN   KSFILE SFLREC
     FCPRBAL  UF  E           K        DISK                      A
     FINVMST  UF  E           K        DISK                      A
0812AFINVMSTG O   E           K        DISK
0812AF            INREC                             KRENAMEINVMG
     FINVDTL  UF  E           K        DISK                      A
0812AFINVDTLG O   E           K        DISK
0812AF            IVREC                             KRENAMEINVDG
     FTRNDTLL2UF  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FSAMAST  IF  E           K        DISK
     FHIPROD  IF  E           K        DISK
     FGENSEQ  UF  E           K        DISK
9108 FARBTAX  UF  E           K        DISK                      A
9302 FARINVM  UF  E           K        DISK
0301AFARDSDTL3UF  E           K        DISK
0612AFDELMST  IF  E           K        DISK
0706AFINEDIN  UF  E           K        DISK                      A
0612AFINEDING O   E           K        DISK
0612AF            RINEDIN                           KRENAMEINEDG
0703AFINENBU  IF  E           K        DISK
1205AFARDLWT  UF  E           K        DISK
     F*----------------------------------------------------------------
     FCAMBALT IF  E           K        DISK                      A
     E*************************************************************
0612AE                    TAB1    5   5  1   TAB2    6   *公司代號
0801AE                    ERR     1  55 70
0612AE                    ERR1    1   1 40
     I*************************************************************
     IAADS        DS
9708 I                                        1   6 W1CUNO
     I                                        1   1 S1KIND
     I                                        2   2 S1CUN1
     I                                        3   5 S1CUN2
9008 I                                        6   6 S1CD01
     I                                       11  20 GRP
     I                                       11  11 GE1
9806 I                                       12  17 GE2
     I                                       18  20 GE3
9008 I                                       21  280YMD
LYW  I                                       21  260YM
 .   I                                       27  280DD
     I                                       31  40 NOG
     I                                       31  32 NOA
     I                                       33  400NOB
9008 I                                       41  460YYMM
LYW  I                                       45  460MM
9008 I                                       51  580EDATE
LYW  I                                       51  540EYY
 .   I                                       55  560EMM
 .   I                                       57  580EDD
     I*
     I            DS
     I                                        1  140W#SYST
     I                                        1   60D#ST
     I                                        7  100D#SY
     I                                       11  140D#SMD
9108 I            DS
9108 I                                        1   80D#ACDT
9108 I                                        1   40D#YY
9108 I                                        5   60D#MM
9108 I                                        7   80D#DD
9112 I            DS
9112 I                                        1   80D#VUNO
9112 I                                        1   40D#YY1
9112 I                                        5   60D#MM1
9112 I                                        7   80D#DD1
9112 I                                        1   60D#YYMM
0010AI            DS
0010AI                                        1   6 INRESV
0010AI                                        6   6 D#RESV
     I*
0411AI            DS
0411AI                                        1   80SDATE
0411AI                                        1   60D#SDAT
0411AI            DS
0411AI                                        1   80DATE1
0411AI                                        1   60D#DAT1
0612AI            DS
0612AI                                        1   60W#DETM
0612AI                                        1   2 D#H
0612AI                                        3   4 D#M
0612AI                                        5   6 D#S
0612AI            DS
0612AI                                        1   8 DNDETM
0612AI                                        1   2 D1HH
0612AI                                        3   3 D1C1
0612AI                                        4   5 D1MM
0612AI                                        6   6 D1C2
0612AI                                        7   8 D1SS
0612AI*0805A      DS
0612AI*0805A                                  1  40 DNCLRN
0612AI*0805A                                  1   1 BOE
0612AI*0805A                                  2  38 D#CLRN
0612AI*0805A                                 40  40 BOF
1103AI            DS
1103AI                                        1  30 IVMAK1
1103AI                                        1   1 D#MAK1
     I           UDS
     I                                        1   1 DEPT
     I                                      951 985 COMP
     I                                     10011010 U#USID
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
0711AIINTR       UDS                             50
0711AI                                        1   80D#INTR
     C**************************************************************
     C           INKEY     KLIST
     C                     KFLD           INNO
     C           IVKEY     KLIST
     C                     KFLD           IVNO
     C                     KFLD           IVACNT
     C                     KFLD           IVITEM
     C           TXKEY     KLIST
     C                     KFLD           TXIVNO
     C           CBKEY     KLIST
     C                     KFLD           CBCUNO
     C           S1KEY     KLIST
     C                     KFLD           S1OREA
     C                     KFLD           S1ORNO
     C                     KFLD           S1ORTM
1012AC           S1KEY1    KLIST
1012AC                     KFLD           S1OREA
1012AC                     KFLD           S1ORNO
     C           GEKEY     KLIST
     C                     KFLD           GEKIND
     C                     KFLD           GEPRIN
     C*
     C           K#CT      KLIST
     C                     KFLD           CTDATE
     C                     KFLD           CTTIME
     C                     KFLD           CTAREA
     C                     KFLD           CTDSPN
9108 C           K#BTAX    KLIST
9108 C                     KFLD           AXAREA
9108 C                     KFLD           AXORNO
9108 C                     KFLD           AXYYMM
9108 C                     KFLD           AXITEM
     C*
0703AC           KEY01     KLIST
0703AC                     KFLD           TXAR
0703AC                     KFLD           YYMM
     C*
1205AC           W#DLWT    KLIST
1205AC                     KFLD           AWORNO           訂單號碼
1205AC                     KFLD           AWWTNO           磅單號碼
     C*
     C**************************************************************
9908AC           *DATE     SUB  19000000  U#SYSD  80
0711AC           *NAMVAR   DEFN ARINTRCTL INTR
0711AC                     UNLCKINTR
9908AC* N90                Z-ADDUDATE     YMD
9908AC  N90                Z-ADDU#SYSD    YMD
9008 C  N90      YMD       DIV  100       YYMM
9908AC* N90                Z-ADDUDATE     EDATE
9908AC  N90                Z-ADDU#SYSD    EDATE
     C  N90                MOVE '1'       SCRN    1
     C  N90                MOVE '1'       *IN,90
     C**************************************************************
     C           *IN03     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     CSR         SR#01     BEGSR
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C                     EXSR CK#01
     C  N99                EXSR PR#02
     C  N99                MOVE '2'       SCRN
     CSR         SR#01Z    ENDSR
     C******
     CSR         SR#02     BEGSR
9105 C                     Z-ADD0         W#WIND  10
     C                     WRITEERRFMT
     C                     EXFMTSFLCTL
     C   KC                EXSR KC#01
     C   KL                MOVE '1'       SCRN
     C   KL                MOVE *BLANK    ERRMSG
     C   KL                GOTO SR#02Z
     C   KJ                EXSR KJ#02
     C   KJ                GOTO SR#02Z
9105 C   KDN70             Z-ADD1         W#WIND
9105 C   KDN70             EXFMTAR001W
9105 C           W#WIND    IFEQ 1
9105 C                     GOTO SR#02Z
9105 C                     ENDIF
     C                     SELEC
     C           CODE      WHEQ '1'
     C                     EXSR CK#02
     C           CODE      WHEQ '2'
     C           CODE      OREQ '3'
     C                     EXSR CK#03
     C                     OTHER
     C                     MOVE '1'       SCRN
     C                     ENDSL
     CSR         SR#02Z    ENDSR
     C*****
     CSR         KC#01     BEGSR
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C****
     CSR         KJ#02     BEGSR
     C                     SELEC
     C           CODE      WHEQ '1'
     C                     EXSR CK#02
     C           CODE      WHEQ '2'
     C           CODE      OREQ '3'
     C                     EXSR CK#03
     C                     ENDSL
     C           *IN99     IFEQ '0'
CLJ  C                     MOVEL'F'       W#FLAG  1        異動旗標
     C                     EXSR FL#02
     C           W#FLAG    IFEQ 'T'
     C                     EXSR SR9200
---  C                     ENDIF
     C                     MOVE '1'       SCRN
     C                     ENDIF
     CSR         KJ#02Z    ENDSR
     C****
     CSR         CK#01     BEGSR
     C                     SETOF                     414299
     C                     SETOF                     437444
     C                     MOVE *BLANK    ERRMSG
     C                     SELEC
     C           CODE      WHEQ ' '
     C                     SETON                     4199
     C                     MOVE ERR,1     ERRMSG
     C           CODE      WHEQ '1'
     C                     MOVE '新增'  MOD
     C                     MOVE '0'       *IN70
     C           CODE      WHEQ '2'
     C                     MOVE '更正'  MOD
     C                     MOVE '1'       *IN70
     C           CODE      WHEQ '3'
     C                     MOVE '作廢'  MOD
     C                     MOVE '1'       *IN70
     C           CODE      WHEQ '4'
     C                     MOVE '查詢'  MOD
     C                     MOVE '1'       *IN70
     C                     OTHER
     C                     SETON                     4199
     C                     MOVE ERR,2     ERRMSG
     C                     ENDSL
     C******
     C                     SELEC
     C           CODE      WHNE '1'
     C           NO        ANDEQ*BLANK
     C                     SETON                     4299
     C                     MOVE ERR,11    ERRMSG
     C           CODE      WHEQ '1'
     C           NO        ANDNE*BLANK
     C                     SETON                     4299
     C                     MOVE ERR,15    ERRMSG
     C           CODE      WHEQ '1'
     C           IVTP      ANDNE'2'
     C           IVTP      ANDNE'3'
     C                     SETON                     4399
     C                     MOVE ERR,17    ERRMSG
     C           CODE      WHEQ '1'
     C           YYMM      ANDEQ0
     C                     SETON                     4499
     C                     MOVE ERR,31    ERRMSG
     C           CODE      WHEQ '1'
     C           MM        IFLT 01
     C           MM        ORGT 12
     C                     SETON                     4499
     C                     MOVE ERR,31    ERRMSG
     C                     ENDIF
     C                     ENDSL
     C*
0703AC           *IN99     IFEQ *OFF
0703AC           CODE      ANDEQ'1'
0703AC           KEY01     CHAINRINENBU             N97
0703AC  N97                SETON                     4499
0703AC  N97                MOVE ERR,46    ERRMSG
0703AC                     ENDIF
     C**
     C           *IN99     IFEQ '0'
     C           NO        CHAININVMST              N97
     C**
     C  N97      INEAMT    ADD  INFAMT    CHAMT  110
     C  N97                Z-ADDININDT    EDATE
1204AC* N97                Z-ADD04        EDD              次月三日
1204AC  N97                Z-ADD02        EDD              次月一日
     C  N97      EMM       IFLT 12
     C                     ADD  1         EMM
     C                     ELSE
     C                     Z-ADD1         EMM
     C                     ADD  1         EYY
     C                     ENDIF
     C**
     C                     SELEC
     C           CODE      WHEQ '1'
     C           *IN97     ANDEQ'0'
     C                     SETON                     4299
     C                     MOVE ERR,3     ERRMSG
     C           CODE      WHNE '1'
     C           *IN97     ANDEQ'1'
     C                     SETON                     4299
     C                     MOVE ERR,4     ERRMSG
     C           CODE      WHEQ '3'
     C           *IN97     ANDEQ'0'
     C           INDECD    ANDNE' '
     C                     SETON                     4299
     C                     MOVE ERR,5     ERRMSG
     C           CODE      WHEQ '3'
     C           *IN97     ANDEQ'0'
     C           INAREA    ANDNETXAR
     C                     SETON                     4299
     C                     MOVE ERR,20    ERRMSG
     C           CODE      WHEQ '3'
     C           *IN97     ANDEQ'0'
     C           CHAMT     ANDNE0
     C                     SETON                     4299
     C                     MOVE ERR,19    ERRMSG
     C           CODE      WHEQ '3'
     C           *IN97     ANDEQ'0'
     C           DEPT      ANDEQ'B'
9908AC*          UDATE     ANDGEEDATE
9908AC           U#SYSD    ANDGEEDATE
     C                     SETON                     4299
     C                     MOVE ERR,32    ERRMSG
     C           CODE      WHEQ '2'
     C           *IN97     ANDEQ'0'
     C           INEAMT    ANDNE0
     C                     SETON                     4299
     C                     MOVE ERR,24    ERRMSG
     C           CODE      WHEQ '2'
     C           *IN97     ANDEQ'0'
     C           INPRTC    ANDNE' '
     C                     SETON                     4299
     C                     MOVE ERR,25    ERRMSG
     C*          CODE      WHEQ '2'
     C*          *IN97     ANDEQ'0'
     C*          INTYPE    ANDNE'1'
     C*                    SETON                     4299
     C*                    MOVE ERR,28    ERRMSG
     C           CODE      WHEQ '2'
     C           *IN97     ANDEQ'0'
     C           INAREA    ANDNETXAR
     C                     SETON                     4299
     C                     MOVE ERR,29    ERRMSG
     C           CODE      WHEQ '2'
     C           *IN97     ANDEQ'0'
     C           INDECD    ANDEQ'D'
     C                     SETON                     4299
     C                     MOVE ERR,30    ERRMSG
 9201C           CODE      WHEQ '3'
     C           *IN97     ANDEQ'0'
     C           INBAMT    ANDNE0
     C*                    SETON                     4299
     C                     MOVE ERR,41    ERRMSG
     C                     ENDSL
     C                     ENDIF
0801AC  N99      CODE      IFEQ '3'
0801AC           *IN97     ANDEQ'0'
0801AC           INPRTC    ANDEQ' '
0801AC                     SETON                     4299
0801AC                     MOVELERR,47    ERRMSG
0801AC                     ENDIF
     CSR         CK#01Z    ENDSR
     C******
     CSR         PR#02     BEGSR
     C                     SETON                     80
     C                     WRITESFLCTL
     C                     SETOF                     80
     C                     Z-ADD0         RRN     30
     C                     Z-ADD0         SAMT   110
     C                     MOVEA'000000'  *IN,43
     C                     MOVEA'000000'  *IN,51
1103AC                     MOVEL*OFF      *IN65
1103AC                     MOVEL*BLANKS   W#CHYN  1
     C                     MOVE *BLANK    CUNO
     C                     MOVE *BLANK    CUNM
     C                     MOVE *BLANK    TYPE
     C                     MOVE *BLANK    ORNO
     C                     MOVE *BLANK    SALE
     C                     MOVE *BLANK    CHK
     C                     MOVE *BLANK    RVID
     C                     MOVE *BLANK    TYP1
0010AC                     MOVEL'N'       S#IVYN
     C                     Z-ADD0         TTLAMT
     C**
     C           CODE      CASEQ'1'       PR#ADD
     C                     CAS            PR#OTH
     C                     ENDCS
     CSR         PR#02Z    ENDSR
     C******
     CSR         PR#ADD    BEGSR
     C                     MOVELTXAR      AREA
     C                     EXSR @GETNO
     C                     CLEARSFLREC
     C           RRN       DOUGE150
     C                     ADD  1         RRN
     C                     MOVE RRN       ITEM
     C                     WRITESFLREC
     C                     ENDDO
     C                     SETON                     10
     CSR         PR#ADZ    ENDSR
     C******
     C******
     CSR         @GETNO    BEGSR
     C           IVTP      IFEQ '3'
     C                     MOVEL'02'      GEKIND
     C                     ELSE
     C                     MOVEL'01'      GEKIND
     C                     ENDIF
     C                     MOVE *BLANK    GEPRIN
     C                     MOVELTXAR      GE1
9806 C                     MOVELYYMM      GE2
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ              N97
     C   97                SETON                     9942
     C   97                MOVELERR,16    ERRMSG
     C   97                GOTO GETNOZ
     C                     MOVELGEPRE     NOA
     C           GECUNO    ADD  1         NOB
     C           NOB       IFGT GEENNO
     C                     SETON                     9942
     C                     MOVELERR,18    ERRMSG
     C                     GOTO GETNOZ
     C                     ENDIF
     C                     MOVELNOG       NO
     CSR         GETNOZ    ENDSR
     C***
     CSR         PR#OTH    BEGSR
     C           INTYPE    IFEQ '1'
     C                     Z-ADDINBAMT    SAMT
     C                     ENDIF
     C           INTYPE    IFEQ '2'
     C                     Z-ADDINAAMT    SAMT
     C                     ENDIF
     C                     MOVELINCUNO    CUNO
     C                     MOVELINCUNM    CUNM
     C                     MOVELINSATP    TYPE
     C                     MOVELINORNO    ORNO
     C                     MOVELINSALE    SALE
     C                     MOVELINKIND    CHK
     C                     MOVELINRVID    RVID
     C                     MOVELINTYPE    TYP1
     C                     MOVELINAREA    AREA
     C                     MOVELINAPNO    APNO1
0010AC                     MOVELD#RESV    S#IVYN
     C           INDECD    IFEQ 'D'
     C                     SETON                     74
     C                     ENDIF
     C**
     C                     MOVELNO        IVNO
     C                     MOVE *BLANK    IVACNT
     C                     Z-ADD0         IVITEM
     C           IVKEY     SETLLINVDTL               97
     C  N97      NO        READEINVDTL                   97
     C           RRN       DOWLE150
     C           *IN97     ANDEQ'0'
LYW..C*          IVDECD    IFNE 'D'
     C                     ADD  1         RRN
     C                     Z-ADDIVITEM    ITEM
     C                     MOVELIVACNT    ACNT
     C                     MOVELIVPDCD    PDNM
1103AC           D#MAK1    IFEQ 'S'
1103AC                     MOVEL*ON       *IN65
1103AC                     MOVEL'Y'       W#CHYN
1103AC                     ELSE
1103AC                     MOVEL*OFF      *IN65
1103AC                     ENDIF
     C*
     C                     Z-ADDIVQTY     QTY
     C                     Z-ADDIVUPRC    UPRC
     C           IVACNT    IFEQ '1'
     C                     MOVELIVRESV    TPRCA   2
     C                     SETOF                     21
     C                     TESTN          TPRCA      21
     C   21                MOVE TPRCA     TPRCN   20
9506 C   21      IVUPRC    MULT 100000    TPRC    90
     C   21                ADD  TPRCN     TPRC
     C   21      TPRC      DIV  100000    UPRC
     C                     ENDIF
     C                     Z-ADDIVAMT     AMT
0812AC                     Z-ADDIVAMT     S1AMT            *原金額
0812AC           QTY       MULT UPRC      S2AMT     H      *計算金額
1004AC*
1004AC*若為調整單，有可能出現數量*單價=0，但實際上發票有金額之情
1004AC*況，此情況則將計算金額直接代入原發票金額
1004AC*
1004AC           S2AMT     IFEQ 0
1004AC                     Z-ADDIVAMT     S2AMT
1004AC                     ENDIF
1004AC*
     C                     Z-ADDIVACDT    ACDT
     C                     MOVELIVAPNO    APNO
     C                     MOVELIVACNO    ACNO
 9111C                     MOVELIVDECD    S#DECD
     C           S#DECD    IFEQ *BLANK
     C                     ADD  IVAMT     TTLAMT
 9111C                     ENDIF
     C                     SELEC
0812AC           IVACNT    WHEQ '1'
0812AC           CODE      ANDEQ'2'
0812AC           IVACNO    ANDEQ*BLANKS
0812AC           IVFL01    ANDEQ*BLANKS
0812AC                     SETOF                     70
0812AC                     SETON                     71
0812AC                     MOVEL'0'       HFLD1
0812AC                     MOVEL'1'       HFLD2
     C           IVACNT    WHEQ '4'
     C           CODE      ANDEQ'2'
     C           IVACNO    ANDEQ*BLANK
     C           IVFL01    ANDEQ*BLANK
     C                     SETOF                     70
     C                     SETON                     71
     C                     MOVEL'0'       HFLD1
     C                     MOVEL'1'       HFLD2
     C           IVACNT    WHEQ '5'
     C           CODE      ANDEQ'2'
     C           IVACNO    ANDEQ*BLANK
     C           IVFL01    ANDEQ*BLANK
     C                     SETOF                     70
     C                     SETON                     71
     C                     MOVEL'0'       HFLD1
     C                     MOVEL'1'       HFLD2
     C                     OTHER
     C                     SETON                     70
     C                     SETON                     71
     C                     MOVEL'1'       HFLD1
     C                     MOVEL'1'       HFLD2
     C                     ENDSL
     C                     WRITESFLREC
   ..C*                    ENDIF
     C           NO        READEINVDTL                   97
     C                     ENDDO
     C***
     C           CODE      IFEQ '2'
     C           RRN       DOWLE89
     C                     ADD  1         RRN
     C                     SETOF                     7071
     C                     Z-ADDRRN       ITEM
     C                     MOVEL*BLANK    ACNT
     C                     MOVEL*BLANK    PDNM
     C                     Z-ADD0         QTY
     C                     Z-ADD0         UPRC
     C                     Z-ADD0         AMT
     C                     Z-ADD0         ACDT
     C                     MOVEL*BLANK    APNO
     C                     MOVEL*BLANK    ACNO
     C                     MOVEL'0'       HFLD1
     C                     MOVEL'0'       HFLD2
     C                     WRITESFLREC
     C                     ENDDO
     C                     ENDIF
     C                     SETON                     107071
1103AC           W#CHYN    IFEQ 'Y'
1103AC                     MOVELERR,51    ERRMSG
1103AC                     MOVEL*BLANKS   W#CHYN
1103AC                     ENDIF
     C*
1205AC           CODE      IFEQ '3'
1205AC           INTYPE    ANDEQ'3'
1205AC           DEPT      ANDEQ'B'
1205AC           TXAR      IFEQ 'P'
1205AC           TXAR      OREQ 'M'
1205AC           TXAR      OREQ 'U'
1205AC                     MOVELERR,54    ERRMSG
1205AC                     ENDIF
1205AC                     ENDIF
     C*
     CSR         PR#OTZ    ENDSR
     C******
     CSR         CK#02     BEGSR
     C                     SETOF                     99
     C                     MOVEA'000000'  *IN,43
     C                     MOVE *BLANK    ERRMSG
     C                     MOVE *BLANK    CUNM
     C                     MOVE *BLANK    TYPE
     C                     MOVE *BLANK    SALE
     C                     MOVE *BLANK    CHK
     C                     MOVE *BLANK    RVID
     C                     Z-ADD0         TTLAMT
     C                     Z-ADD0         AMT1
     C                     Z-ADD0         AMT2
     C                     Z-ADD0         TAX1
9710 C                     Z-ADD0         SDATE   80
     C***
     C                     CLEARGEREC
     C                     MOVEL'99'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVE *BLANK    GRP
     C                     MOVELTXAR      GE1
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ              N97
9710 C   97                Z-ADD99999999  DATE1   80
     C  N97                Z-ADDGECUNO    DATE1
     C***
     C                     SELEC
     C           TYP1      WHEQ '9'
     C           ORNO      ANDNE*BLANK
     C                     SETON                     4699
     C                     MOVELERR,21    ERRMSG
     C           TYP1      WHEQ '9'
     C           CUNO      ANDEQ*BLANK
     C                     SETON                     4599
     C                     MOVELERR,22    ERRMSG
     C           TYP1      WHEQ '9'
     C           CUNO      ANDNE*BLANK
     C                     MOVELCUNO      CBCUNO
     C           CBKEY     CHAINCBCUST               97
     C  N97                MOVELCBCUNM    CUNM
     C  N97                MOVELCBSANO    TYPE
     C  N97                MOVELCBIVCO    CHK
     C  N97                MOVELCBSLMN    SALE
     C  N97                MOVELCBRVCO    RVID
     C   97                SETON                     9945
     C   97                MOVELERR,23    ERRMSG
1006AC   97                GOTO CK#02Z
9105 C*BY S02YSH
  .  C           CHK       IFNE IVTP
1111AC  N97                SETON                     9945
1111AC  N97                MOVELERR,39    ERRMSG
1111AC                     GOTO CK#02Z
  .  C                     ENDIF
     C*
     C                     OTHER
9008 C                     MOVELORNO      S1OREA
LYW  C                     MOVE ORNO      S1ORNO
     C                     Z-ADD1         S1ORTM
1108AC           S1KEY1    CHAINSAMAST               97
     C   97                SETON                     4699
     C   97                MOVELERR,6     ERRMSG
1006AC   97                GOTO CK#02Z
1006AC*
1012AC           S1OREA    IFNE 'H'
1012AC                     MOVEL*BLANKS   W#SACK  1
1012AC                     MOVEL*OFF      *IN96
1012AC           S1KEY1    SETLLSAMAST
1012AC           *IN96     DOWEQ*OFF
1012AC           S1KEY1    READESAMAST              N    96
1012AC   96                LEAVE
1006AC           S1CLOS    IFNE *BLANKS
1012AC                     MOVEL'Y'       W#SACK
1012AC                     ELSE
1012AC                     MOVEL*BLANKS   W#SACK
1012AC                     LEAVE
1012AC                     ENDIF
1012AC                     ENDDO
1012AC           W#SACK    IFEQ 'Y'
1006AC                     SETON                     4699
1006AC                     MOVELERR,50    ERRMSG
1006AC                     GOTO CK#02Z
1006AC                     ENDIF
1012AC                     ENDIF
     C*
9708 C           CUNO      IFNE *BLANK
 .   C           W1CUNO    ANDNECUNO
 .   C                     SETON                     4699
 .   C                     MOVELERR,43    ERRMSG
1012AC                     GOTO CK#02Z
9708 C                     ENDIF
     C*
9008 C  N97                MOVELS1CD01    BK05    1
9708 C  N97N99             MOVELW1CUNO    CUNO
9708 C  N97N99             MOVE BK05      CUNO
     C  N97                MOVE S1SND     SALE
     C  N97                MOVE S1RECV    RVID
     C***
     C  N97                MOVELCUNO      CBCUNO
     C  N97      CBKEY     CHAINCBCUST               97
     C  N97                MOVELCBCUNM    CUNM
     C  N97                MOVELCBSANO    TYPE
     C  N97                MOVELCBIVCO    CHK
     C                     ENDSL
 9408C*BY S02CSF
  .  C           CHK       IFNE IVTP
  .  C                     SETON                     9945
  .  C                     MOVELERR,39    ERRMSG
  .  C                     ENDIF
     C***
     C                     SELEC
     C           TYP1      WHEQ *BLANK
     C                     MOVE ERR,12    ERRMSG
     C                     SETON                     9947
     C           TYP1      WHNE '2'
     C           TYP1      ANDNE'9'
     C                     MOVE ERR,13    ERRMSG
     C                     SETON                     9947
     C                     ENDSL
0010AC           S#IVYN    IFEQ 'Y'
0010AC           TYP1      ANDNE'2'
0010AC                     MOVE ERR,44    ERRMSG
0010AC                     SETON                     9947
0010AC                     ENDIF
     C***:
     C                     Z-ADD0         RRN
     C           RRN       DOWLT150
     C                     MOVEA'0000000' *IN,51
1103AC                     MOVEL*OFF      *IN65
     C                     ADD  1         RRN
     C           RRN       CHAINSFLREC               97
     C   97                LEAVE
     C           ACNT      IFEQ *BLANK
     C           PDNM      ANDEQ*BLANK
     C                     ITER
     C                     ENDIF
     C***
     C           AMT       IFEQ 0
     C           QTY       MULT UPRC      AMT       H
     C                     ENDIF
     C*
     C                     Z-ADDACDT      SDATE
     C                     SELEC
     C           ACNT      WHEQ ' '
     C                     SETON                     9951
     C                     MOVE ERR,7     ERRMSG
     C           ACNT      WHEQ '9'
     C                     ADD  AMT       AMT1   110
     C           ACNT      WHEQ '5'
     C*** 9105 BY S02YSH
9105 C           CHK       IFEQ '2'
9105 C                     SETON                     9951
9105 C                     MOVE ERR,40    ERRMSG
9105 C                     ELSE
     C                     ADD  AMT       TAX1   110
9105 C                     ENDIF
     C           ACNT      WHEQ '4'
     C           AMT       ANDGT0
     C                     ADD  AMT       AMT1   110
     C           ACNT      WHEQ '4'
     C           AMT       ANDLT0
     C                     ADD  AMT       AMT2   110
     C           ACNT      WHNE '4'
     C           ACNT      ANDNE'5'
     C           ACNT      ANDNE'9'
     C                     SETON                     9951
     C                     MOVE ERR,8     ERRMSG
     C                     ENDSL
     C***
     C           PDNM      IFNE *BLANK
0409AC                     MOVELPDNM      F4NAME
0409AC           F4NAME    CHAINHIPROD               97
     C   97                SETON                     9952
     C   97                MOVE ERR,9     ERRMSG
     C                     ENDIF
     C*
0111AC           ACNT      IFEQ '9'
0111AC           PDNM      ANDEQ*BLANKS
0111AC                     SETON                     9952
0111AC                     MOVE ERR,45    ERRMSG
0111AC                     ENDIF
     C*
1108BC  N99      TYP1      IFEQ '2'
1108BC           ACNT      IFNE '4'
1108BC           ACNT      ANDNE'5'
1108BC                     SETON                     9951
1108BC                     MOVELERR,52    ERRMSG
1108BC                     ENDIF
1108BC                     ENDIF
1108BC*
1108BC  N99      TYP1      IFEQ '9'
1108BC           ACNT      IFNE '9'
1108BC           ACNT      ANDNE'5'
1108BC                     SETON                     9951
1108BC                     MOVELERR,53    ERRMSG
1108BC                     ENDIF
1108BC                     ENDIF
     C****
     C*                    CALL 'C01'
     C*                    PARM           ACDT
     C*                    PARM           FLAG1   1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE ACDT      P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM ' '       FLAG1   1
     C           FLAG1     IFNE '0'
     C                     SETON                     9957
     C                     MOVELERR,14    ERRMSG
     C                     ENDIF
     C                     Z-ADDACDT      SDATE
     C***
0001AC*          ACDT      DIV  100       YMCHK   40
0001AC                     MOVELACDT      YMCHK   60
     C           CODE      IFEQ '1'
     C           YMCHK     ANDNEYYMM
     C                     SETON                     9957
     C                     MOVELERR,37    ERRMSG
     C                     ENDIF
     C***
9105 C*9908A               Z-ADDUDATE     W#DATE  80
9908AC           *DATE     SUB  19000000  W#DATE  80
9105 C                     MOVE '03'      W#DATE           本月三日
     C           SDATE     IFLT DATE1
9105 C*          UDATE     ANDGTW#DATE
9908AC           U#SYSD    ANDGTW#DATE
     C           TYP1      ANDNE'9'
     C                     SETON                     9957
     C                     MOVELERR,34    ERRMSG
     C                     ENDIF
0411AC*
0411AC           D#SDAT    IFEQ D#DAT1
0411AC           SDATE     ANDLTDATE1
0411AC           TYP1      ANDEQ'9'
0411AC                     SETON                     9957
0411AC                     MOVELERR,34    ERRMSG
0411AC                     ENDIF
     C***
9301 C*開立日期不可大於系統日期！
9908AC*          ACDT      IFGT UDATE
9908AC           ACDT      IFGT U#SYSD
  .  C                     SETON                     9957
  .  C                     MOVELERR,42    ERRMSG
9301 C                     ENDIF
     C***
     C                     ADD  AMT       TTLAMT
     C                     UPDATSFLREC
     C***
     C                     ENDDO
     C           CHK       IFEQ '3'
     C           AMT1      ADD  AMT2      TMAMT  110
     C           TMAMT     MULT 0.05      TMTAX  110
     C           TAX1      SUB  TMTAX     DIFAMT 110
     C           DIFAMT    IFGT 2
     C           DIFAMT    ORLT -2
     C                     SETON                     99
     C                     MOVELERR,33    ERRMSG
     C                     ENDIF
     C                     ENDIF
     C******
     CSR         CK#02Z    ENDSR
     C******
     CSR         CK#03     BEGSR
     C                     MOVE *BLANK    ERRMSG
     C                     SETOF                     99
     C                     Z-ADD0         TTLAMT
     C                     Z-ADD0         AMT1
     C                     Z-ADD0         AMT2
     C                     Z-ADD0         TAX1
0812AC                     Z-ADD0         W#AMT   90       差異金額
0812AC                     Z-ADD0         W1AMT   90       差異金額合計
0812AC                     Z-ADD0         W#AMT2  90       原發票金額差額
0812AC                     Z-ADD0         W2AMT   90       原發票差異金額合計
     C           CODE      IFEQ '3'
1205AC           INTYPE    IFEQ '3'
1205AC           DEPT      ANDEQ'B'
1205AC           TXAR      IFEQ 'P'
1205AC           TXAR      OREQ 'M'
1205AC           TXAR      OREQ 'U'
1205AC                     MOVELERR,54    ERRMSG
1205AC                     ENDIF
1205AC                     ENDIF
     C                     GOTO CK#03Z
     C                     ENDIF
     C***
     C                     Z-ADD0         RRN
     C           RRN       DOWLT150
     C                     MOVEA'0000000' *IN,51
1103AC                     MOVEL*OFF      *IN65
     C                     ADD  1         RRN
     C           RRN       CHAINSFLREC               97
     C   97                LEAVE
     C           ACNT      IFEQ *BLANK
     C           PDNM      ANDEQ*BLANK
     C                     ITER
     C                     ENDIF
     C***
     C                     SELEC
     C           ACNT      WHEQ ' '
     C                     SETON                     9951
     C                     MOVE ERR,7     ERRMSG
     C           ACNT      WHEQ '1'
     C                     ADD  AMT       AMT1   110
     C           ACNT      WHEQ '9'
     C                     ADD  AMT       AMT1   110
     C           ACNT      WHEQ '5'
     C                     ADD  AMT       TAX1   110
     C           ACNT      WHEQ '4'
     C           AMT       ANDLT0
     C                     ADD  AMT       AMT2   110
     C           ACNT      WHEQ '4'
     C           AMT       ANDGT0
     C                     ADD  AMT       AMT1   110
     C           HFLD2     WHEQ '0'
     C           ACNT      ANDNE'4'
     C           ACNT      ANDNE'5'
     C                     SETON                     9951
     C                     MOVE ERR,26    ERRMSG
     C                     ENDSL
     C***
     C           PDNM      IFNE *BLANK
0409AC                     MOVELPDNM      F4NAME
0409AC           F4NAME    CHAINHIPROD               97
     C   97                SETON                     9952
     C   97                MOVE ERR,9     ERRMSG
     C                     ENDIF
     C***
     C*          HFLD1     IFEQ '0'
     C*          ACNT      ANDEQ'4'
     C*          AMT       ANDGE0
     C*                    SETON                     9955
     C*                    MOVE ERR,27    ERRMSG
     C*                    ENDIF
     C****
     C*                    CALL 'C01'
     C*                    PARM           ACDT
     C*                    PARM           FLAG1   1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE ACDT      P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE
     C                     PARM '1'       P#MODE
     C                     PARM           P#MTL
     C                     PARM           P#LY
     C                     PARM           FLAG1
     C           FLAG1     IFNE '0'
     C                     SETON                     9957
     C                     MOVELERR,14    ERRMSG
     C                     ENDIF
     C***
0812AC           ACNT      IFEQ '1'
0812AC           S1AMT     SUB  AMT       W#AMT
0812AC           W#AMT     IFLT 0
0812AC                     MULT -1        W#AMT
0812AC                     ENDIF
0812AC                     ADD  W#AMT     W1AMT
0812AC           S2AMT     SUB  AMT       W#AMT2
0812AC           W#AMT2    IFLT 0
0812AC                     MULT -1        W#AMT2
0812AC                     ENDIF
0812AC                     ADD  W#AMT2    W2AMT
0812AC                     ENDIF
     C*
     C                     ADD  AMT       TTLAMT
     C                     MOVELHFLD1     *IN70
     C                     MOVELHFLD2     *IN71
     C                     UPDATSFLREC
     C***
     C                     ENDDO
     C***
0812AC  N99      W1AMT     IFGT 8
0812AC                     SETON                     99
0812AC                     MOVELERR,48    ERRMSG
0812AC                     GOTO CK#03Z
0812AC                     ENDIF
0812AC*
0812AC  N99      W2AMT     IFGT 8
0812AC                     SETON                     99
0812AC                     MOVELERR,49    ERRMSG
0812AC                     GOTO CK#03Z
0812AC                     ENDIF
0812AC*
     C           CHK       IFEQ '3'
     C           AMT1      ADD  AMT2      TMAMT  110
     C           TMAMT     MULT 0.05      TMTAX  110
     C           TAX1      SUB  TMTAX     DIFAMT 110
     C           DIFAMT    IFGT 2
     C           DIFAMT    ORLT -2
     C                     SETON                     99
     C                     MOVELERR,33    ERRMSG
     C                     ENDIF
     C                     ENDIF
     C                     SETON                     7170
     C*****
     C           CK#03A    TAG
     C******
     C  N99      TYP1      IFEQ '1'
     C           ORNO      CHAINCPRBAL              N31
     C   31                Z-ADD0         CPNBAL
     C           INKIND    IFEQ '2'
     C           SAMT      DIV  1.05      WSAMT  110
     C           AMT2      DIV  1.05      WAMT2  110
     C                     ENDIF
     C                     SUB  WSAMT     CPNBAL
     C           CPNBAL    ADD  WAMT2     BAL1   110
CLJ  C*          BAL1      IFLT 0
     C*                    SETON                     99
     C*                    MOVELERR,35    ERRMSG
     C*                    ENDIF
     C                     ENDIF
     C***
     C  N99      TYP1      IFEQ '2'
     C           ORNO      CHAINCPRBAL              N31
     C   31                Z-ADD0         CPNBAL
     C           INKIND    IFEQ '2'
     C           SAMT      DIV  1.05      WSAMT
     C           AMT1      DIV  1.05      WAMT1  110
     C                     ENDIF
     C                     SUB  WSAMT     CPNBAL
     C           CPNBAL    ADD  WAMT1     BAL1   110
     C           BAL1      IFLT 0
     C                     SETON                     99
     C                     MOVELERR,36    ERRMSG
     C                     ENDIF
     C                     ENDIF
     C***
     C******
     CSR         CK#03Z    ENDSR
     C******
     CSR         FL#02     BEGSR
     C           CODE      CASEQ'3'       DL#02
     C           CODE      CASEQ'1'       WR#02
     C           CODE      CASEQ'2'       UP#02
     C                     ENDCS
     CSR         FL#02Z    ENDSR
     C****
     CSR         DL#02     BEGSR
     C*9108判斷是否做專案退稅START ---
     C*報稅日期
     C                     Z-ADDININDT    D#ACDT
     C                     SELEC
     C           D#MM      WHEQ 1
     C           D#MM      OREQ 2
     C                     Z-ADD3         D#MM
     C           D#MM      WHEQ 3
     C           D#MM      OREQ 4
     C                     Z-ADD5         D#MM
     C           D#MM      WHEQ 5
     C           D#MM      OREQ 6
     C                     Z-ADD7         D#MM
     C           D#MM      WHEQ 7
     C           D#MM      OREQ 8
     C                     Z-ADD9         D#MM
     C           D#MM      WHEQ 9
     C           D#MM      OREQ 10
     C                     Z-ADD11        D#MM
     C           D#MM      WHEQ 11
     C           D#MM      OREQ 12
     C                     Z-ADD1         D#MM
     C                     ADD  1         D#YY
     C*
     C                     ENDSL
     C*                    Z-ADD5         D#DD
     C*                    Z-ADD13        D#DD
     C                     Z-ADD7         D#DD
     C*
9908AC*                    MOVE UDATE     W#UDAT  60       作廢當日
9908AC                     MOVE U#SYSD    W#UDAT  80       作廢當日
     C*
     C           W#UDAT    IFGT D#ACDT
911220*判斷該張發票之歸屬年月
     C*若該張發票的作廢日小於當月15號，則歸屬年月為上個月
     C*若該張發票的作廢日大於當月15號，則歸屬年月為這個月
9908AC*                    Z-ADDUDATE     D#VUNO
9908AC                     Z-ADDU#SYSD    D#VUNO
     C           D#DD1     IFLT 7
     C           D#MM1     IFEQ 1
     C                     SUB  1         D#YY1
     C                     Z-ADD12        D#MM1
     C                     ELSE
     C                     SUB  1         D#MM1
     C                     ENDIF
     C                     ENDIF
911220*
     C*取得正確的資料項次
     C                     MOVELTXAR      AXAREA
     C                     MOVELORNO      AXORNO
9112 C                     MOVELD#YYMM    W#YYMM  6
     C                     MOVE W#YYMM    AXYYMM           歸屬年月
     C                     Z-ADD999       AXITEM
     C*
     C           K#BTAX    SETGTRARBTAX                    移至檔尾
     C                     READPRARBTAX             N    97
     C           *IN97     IFEQ *ON
     C           AXAREA    ORNE AXAREA
     C           AXORNO    ORNE AXORNO
     C                     Z-ADD1         W#AXTM  30       新資料項次
     C                     ELSE
     C           AXITEM    ADD  1         W#AXTM           該訂單項次
     C                     ENDIF
     C*開始寫入
     C                     MOVELTXAR      AXAREA
     C                     MOVELORNO      AXORNO
     C                     MOVELD#YYMM    W#YYMM  6
     C                     MOVE W#YYMM    AXYYMM           歸屬年月
     C                     Z-ADDW#AXTM    AXITEM
     C*
     C           K#BTAX    CHAINRARBTAX              97
     C                     CLEARRARBTAX
     C                     MOVE 'Y'       AXTRFL           傳輸碼
     C                     MOVE 'C'       AXFLAG           處理代碼
     C                     MOVE TXAR      AXAREA           作廢廠區
 9112C                     MOVELD#YYMM    W#YYMM  6
     C                     MOVE W#YYMM    AXYYMM           歸屬年月
     C                     MOVE INCUNO    AXCUNO           客戶編號
     C                     MOVE INCUNM    AXCUNM           客戶名稱
     C                     MOVE ORNO      AXORNO           訂單號碼
 9201C                     Z-ADDW#AXTM    AXITEM           資料項次
     C                     MOVE NO        AXIVNO           作廢發票
     C                     MOVE ININDT    AXDLDT           開立日期
     C                     MOVE INATAX    AXATTX           銷項稅額
     C*
     C           INNO      CHAINTXREC               N98    一定找到
     C*
     C           TXCODE    IFEQ 'SA04'
     C                     MOVE '1'       AXCODE           單據別
     C                     ELSE
     C                     MOVE '2'       AXCODE
     C                     ENDIF
     C*
     C                     MOVE TXNO      AXVNNO           單據號碼
 9201C                     MOVELD#ACDT    W#ACDT  8
 9201C                     MOVE W#ACDT    AXVUNO           資料年月
     C                     MOVE U#USID    AXADDM           建立人員
9908AC*                    MOVE UDATE     AXADDD           建立日期
9908AC                     MOVE U#SYSD    AXADDD           建立日期
     C                     TIME           AXADDT           建立時間
     C                     MOVE U#USID    AXUPDM           異動人員
9908AC*                    MOVE UDATE     AXUPDD           異動日期
9908AC                     MOVE U#SYSD    AXUPDD           異動日期
     C                     TIME           AXUPDT           異動時間
     C*
     C   97                WRITERARBTAX
     C  N97                UPDATRARBTAX
     C                     ENDIF
     C*9108判斷是否做專案退稅END ---
     C                     MOVE NO        INNO
     C           INNO      CHAININVMST               97
     C  N97                MOVE 'C'       INFLAG
     C  N97                MOVE 'D'       INDECD
9908AC* N97                Z-ADDUDATE     INDEDT
9908AC  N97                Z-ADDU#SYSD    INDEDT
     C  N97                MOVE TXAR      INTXAR
9908AC* N97                Z-ADDUDATE     INTXDT
9908AC  N97                Z-ADDU#SYSD    INTXDT
     C  N97                UPDATINREC
0812AC                     MOVELU#USID    INUPDM
0812AC                     Z-ADDU#SYSD    INUPDD
0812AC                     TIME           INUPDT
0812AC                     MOVEL'ARE001R' INAPNM
0812AC                     WRITEINVMG
     C****
     C                     MOVE *BLANK    PNO     8
     C                     MOVELNO        IVNO
     C                     MOVE *BLANK    IVACNT
     C                     Z-ADD0         IVITEM
     C           IVKEY     SETLLIVREC                97
     C           *IN97     DOWEQ'0'
     C  N97      IVNO      READEIVREC                    97
     C  N97      IVACNT    IFEQ '1'
     C                     MOVELIVAPNO    PNO
     C                     ENDIF
     C  N97                MOVE 'C'       IVFLAG
     C  N97                MOVE 'D'       IVDECD
9908AC* N97                Z-ADDUDATE     IVDEDT
9908AC  N97      *DATE     SUB  19000000  IVDEDT
9908AC* N97                Z-ADDUDATE     IVTXDT
9908AC  N97      *DATE     SUB  19000000  IVTXDT
     C  N97                MOVE TXAR      IVTXAR
     C  N97                UPDATIVREC
0812AC                     MOVELU#USID    IVUPDM
0812AC                     Z-ADDU#SYSD    IVUPDD
0812AC                     TIME           IVUPDT
0812AC                     MOVEL'ARE001R' IVAPNM
0812AC                     WRITEINVDG
     C                     ENDDO
     C****
0612AC*電子發票介面檔
0612AC*
0711AC           ININDT    IFGE D#INTR
0612AC           INAREA    LOKUPTAB1      TAB2           50*EQ
0612AC   50                MOVELTAB2      W1CUNO  6
0706AC           INNO      CHAINRINEDIN              89
0706AC*
0612AC   89                CLEARRINEDIN
0706AC   89                MOVEL'A'       DNFLAG
0706AC  N89                MOVEL'C'       DNFLAG
0706AC                     MOVELTXAR      DNAREA
0710AC                     SELEC
0710AC           INKIND    WHEQ '3'                        三聯式
0612AC                     MOVEL'A0501'   DNMSTE
0710AC           INKIND    WHEQ '2'                        二聯式
0710AC                     MOVEL'C0501'   DNMSTE
0710AC                     ENDSL
0612AC                     MOVELINNO      DNNO
0612AC           ININDT    ADD  19110000  W#INDT  80
0612AC                     MOVELW#INDT    DNINDT
0612AC           INCUNO    CHAINCBCUST              N97
0612AC                     MOVELCBMUID    DNBYID
0612AC           W1CUNO    CHAINCBCUST              N97
0612AC                     MOVELCBMUID    DNSLID
0612AC           INDEDT    ADD  19110000  W#DEDT  80
0612AC                     MOVELW#DEDT    DNDEDT
0612AC                     TIME           W#DETM
0612AC                     MOVELD#H       D1HH
0612AC                     MOVEL':'       D1C1
0612AC                     MOVELD#M       D1MM
0612AC                     MOVEL':'       D1C2
0612AC                     MOVELD#S       D1SS
0612AC*
0612AC           INNO      CHAINRDELMST             N97
0612AC* N97                MOVEL''      BOE
0805AC  N97                MOVELDMDESP    DNCLRN
0612AC* N97                MOVE ''      BOF
0705AC  N97      DMDESP    IFEQ ' '
0705AC                     MOVEL*ON       *IN97
0705AC                     ENDIF
0612AC*
0612AC   97                MOVELERR1,1    DNCLRN
0612AC*
0612AC           *DATE     SUB  19000000  DNXPDD
0612AC                     TIME           DNXPDT
0612AC                     MOVELU#USID    DNXUSR
0612AC*
0612AC   89                WRITERINEDIN
0706AC  N89                UPDATRINEDIN
0612AC                     MOVEL'ARE001R' DNAPNM
0805AC           *DATE     SUB  19000000  DNCHDD
0805AC                     TIME           DNCHDT
0805AC                     MOVELU#USID    DNCUSR
0612AC                     WRITEINEDG
0612AC*
0711AC                     ENDIF
     C                     MOVE *BLANK    TXIVNO
     C                     MOVELNO        TXIVNO
     C           NO        SETLLTRNDTLL2             97
     C  N97      NO        READETRNDTLL2                 97
     C           *IN97     DOWEQ'0'
     C                     MOVE 'C'       TXFLAG
     C                     MOVE ' '       TXFL02
     C           DEPT      IFNE 'B'
     C                     MOVEL'*'       TXIVNO
     C                     ELSE
     C                     MOVE *BLANK    TXIVNO
1205AC*
1205AC*廠區P.M.U，作廢調整發票一併刪除調整單
1205AC*
1205AC           TXAR      IFEQ 'P'
1205AC           TXAR      OREQ 'M'
1205AC           TXAR      OREQ 'U'
1205AC           TXCODE    IFEQ 'AR05'
1205AC                     MOVE 'D'       TXFLAG
1205AC                     EXSR UP#DW
1205AC                     ENDIF
1205AC                     ENDIF
     C                     ENDIF
     C                     MOVE TXAR      TXTXAR
9908AC*                    Z-ADDUDATE     TXTXDT
9908AC           *DATE     SUB  19000000  TXTXDT
     C                     UPDATTXREC
     C           NO        READETRNDTLL2                 97
     C                     ENDDO
     C**** 更新預收餘額
     C           SAMT      IFNE 0
     C           INORNO    CHAINCPRBAL               31
     C   31                GOTO DL#02Z
     C           INTYPE    IFEQ '1'
     C                     ADD  WSAMT     CPBAMT
     C                     SUB  WSAMT     CPNBAL
CLJ  C           INBAMT    IFNE 0                          有扣預收
     C                     Z-ADDINBAMT    W#XAMT 110       扣預沖回
     C                     EXSR SR9000
---  C                     ENDIF
     C                     ENDIF
     C           INTYPE    IFEQ '2'
     C                     SUB  WSAMT     CPAAMT
     C                     SUB  WSAMT     CPNBAL
     C                     ENDIF
     C                     UPDATCPREC
     C                     ENDIF
     C*
     C*9302判斷發票是否有進入承購系統START ---
     C           INNO      CHAINRARINVM              50
     C           *IN50     IFEQ *OFF
     C           AMDLC1    ANDEQ*BLANK                     承購下載碼
     C           AMBLCB    ANDEQ*BLANK                     承購下載批號
     C           AMDLM1    ANDEQ*BLANK                     承購下載人員
     C           AMAPCD    ANDEQ*BLANK                     承購確認碼
     C           AMDLD1    ANDEQ0                          承購下載日期
     C                     MOVEL'C'       AMFLAG
     C                     MOVEL'D'       AMDELT           作廢碼
9908AC*                    MOVE UDATE     AMDELD           作廢日期
9908AC           *DATE     SUB  19000000  AMDELD           作廢日期
     C                     MOVELU#USID    AMUPDM           異動人員
9908AC*                    MOVE UDATE     AMUPDD           異動時間
9908AC           *DATE     SUB  19000000  AMUPDD           異動時間
     C                     TIME           AMUPDT           異動時間
     C                     UPDATRARINVM
     C                     ENDIF
     C*9302判斷發票是否有進入承購系統END ---
     C*
0301AC*發票作廢，ARDSDT上作廢碼
0301AC           INNO      SETLLRARDSDT
0301AC                     MOVEL*OFF      *IN67
0301AC           *IN67     DOWEQ*OFF
0301AC           INNO      READERARDSDT                  67
0301AC   67                LEAVE
0301AC                     MOVEL'C'       ASFLAG
0301AC                     MOVEL'D'       ASDECD
0301AC           *DATE     SUB  19000000  ASDEDT
0301AC                     MOVELU#USID    ASUPDM           異動人員
0301AC           *DATE     SUB  19000000  ASUPDD
0301AC                     TIME           ASUPDT
0301AC  N67                UPDATRARDSDT
0301AC                     ENDDO
0301AC*
     CSR         DL#02Z    ENDSR
     *****
1205AC****************************************************************
1205AC           UP#DW     BEGSR
1205AC****************************************************************
1205AC***  UPDATE TO ARDLWT  ***
1205AC*
1205AC*寫入ARDLWT調整單號
1205AC                     MOVELTXORNO    AWORNO           訂單編號
1205AC                     MOVELTXPCNO    AWWTNO           磅單編號
1205AC           W#DLWT    CHAINRARDLWT              40
1205AC*
1205AC*清空ARDLWT調整單號
1205AC*
1205AC                     MOVEL*BLANK    AWTXNO           調整單號
1205AC                     Z-ADD0         AWTXDT           調整單開立日
1205AC                     MOVEL*BLANK    AWCOD1           調整開立碼
1205AC*
1205AC  N40                UPDATRARDLWT
1205AC*
1205ACSR                   ENDSR
     C*****************************************************************
     CSR         WR#02     BEGSR
     C           IVTP      IFEQ '2'
     C                     MOVEL'01'      GEKIND
     C                     ELSE
     C                     MOVEL'02'      GEKIND
     C                     ENDIF
     C                     MOVE *BLANK    GEPRIN
     C                     MOVELTXAR      GE1
9806 C                     MOVELYYMM      GE2
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ               97
     C  N97                ADD  1         GECUNO
     C  N97                UPDATGEREC
     C                     MOVELGEPRE     NOA
     C                     Z-ADDGECUNO    NOB
     C                     MOVELNOG       NO
     C***
     C                     CLEARINREC
     C                     MOVE NO        INNO
     C                     MOVE CUNO      INCUNO
     C                     MOVE CUNM      INCUNM
9008 C                     MOVELORNO      INORNO
     C                     Z-ADDSDATE     ININDT
     C                     MOVELCHK       INKIND
     C                     MOVELSALE      INSALE
     C                     MOVELTYPE      INSATP
     C                     MOVELRVID      INRVID
     C                     MOVELTYP1      INTYPE
0010AC                     MOVELS#IVYN    D#RESV
     C                     MOVELTXAR      INAREA
     C                     MOVE '1'       INTXTP
     C                     Z-ADDAMT1      INAAMT
     C                     Z-ADDTAX1      INATAX
     C                     Z-ADDAMT2      INBAMT
     C                     ADD  INAAMT    INNBAL
     C                     ADD  INATAX    INNBAL
     C                     ADD  INBAMT    INNBAL
     C                     ADD  INCAMT    INNBAL
     C                     ADD  INCTAX    INNBAL
     C                     ADD  INDAMT    INNBAL
     C                     ADD  INDTAX    INNBAL
     C                     ADD  INEAMT    INNBAL
     C                     MOVE 'A'       INFLAG
     C                     MOVE TXAR      INTXAR
9908AC*                    Z-ADDUDATE     INTXDT
9908AC           *DATE     SUB  19000000  INTXDT
     C                     WRITEINREC
0812AC                     MOVELU#USID    INUPDM
0812AC                     Z-ADDU#SYSD    INUPDD
0812AC                     TIME           INUPDT
0812AC                     MOVEL'ARE001R' INAPNM
0812AC                     WRITEINVMG
     C****
     C***
     C                     Z-ADD0         RRN
     C           RRN       DOWLT150
     C                     ADD  1         RRN
     C           RRN       CHAINSFLREC               97
     C   97                LEAVE
     C           ACNT      IFEQ *BLANK
     C           PDNM      ANDEQ*BLANK
     C                     ITER
     C                     ENDIF
     C***
     C                     CLEARIVREC
     C                     MOVEL'A'       IVFLAG
     C                     MOVELNO        IVNO
     C                     Z-ADDITEM      IVITEM
     C                     MOVELACNT      IVACNT
     C                     MOVELPDNM      IVPDCD
     C                     Z-ADDQTY       IVQTY
     C                     Z-ADDUPRC      IVUPRC
     C           UPRC      MULT 100000    TPRC
     C                     MOVE TPRC      TPRCA
     C                     MOVELTPRCA     IVRESV
     C                     Z-ADDAMT       IVAMT
     C                     MOVELORNO      IVORNO
     C                     MOVELTXAR      IVTXAR
     C                     Z-ADDACDT      IVACDT
9908AC*                    Z-ADDUDATE     IVTXDT
9908AC           *DATE     SUB  19000000  IVTXDT
     C                     MOVE 'Y'       IVFL02
     C                     SELEC
     C           TYP1      WHEQ '1'
     C           ACNT      IFEQ '4'
     C                     MOVE 'E'       IVFL03
CLJ  C                     Z-ADD0         W#IAMT 110       原明細金額
CLJ  C                     EXSR SR9100
     C                     ELSE
     C                     MOVE 'A'       IVFL03
     C                     ENDIF
     C           TYP1      WHEQ '2'
     C                     MOVE 'F'       IVFL03
     C           TYP1      WHEQ '3'
     C                     MOVE 'B'       IVFL03
     C                     OTHER
     C                     MOVE 'Z'       IVFL03
     C                     ENDSL
     C                     WRITEIVREC
0812AC                     MOVELU#USID    IVUPDM
0812AC                     Z-ADDU#SYSD    IVUPDD
0812AC                     TIME           IVUPDT
0812AC                     MOVEL'ARE001R' IVAPNM
0812AC                     WRITEINVDG
     C***
     C                     ENDDO
     C******
     C                     CLEARGEREC
     C                     MOVEL'99'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVE *BLANK    GRP
     C                     MOVELTXAR      GE1
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ               97
     C                     Z-ADDININDT    GECUNO
     C  N97                UPDATGEREC
     C**** 更新預收餘額
     C           INTYPE    IFEQ '2'
     C           INORNO    CHAINCPRBAL               31
     C   31                CLEARCPREC
     C                     MOVELINORNO    CPORNO
     C                     MOVELINCUNO    CPCUNO
     C                     MOVELINCUNM    CPCUNM
     C                     ADD  INAAMT    CPAAMT
     C                     ADD  INAAMT    CPNBAL
     C   31                WRITECPREC
     C  N31                UPDATCPREC
     C                     ENDIF
     C****
     C***
     CSR                   ENDSR
     *****
     CSR         UP#02     BEGSR
     C***
     C           NO        CHAININVMST               97
     C   97                GOTO UP#02A
     C                     Z-ADDAMT1      INAAMT
     C                     Z-ADDTAX1      INATAX
     C                     Z-ADDAMT2      INBAMT
     C                     Z-ADDINAAMT    INNBAL
     C                     ADD  INATAX    INNBAL
     C                     ADD  INBAMT    INNBAL
     C                     ADD  INCAMT    INNBAL
     C                     ADD  INCTAX    INNBAL
     C                     ADD  INDAMT    INNBAL
     C                     ADD  INDTAX    INNBAL
     C                     ADD  INEAMT    INNBAL
     C                     ADD  INFAMT    INNBAL
     C                     MOVE 'C'       INFLAG
     C                     MOVE TXAR      INTXAR
9908AC*                    Z-ADDUDATE     INTXDT
9908AC           *DATE     SUB  19000000  INTXDT
     C                     UPDATINREC
0812AC                     MOVELU#USID    INUPDM
0812AC                     Z-ADDU#SYSD    INUPDD
0812AC                     TIME           INUPDT
0812AC                     MOVEL'ARE001R' INAPNM
0812AC                     WRITEINVMG
     C****
     C           UP#02A    TAG
     C***
     C                     Z-ADD0         RRN
     C           RRN       DOWLT150
     C                     ADD  1         RRN
     C           RRN       CHAINSFLREC               97
     C   97                LEAVE
     C           ACNT      IFEQ *BLANK
     C           PDNM      ANDEQ*BLANK
     C           HFLD1     OREQ '1'
     C                     ITER
     C                     ENDIF
     C***
     C                     MOVELNO        IVNO
     C                     Z-ADDITEM      IVITEM
     C                     MOVELACNT      IVACNT
     C           IVKEY     CHAININVDTL               97
     C   97                EXSR @WR#DL
     C  N97                EXSR @UP#DL
     C***
     C                     ENDDO
     C******
     C**** 更新預收餘額
     C           INTYPE    IFEQ '2'
     C           INORNO    CHAINCPRBAL               31
     C           *IN31     IFEQ '0'
     C           INKIND    IFEQ '2'
     C           INAAMT    DIV  1.05      WAAMT  110
     C                     ELSE
     C                     Z-ADDINAAMT    WAAMT
     C                     ENDIF
     C                     ADD  WAAMT     CPAAMT
     C                     ADD  WAAMT     CPNBAL
     C                     SUB  WSAMT     CPAAMT
     C                     SUB  WSAMT     CPNBAL
     C                     UPDATCPREC
     C                     ENDIF
     C                     ENDIF
     C****
     C**** 更新預收餘額
     C           INTYPE    IFEQ '1'
     C           INORNO    CHAINCPRBAL               31
     C           *IN31     IFEQ '0'
     C           INKIND    IFEQ '2'
     C           INBAMT    DIV  1.05      WBAMT  110
     C                     ELSE
     C                     Z-ADDINBAMT    WBAMT
     C                     ENDIF
     C                     SUB  WBAMT     CPBAMT
     C                     ADD  WBAMT     CPNBAL
     C                     ADD  WSAMT     CPBAMT
     C                     SUB  WSAMT     CPNBAL
     C                     UPDATCPREC
     C                     ENDIF
     C                     ENDIF
     C****
     CSR                   ENDSR
     C**---------------------------------------**
     C**   WRITE  TO  INVDTL                   **
     C**---------------------------------------**
     CSR         @WR#DL    BEGSR
     C                     CLEARIVREC
     C                     MOVEL'A'       IVFLAG
     C                     MOVELNO        IVNO
     C                     Z-ADDITEM      IVITEM
     C                     MOVELACNT      IVACNT
     C                     MOVELPDNM      IVPDCD
     C                     Z-ADDQTY       IVQTY
     C                     Z-ADDUPRC      IVUPRC
     C           UPRC      MULT 100000    TPRC
     C                     MOVE TPRC      TPRCA
     C                     MOVELTPRCA     IVRESV
     C                     Z-ADDAMT       IVAMT
     C                     MOVELORNO      IVORNO
     C                     MOVELTXAR      IVTXAR
     C                     Z-ADDACDT      IVACDT
9908AC*                    Z-ADDUDATE     IVTXDT
9908AC           *DATE     SUB  19000000  IVTXDT
     C                     MOVE 'Y'       IVFL02
     C                     SELEC
     C           ACNT      WHEQ '4'
     C                     MOVE 'E'       IVFL03
CLJ  C                     Z-ADD0         W#IAMT
CLJ  C                     EXSR SR9100
     C           ACNT      WHEQ '5'
     C                     MOVE 'A'       IVFL03
     C                     ENDSL
     C                     WRITEIVREC
0812AC                     MOVELU#USID    IVUPDM
0812AC                     Z-ADDU#SYSD    IVUPDD
0812AC                     TIME           IVUPDT
0812AC                     MOVEL'ARE001R' IVAPNM
0812AC                     WRITEINVDG
     CSR                   ENDSR
     C**---------------------------------------**
     C**   WRITE  TO  INVDTL                   **
     C**---------------------------------------**
     CSR         @UP#DL    BEGSR
CLJ  C                     Z-ADDIVAMT     W#IAMT
     C                     MOVEL'C'       IVFLAG
     C                     MOVELPDNM      IVPDCD
     C                     Z-ADDQTY       IVQTY
     C                     Z-ADDUPRC      IVUPRC
     C           UPRC      MULT 100000    TPRC
     C                     MOVE TPRC      TPRCA
     C                     MOVELTPRCA     IVRESV
     C                     Z-ADDAMT       IVAMT
     C                     MOVELTXAR      IVTXAR
     C                     Z-ADDACDT      IVACDT
9908AC*                    Z-ADDUDATE     IVTXDT
9908AC           *DATE     SUB  19000000  IVTXDT
CLJ  C                     EXSR SR9100
     C                     UPDATIVREC
0812AC                     MOVELU#USID    IVUPDM
0812AC                     Z-ADDU#SYSD    IVUPDD
0812AC                     TIME           IVUPDT
0812AC                     MOVEL'ARE001R' IVAPNM
0812AC                     WRITEINVDG
     CSR                   ENDSR
     C*
     C*****************************************************************
     C*  扣預收款寫入授信集中管控異動記錄並傳回值 W#XAMT (I/O)
     C*****************************************************************
     CSR         SR9000    BEGSR
     C                     CLEARCTREC
9908AC*                    Z-ADDUDATE     CTDATE           日期
9908AC           *DATE     SUB  19000000  CTDATE           日期
     C                     TIME           W#SYST
     C                     Z-ADDD#ST      CTTIME           時間
     C                     MOVELTXAR      CTAREA           廠區
     C                     MOVELDEVNM     CTDSPN           終端機
     C                     MOVELU#USID    CTUSER           使用者
     C                     MOVELINCUNO    CTCUNO           客戶
     C                     MOVEL'AR01'    CTTXID           異動代號
     C                     MOVELINORNO    CTAPNO           單據編號
     C                     Z-ADDW#XAMT    CTXAMT           異動金額
     C                     MOVELINNO      CTRESV           保留碼
     C                     WRITECTREC
     C*
     C           'CCLIB/CC'CAT  'P300P':0 W#PGMN 13         CCP300P
     C                     CALL W#PGMN                     呼叫介面
     C*
     C           K#CT      CHAINCTREC                69
     C           CTRTFL    IFEQ 'Y'
     C                     Z-ADDCTXAMT    W#XAMT           應扣額
     C                     ELSE
     C                     Z-ADD0         W#XAMT
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*****************************************************************
     C*  扣預收款判斷（發票明細異動時）
     C*  扣預收金額為負值，但異動金額為正表示要再扣多少金額；
     C*  所以異動金額＝原金額─新金額
     C*****************************************************************
     CSR         SR9100    BEGSR
     C           INTYPE    IFEQ '1'                        出貨發票
     C           IVACNT    ANDEQ'4'                        且扣預收
     C           W#IAMT    SUB  IVAMT     W#XAMT           異動金額
     C                     Z-ADDW#XAMT    W#TAMT 110       暫存金額
     C                     EXSR SR9000
     C           W#IAMT    SUB  W#XAMT    IVAMT            還原金額
     C           W#XAMT    IFNE W#TAMT
     C                     MOVEL'T'       W#FLAG           異動旗標
     C                     MOVELERR,38    ERRMSG
     C*
     C                     SUB  W#XAMT    W#TAMT
     C           INNO      CHAININREC                69
     C                     ADD  W#TAMT    INBAMT
     C                     UPDATINREC
0812AC                     MOVELU#USID    INUPDM
0812AC                     Z-ADDU#SYSD    INUPDD
0812AC                     TIME           INUPDT
0812AC                     MOVEL'ARE001R' INAPNM
0812AC                     WRITEINVMG
     C                     ENDIF
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*****************************************************************
     C*  發票主副檔重新計算（稅額）並存檔
     C*****************************************************************
     CSR         SR9200    BEGSR
     C                     MOVELINNO      IVNO
     C                     MOVEL'5'       IVACNT
     C                     Z-ADD0         IVITEM
     C           IVKEY     SETLLIVREC                69
     C*
     C           1         DOWEQ1
     C                     READ IVREC                    69
     C           *IN69     IFEQ '1'
     C                     LEAVE
     C                     ENDIF
     C           IVACNT    IFNE '5'
     C                     LEAVE
     C                     ENDIF
     C*
0409AC           IVPDCD    IFEQ *BLANKS                    出貨稅額
     C           INAAMT    ADD  INBAMT    W#TAMT
     C           W#TAMT    MULT 0.05      INATAX    H
     C                     Z-ADDINATAX    IVAMT
     C                     UPDATIVREC
0812AC                     MOVELU#USID    IVUPDM
0812AC                     Z-ADDU#SYSD    IVUPDD
0812AC                     TIME           IVUPDT
0812AC                     MOVEL'ARE001R' IVAPNM
0812AC                     WRITEINVDG
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C*
     C           INNO      CHAININREC                69
     C                     EXSR SR9210                     試算主檔
     C                     UPDATINREC
0812AC                     MOVELU#USID    INUPDM
0812AC                     Z-ADDU#SYSD    INUPDD
0812AC                     TIME           INUPDT
0812AC                     MOVEL'ARE001R' INAPNM
0812AC                     WRITEINVMG
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  試算發票主檔各項金額
     C*----------------------------------------
     CSR         SR9210    BEGSR
     C                     Z-ADD0         INAAMT           出貨金額
     C                     Z-ADD0         INATAX           出貨稅額
     C                     Z-ADD0         INBAMT           扣預收
     C                     Z-ADD0         INCAMT           折退金額
     C                     Z-ADD0         INCTAX           折退稅額
     C                     Z-ADD0         INDAMT           扣預金沖回
     C                     Z-ADD0         INDTAX           扣預稅沖回
     C                     Z-ADD0         INEAMT           繳款金額
     C                     Z-ADD0         INFAMT           退票金額
     C                     Z-ADD0         INNBAL           未收餘額
     C*
     C           INNO      CHAINIVREC               N69
     C           *IN69     DOWEQ'0'
     C           INDECD    IFEQ 'D'                        已作廢全計
     C           IVFLAG    ORNE 'D'                        刪除者不計
     C           IVDECD    ANDNE'D'
     C                     SELEC
     C*
     C           IVACNT    WHEQ '1'                        出貨
     C                     ADD  IVAMT     INAAMT
     C           IVACNT    WHEQ '2'                        退貨
     C                     ADD  IVAMT     INCAMT
     C           IVACNT    WHEQ '3'                        折讓
     C                     ADD  IVAMT     INCAMT
     C           IVACNT    WHEQ '4'
     C           IVAMT     IFLT 0                          負數
     C           INTYPE    IFEQ '2'                        預收發票
     C                     ADD  IVAMT     INCAMT           折預收
     C                     ELSE
     C                     ADD  IVAMT     INBAMT           扣預收
     C                     ENDIF
     C                     ELSE                            正數
     C           INTYPE    IFEQ '2'                        預收發票
     C                     ADD  IVAMT     INAAMT           預收金額
     C                     ELSE
     C                     ADD  IVAMT     INDAMT           扣預收沖回
     C                     ENDIF
     C                     ENDIF
     C           IVACNT    WHEQ '5'
     C                     SELEC
0409AC           IVPDCD    WHEQ *BLANKS                    出貨稅額
     C                     ADD  IVAMT     INATAX
     C           IVPDCD    WHEQ 'A    '                    預收沖回稅
     C                     ADD  IVAMT     INDTAX
     C           IVPDCD    WHEQ 'B    '                    退貨稅額
     C                     ADD  IVAMT     INCTAX
     C           IVPDCD    WHEQ 'C    '                    折讓稅額
     C                     ADD  IVAMT     INCTAX
     C                     OTHER                           打錯
     C                     ADD  IVAMT     INATAX
     C                     ENDSL
     C           IVACNT    WHEQ '6'                        繳款
     C                     ADD  IVAMT     INEAMT
     C           IVACNT    WHEQ '7'                        退票轉出
     C                     ADD  IVAMT     INFAMT
     C           IVACNT    WHEQ '8'                        勞務
     C                     ADD  IVAMT     INAAMT
     C           IVACNT    WHEQ '9'                        其他
     C                     ADD  IVAMT     INAAMT
     C*
     C                     ENDSL
     C                     ENDIF
     C           INNO      READEIVREC               N    69
     C                     ENDDO
     C*
     C           INAAMT    ADD  INATAX    INNBAL           彙總未收
     C                     ADD  INBAMT    INNBAL
     C                     ADD  INCAMT    INNBAL
     C                     ADD  INCTAX    INNBAL
     C                     ADD  INDAMT    INNBAL
     C                     ADD  INDTAX    INNBAL
     C                     ADD  INEAMT    INNBAL
     C                     ADD  INFAMT    INNBAL
     CSR                   ENDSR
     C*
     C*****************************************************************
**  TAB1 TAB2
PMM001 MMM007 HMM005 KMM003 UMM006
** ERR
０１－功能代碼必須輸入．
０２－功能代碼必須為１，３，４
０３－發票號碼已存在，不可新增
０４－發票號碼不存在
０５－此張發票已作廢，不可再作廢
０６－訂單號碼不存在
０７－種類必須輸入
０８－種類必須為４，５，９
０９－品名代號不存在
１０－訂單無此項目
１１－非新增時發票號碼必須輸入
１２－發票種類必須輸入
１３－發票類別必須為２，９
１４－日期錯誤
１５－新增時發票號碼不可輸入
１６－本月份發票起迄號碼尚未設定，請先設定再開發票
１７－發票聯式必須為２或３．
１８－發票號碼已超過截止號碼，請查核．
１９－此張發票已繳款，不可作廢，請查核．
２０－此張發票非屬本廠區，不可作廢，請查核．
２１－此張發票為其他發票，訂單號碼不可輸入．
２２－此張發票為其他發票，客戶編號必須輸入．
２３－客戶編號不存在．
２４－此張發票已繳款，不可更正，請查核．
２５－此張發票已列印，不可更正，請作廢重開．
２６－發票更正時，輸入種類必須為苠傀虳恛苠椅苤D
２７－發票更正時，扣預收金額或稅額必須小於０．
２８－此張發票類別並非出貨不可更正，請作廢重開．
２９－此張發票非屬本廠區，不可更正，請查核．
３０－此張發票已作廢，不可更正，請查核．
３１－年月錯誤
３２－此張發票已超過作廢期限，不可作廢，需會同財會人員作廢，請查核．
３３－此張發票稅額與銷售額計算出之稅額相差２元以上，不合理，請查核．
３４－此張發票之發票日期小於已開過之發票日期，不合理，請查核．
３５－此訂單之預收貨款不足扣，請查核．
３６－訂單之預收貨款已扣，更改或刪除預收發票將導致超扣，請查核．
３７－入帳日期之年月與開立年月不符合，請查核．
３８－出貨發票扣預收金額不足，發票明細金額與原畫面不同，請查核！
３９－欲開之發票聯式與客戶基本設定不同，請查核！
４０－二聯式發票稅額不得外加，請查核！
４１－欲作廢之發票有扣預收金額，請通知財會代開與重新合計！
４２－開立日期不可大於系統日期！
４３－此訂單號碼不屬於此客戶，請查核！
４４－須為預收發票，預收─鋼筋欄位才可為Y！
４５－欄位不可空白!
４６－空白發票號碼已上送電子發票介面檔，不可再開立發票
４７－請先列印發票再作廢!!
４８－發票調整金額超過8元，請確認!!
４９－發票已調整過，再調整金額超過原發票金額8元，請確認!!
５０－訂單已上結案碼，不可開立發票!!
５１－品名反白表示為運費折扣資料
５２－預收貨款發票，種類只能為4或5!!
５３－其他類發票，種類只能為9或5!!
５４－調整發票作廢，會一併刪除調整單!!
** ERR1
發票內容有誤
