     H****************************************************************
     H*
     H*            UPDATE  DATE  99/08/31  2010AR517  S00WCJ (9908A)
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FRCVDTL  IP  E                    DISK
     FBANKID  IF  E           K        DISK
     FBANKAC  IF  E           K        DISK
     FQPRINT  O   F     158            PRINTER
      *************************************************************
     E                    TAB1    1   6  1   TABR1  10
     I*************************************************************
     IRVREC
     I                                              RVDUDTL3
     I                                              RVTANOL2
     I                                              RVNO  L1
     I****
     IAADS        DS
     I              'も當日託收銀行票據-C         CON3                       X
     I              '明細表も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     I            DS
     I                                        1   7 RVVN01
     I                                        3   3 W#N01
     IBBDS        DS
     I                                        1  10 RVTANO
     I                                        1   1 ACTYPE
     I                                        2   2 ACCURR
     I                                        3  10 ACACNO
     I                                       11  17 ACBAID
     I                                       11  13 BAID1
     I                                       14  17 BAID2
      *
9008 I                                       21  280YMD
LYW  I                                       21  260YM
 .   I                                       21  240YY
 .   I                                       25  260MM
 .   I                                       27  280DD
     I           UDS
9008 I                                        1   80DATE1
LYW  I                                        9  160DATE2
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
9908AC           *DATE     SUB  19000000  U#SYSD  80
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L3                EXSR DL#L3
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     CL3                   EXSR TL#L3
     CLR                   EXSR TL#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C           BKKEY     KLIST
     C                     KFLD           BAID1
     C                     KFLD           BAID2
     C           ACKEY     KLIST
     C                     KFLD           ACTYPE
     C                     KFLD           ACCURR
     C                     KFLD           ACACNO
      *
     C                     Z-ADD0         BAMT   110
     C                     Z-ADD0         BQTY    50
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD55        TCNT    30
     C                     MOVE *ALL'='   ALS   145
     C                     MOVE *ALL'-'   ALQ    96
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD8         LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L3                             **
     C**----------------------------------------------**
     CSR         DL#L3     BEGSR
     C                     Z-ADD0         EAMT   110
     C                     Z-ADD0         EQTY    50
     C                     Z-ADD0         FAMT   110
     C                     Z-ADD0         FQTY    50
     C                     Z-ADD0         GAMT   110
     C                     Z-ADD0         GQTY    50
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         AAMT   110
     C                     Z-ADD0         AQTY    50
     C                     Z-ADD0         CAMT   110
     C                     Z-ADD0         CQTY    50
     C                     Z-ADD0         DAMT   110
     C                     Z-ADD0         DQTY    50
      *
     C           ACKEY     CHAINBANKAC               31
     C  N31      BKKEY     CHAINBANKID               31
     C  N31                MOVELBABNAM    CHARAX 10
     C   31                MOVEL*BLANK    CHARAX
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     MOVE *BLANK    CHAR10 10
     C           RVNTTP    LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     CHAR10
     C  N31                MOVEL'其他'  CHAR10
      *
     C           RVPLAC    COMP 'D'                      31
     C   31                MOVEL'本埠'  CHAR6   6
     C  N31                MOVEL'外埠'  CHAR6
      *
     C                     MOVE *BLANK    CHAR    1
9908AC           RVDUDT    COMP U#SYSD               31  31
     C   31                MOVEL'*'       CHAR
      *
     C                     Z-ADDRVRAMT    DEC9    90
      *
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
      *
     C           W#N01     IFEQ 'J'
     C           RVTANO    IFEQ *BLANK
     C                     MOVE RVSANO    RVTANO
     C                     ENDIF
      *
     C           ACKEY     CHAINBANKAC               31
     C  N31                MOVELACBNAM    CHARAX 10
     C   31                MOVEL*BLANK    CHARAX
     C                     ENDIF
      *
     C                     MOVEL*BLANK    R#NTNO 20
     C                     MOVELRVNTNO    R#NTNO
      *
     C                     EXCPTED#01
     C                     ADD  1         AQTY
     C                     ADD  RVRAMT    AAMT
     C                     ADD  1         GQTY
     C                     ADD  RVRAMT    GAMT
     C                     ADD  1         BQTY
     C                     ADD  RVRAMT    BAMT
     C                     SELEC
     C           RVPLAC    WHEQ 'D'
     C                     ADD  1         CQTY
     C                     ADD  RVRAMT    CAMT
     C           RVPLAC    WHEQ 'E'
     C                     ADD  1         DQTY
     C                     ADD  RVRAMT    DAMT
     C                     ENDSL
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C           CQTY      COMP 0                    51
     C           DQTY      COMP 0                    52
     C                     ADD  CQTY      EQTY
     C                     ADD  CAMT      EAMT
     C                     ADD  DQTY      FQTY
     C                     ADD  DAMT      FAMT
     C                     EXCPTET#L2
     C                     ADD  3         LCNT
     C   51                ADD  1         LCNT
     C   52                ADD  1         LCNT
     CSR         TL#L2Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L3 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L3     BEGSR
     C           EQTY      COMP 0                    51
     C           FQTY      COMP 0                    52
     C                     EXCPTET#L3
     C                     ADD  3         LCNT
     C   51                ADD  1         LCNT
     C   52                ADD  1         LCNT
     CSR         TL#L3Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME LR PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     CSR         TL#LRZ    ENDSR
      ***********************************************************
      ****   DEFINE OUPUT FORMAT                             ****
      ***********************************************************
     OQPRINT  E   02           EH#01
     O                                   17 '報表編號: AR018'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
9908AO                         U#SYSDZ  132
     O        E 1              EH#01
     O                         CON3      76                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 2              EH#01
     O                                   11 '到期日期:'
     O                         DATE1   +  1 '    /  /  '
     O                                 +  1 '至'
     O                         DATE2   +  1 '    /  /  '
     O        E 1              EH#01
     O                         ALS      145
     O        E 1              EH#01
     O                                   11 '到期日期'
     O                                   21 '託收銀行'
     O                                   31 '託收帳號'
     O                                   42 '客戶編號'
     O                                   52 '客戶名稱'
     O                                   62 '付款銀行'
     O                                   76 '付款帳號'
     O                                   88 '票據號碼'
     O                                  105 '款項'
     O                                  113 '票據別'
     O                                  123 '付款地'
     O                                  133 '繳款單號'
     O                                  145 '金　額'
     O        E 1              EH#01
     O                         ALS      145
     O        E 1              ED#01
     O                 L3      RVDUDT    10 '    /  /  '
     O                 L2      CHARAX    21
     O                 L2      RVTANO    32
     O                 75      RVDUDT    10 '    /  /  '
     O                 75      CHARAX    21
     O                 75      RVTANO    32
     O                         RVCUNO    40
     O                         RVCUNM    52
     O                         RVPBID    66
     O                         RVPANO    77
     O                         R#NTNO    99
     O                         RVUSTP   103
     O                         CHAR10   115
     O                         CHAR6    122
     O                         RVNO     131
     O                         DEC9  J  145
     O        E 1              ET#L2
     O                         ALQ      145
     O        E 1      51      ET#L2
     O                                   86 '本埠票據小計：　共'
     O                         CQTY  J   96
     O                                  100 '張'
     O                                  117 '總金額:'
     O                         CAMT  J  132
     O        E 1      52      ET#L2
     O                                   86 '外埠票據小計：　共'
     O                         DQTY  J   96
     O                                  100 '張'
     O                                  117 '總金額:'
     O                         DAMT  J  145
     O        E 1              ET#L2
     O                         CHARAX    55
     O                         RVTANO    65
     O                                   86 '託收帳號小計：　共'
     O                         AQTY  J   96
     O                                  100 '張'
     O                                  117 '總金額:'
     O                         AAMT  J  145
     O        E 1              ET#L2
     O                         ALQ      145
     O        E 1      51      ET#L3
     O                                   86 '本埠票據小計：　共'
     O                         EQTY  J   96
     O                                  100 '張'
     O                                  117 '總金額:'
     O                         EAMT  J  145
     O        E 1      52      ET#L3
     O                                   86 '外埠票據小計：　共'
     O                         FQTY  J   96
     O                                  100 '張'
     O                                  117 '總金額:'
     O                         FAMT  J  145
     O        E 11             ET#L3
     O                         RVDUDTY   65
     O                                   86 '到期日期小計：　共'
     O                         GQTY  J   96
     O                                  100 '張'
     O                                  117 '總金額:'
     O                         GAMT  J  145
     O        E 1              ET#L3
     O                         ALQ      145
     O        E 1              ET#LR
     O                                   86 '　總合計：　共'
     O                         BQTY  J   96
     O                                  100 '張'
     O                                  117 '總金額:'
     O                         BAMT  J  145
     O*****************************************************************
** TAB1
A支票
B背書支票
C本票
D背書本票
E銀行本票
I信用狀　
