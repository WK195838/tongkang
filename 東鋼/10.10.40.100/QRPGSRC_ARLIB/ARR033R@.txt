     H************************************************************
     H*                                                          *
     H*         PROGRAM   ID        :   ARR033R                  *
     H*         PROGRAM   FUNCTION  :  銷退或折讓證明單列印    *
     H*         AUTHER              :   NANCY                    *
     H*         CREATE    DATE      :   81/09/29                 *
     H*         MAINTAIN  DATE      :                            *
     H*         RELATION  PROGRAM   :   ARR033P                  *
     H*         NOTE                :                            *
     H*                                                          *
     H************************************************************
     H        1   Y                                     1
     FTRNDTL  IF  E           K        DISK
     FINVMST  IF  E           K        DISK
     FHIPROD  IF  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FQPRINT  O   F      96            PRINTER
     F*---------------------------------------------------------------
     E                    HD      1  14 40
CLJ  E                    ARY1       12  1
     E*---------------------------------------------------------------
     I            DS
     I                                        1   6 YMD
     I                                        1   2 Y
     I                                        3   4 M
     I                                        5   6 D
     I                                        7  120ININDT
     I                                        7   80INDT1
     I                                        9  100INDT2
     I                                       11  120INDT3
     I                                       13  22 TXIVNO
     I                                       13  14 IVNO1
     I                                       15  22 IVNO2
CLJ  I            DS
     I                                        1   2 W#OEOF
     I                                        1   1 D#OE
     I                                        2   2 D#OF
     I            DS
     I                                        1  12 PDNM
---  I                                       12  12 D#NOF
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C*----------------------------------------------------
     C           *ENTRY    PLIST
     C                     PARM           NO      6
     C           TXKEY     KLIST
     C                     KFLD           TXCODE
     C                     KFLD           TXNO
     C           CBKEY     KLIST
     C                     KFLD           CBCUNO
     C*** MAIN LOGIC
     C                     EXSR IN#01
     C                     EXSR DP#01
     C                     SETON                     LR
     C                     RETRN
     C**********
     C* IN#01  *
     C**********
     CSR         IN#01     BEGSR
     C                     Z-ADD0         TOT1    90
     C                     Z-ADD0         TOT2    70
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD55        TOTL    20
     C                     MOVEL*BLANK    NAME   36
     C                     MOVEL*BLANK    BCID   10
     C                     MOVEL*BLANK    ADDR   62
     CSR         INZ#01    ENDSR
     C**********
     C* DP#01  *
     C**********
     CSR         DP#01     BEGSR
     C                     SETOF                     21
     C                     MOVEL'AR06'    TXCODE
     C                     MOVEL*BLANK    TXNO
     C                     MOVELNO        TXNO
     C           TXKEY     SETLLTRNDTL               97
     C           *IN97     DOWEQ'0'
     C           TXKEY     READETRNDTL                   97
     C   97                LEAVE
     C           TXFLAG    IFNE 'D'
     C  N21                EXSR @SET
     C                     EXSR PR#01
     C                     ENDIF
     C                     ENDDO
     C                     EXCPTET#01
     CSR         DP#01Z    ENDSR
     C**---------------------------------------**
     C**   PRINT  REPORT                       **
     C**---------------------------------------**
     CSR         PR#01     BEGSR
     C                     MOVELTXDATE    YMD
     C           LCNT      COMP TOTL                 75  75
     C   75                EXCPTEH#01
     C   75                Z-ADD10        LCNT
     C   75                MOVELTXCUNO    CBCUNO
     C   75      CBKEY     CHAINCBCUST               80
     C  N80 75             MOVELCBFNAM    NAME
     C  N80 75             MOVELCBMUID    BCID
     C  N80 75             MOVELCBADR1    ADDR
     C****
     C           TXIVNO    CHAININVMST               95
     C   95                Z-ADD0         ININDT
     C***:
     C                     MOVE *BLANK    MARK1   1
     C                     MOVE *BLANK    MARK2   1
     C           TXACNT    IFEQ '2'
     C                     MOVE 'V'       MARK1
     C                     END
     C           TXACNT    IFEQ '3'
     C                     MOVE 'V'       MARK2
     C                     END
     C***
     C           TXACNT    IFEQ '4'
     C                     MOVE SMARK1    MARK1
     C                     MOVE SMARK2    MARK2
     C           TXAMT     IFGE 0
     C                     MOVE 'V'       MARK1
     C                     MOVE ' '       MARK2
     C                     ENDIF
     C                     ENDIF
     C***
     C                     MOVE MARK1     SMARK1  1
     C                     MOVE MARK2     SMARK2  1
     C***
     C                     MOVE 'V'       MARK3   1
     C*
     C           TXACNT    IFEQ '4'
     C           TXAMT     IFGE 0
     C                     MOVELHD,10     PDNM   12
     C                     ELSE
     C                     MOVELHD,11     PDNM
     C                     END
     C                     ELSE
     C           TXPDNM    CHAINHIPROD               98
CLJ  C* N98                MOVELACPRNM    PDNM
     C           *IN98     IFEQ '0'
     C                     MOVE ''      W#OEOF
     C                     MOVELF4CHIN    PDNM
     C                     MOVEAPDNM      ARY1
     C                     Z-ADD1         I       20
     C                     DO   10                         12-2
     C                     ADD  1         I
     C                     MOVEAARY1,I    W#CHAR  1
     C           W#CHAR    IFEQ D#OE
     C           W#CHAR    OREQ D#OF
     C                     MOVEA' '       ARY1,I
     C                     ENDIF
     C                     ENDDO
     C                     MOVEAARY1      PDNM
     C                     MOVE D#OF      D#NOF
---  C                     ENDIF
     C   98                MOVELTXPDNM    PDNM
     C                     END
     C*
     C                     Z-ADDTXQTY     QTY     70
     C                     Z-ADDTXUPRC    UPRC    53
     C                     Z-ADDTXAMT     AMT     90
     C                     Z-ADDTXTAX     TAX     70
     C                     ADD  AMT       TOT1
     C                     ADD  TAX       TOT2
     C                     EXCPTED#01
     CSR                   ENDSR
     C**********
     CSR         @SET      BEGSR
     C           TXIVNO    CHAININVMST               95
     C   95                Z-ADD0         ININDT
     C   95                MOVEL' '       INAREA
     C                     SELEC
     C           INAREA    WHEQ 'K'
     C                     MOVELHD,3      CMPY   40
     C                     MOVELHD,6      MUID   40
     C                     MOVELHD,9      MUAD   40
     C           INAREA    WHEQ 'H'
     C                     MOVELHD,12     CMPY
     C                     MOVELHD,13     MUID
     C                     MOVELHD,14     MUAD
     C           INAREA    WHEQ 'M'
CLJ  C           ININDT    IFLT 871101                     仍為台中
     C                     MOVELHD,2      CMPY
     C                     MOVELHD,5      MUID
     C                     MOVELHD,8      MUAD
     C                     ELSE
     C                     MOVELHD,1      CMPY
     C                     MOVELHD,4      MUID
     C                     MOVELHD,7      MUAD
---  C                     ENDIF
     C                     OTHER
     C                     MOVELHD,1      CMPY
     C                     MOVELHD,4      MUID
     C                     MOVELHD,7      MUAD
     C                     ENDSL
     C                     SETON                     21
     CSR                   ENDSR
     C*---------------------------------------------------------------
     OQPRINT  E   02           EH#01
     O*                        CMPY      51
     O*       E 1              EH#01
     O                         CMPY      51
     O        E 2              EH#01
     O                         MUID      51
     O        E 1              EH#01
     O                         Y         67
     O                         M         73
     O                         D         80
     O        E 1              EH#01
     O                         MUAD      51
     O                         NO        91
     O        E 11             EH#01
     O        E 2              EH#01
     O        E 1              ED#01
     O                         TXIVTP B   1
     O                         ININDT B  10 '  /  /  '
     O                         IVNO1  B  13
     O                         IVNO2  B  23
     O                         MARK1     27
     O                         MARK2     29
     O                         PDNM   B  42
     O                         TXPDNM B  47
     O                         QTY   OB  53
     O                         UPRC   B  61 '0 .   '
     O                         TAX   OB  85
     O                         AMT   OB  76
     O                         MARK3  B  87
     O        E   21           ET#01
     O                         TOT2  OB  85
     O                         TOT1  OB  76
     O        E 1              ET#01
     O        E 3              ET#01
     O                         NAME   B  50
     O        E 2              ET#01
     O                         BCID   B  25
     O        E 2              ET#01
     O                         ADDR   B  76
** HD
東和鋼鐵企業股份有限公司
東和鋼鐵企業股份有限公司台中營業所
東和鋼鐵企業股份有限公司（高雄廠）
７５２８０２０９
７７６０９１１７
０７９８９２０６
台北市長安東路一段９號６樓
台中市明德里大雅路３３７號８樓
高雄市小港區嘉興街８號
預收貨款
扣預收款
東和鋼鐵企業股份有限公司（苗栗分公司）
８４７０７１０８
苗栗縣西湖鄉二湖村坪頂２２號
