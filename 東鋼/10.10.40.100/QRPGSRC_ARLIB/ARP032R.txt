     H*************************************************************
     H*                                                           *
     H*         PROGRAM   ID        :   ARP032R                   *
     H*         PROGRAM   FUNCTION  :  銷項統一發票明細表處理   *
     H*         AUTHER              :   ANNCY                     *
     H*         CREATE    DATE      :   81/09/23                  *
     H*         MAINTAIN  DATE      :                             *
     H*         RELATION  PROGRAM   :   ARR032P,ARR032R           *
     H*         NOTE                :                             *
     H*                                                           *
     H*************************************************************
     H        1   Y                                     1                 BR003
     FINVMST  IP  E           K        DISK
9007 FINVDTL  IF  E           K        DISK
     FAR1WF1  O   E           K        DISK                      A
     FCBCUST  IF  E           K        DISK
     F*---------------------------------------------------------------
     I            DS
     I                                        1  10 INNO
     I                                        7  10 QUIT
     I                                        9  10 NO
     I*---------------------------------------------------------------
     C           KEY1      KLIST
     C                     KFLD           INCUNO
9007 C           K#VDTL    KLIST
 .   C                     KFLD           IVNO
 .   C                     KFLD           IVACNT
 .   C                     KFLD           IVITEM
 .   C*****************************************************************
     C                     MOVELINNO      D#IN1   2
     C                     MOVE INNO      D#IN2   8
     C           D#IN1     IFEQ 'ZW'
     C           D#IN2     ANDEQ'49421575'
     C                     Z-ADD0         W#TEST  10
     C                     ENDIF
     C                     CLEARRAR1WF1
     C           KEY1      CHAINCBCUST               11
     C  N11                MOVELCBMUID    A1IDNO
     C                     MOVE NO        A1ITEM
     C           A1ITEM    IFLT 50
     C                     MOVEL'1'       A1SEQ
     C                     ELSE
     C                     MOVEL'2'       A1SEQ
     C                     END
     C                     MOVELINNO      A1INVO
     C                     MOVELINDECD    A1DECD
     C                     MOVELINTXTP    A1TXNO
     C                     MOVELQUIT      A1QUIT
     C                     Z-ADDININDT    A1DATE
     C           INBAMT    ADD  INAAMT    A1AMT
     C                     Z-ADDINATAX    A1TAX
     C           INKIND    IFEQ '2'
     C           INTXTP    IFEQ '1'
     C*
9007 C                     MOVE INNO      IVNO
 .   C                     MOVE *BLANK    IVACNT
 .   C                     Z-ADD0         IVITEM
 .   C           K#VDTL    SETLLINVDTL
 .   C                     READ IVREC                    40
 .   C           *IN40     DOWEQ*OFF
 .   C*
 .   C           INNO      IFNE IVNO
 .   C                     LEAVE
 .   C                     ENDIF
 .   C*
 .   C           IVACNT    IFEQ '1'
 .   C           IVACNT    OREQ '9'
9211 C           IVACNT    OREQ '4'
9707 C           IVACDT    ANDNE99999999
 .   C           IVAMT     DIV  1.05      W#AMT1 110H
 .   C           IVAMT     SUB  W#AMT1    W#TAX1 110
 .   C                     ADD  W#TAX1    A1TAX
 .   C                     ENDIF
 .   C*
 .   C                     READ IVREC                    40
 .   C                     ENDDO
 .   C*
     C                     SUB  A1TAX     A1AMT
     C                     ELSE
     C                     Z-ADD0         A1TAX
     C                     ENDIF
     C                     ENDIF
     C                     WRITERAR1WF1
