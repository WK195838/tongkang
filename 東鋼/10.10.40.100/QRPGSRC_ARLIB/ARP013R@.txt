     H        1   Y                                     1                 BR003
     FARWF02  IP  E                    DISK
     FINVDTL  UF  E           K        DISK
     FACTRAN  O   E           K        DISK                      A
     FQPRINT  O   F     132            PRINTER
     ****
     E                    TAB1    1   5  1   TABR1  18
     IRARWF02
     I                                              W2ACDTL2
     I                                              W2AREAL1
     ****
     I            DS
     I                                        1  18 KGRP
     I                                        1   4 KACNO
     I                                        5  14 KACNON
     I                                       15  18 KDPNO
     I            DS
     I                                        1   4 VRNOX
     I                                        2   40NO1
     I                                       11  17 VUNOG
     I                                       11  11 VUNOG1
     I                                       12  12 VUNOG2
     I                                       13  16 VUNOG3
     I                                       17  17 VUNOG4
     I            DS
     I                                        1   4 VRNOY
     I                                        2   40NO2
     I           UDS
     I                                        1   40YM
     I                                     10211021 TXAR
     C*****
     C           IVKEY     KLIST
     C                     KFLD           IVNO
     C                     KFLD           IVACNT
     C                     KFLD           IVITEM
CLJ  C  N99                SETON                     99
     C  N10                EXSR IN#01
     C   L1                EXSR DL#01
     C                     EXSR DP#01
     CL1 99                EXSR TL#01
     CLR 99                EXSR TL#LR
CLJ  CLRN99                EXCPTEH#01
     C**--------------------------------------------------**
     CSR         IN#01     BEGSR
     C                     MOVE 'E001'    VRNOX
     C                     MOVE 'E001'    VRNOY
     C                     Z-ADD0         AMTT   132
     C                     Z-ADD0         AMTTT  132
     C                     Z-ADD1         ITEM    20
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD58        TCNT    20
     C                     Z-ADD0         PAG     30
     C                     Z-ADD0         COUNT   30
     C                     MOVE *BLANK    VUNOG
     C                     MOVEL*ALL'-'   ALDASH120
     C                     SETON                     10
     CSR                   ENDSR
     C***
     C**----------------------------------------------**
     C**    L1 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#01     BEGSR
     C           W2AREA    IFEQ 'P'
     C                     Z-ADD0         ITEM
     C                     SETON                     32
     C                     ELSE
     C                     Z-ADD1         ITEM
     C                     SETOF                     32
     C                     ENDIF
     C                     Z-ADD0         AMTT
     C           NO1       ADD  1         NO2
     C                     MOVE *BLANK    KGRP
     C           W2AREA    LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     KGRP
     CSR                   ENDSR
     C**----------------------------------------------**
     C**       DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
CLJ  C*          --------------------------------------
     C*          由於項次超過一百項，故修改成跳至下
     C*          一號，項次從一開始。
     C*             86/1/7 S02CLJ
     C*          --------------------------------------
     C           *IN32     IFEQ '1'                        台北且
     C           ITEM      ANDGE98                         項次己滿
     C                     EXSR TL#01
     C                     EXSR DL#01
     C                     ENDIF
     C*
     C           *IN32     IFEQ '0'                        非台北且
     C           ITEM      ANDEQ99                         項次已滿
     C                     EXSR TL#01
     C                     EXSR DL#01
     C                     ENDIF
CLJ  C*          --------------------------------------
     C                     ADD  1         ITEM
     C                     MOVELW2ORNO    KORNO   5
     C           W2AMT     IFGE 0
     C                     Z-ADDW2AMT     VAMT   132
     C                     MOVEL'1'       *IN53
     C                     MOVEL'0'       *IN54
     C                     ELSE
     C                     Z-SUBW2AMT     VAMT
     C                     MOVEL'0'       *IN53
     C                     MOVEL'1'       *IN54
     C                     END
     C***
     C*          W2KIND    IFEQ '2'
     C*          VAMT      DIV  1.05      AMTY    90H
     C*                    Z-ADDAMTY      VAMT
     C*          W2AMT     DIV  1.05      AMTX    90H
     C*                    ELSE
     C                     Z-ADDW2AMT     AMTX    90H
     C*                    END
     C***
     C                     ADD  AMTX      AMTT
     C                     ADD  1         COUNT
     C                     EXSR WR#01
     C                     EXSR PR#01
     C                     MOVELW2NO      IVNO
     C                     MOVELW2ACNT    IVACNT
     C                     Z-ADDW2ITEM    IVITEM
     C           IVKEY     CHAININVDTL               97
     C  N97                MOVEL'C'       IVFLAG
     C  N97                MOVELTXAR      IVTXAR
     C  N97                Z-ADDUDATE     IVTXDT
     C  N97                MOVELTXAR      VUNOG1
     C  N97                MOVELW2AREA    VUNOG2
     C  N97                MOVELVRNOX     VUNOG3
     C  N97                MOVELVUNOG     IVACNO
     C  N97                UPDATIVREC
     CSR                   ENDSR
     C**----------------------------------**
     C**   L1 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#01     BEGSR
     C                     ADD  AMTT      AMTTT
     C           AMTT      IFLT 0
     C                     Z-SUBAMTT      AMTT
     C                     MOVEL'0'       *IN,51
     C                     MOVEL'1'       *IN,52
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     MOVEL'0'       *IN,52
     C                     ENDIF
     C***
     C   32                EXCPTSP#01
     C   32                ADD  1         LCNT
     C  N32                EXSR WR#02
     C  N32                EXSR PR#02
     C***
     C                     Z-ADD0         AMTT
     C   32                ADD  1         NO1
     C  N32                ADD  2         NO1
     CSR                   ENDSR
     C*****
     C**----------------------------------**
     C**   LR BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     C                     EXCPTET#LRR
     CSR                   ENDSR
     C*****
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#01     BEGSR
     C****  WRITE  TO TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'W'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     Z-ADDW2ACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM      ACITEM
     C   54                MOVEL'C'       ACDC
     C   53                MOVEL'D'       ACDC
     C                     MOVEL'1134'    ACNO
     C                     MOVEL'G'       ACTYCO
     C                     MOVELKORNO     ACRLNO
     C                     MOVELW2CUNM    ACCSNM
     C                     Z-ADDVAMT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C                     WRITERACVOUR
     C****  WRITE  TO NON-TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'W'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELW2AREA    ACCARD
     C                     MOVEL'A'       ACKIND
     C                     Z-ADDW2ACDT    ACYMD
     C   32                MOVELVRNOX     ACVRNO
     C   32                Z-ADDITEM      ITEM1   20
     C   32                ADD  1         ITEM
     C  N32                MOVELVRNOY     ACVRNO
     C                     Z-ADDITEM      ACITEM
     C   54                MOVEL'D'       ACDC
     C   53                MOVEL'C'       ACDC
     C                     MOVEL'4113'    ACNO
     C                     MOVEL'L'       ACTYCO
     C                     MOVELKDPNO     ACDPNO
     C                     MOVELKORNO     ACRLNO
     C                     MOVELW2CUNM    ACCSNM
     C                     Z-ADDVAMT      ACAMT
     C                     MOVELW2PDCD    ACDSC1
     C                     MOVEL'A'       ACSEPT
     C                     WRITERACVOUR
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#01     BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD10        LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#01
     C                     ADD  2         LCNT
     CSR                   ENDSR
     C**------------------------------------------**
     C**    TOTAL  TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#02     BEGSR
     C****  WRITE  TO TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'W'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     Z-ADDW2ACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADD01        ACITEM
     C   51                MOVEL'C'       ACDC
     C   52                MOVEL'D'       ACDC
     C                     MOVELKACNO     ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C                     WRITERACVOUR
     C****  WRITE  TO NON-TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'W'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELW2AREA    ACCARD
     C                     MOVEL'A'       ACKIND
     C                     Z-ADDW2ACDT    ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADD01        ACITEM
     C   51                MOVEL'D'       ACDC
     C   52                MOVEL'C'       ACDC
     C                     MOVEL'3511'    ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C                     WRITERACVOUR
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#02     BEGSR
     C                     EXCPTET#01
     C                     ADD  3         LCNT
     CSR                   ENDSR
     OQPRINT  E  103           EH#01
     O                                   12 '報表編號：'
     O                                   18 'ARP013'
     O                                   71 '折讓調整入帳清單'
     O                                  112 '列表日期:'
     O                         UDATE Y  120
     O        E  1             EH#01
     O                                   72 '********************'
     O                                  112 '頁　　數:'
     O                         PAG   Z  115
     O        E  1             EH#01
     O                                   63 '月份:'
     O                         YM    Y   68
     O        E  1             EH#01
     O                                   41 '借'
     O        E  1             EH#01
     O                                   10 '入帳日期'
     O                                   21 '發票號碼'
     O                                   28 '訂單'
     O                                   38 '客戶名稱'
     O                                   41 '貸'
     O                                   52 '會計科目'
     O                                   72 '借方金額'
     O                                   92 '貸方金額'
     O                                  106 '傳票編號'
     O*                                 114 '部門'
     O                                  114 '品名'
     O        E  1             EH#01
     O                         ALDASH   120
     O        E  1             ED#01
     O                         W2ACDTY    9
     O                         W2NO      21
     O                         KORNO     28
     O                         W2CUNM    38
     O                    53             55 'C 4113銷貨折讓'
     O                    54             55 'D 4113銷貨折讓'
     O                    54   VAMT  K   74
     O                    53   VAMT  K   94
     O                         TXAR      97
     O                         W2AREA    98
     O                    32   VRNOX    102
     O                   N32   VRNOY    102
     O                                  103 '-'
     O                         ITEM     105
     O*                        W2AREA   112
     O                         W2PDCD   112
     O        E  1             ED#01
     O                         W2ACDTY    9
     O                         W2NO      21
     O                    54             55 'C 1134應收帳款'
     O                    53             55 'D 1134應收帳款'
     O                    53   VAMT  K   74
     O                    54   VAMT  K   94
     O                         TXAR      97
     O                                   98 'P'
     O                         VRNOX    102
     O                                  103 '-'
     O                    32   ITEM1    105
     O                   N32   ITEM     105
     O**                                112 'P'
     O        E  1             SP#01
     O        E  1             ET#01
     O                 51                40 'C'
     O                 52                40 'D'
     O                                   55 '3511公司往來'
     O                 52      AMTT  K   74
     O                 51      AMTT  K   94
     O                         TXAR      97
     O                         W2AREA    98
     O                         VRNOY    102
     O                                  103 '-'
     O                                  105 '01'
     O*                        W2AREA   112
     O        E  2             ET#01
     O                 52                40 'C'
     O                 51                40 'D'
     O                         KACNO     45
     O                         KACNON    55
     O                 51      AMTT  K   74
     O                 52      AMTT  K   94
     O                         TXAR      97
     O                                   98 'P'
     O                         VRNOX    102
     O                                  103 '-'
     O                                  105 '01'
     O*                                 112 'P'
     O        E  1             ET#LR
     O                                   52 '調整金額合計'
     O                         AMTTT 2   74
     O                                   22 '調整筆數'
     O                         COUNT K   27
     O        E   60           ET#LRR
     O                                   50 '經理（廠長）：'
     O                                   73 '複核：'
     O                                   96 '製票：'
** TAB1
K3512高雄往來K210
T3513桃園往來T380
H3514苗栗往來H800
M3515台中往來W040
L3516林口往來L080
