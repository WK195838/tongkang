     H        1   Y                                     1                 BR003
     FARSTWF1 IP  E                    DISK
     FINVMST  IF  E           K        DISK
     FQPRINT  O   F     100            PRINTER
      *************************************************************
     IA1REC
     I                                              A1YEARL2
     I                                              A1CUNML1
     I****
     IAADS        DS
     I              'も未開發票磅單明細-C         CON3                       X
     I              '表も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
      *
     IBBDS        DS
     I                                       21  260YMD
     I                                       21  240YM
     I                                       21  220YY
     I                                       23  240MM
     I                                       25  260DD
     I           UDS
     I                                        1   40YMA
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L1                EXSR DL#L1
     C   L2                EXSR DL#L2
     C                     EXSR DP#01
     C*
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     CLR                   EXSR TL#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD0         HAMT1  100
     C                     Z-ADD0         HAMT2  100
     C                     Z-ADD59        OVRFLW  30
     C                     MOVE *ALL'='   ALS   100
     C                     MOVE *ALL'-'   ALQ    50
     C                     MOVE *ALL'*'   ALP    50
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP OVRFLW               75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD10        LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         GAMT1   90
     C                     Z-ADD0         GAMT2   90
     C                     Z-ADDOVRFLW    LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         FAMT1   90
     C                     Z-ADD0         FAMT2   90
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     MOVELA1IVNO    A1FIR   1
     C           A1FIR     IFNE ' '
     C           A1FIR     ANDNE'*'
     C           A1FIR     ANDNE'#'
     C           A1IVNO    CHAININVMST               97
     C  N97      INDECD    IFEQ 'D'
     C                     SETON                     97
     C                     ENDIF
     C   97                GOTO DP#01Z
     C                     ENDIF
     C                     Z-ADDA1AAMT    TAMT1   80
     C                     Z-ADDA1HAMT    TAMT2   80
      *
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#01
      *
     C                     ADD  TAMT1     FAMT1
     C                     ADD  TAMT2     FAMT2
     C                     ADD  TAMT1     GAMT1
     C                     ADD  TAMT2     GAMT2
     C                     ADD  TAMT1     HAMT1
     C                     ADD  TAMT2     HAMT2
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C                     EXCPTET#L2
     C                     ADD  2         LCNT
     CSR         TL#L2Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C                     EXCPTET#L1
     C                     ADD  3         LCNT
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME LR PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     CSR         TL#LRZ    ENDSR
      ***********************************************************
      ****   DEFINE OUPUT FORMAT                             ****
      ***********************************************************
     OQPRINT  E   03           EH#01
     O                                   17 '報表編號: AR056'
     O                         CON4      53                                    X
     O                                   75 '製表日期'
     O                                   76 ':'
     O                         UDATE Y   84
     O        E 1              EH#01
     O                         CON3      53                                    X
     O                                   75 '頁　　次'
     O                                   76 ':'
     O                         PAG   Z   79
     O        E 2              EH#01
     O                                   11 '截止年月:'
     O                         YMA     +  1 '  /  '
     O        E 1              EH#01
     O                         ALS      100
     O        E 1              EH#01
     O                                    6 '年度'
     O                                   16 '客戶名稱'
     O                                   26 '客戶編號'
     O                                   36 '訂單號碼'
     O                                   46 '磅單號碼'
     O                                   56 '出貨日期'
     O                                   70 '貨　款'
     O                                   84 '應收帳款'
     O                                  100 '開立發票號碼'
     O        E 1              EH#01
     O                         ALS      100
     O        E 1              ED#01
     O                 L2      A1YEAR     4
     O                 75      A1YEAR     4
     O                 L1      A1CUNM    16
     O                 75      A1CUNM    16
     O                 L1      A1CUNO    23
     O                 75      A1CUNO    23
     O                         A1ORNO    33
     O                         A1PCNO    47
     O                         A1DATEY   55
     O                         TAMT1 N   69
     O                         TAMT2 N   83
     O                         A1IVNO    96
     O        E 1              ET#L1
     O                         ALP       84
     O        E 1              ET#L1
     O                                   50 '客戶小計：'
     O                         FAMT1 N   69
     O                         FAMT2 N   83
     O        E 1              ET#L1
     O                         ALP       84
     O        E 1              ET#L2
     O                                   50 '年度小計：'
     O                         GAMT1 N   69
     O                         GAMT2 N   83
     O        E 1              ET#L2
     O                         ALP       84
     O        E 1              ET#LR
     O                                   50 '　總合計：'
     O                         HAMT1 N   69
     O                         HAMT2 N   83
     O        E 1              ET#LR
     O                         ALQ       84
