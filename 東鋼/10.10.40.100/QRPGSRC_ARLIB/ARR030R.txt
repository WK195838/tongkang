     H        1   Y                                     1                 BR003
     FAFCBAL  IP  E           K        DISK
     FQPRINT  O   F     132            PRINTER
      *************************************************************
     I*************************************************************
     IAFREC       01
     I                                              AFCUNOL2
     IAADS        DS
     I              'も預收貨款兌現資料-C         CON3                       X
     I              '明細表も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
      *
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L2                EXSR D#L2
     C                     EXSR DP#01
     CL2                   EXSR T#L2
     CLR                   EXCPTET#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD60        TCNT    30
     C                     MOVE *ALL'='   ALS   132
     C                     MOVE *ALL'-'   ALQ   127
     CSR                   ENDSR
      *
     CSR         D#L2      BEGSR
      *
     C                     Z-ADD0         LIN     20
     C                     Z-ADD0         FAMTL2 110
     C                     Z-ADD0         XAMTL2 110
     C                     Z-ADD0         NAMTL2 110
     C                     Z-ADD0         EAMTL2 110
     C                     Z-ADD0         GAMTL2 110
     C                     Z-ADD0         HAMTL2 110
     C                     Z-ADD0         NBALL2 110
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD8         LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     ADD  AFFAMT    FAMTL2
     C                     ADD  AFXAMT    XAMTL2
     C                     ADD  AFNAMT    NAMTL2
     C                     ADD  AFEAMT    EAMTL2
     C                     ADD  AFGAMT    GAMTL2
     C                     ADD  AFHAMT    HAMTL2
     C                     ADD  AFNBAL    NBALL2
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C                     EXCPTET#01
     C                     ADD  1         LIN
     C   75                ADD  1         LCNT
     CSR         DP#01Z    ENDSR
     C**----------------------------------------------**
     C* TOTAL  PROCESS  L2                            **
     C**----------------------------------------------**
     CSR         T#L2      BEGSR
     C                     ADD  FAMTL2    FAMTLR 110
     C                     ADD  XAMTL2    XAMTLR 100
     C                     ADD  NAMTL2    NAMTLR 110
     C                     ADD  EAMTL2    EAMTLR 110
     C                     ADD  GAMTL2    GAMTLR 110
     C                     ADD  HAMTL2    HAMTLR 110
     C                     ADD  NBALL2    NBALLR 110
     C           LIN       IFGT 1
     C                     ADD  2         LCNT
     C                     ENDIF
     C                     EXSR @CHKCT
     C           LIN       IFGT 1
     C                     EXCPTET#L2
     C   75                ADD  2         LCNT
     C                     ENDIF
     CSR         T#L2Z     ENDSR
      ***********************************************************
      ****   DEFINE OUPUT FORMAT                             ****
      ***********************************************************
     OQPRINT  E   02           EH#01
     O                                   17 '報表編號: ARR30'
     O                         CON4      70                                    X
     O                                  122 '製表日期'
     O                                  123 ':'
     O                         UDATE Y  132
     O        E 1              EH#01
     O                         CON3      71                                    X
     O                                  122 '頁　　次'
     O                                  123 ':'
     O                         PAG   Z  127
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              EH#01
     O                                   10 '客戶編號'
     O                                   20 '客戶名稱'
     O                                   30 '訂單號碼'
     O                                   45 '　預收金額'
     O                                   59 '　銷項稅額'
     O                                   73 '兌現金額'
     O                                   88 '扣預收金額'
     O                                  103 '退預收金額'
     O                                  118 '扣預收沖回'
     O                                  132 '兌現餘額'
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              ET#01
     O                 L2      AFCUNO     7
     O                 L2      AFCUNM    20
     O                         AFORNO    29
     O                         AFFAMTJ   45
     O                         AFXAMTJ   59
     O                         AFNAMTJ   73
     O                         AFEAMTJ   88
     O                         AFGAMTJ  103
     O                         AFHAMTJ  118
     O                         AFNBALJ  132
     O        E 1              ET#L2
     O                                   12 '客戶小計：'
     O                         FAMTL2J   45
     O                         XAMTL2J   59
     O                         NAMTL2J   73
     O                         EAMTL2J   88
     O                         GAMTL2J  103
     O                         HAMTL2J  118
     O                         NBALL2J  132
     O        E 1              ET#L2
     O                         ALQ      132
     O        E 1              ET#LR
     O                                   12 '總合計：'
     O                         FAMTLRJ   45
     O                         XAMTLRJ   59
     O                         NAMTLRJ   73
     O                         EAMTLRJ   88
     O                         GAMTLRJ  103
     O                         HAMTLRJ  118
     O                         NBALLRJ  132
     O        E 1              ET#LR
     O                         ALQ      132
