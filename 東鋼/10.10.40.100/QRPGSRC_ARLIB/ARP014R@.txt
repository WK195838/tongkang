     H        1   Y                                     1                 BR003
     FARWF02  IP  E                    DISK
     FINVDTL  UF  E           K        DISK
     FACTRAN  O   E           K        DISK                      A
     FQPRINT  O   F     132            PRINTER
     ****
     IRARWF02
     I                                              W2ACDTL1
     ****
     I            DS
     I                                        1   4 VRNOX
     I                                        2   40NO1
     I                                       11  17 VUNOG
     I                                       11  11 VUNOG1
     I                                       12  12 VUNOG2
     I                                       13  16 VUNOG3
     I                                       17  17 VUNOG4
     I           UDS
     I                                        1   40YM
     I                                     10211021 TXAR
     C*****
     C           IVKEY     KLIST
     C                     KFLD           W2NO
     C                     KFLD           W2ACNT
     C                     KFLD           W2ITEM
     C  N10                EXSR IN#01
     C   L1                EXSR DL#01
     C                     EXSR DP#01
     CL1                   EXSR TL#01
     CLR                   EXSR TL#LR
     C**--------------------------------------------------**
     CSR         IN#01     BEGSR
     C                     MOVE 'F001'    VRNOX
     C                     Z-ADD0         AMTT   132
     C                     Z-ADD0         AMTTF  132
     C                     Z-ADD0         ITEM    20
     C                     Z-ADD0         ITEM1   20
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD58        TCNT    20
     C                     Z-ADD0         PAG     30
     C                     Z-ADD0         COUNT   30
     C                     MOVE *ALL'-'   ALDSH 120
     C                     MOVEL*BLANK    VUNOG
     C                     SETON                     10
     CSR                   ENDSR
     C***
     C**----------------------------------------------**
     C**    L1 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#01     BEGSR
     C                     Z-ADD0         ITEM
     C                     Z-ADD0         ITEM1
     CSR                   ENDSR
     C**----------------------------------------------**
     C**       DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C           ITEM1     IFGE 98
     C                     Z-ADD0         ITEM1
     C                     Z-ADD0         ITEM
     C                     ADD  1         NO1
     C                     ENDIF
     C                     Z-ADDITEM1     ITEM
     C                     ADD  1         ITEM
     C           ITEM      ADD  1         ITEM1
     C                     MOVELW2ORNO    KORNO   5
     C           W2AMT     IFGE 0
     C                     Z-ADDW2AMT     VAMT   132
     C                     MOVEL'0'       *IN50
     C                     ELSE
     C                     Z-SUBW2AMT     VAMT
     C                     MOVEL'1'       *IN50
     C                     END
     C***
     C                     Z-ADDW2AMT     AMTX
     C           W2KIND    IFEQ '2'
     C           W2FL03    IFEQ 'F'
     C           W2FL03    OREQ 'E'
     C           VAMT      DIV  1.05      AMTY    90H
     C                     Z-ADDAMTY      VAMT
     C           W2AMT     DIV  1.05      AMTX    90H
     C                     END
     C                     END
     C***
     C*** 830107 UPDATE BY JEFF
     C  N50                ADD  AMTX      AMTT
     C   50                SUB  AMTX      AMTTF
     C*** END  UPDATE
     C                     ADD  1         COUNT
     C                     EXSR WR#01
     C                     EXSR PR#01
     C           IVKEY     CHAININVDTL               97
     C  N97                MOVEL'C'       IVFLAG
     C  N97                MOVELTXAR      IVTXAR
     C  N97                Z-ADDUDATE     IVTXDT
     C  N97                MOVELTXAR      VUNOG1
     C  N97                MOVEL'P'       VUNOG2
     C  N97                MOVELVRNOX     VUNOG3
     C  N97                MOVELVUNOG     IVACNO
     C  N97                UPDATIVREC
     CSR                   ENDSR
     *****
     CSR         TL#01     BEGSR
     C                     ADD  1         NO1
     C                     ENDSR
     C**----------------------------------**
     C**   LR BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#LR     BEGSR
     C**** 830107 UPDATE BY JEFF
     C*          AMTT      IFLT 0
     C*                    SETON                     51
     C*                    ELSE
     C*                    SETOF                     51
     C*                    ENDIF
     C*** END UPDATE
     C           AMTT      SUB  AMTTF     AMTTN  132
     C                     EXCPTET#LR
     C                     EXCPTET#LRR
     CSR                   ENDSR
     C*****
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#01     BEGSR
     C****  WRITE  TO TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'V'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     Z-ADDW2ACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM      ACITEM
     C   50                MOVEL'C'       ACDC
     C  N50                MOVEL'D'       ACDC
     C                     MOVEL'1134'    ACNO
     C                     MOVEL'G'       ACTYCO
     C                     MOVELKORNO     ACRLNO
     C                     MOVELW2CUNM    ACCSNM
     C                     Z-ADDVAMT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C                     WRITERACVOUR
     C****  WRITE  TO NON-TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'V'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     Z-ADDW2ACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM1     ACITEM
     C   50                MOVEL'D'       ACDC
     C  N50                MOVEL'C'       ACDC
     C                     MOVEL'2131'    ACNO
     C                     MOVEL'G'       ACTYCO
     C                     MOVELKORNO     ACRLNO
     C                     MOVELW2CUNM    ACCSNM
     C                     Z-ADDVAMT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C                     WRITERACVOUR
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#01     BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD10        LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#01
     C                     ADD  2         LCNT
     CSR                   ENDSR
     OQPRINT  E  103           EH#01
     O                                   12 '報表編號：'
     O                                   18 'ARP014'
     O                                   71 '預收貨款入帳清單'
     O                                  112 '列表日期:'
     O                         UDATE Y  120
     O        E  1             EH#01
     O                                   72 '********************'
     O                                  112 '頁　　數:'
     O                         PAG   Z  115
     O        E  1             EH#01
     O                                   63 '月份:'
     O                         YM    Y   68
     O        E  1             EH#01
     O                                   41 '借'
     O        E  1             EH#01
     O                                   10 '入帳日期'
     O                                   21 '發票號碼'
     O                                   28 '訂單'
     O                                   37 '客　戶'
     O                                   41 '貸'
     O                                   52 '會計科目'
     O                                   72 '借方金額'
     O                                   92 '貸方金額'
     O                                  106 '傳票編號'
     O        E  1             EH#01
     O                         ALDSH    120
     O        E  1             ED#01
     O                         W2ACDTY    9
     O                         W2NO      21
     O                         KORNO     27
     O                         W2CUNM    37
     O                   N50             56 'D  1134應收帳款'
     O                    50             56 'C 1134應收帳款'
     O                   N50   VAMT  K   74
     O                    50   VAMT  K   94
     O                         TXAR      97
     O                                   98 'P'
     O                         VRNOX    102
     O                                  103 '-'
     O                         ITEM     105
     O        E  1             ED#01
     O                         W2ACDTY    9
     O                         W2NO      21
     O                    50             56 'D  2131預收貨款'
     O                   N50             56 'C 2131預收貨款'
     O                    50   VAMT  K   74
     O                   N50   VAMT  K   94
     O                         TXAR      97
     O                                   98 'P'
     O                         VRNOX    102
     O                                  103 '-'
     O                         ITEM1    105
     O        E  2             ET#LR
     O                                   22 '預收款筆數'
     O                                   56 '預收貨款合計'
     O                         COUNT K   27
     O                         AMTTF K   74
     O                         AMTT  K   94
     O                                  104 '預收淨額'
     O                         AMTTN K  124
     O        E   60           ET#LRR
     O                                   50 '經理（廠長）：'
     O                                   73 '複核：'
     O                                   96 '製票：'
