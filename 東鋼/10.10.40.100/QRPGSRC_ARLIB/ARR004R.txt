     H****************************************************************
     H*
     H*            UPDATE  DATE  99/08/31 2010AR517  S00WCJ (9908A)
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FARSTAT  IP  E           K        DISK
     FQPRINT  O   F     132            PRINTER
     I*************************************************************
     IARREC
     I                                              ARCUNOL1
     I****
     IAADS        DS
     I              'も客戶別訂單應收帳-C         CON3                       X
     I              '款餘額表も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
9908AC           *DATE     SUB  19000000  U#SYSD  80
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD55        TCNT    20
     C                     Z-ADD0         CNT
     C                     MOVE *ALL'='   ALS   132
     C                     MOVE *ALL'-'   ALQ    92
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         AAMTT   90
     C                     Z-ADD0         BAMTT   90
     C                     Z-ADD0         CAMTT   90
     C                     Z-ADD0         DAMTT   90
     C                     Z-ADD0         EAMTT   90
     C                     Z-ADD0         FAMTT   90
     C                     Z-ADD0         GAMTT   90
     C                     Z-ADD0         HAMTT   90
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     Z-ADDARAAMT    AAMT    90
     C                     Z-ADDARBAMT    BAMT    90
     C                     Z-ADDARCAMT    CAMT    90
     C                     Z-ADDARDAMT    DAMT    90
     C                     ADD  ARFAMT    DAMT
     C                     Z-ADDARGAMT    EAMT    90
     C                     Z-ADDARATAX    FAMT    90
     C                     ADD  ARCTAX    FAMT
     C                     ADD  ARDTAX    FAMT
     C                     ADD  ARFTAX    FAMT
     C                     ADD  ARGTAX    FAMT
     C                     Z-ADDARIAMT    GAMT    90
     C                     ADD  ARKAMT    GAMT
     C                     Z-ADDARNBAL    HAMT    90
     C                     ADD  AAMT      AAMTT
     C                     ADD  BAMT      BAMTT
     C                     ADD  CAMT      CAMTT
     C                     ADD  DAMT      DAMTT
     C                     ADD  EAMT      EAMTT
     C                     ADD  FAMT      FAMTT
     C                     ADD  GAMT      GAMTT
     C                     ADD  HAMT      HAMTT
     C****
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD5         LCNT
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     C                     ADD  1         CNT     30
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C                     EXCPTET#L1
     C                     ADD  2         LCNT
     CSR         TL#L1Z    ENDSR
     C******
     OQPRINT  E   02           EH#01
     O                                   17 '報表編號: AR004'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
9908AO                         U#SYSDZ  132
     O        E 1              EH#01
     O                         CON3      77                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 2              EH#01
     O                         ALS      132
     O        E 1              EH#01
     O                                   10 '客戶編號'
     O                                   20 '客戶名稱'
     O                                   30 '訂單編號'
     O                                   44 '出貨金額'
     O                                   56 '扣預收貨款'
     O                                   68 '調整金額'
     O                                   80 '折讓／銷退'
     O                                   92 '扣預收沖回'
     O                                  104 '銷項稅額'
     O                                  118 '繳款金額'
     O                                  132 '應收餘額'
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              ED#01
     O                 L1      ARCUNO     8
     O                 L1      ARCUNM    20
     O                 75      ARCUNO     8
     O                 75      ARCUNM    20
     O                         ARORNO    27
     O                         AAMT  J   44
     O                         BAMT  J   56
     O                         CAMT  J   68
     O                         DAMT  J   80
     O                         EAMT  J   92
     O                         FAMT  J  104
     O                         GAMT  J  118
     O                         HAMT  J  132
     O        E 1              ET#L1
     O                                   30 '客戶小計：'
     O                         AAMTT J   44
     O                         BAMTT J   56
     O                         CAMTT J   68
     O                         DAMTT J   80
     O                         EAMTT J   92
     O                         FAMTT J  104
     O                         GAMTT J  118
     O                         HAMTT J  132
     O        E 1              ET#L1
     O*****
