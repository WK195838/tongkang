     H        1   Y                                     1                 BR003
     FINVMST  IP  E           K        DISK
     FRCVDTLL3IF  E           K        DISK
     FQPRINT  O   F     132            PRINTER
     I*************************************************************
     IINREC
     I                                              INCUNOL1
     I****
     IAADS        DS
     I              '預收貨款未收款及未-C         CON3                       X
     I              '兌現明細表'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     I           UDS
     I                                        1   6 START
     I                                        7  12 END
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1    96             EXSR TL#L1
     CLR                   EXSR TL#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD58        TCNT    20
     C                     Z-ADD0         CNT
     C                     MOVE *ALL'='   ALS   132
     C                     MOVE *ALL'-'   ALQ    92
     C                     Z-ADD0         TAMT4   90
     C                     Z-ADD0         TAMT5   90
     C                     Z-ADD0         TAMT6   90
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     SETOF                     747370
     C                     SETOF                     96
     C                     Z-ADD0         TAMT1   90
     C                     Z-ADD0         TAMT2   90
     C                     Z-ADD0         TAMT3   90
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     Z-ADDINNBAL    AMT2    90
     C                     Z-ADDINAAMT    AMT1    90
     C                     Z-ADDINATAX    TAX1    70
     C****
     C           INNBAL    IFNE 0
     C                     EXSR PR#01
     C                     ENDIF
     C                     SETON                     96
     CSR         DP#01Z    ENDSR
     C*******
     CSR         PR#01     BEGSR
     C                     SETON                     74
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD9         LCNT
     C                     EXCPTED#01
     C                     SETON                     7473
     C                     ADD  1         LCNT
     C                     ADD  1         CNT     30
     C                     ADD  AMT2      TAMT2
     C                     ADD  AMT1      TAMT1
     CSR         PR#01Z    ENDSR
     C*******
     CSR         PR#02     BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD9         LCNT
     C                     EXCPTED#02
     C                     ADD  1         LCNT
     C                     SETON                     74
     CSR         PR#02Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C           INCUNO    SETLLRCVDTLL3             98
     C           *IN98     DOWEQ'0'
     C           INCUNO    READERCVDTLL3                 98
     C   98                LEAVE
     C           RVACNO    IFNE '1131'
     C                     ITER
     C                     ENDIF
     C           RVNTCD    IFNE ' '
     C                     ITER
     C                     ENDIF
     C           RVUSTP    IFNE 'A2'
     C                     ITER
     C                     ENDIF
     C                     Z-ADDRVRAMT    AMT3    90
     C                     ADD  AMT3      TAMT3
     C                     EXSR PR#02
     C                     ENDDO
     C                     SETON                     75
     C           TAMT2     ADD  TAMT3     TTAMT   90
     C           TAMT2     IFEQ 0
     C           TAMT3     ANDEQ0
     C                     GOTO TL#L1Z
     C                     ENDIF
     C                     EXCPTET#L1
     C                     ADD  2         LCNT
     C                     ADD  TAMT1     TAMT4
     C                     ADD  TAMT2     TAMT5
     C                     ADD  TAMT3     TAMT6
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME LR PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#LR     BEGSR
     C           TAMT5     ADD  TAMT6     TTAMT   90
     C                     EXCPTET#LR
     CSR         TL#LRZ    ENDSR
     C******
     OQPRINT  E   02           EH#01
     O                                   17 '報表編號: AR046'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
     O                         UDATE Y  132
     O        E 1              EH#01
     O                         CON3      77                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 2              EH#01
     O                                   11 '客戶編號:'
     O                         START     17
     O                                   19 '-'
     O                         END       26
     O                                   44 '截止日期：'
     O                         UDATE Y   52
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              EH#01
     O                                   10 '客戶編號'
     O                                   20 '客戶名稱'
     O                                   30 '訂單號碼'
     O                                   40 '發票日期'
     O                                   50 '發票號碼'
     O                                   64 '預收金額'
     O                                   74 '稅　額'
     O                                   88 '未收金額'
     O                                  102 '票據金額'
     O                                  117 '到期日期'
     O                                  130 '繳款單號'
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              ED#01
     O                N73      INCUNO    08
     O                N73      INCUNM    20
     O                 75      INCUNO    08
     O                 75      INCUNM    20
     O                         INORNO    27
     O                         ININDTY   39
     O                         INNO      50
     O                         AMT1  N   63
     O                         TAX1  1   73
     O                         AMT2  N   87
     O        E 1              ED#02
     O                N74      INCUNO    08
     O                N74      INCUNM    20
     O                 75      INCUNO    08
     O                 75      INCUNM    20
     O                         RVDATEY   39
     O                         RVNTNO    50
     O                         AMT3  N  101
     O                         RVDUDTY  116
     O                         RVNO     127
     O                                  128 '-'
     O                         RVITEM   130
     O        E 1              ET#L1
     O                                   38 '=======================>'
     O                                   50 '客戶小計：'
     O                         TAMT1 N   63
     O                         TAMT2 N   87
     O                                   90 '+'
     O                         TAMT3 N  101
     O                                  104 '='
     O                         TTAMT N  116
     O        E 1              ET#L1
     O                                   88 '------------'
     O                                  102 '------------'
     O                                  118 '------------'
     O*****
     O        E 1              ET#LR
     O                                   36 '=======================>'
     O                                   50 '　　總合計：'
     O                         TAMT4 N   63
     O                         TAMT5 N   87
     O                                   90 '+'
     O                         TAMT6 N  101
     O                                  104 '='
     O                         TTAMT N  116
     O        E 1              ET#LR
     O                                   88 '------------'
     O                                  102 '------------'
     O                                  118 '------------'
