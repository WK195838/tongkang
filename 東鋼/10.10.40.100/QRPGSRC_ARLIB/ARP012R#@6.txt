     H        1   Y                                     1                 BR003
     FTRNDTL  IP  E                    DISK
     FTRNDTLUPUF  E           K        DISK
     F            TXREC                             KRENAMETXW
     FACTRAN  UF  E           K        DISK                      A
     FHIPROD  IF  E           K        DISK
     FQPRINT  O   F     156            PRINTER
     ******************************************************************
     E                    TAB1    1   5  1   TABR1  18
     ******************************************************************
     ITXREC
     I                                              TXACDTL4
     I                                              TXFL02L3
     I                                              TXIVNOL2
     I                                              TXCUNOL2
     I                                              TXORNOL2
     I                                              TXRESVL1
     I                                              TXUPRCL1
     I*----------------------------------------------------------------
     ITXW
     I              TXFLAG                          TWFLAG
     I              TXCODE                          TWCODE
     I              TXNO                            TWNO
     I              TXITEM                          TWITEM
     I              TXACNT                          TWACNT
     I              TXDATE                          TWDATE
     I              TXACDT                          TWACDT
     I              TXCUNO                          TWCUNO
     I              TXCUNM                          TWCUNM
     I              TXORNO                          TWORNO
     I              TXIVNO                          TWIVNO
     I              TXPCNO                          TWPCNO
     I              TXVUNO                          TWVUNO
     I              TXRVID                          TWRVID
     I              TXSALE                          TWSALE
     I              TXSATP                          TWSATP
     I              TXIVTP                          TWIVTP
     I              TXPDNM                          TWPDNM
     I              TXQTY                           TWQTY
     I              TXUPRC                          TWUPRC
     I              TXAMT                           TWAMT
     I              TXTAX                           TWTAX
     I              TXFL01                          TWFL01
     I              TXFL02                          TWFL02
     I              TXTXAR                          TWTXAR
     I              TXTXDT                          TWTXDT
     I              TXRESV                          TWRESV
     I*----------------------------------------------------------------
     I            DS
     I                                        1  18 KGRP
     I                                        1   4 KACNO
     I                                        5  14 KACNON
     I                                       15  18 KDPNO
     I            DS
     I                                        1   6 VRNOX
     I                                        2   60NO1
     I                                       11  16 VRNON
     I                                       12  160NO3
     I            DS
     I                                        1   6 VRNOY
     I                                        2   60NO2
     I            DS
     I                                        1   7 TWVUNO
     I                                        1   1 TTT1
     I                                        2   2 TTT2
     I                                        3   3 TTT3
     I                                        4   7 TTT4
CLJ  I*
     I            DS
     I                                        1   2 W#OEOF
     I                                        1   1 D#OE
     I                                        2   2 D#OF
     I            DS
     I                                        1  12 NAME1
     I                                       12  12 D#NOF
---  I*
     I           UDS
9008 I                                        1   60U#YYMM
LYW  I                                        1   8 YMD
     I                                        9   9 TXAR
     I                                     10011010 D#USER
     C*****************************************************************
     C*          KEY  LIST
     C*****************************************************************
     C           ACKEY1    KLIST
     C                     KFLD           TXAR
     C                     KFLD           U#YYMM
     C                     KFLD           ACVRNO
     C                     KFLD           ACITEM
     C*
     C           TXKEY     KLIST
     C                     KFLD           TXCODE
     C                     KFLD           TXNO
     C                     KFLD           TXITEM
     C*****************************************************************
     C*          MAIN  PROGRAM
     C*****************************************************************
     C  N10                EXSR IN#01
     C   L3                EXSR DL#03
     C   L2                EXSR DL#02
     C   L1                EXSR DL#01
     C                     EXSR DP#01
     CL1                   EXSR TL#01
     CL2                   EXSR TL#02
     CL3                   EXSR TL#L3
     CLR                   EXSR TL#LR
     C**--------------------------------------------------**
     CSR         IN#01     BEGSR
     C*                    MOVE YMD       DATEA   80
9008 C                     Z-ADDU#YYMM    DATEA   60
     C                     Z-ADD0         AMTT   132
     C                     Z-ADD0         AMTTT  132
     C                     Z-ADD1         ITEM1   30
     C                     Z-ADD1         ITEM2   30
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD58        TCNT    20
     C                     Z-ADD0         PAG     30
     C                     Z-ADD0         COUNT   40
     C                     MOVE *BLANK    KGRP
     C           TXAR      LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     KGRP
     C                     SETON                     10
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**  FIND LAST NO                                **
     C**----------------------------------------------**
     CSR         @FDNO     BEGSR
     C*
     C                     MOVEL'D99999'  ACVRNO
     C                     Z-ADD999       ACITEM
     C                     SETOF                     95
     C           ACKEY1    SETGTACTRAN
     C                     READPACTRAN                   95
     C  N95                MOVELACVRNO    TYPE    1
     C  N95      TYPE      COMP 'D'                  9595
     C  N95                MOVELACVRNO    VRNON
     C  N95                ADD  1         NO3
     C  N95                MOVELVRNON     VRNOX
     C   95                MOVEL'D00001'  VRNOX
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**    L3 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#03     BEGSR
      *
      * INITIAL NEXT VOUCHER NO
      *
     C                     EXSR @FDNO
     C                     MOVELVRNOX     VRNOY
     C           NO1       ADD  1         NO2
     C                     Z-ADD1         ITEM1   30
     C                     Z-ADD1         ITEM2   30
     C                     Z-ADD0         AMTT   132
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**    L2 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#02     BEGSR
     C*
     C                     Z-ADD0         AMT2   132
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**    L1 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#01     BEGSR
     C*
     C           TXPDNM    CHAINHIPROD               31
CLJ  C   31                MOVE *BLANK    NAME1
     C           *IN31     IFEQ '0'
     C                     MOVE ''      W#OEOF
     C                     MOVELF4CHIN    NAME1
     C                     MOVE D#OF      D#NOF
---  C                     ENDIF
     C                     Z-ADD0         AMT1   132
     C                     Z-ADD0         QTY1    80
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**       DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
TEST C           TXNO      IFEQ 'P00598'
  .  C                     Z-ADD0         W#TEST  10
  .  C                     ENDIF
     C*
     C                     ADD  TXAMT     AMT1
     C                     ADD  TXAMT     AMT2
     C                     ADD  TXQTY     QTY1
     C***
     C                     ADD  TXAMT     AMTT
     C                     ADD  TXAMT     AMTTT
     C                     ADD  1         COUNT
      *
     C           TXKEY     CHAINTRNDTLUP             31
CLJ  C  N31                MOVELTXAR      TTT1
CLJ  C  N31                MOVELTWPCNO    TX@     1
     C  N31                MOVELTX@       TTT2
     C  N31                MOVELVRNOY     TTT3
     C  N31                MOVE VRNOY     TTT4
     C  N31                UPDATTXW
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   L1 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#01     BEGSR
     C*
     C           AMT1      IFNE 0
     C                     ADD  1         ITEM1
     C           AMT1      IFLT 0
     C                     Z-SUBAMT1      AMT1
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXSR WR#01
     C                     EXSR PR#01
     C                     ENDIF
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   L2 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#02     BEGSR
     C*
     C           AMT2      IFNE 0
     C                     ADD  1         ITEM2
     C           AMT2      IFLT 0
     C                     Z-SUBAMT2      AMT2
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXSR WR#02
     C                     EXSR PR#02
     C                     ENDIF
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   L3 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#L3     BEGSR
     C*
     C           AMTT      IFNE 0
     C           AMTT      IFLT 0
     C                     Z-SUBAMTT      AMTT
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXSR WR#03
     C                     EXSR PR#03
     C                     EXSR WR#04
     C                     EXSR PR#04
     C                     ENDIF
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   LR BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#LR     BEGSR
     C*
     C           AMTTT     IFNE 0
     C                     EXCPTET#LR
     C                     EXCPTET#LRR
     C                     ENDIF
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    TOTAL  TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#01     BEGSR
     C*
     C****  WRITE  TO NON-TAIPEI "4111" 銷貨收入
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXPCNO    TX@     1
     C                     MOVELTX@       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDU#YYMM    ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADDITEM1     ACITEM
     C   51                MOVEL'C'       ACDC
     C  N51                MOVEL'D'       ACDC
     C                     MOVEL'4111'    ACNO
     C                     MOVEL'L'       ACTYCO
     C                     MOVE *BLANK    KGRP
     C           TX@       LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     KGRP
     C                     MOVELKDPNO     ACDPNO
     C                     MOVELACDPNO    W#DPNO  4
     C                     MOVELTXORNO    KORNO   6
     C                     MOVELKORNO     ACRLNO
     C                     MOVELTXCUNM    ACCSNM
     C                     Z-ADDAMT1      ACAMT
CLJ  C*                    MOVELTXPDNM    ACDSC1           移至下面
     C*
     C           TXACNT    IFNE '2'
     C                     Z-ADD0         QTY1
     C                     ENDIF
     C                     MOVELQTY1      ACDSC2
     C                     MOVEL'A'       ACSEPT
CLJ  C                     EXSR SR8000                     設ACDSC1
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
     C                     MOVE UDATE     ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
     C                     MOVE UDATE     ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#02     BEGSR
     C*
     C****  WRITE  TO TAIPEI  "1134"應收帳款
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDU#YYMM    ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM2     ACITEM
     C  N51                MOVEL'C'       ACDC
     C   51                MOVEL'D'       ACDC
     C                     MOVEL'1134'    ACNO
     C                     MOVEL'G'       ACTYCO
     C                     MOVELKORNO     ACRLNO
     C                     MOVELTXCUNM    ACCSNM
     C                     Z-ADDAMT2      ACAMT
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
     C                     MOVE UDATE     ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
     C                     MOVE UDATE     ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#03     BEGSR
     C*
     C****  WRITE  TO TAIPEI  "3511"公司往來
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXPCNO    TX@     1
     C                     MOVELTX@       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDU#YYMM    ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADD1         ACITEM
     C  N51                MOVEL'C'       ACDC
     C   51                MOVEL'D'       ACDC
     C                     MOVEL'3511'    ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
     C                     MOVE UDATE     ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
     C                     MOVE UDATE     ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#04     BEGSR
     C*
     C****  WRITE  TO TAIPEI  "3512-6"廠區往來
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDU#YYMM    ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADD1         ACITEM
     C   51                MOVEL'C'       ACDC
     C  N51                MOVEL'D'       ACDC
     C                     MOVE *BLANK    KGRP
     C           TX@       LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     KGRP
     C                     MOVELKACNO     ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
     C                     MOVE UDATE     ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
     C                     MOVE UDATE     ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#01     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#02     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#02
     C                     ADD  1         LCNT
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#03     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#03
     C                     ADD  1         LCNT
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#04     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#04
     C                     ADD  2         LCNT
     C*
     CSR                   ENDSR
CLJB C*****************************************************************
     C*  更改摘要一之品名為磅單內銷貨數量最多之品名
     C*****************************************************************
     CSR         SR8000    BEGSR
     C           K#TX      KLIST
     C                     KFLD           F#CODE  4
     C                     KFLD           F#NO    8
     C*
     C                     MOVEL'SA04'    F#CODE
     C                     MOVELTXPCNO    F#NO
     C                     MOVEL*BLANK    W#PDNM  3
     C                     Z-ADD0         W#QTY   70
     C           K#TX      CHAINTXW                  69
     C           *IN69     DOWEQ'0'
     C           TWQTY     IFGT W#QTY
     C                     Z-ADDTWQTY     W#QTY
     C                     MOVELTWPDNM    W#PDNM           量多之品名
     C                     ENDIF
     C           TXRESV    IFNE *BLANK
     C           TXRESV    ANDEQTWPDNM
     C                     MOVELTWPDNM    W#PDNM           品不為空白
     C                     ENDIF
     C           K#TX      READETXW                      69
     C                     ENDDO
     C*
     C                     MOVELW#PDNM    ACDSC1           摘要一
     C*
     CSR                   ENDSR
---  C*****************************************************************
     OQPRINT  E   03           EH#01
     O                                   12 '報表編號：'
     O                                   18 'ARP012'
     O                                   85 '調整單入帳清單（整月）'
     O                                  140 '列表日期:'
     O                         UDATE Y  148
     O        E 1              EH#01
     O                                   82 '******************'
     O                                  140 '頁　　數:'
     O                         PAG   Z  143
     O        E 1              EH#01
     O                                   71 '年月:'
CLJ  O                         DATEA     80 '    /  '
     O        E 1              EH#01
     O                                   41 '借'
     O        E 1              EH#01
     O                                   10 '入帳日期'
     O                                   20 '磅單編號'
     O                                   26 '訂單'
     O                                   35 '客　戶'
     O                                   41 '貸'
     O                                   51 '會計科目'
     O                                   65 '品　名'
     O                                   87 '數量'
     O                                   97 '單價'
     O                                  114 '借方金額'
     O                                  131 '貸方金額'
     O                                  138 '部門'
     O                                  152 '傳票編號'
     O        E 1              EH#01
     O                                   20 '--------------------'
     O                                   40 '--------------------'
     O                                   60 '--------------------'
     O                                   80 '--------------------'
     O                                  100 '--------------------'
     O                                  120 '--------------------'
     O                                  132 '--------------------'
     O                                  150 '--------------------'
     O                                  155 '-----'
     O        E 1              ED#01
     O                         ACYMD     10 '    /  /  '
     O                         TXPCNO    19
     O                         KORNO     26
     O                         ACCSNM    37
     O                    51             56 'C 4111銷貨收入L'
     O                   N51             56 'D 4111銷貨收入L'
     O*                        TXPDNM    61
CLJ  O                         W#PDNM    61
     O                         NAME1   +  1
     O                         QTY1  K   87
     O                         TXUPRCK   97
     O                   N51   ACAMT K  114
     O                    51   ACAMT K  131
     O                         W#DPNO   141
     O                         TXAR     144
     O                         TX@      145
     O                         VRNOY    151
     O                                      '-'
     O                         ACITEM   155
     O        E 1              ED#02
     O                         ACYMD     10 '    /  /  '
     O*                        TXPCNO    19
     O                         KORNO     26
     O                         ACCSNM    37
     O                   N51             56 'C 1134應收帳款G'
     O                    51             56 'D 1134應收帳款G'
     O                    51   ACAMT K  114
     O                   N51   ACAMT K  131
     O                         TXAR     144
     O                                  145 'P'
     O                         VRNOX    151
     O                                      '-'
     O                         ACITEM   155
     O        E 1              ED#03
     O                         ACYMD     10 '    /  /  '
     O*                        TXPCNO    20
     O                   N51             56 'C 3511公司往來A'
     O                    51             56 'D 3511公司往來A'
     O                    51   ACAMT K  114
     O                   N51   ACAMT K  131
     O                         TXAR     144
     O                         TX@      145
     O                         VRNOY    151
     O                                      '-'
     O                         ACITEM   155
     O        E 1              ED#04
     O                         ACYMD     10 '    /  /  '
     O*                        TXNO      20
     O                   N51             41 'D '
     O                    51             41 'C '
     O                         KACNO
     O                         KACNON
     O                                   56 'A'
     O                   N51   ACAMT K  114
     O                    51   ACAMT K  131
     O                         TXAR     144
     O                                  145 'P'
     O                         VRNOX    151
     O                                      '-'
     O                         ACITEM   155
     O        E 1              ED#04
     O        E 1              ET#LR
     O                                   60 '入帳筆數'
     O                         COUNT K +  1
     O                N51                96 '銷貨調整金額合計'
     O                N51      AMTTT K  114
     O                 51               113 '銷貨調整金額合計'
     O                 51      AMTTT K  131
     O        E   60           ET#LRR
     O                                   50 '經理（廠長）：'
     O                                   73 '複核：'
     O                                   96 '製票：'
** TAB1
K3512高雄往來K210
T3513桃園往來T380
H3514苗栗往來H800
M3515台中往來W040
L3516林口往來L080
