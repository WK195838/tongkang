     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP050R5
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     型鋼地磅重與銷貨重差異分析（報表）
     H*            5.DATE-WRITTEN  86/04/26
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARP050F IP  E           K        DISK
     FARP050T O   E             66     PRINTER
     F*****************************************************************
     E                    TAB1    1   5  1   TABR1   8
     E*****************************************************************
     IWDREC
     I                                              WDCUNOL1
     I*------------------------------------------------------
     I           UDS
9008 I                                        1   60R#YMS
LYW  I                                        7  120R#YME
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*
     C*********************************
     C*  初始程式
     C*********************************
     CSR         INISR     BEGSR
     C                     Z-ADD0         W#RCNT  60       列印筆數
     C                     SETON                     66    首頁表頭
     C                     Z-ADD0         R#TTWT
     C                     Z-ADD0         R#TT04
     C                     Z-ADD0         R#TT05
     C                     Z-ADD0         R#TT06
     C                     Z-ADD0         R#TTSM
     C                     Z-ADD0         R#TTDF
     C*
     C           U#AREA    LOKUPTAB1      TABR1          69
     C   69                MOVELTABR1     R#AREA
     CSR                   ENDSR
     C*
     C*********************************
     C*  L1清除函式
     C*********************************
     CSR         L1CLR     BEGSR
     C                     MOVEL'T'       W#WRCU  1        寫客戶旗標
     CSR                   ENDSR
     C*
     C*********************************
     C*  DETAIL TIME 函式
     C*********************************
     CSR         DTLSR     BEGSR
     C                     CLEARAR050T1D
     C*
     C           W#WRCU    IFEQ 'T'
     C                     MOVELWDCUNO    R#CUNO           客戶編號
     C                     MOVELWDCUNM    R#CUNM           客戶簡稱
     C                     MOVEL'F'       W#WRCU
     C                     ENDIF
     C*
     C                     MOVELWDORNO    R#ORNO           訂單號碼
     C                     Z-ADDWDPDWT    R#PDWT           地磅重
     C                     Z-ADDWDSA04    R#SA04           銷貨重
     C                     Z-ADDWDAR05    R#AR05           調整重
     C                     Z-ADDWDAR06    R#AR06           折退重
     C                     Z-ADDWDARSM    R#ARSM           營業重合計
     C                     Z-ADDWDPADF    R#PADF           差異重
     C*
     C                     ADD  1         W#RCNT
     C           *IN66     IFEQ '1'
     C                     WRITEAR050T1H
     C                     SETOF                     66
     C                     ENDIF
     C                     WRITEAR050T1D
     C*
     C                     ADD  R#PDWT    R#TTWT           合計
     C                     ADD  R#SA04    R#TT04
     C                     ADD  R#AR05    R#TT05
     C                     ADD  R#AR06    R#TT06
     C                     ADD  R#ARSM    R#TTSM
     C                     ADD  R#PADF    R#TTDF
     CSR                   ENDSR
     C*
     C*********************************
     C*  L1 函式  存檔
     C*********************************
     CSR         L1SR      BEGSR
     CSR                   ENDSR
     C*
     C*********************************
     C*  LR 函式  印核覆
     C*********************************
     CSR         LRSR      BEGSR
     C           W#RCNT    IFEQ 0
     C                     EXSR MSGSR
     C                     ELSE
     C                     WRITEAR050T1T
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*********************************
     C*  查無資料函式
     C*********************************
     CSR         MSGSR     BEGSR
     C                     WRITEAR050T1H
     C                     WRITEAR050T1M
     CSR                   ENDSR
     C*
     C*********************************
     C*  子函式集
     C*********************************
** TAB1
K高雄廠
T桃園廠
H苗栗廠
M台中廠
P台北
