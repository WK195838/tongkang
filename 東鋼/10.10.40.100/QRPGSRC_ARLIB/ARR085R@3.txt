     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR085R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02YSH
     H*            4.FUNCTION     本廠出貨他廠開立發票明細表
     H*            5.DATE-WRITTEN  89/04/08
     H*            6.UPDATE  DATE  99/08/17  2010AR517 S00WCJ (9908A)
     H*                            99/12/06  2010ARXXX S00WCJ (9912A)
     H*                            增加U廠選項
     H*
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTLX IP  E           K        DISK
     FARR085T O   E             69     PRINTER
     ITXREC       01
     I                                              TXFL02L1
     I                                              TXTXARL1
     I           UDS
9908AI*                                     506 5090T#YYMM
9908AI                                      508 5130T#YYMM
9908AI*                                     512 512 D#AREA
9908AI                                      516 516 D#AREA
     I                                     10011010 T#USID
     C*****************************************************************
     C*              MAIN  PROGRAM
     C*****************************************************************
     C           *IN99     IFEQ *OFF
9908AC           *DATE     SUB  19000000  U#DATE  80
     C                     EXSR INISR
     C                     MOVE *ON       *IN99
     C                     ENDIF
     C   L1                EXSR L1CLR
     C   01                EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR 99                EXSR LRSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     MOVE *ALL'='   T#LIN1
     C                     MOVE *ALL'='   T#LIN2
     C                     MOVE *ON       *IN69
     C                     ENDSR
     C*****************************************************************
     C           L2CLR     BEGSR
     C*****************************************************************
     C                     MOVE *ON       *IN69
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C*開立廠區別小計
     C                     Z-ADD0         T#QTY1
     C                     Z-ADD0         T#AMT1
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     SELEC
     C           D#AREA    WHEQ 'T'
9912AC                     MOVE '八德'  T#BARA
9912AC           D#AREA    WHEQ 'U'
9912AC                     MOVE '桃園'  T#BARA
     C           D#AREA    WHEQ 'H'
     C                     MOVE '苗栗'  T#BARA
     C           D#AREA    WHEQ 'M'
     C                     MOVE '台中'  T#BARA
     C           D#AREA    WHEQ 'K'
     C                     MOVE '高雄'  T#BARA
     C                     OTHER
     C                     MOVE '全部'  T#BARA
     C                     ENDSL
     C*
     C                     MOVE TXCUNO    T#CUNO           客戶編號
     C                     MOVE TXCUNM    T#CUNM           客戶名稱
     C                     Z-ADDTXDATE    T#DATE           出貨日期
9008 C                     MOVELTXORNO    T#ORNO
     C           TXCODE    IFEQ 'SA04'
     C                     MOVE TXNO      T#NO             磅單號碼
     C                     MOVE *BLANK    T#PCNO
     C                     ELSE
     C                     MOVE TXPCNO    T#NO
     C                     MOVELTXNO      T#PCNO
     C                     ENDIF
     C                     MOVE TXPDNM    T#PDNM           品名
     C                     Z-ADDTXUPRC    T#UPRC           單價
     C                     Z-ADDTXQTY     T#QTY            數量
     C                     Z-ADDTXAMT     T#AMT            金額
     C                     MOVE TXIVNO    T#INNO           發票金額
     C                     MOVE TXTXAR    T#INAR           開立廠區
     C                     Z-ADDTXACDT    T#INDT           開立日期
     C                     Z-ADDTXTAX     T#INMT           發票金額
     C*
     C   69                WRITEAR085TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR085T1
     C*
     C                     ADD  TXQTY     T#QTY1
     C                     ADD  TXAMT     T#AMT1
     C*
     C                     ADD  TXQTY     T#QTY5
     C                     ADD  TXAMT     T#AMT5
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C           T#QTY1    IFNE 0
     C           T#AMT1    ORNE 0
     C   69                WRITEAR085TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR085T2
     C                     ENDIF
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C           T#QTY5    IFNE 0
     C           T#AMT5    ORNE 0
     C   69                WRITEAR085TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR085T6
     C                     ENDIF
     C                     ENDSR
