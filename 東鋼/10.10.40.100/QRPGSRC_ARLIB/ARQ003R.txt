     H****************************************************************
     H*
     H*            UPDATE  DATE  101/09/21 2012AR682 S00WCJ (0109A)
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FARQ003S CF  E                    WORKSTN
     F                                        RRN   KSFILE SF1
CLJ  F                                        RRN3  KSFILE AR003F3
     F                                        RRN4  KSFILE AR003F4
     F                                        RRN5  KSFILE AR003F5
CLJ  FCPRBAL  IF  E           K        DISK
     FARSTATL1IF  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FINVMSTL1IF  E           K        DISK
     FINVMSTL2IF  E           K        DISK
     F            INREC                             KRENAMEINRECL2
     FRCVDTL  IF  E           K        DISK
 9303FSAMAST  IF  E           K        DISK
 9404FAFCBALL1IF  E           K        DISK
     E*************************************************************
     E                    ERR     1   8 70
     I*************************************************************
     IINRECL2
     I              INFLAG                          F#FLAG
     I              INTYPE                          F#TYPE
     I              INDECD                          F#DECD
     I              INDEDT                          F#DEDT
     I              INNO                            F#NO
     I              INCUNO                          F#CUNO
     I              INCUNM                          F#CUNM
     I              INORNO                          F#ORNO
     I              ININDT                          F#INDT
     I              INKIND                          F#KIND
     I              INRVID                          F#RVID
     I              INSALE                          F#SALE
     I              INSATP                          F#SATP
     I              INAREA                          F#AREA
     I              INTXTP                          F#TXTP
     I              INAAMT                          F#AAMT
     I              INATAX                          F#ATAX
     I              INBAMT                          F#BAMT
     I              INCAMT                          F#CAMT
     I              INCTAX                          F#CTAX
     I              INDAMT                          F#DAMT
     I              INDTAX                          F#DTAX
     I              INEAMT                          F#EAMT
     I              INFAMT                          F#FAMT
     I              INNBAL                          F#NBAL
     I              INMAMT                          F#MAMT
     I              INAPNO                          F#APNO
     I              INAPDT                          F#APDT
     I              INRCNO                          F#RCNO
     I              INRCDT                          F#RCDT
     I              INFL01                          F#FL01
     I              INTXAR                          F#TXAR
     I              INTXDT                          F#TXDT
     I              INPRTC                          F#PRTC
     I              INRESV                          F#RESV
     I*
     IAADS        DS
     I                                       21  260YMD
     I                                       21  220YY
     I                                       23  240MM
     I                                       25  260DD
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           ARKEY     KLIST
     C                     KFLD           ARCUNO
     C                     KFLD           ARORNO
     C           CBKEY     KLIST
     C                     KFLD           CBCUNO
     C           W#INVM    KLIST
     C                     KFLD           INCUNO
     C                     KFLD           INAPNO
     C                     KFLD           ININDT
     C                     KFLD           INNO
     C           W#RCVD    KLIST
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
 9303C*FILE==>SAMAST(訂單檔)
   . C           W#SAMT    KLIST
   . C                     KFLD           S1OREA
 9303C                     KFLD           S1ORNO
 9402C*FILE==>INVMSTL2(發票主檔)
   . C           W#INL2    KLIST
   . C                     KFLD           F#ORNO
 9402C                     KFLD           F#NO
 9404C*FILE==>AFCBALL1(預收貨款餘額檔)
   . C           K#AFCB    KLIST
   . C                     KFLD           AFCUNO
 9404C                     KFLD           AFORNO
     C********************************
     C*****                     ******
     C*****  INITIAL VALUES     ******
     C*****                     ******
     C********************************
     C  N90                MOVE *BLANK    CUST1
     C  N90                MOVE *BLANK    ORNO1
     C  N90                MOVE *ALL'9'   ORNO2
     C  N90                MOVE '1'       SCRN    1
     C  N90                MOVE '1'       *IN,90
     C********************************
     C*****                     ******
     C*****     MAIN LOGIC      ******
     C*****                     ******
     C********************************
     C           *IN03     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
CLJ  C           SCRN      CASEQ'3'       SR3000           進入畫面三
     C           SCRN      CASEQ'4'       SR4000           進入畫面四
     C           SCRN      CASEQ'5'       SR5000           進入畫面五
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C********************************
     C*****                     ******
     C***** SCREEN 01 MAIN CTRL ******
     C*****                     ******
     C********************************
     CSR         SR#01     BEGSR
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C   KC                GOTO SR#01Z
     C                     EXSR CK#01
     C  N99                EXSR PR#02
     C  N99                MOVE '2'       SCRN
     C   99                MOVE '1'       SCRN
     CSR         SR#01Z    ENDSR
     C********************************
     C*****                     ******
     C***** SCREEN 02 MAIN CTRL ******
     C*****                     ******
     C********************************
     CSR         SR#02     BEGSR
     C                     WRITEUND01
     C                     EXFMTSFCTL1
     C   KC                EXSR KC#01
     C   KC                GOTO SR#02Z
     C   KL                EXSR KL#02
     C   KL                GOTO SR#02Z
CLJB C* -----------------------------------------------------------
     C           *IN05     IFEQ '1'
     C                     EXSR SR3100                     初始畫面三
     C                     MOVEL'3'       SCRN             進入畫面三
     C                     ENDIF
CSF  C* -----------------------------------------------------------
     C           *IN07     IFEQ '1'
     C                     EXSR SR4100                     初始畫面四
     C                     MOVEL'4'       SCRN             進入畫面四
     C                     ENDIF
CSF  C* -----------------------------------------------------------
     C           *IN08     IFEQ '1'
     C                     EXSR SR5100                     初始畫面五
     C                     MOVEL'5'       SCRN             進入畫面五
     C                     ENDIF
     C* -----------------------------------------------------------
     CSR         SR#02Z    ENDSR
     C********************************
     C*****                     ******
     C*****   CF03 EXIT         ******
     C*****                     ******
     C********************************
     CSR         KC#01     BEGSR
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C********************************
     C*****                     ******
     C*****   CF12 PREVIOUS     ******
     C*****                     ******
     C********************************
     CSR         KL#02     BEGSR
     C                     MOVE *BLANK    ERRMSG
     C                     MOVE '1'       SCRN
     CSR         KL#02Z    ENDSR
     C********************************
     C*****                     ******
     C*****   SCREEN 01 CKECK   ******
     C*****                     ******
     C********************************
     CSR         CK#01     BEGSR
     C                     SETOF                     414299
     C                     SETOF                     434044
     C                     MOVE *BLANK    ERRMSG
      **
     C                     MOVELCUST1     CBCUNO
     C           CBKEY     CHAINCBCUST               31
     C   31                SETON                     9943
     C   31                MOVELERR,3     ERRMSG
     C                     MOVELCBCUNM    CUSTNM
      **
     C           ORNO2     IFEQ *BLANK
     C                     MOVE *ALL'9'   ORNO2
     C                     ENDIF
     C           ORNO1     IFGT ORNO2
     C                     SETON                     9941
     C                     MOVELERR,2     ERRMSG
     C                     ENDIF
     CSR         CK#01Z    ENDSR
     C********************************
     C*****                      *****
     C*****   SCREEN 02 PREPARE  *****
     C*****                      *****
     C********************************
     CSR         PR#02     BEGSR
     C                     MOVE *BLANK    ERRMSG
     C                     SETON                     80
     C                     WRITESFCTL1
     C                     SETOF                     801051
     C                     SETOF                     97
     C                     Z-ADD0         RRN     30
     C                     MOVELCUST1     ARCUNO
     C                     MOVELORNO1     ARORNO
     C           ARKEY     SETLLARSTATL1             97
     C           *IN97     DOWEQ'0'
     C           ARCUNO    READEARSTATL1                 97
     C  N97      ARORNO    COMP ORNO2                97
     C   97                LEAVE
     C                     MOVELARORNO    W01              訂單編號
     C                     Z-ADDARHAMT    W02              預收金額
     C*
     C*若該張訂單為二聯式，則須將稅排除
     C                     Z-ADD0         W#BTAX
     C                     MOVELARORNO    S1OREA
     C                     MOVE ARORNO    S1ORNO
     C           W#SAMT    CHAINRSAMAST              50
     C           *IN50     IFEQ *OFF
     C           S1CHK     ANDEQ'2'
     C           ARBAMT    DIV  1.05      W03       H
     C           ARBAMT    DIV  21        W#BTAX  50H      扣預收稅
     C           W#BTAX    MULT -1        W#BTAX
     C                     ELSE
     C                     Z-ADDARBAMT    W03              扣預收金額
     C                     ENDIF
     C*
     C                     Z-ADDARGAMT    W04              扣預收沖回
     C                     Z-ADDAREAMT    W07              退回預收金額
     C                     Z-ADDARMAMT    W05              預收轉入金額
     C                     Z-ADDARPBAL    W06
     C*                    ADD  ARMAMT    W06              預收餘額
     C                     ADD  W#BTAX    W06
     C                     ADD  1         RRN
     C                     WRITESF1
     C                     ENDDO
     C           RRN       IFEQ 0
     C                     CLEARSF1
     C                     MOVEL'END'     W01
     C                     ADD  1         RRN
     C                     SETON                     51
     C                     WRITESF1
     C                     SETOF                     51
     C                     ENDIF
     C                     SETON                     10
     CSR         PR#02Z    ENDSR
     C*
CLJB C************************************
     C*  畫面三:財會觀點查詢主函式
     C************************************
     CSR         SR3000    BEGSR
     C                     WRITEAR003F3M
     C           S#SFN3    IFNE 0
     C                     SETOF                     71
     C                     SETON                     727374
     C                     ELSE
     C                     SETOF                     7172
     C                     SETON                     7374
     C                     ENDIF
     C                     EXFMTAR003F3C                   螢幕輸入
     C*
     C                     MOVEL'2'       SCRN             回前畫面
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  初始畫面三
     C*----------------------------------------
     CSR         SR3100    BEGSR
     C*
     C                     SETON                     71    清除 SF
     C                     SETOF                     7273
     C                     WRITEAR003F3C
     C*          -------------------------------讀入畫面並計算金額
     C                     Z-ADD0         RRN3    40
     C                     Z-ADD0         S#SFN3  40
     C           *LOVAL    SETLLCPREC
     C                     MOVEL'0'       *IN69
     C*
     C           1         DOWEQ1
     C                     READ CPREC                    69
     C           *IN69     IFEQ '1'
     C                     LEAVE
     C                     ENDIF
     C*
     C           CPCUNO    IFEQ CBCUNO
     C*                    ADD  1         RRN3
     C                     CLEARAR003F3
     C                     MOVELCPORNO    S#ORNO           訂單
     C                     Z-ADDCPPBAL    S#PBAL           前期餘額
     C                     Z-ADDCPAAMT    S#AAMT           本期增加
     C                     Z-ADDCPBAMT    S#BAMT           本期減少
     C                     Z-ADDCPNBAL    S#NBAL           本期餘額
     C*
     C                     MOVELS#ORNO    F#ORNO
     C                     MOVEL*BLANK    F#NO
     C                     Z-ADD1         W#CNT   10
     C           W#INL2    SETLLINRECL2
     C                     MOVE *OFF      *IN60
     C           *IN60     DOWEQ*OFF
9703 C           F#ORNO    READEINRECL2                  60
     C   60                LEAVE
     C*
     C           F#ORNO    IFNE S#ORNO
9703 C           S#ORNO    ANDNE*BLANK
     C                     LEAVE
     C                     ENDIF
     C*
     C           F#DECD    IFNE *BLANK
     C                     ITER
     C                     ENDIF
     C*
     C           F#TYPE    IFEQ '2'
     C           W#CNT     IFNE 1
     C                     MOVEL*BLANK    S#ORNO           訂單
     C                     Z-ADD0         S#PBAL           前期餘額
     C                     Z-ADD0         S#AAMT           本期增加
     C                     Z-ADD0         S#BAMT           本期減少
     C                     Z-ADD0         S#NBAL           本期餘額
     C                     ENDIF
     C                     MOVELF#NO      S#INNO
     C                     ADD  1         S#SFN3
     C                     ADD  1         RRN3
     C                     ADD  1         W#CNT
     C                     WRITEAR003F3
     C                     ENDIF
     C                     ENDDO
     C*
     C                     ENDIF
     C*
     C                     ENDDO
     CSR                   ENDSR
     C*
CLJE C**************************************************************
     C************************************
     C*  畫面四:已訂單編號查詢預收發票
     C************************************
     CSR         SR4000    BEGSR
     C                     WRITEAR003F4M
     C           S#SFN4    IFNE 0
     C                     SETOF                     71
     C                     SETON                     727374
     C                     ELSE
     C                     SETOF                     7172
     C                     SETON                     7374
     C                     ENDIF
     C                     EXFMTAR003F4C                   螢幕輸入
     C*
     C                     MOVEL'2'       SCRN             回前畫面
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  初始畫面四
     C*----------------------------------------
     CSR         SR4100    BEGSR
     C*
     C                     SETON                     71    清除 SF
     C                     SETOF                     7273
     C                     WRITEAR003F4C
     C*          -------------------------------讀入畫面
     C                     Z-ADD0         RRN4    40
     C                     Z-ADD0         S#SFN4  40
     C                     MOVELCBCUNO    INCUNO
     C                     MOVEL*BLANK    INAPNO
     C                     Z-ADD0         ININDT
     C                     MOVEL*BLANK    INNO
     C           W#INVM    SETLLINREC
     C                     MOVE *OFF      *IN40
     C           *IN40     DOWEQ*OFF
     C                     READ INREC                    40
     C   40                LEAVE
     C*
     C           INCUNO    IFNE CBCUNO
     C                     LEAVE
     C                     ENDIF
     C*
     C           INTYPE    IFEQ '2'
     C           INDECD    ANDEQ' '
     C                     ADD  1         RRN4
     C                     CLEARAR003F4
     C                     MOVELINORNO    S#ORNO           訂單
     C                     MOVELINNO      S#INNO           發票號碼
     C                     Z-ADDINEAMT    S#EAMT           繳款金額
     C                     MOVELINRCNO    S#RCNO           繳款單號
     C                     Z-ADDINRCDT    S#RCDT           繳款日期
     C                     EXSR SR4110
     C                     WRITEAR003F4
     C                     ADD  1         S#SFN4
     C                     ENDIF
     C*
     C                     ENDDO
     CSR                   ENDSR
     C*
     C**************************************************************
     CSR         SR4110    BEGSR
     C*
     C                     MOVELS#RCNO    RVNO
     C                     Z-ADD0         RVITEM
     C           W#RCVD    SETLLRVREC
     C                     MOVE *OFF      *IN41
     C           *IN41     DOWEQ*OFF
     C                     READ RVREC                    41
     C   41                LEAVE
     C*
     C           RVNO      IFNE S#RCNO
     C                     LEAVE
     C                     ENDIF
     C*
     C           RVNTTP    IFEQ 'A'
     C           RVNTTP    OREQ 'B'
     C           RVNTTP    OREQ 'C'
     C           RVNTTP    OREQ 'D'
     C           RVNTTP    OREQ 'E'
     C           RVACNO    ANDEQ'1131'
0109AC           RVNTTP    OREQ 'E'
0109AC           RVACNO    ANDEQ'111001'
     C                     SELEC
     C           RVNTCD    WHEQ ' '
     C                     MOVEL'未兌現'S#NTCD
     C           RVNTCD    WHEQ '1'
     C                     MOVEL'兌現'  S#NTCD
     C           RVNTCD    WHEQ '2'
     C                     MOVEL'退票'  S#NTCD
     C                     ENDSL
     C                     ELSE
     C                     MOVEL'兌現'  S#NTCD
     C                     ENDIF
     C*
     C                     LEAVE
     C                     ENDDO
     C*
     CSR                   ENDSR
     C*
     C************************************
     C*  畫面五:已訂單編號查詢兌現明細
     C************************************
     CSR         SR5000    BEGSR
     C                     WRITEAR003F5M
     C           S#SFN5    IFNE 0
     C                     SETOF                     71
     C                     SETON                     727374
     C                     ELSE
     C                     SETOF                     7172
     C                     SETON                     7374
     C                     ENDIF
     C                     EXFMTAR003F5C                   螢幕輸入
     C*
     C                     MOVEL'2'       SCRN             回前畫面
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  初始畫面四
     C*----------------------------------------
     CSR         SR5100    BEGSR
     C*
     C                     SETON                     71    清除 SF
     C                     SETOF                     7273
     C                     WRITEAR003F5C
     C*          -------------------------------讀入畫面
     C                     Z-ADD0         RRN5    40
     C                     Z-ADD0         S#SFN5  40
     C                     MOVELCUST1     AFCUNO
     C                     MOVELORNO1     AFORNO
     C           K#AFCB    SETLLAFREC
     C                     MOVE *OFF      *IN40
     C           *IN40     DOWEQ*OFF
     C                     READ AFREC                    40
     C   40                LEAVE
     C*
     C           AFCUNO    IFNE CUST1
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ADD  1         RRN5
     C                     CLEARAR003F5
     C                     MOVELAFORNO    S#ORNO           訂單
     C                     Z-ADDAFFAMT    S#AAMT           預收金額
     C                     Z-ADDAFEAMT    S#BAMT           扣預收
     C                     Z-ADDAFNBAL    S#CAMT           兌現餘額
     C*          AFFAMT    SUB  AFNAMT    S#DAMT           未兌現金額
     C                     WRITEAR003F5
     C                     ADD  1         S#SFN5
     C*
     C                     ENDDO
     C*
     CSR                   ENDSR
     C******************************* END PROGRAM *****************
** ERR
訂單錯誤
訂單範圍錯誤
客戶編號錯誤
