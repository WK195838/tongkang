     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP119R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02LBU
     H*            4.FUNCTION     票據退回收入傳票代開－暫收其他
     H*            5.DATE-WRITTEN  98/08/31
     H*
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FRCVDTLL1IP  E           K        DISK
     FRCVDTL  UF  E           K        DISK
     F            RVREC                             KRENAMERVREC1
     FRCADTL  UF  E           K        DISK
     FBANKAC  IF  E           K        DISK
     FBCNAME  IF  E           K        DISK
     FACTRAN  UF  E           K        DISK                      A
     FQPRINT  O   F     176            PRINTER
     F*****************************************************************
     E                    TAB1    1   3  1   TABR1  19
     E*----------------------------------------------------------------
     IRVREC
     I                                              RVWRNOL1
     I*
     IRVREC1
     I              RVVNO2                          F#VNO2
     I              RVFLAG                          F#FLAG
     I              RVTXAR                          F#TXAR
     I              RVTXDT                          F#TXDT
     I              RVRLNO                          F#RLNO
     I              RVACNO                          F#RACN
     I              RVRESV                          F#RRES
     I*
     IACREC
     I              ACTYPE                          F#TYPE
     I              ACCURR                          F#CURR
     I              ACACNO                          F#ACNO
     I              ACBNAM                          F#BNAM
     I              ACNAME                          F#NAME
     I              ACAREA                          F#AREA
     I              ACBAID                          F#BAID
     I              ACOLID                          F#OLID
     I              ACRESV                          F#RESV
     ****
     I            DS
     I                                        1  19 KGRP
     I                                        1   5 KACNO
     I                                        6  15 KACNON
     I                                       16  19 KDPNO
     I            DS
     I                                        1   6 VRNOX
     I                                        2   60NO1
     I                                       11  16 VRNON
     I                                       12  160NO3
     I            DS
     I                                        1   6 VRNOY
     I                                        2   60NO2
     I                                       11  17 VRNOG
     I                                       11  11 VRNOG1
     I                                       12  12 VRNOG2
     I                                       13  16 VRNOG3
     I                                       14  160VRNOG4
     I                                       17  17 VRNOG5
     I                                       21  30 RVSANO
     I                                       21  21 ANTYPE
     I                                       22  22 ANCURR
     I                                       23  30 ANACNO
     I            DS
     I                                        1  40 DSC0
     I                                        1  20 DSC1
     I                                       21  40 DSC2
     I            DS
     I                                        1  10 ACNOM
     I                                        1   9 ACNOM1
     I                                       10  10 ACNOM2
     I                                       41  47 VUNOG
     I                                       41  41 VUNOG1
     I                                       42  42 VUNOG2
     I                                       43  43 VUNOG3
     I                                       44  47 VUNOG4
     I            DS
     I                                        1  12 F#RRES
     I                                       11  11 D#RES1
     I           UDS
9008 I                                        1   80YMD
     I                                        1   60YM
     I                                     10011010 D#USER
     I                                     10211021 TXAR
     C*****************************************************************
     C*          KEY  LIST
     C*****************************************************************
     C           ACKEY1    KLIST
     C                     KFLD           TXAR
     C                     KFLD           YM
     C                     KFLD           ACVRNO
     C                     KFLD           ACITEM
     C*
     C           RVKEY     KLIST
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C*
     C           RAKEY     KLIST
     C                     KFLD           RANO
     C                     KFLD           RAITEM
     C*
     C           RAKTY     KLIST
     C                     KFLD           RANO
     C*
     C           ACKEY     KLIST
     C                     KFLD           ACVRNO
     C*
     C           ANKEY     KLIST
     C                     KFLD           ANTYPE
     C                     KFLD           ANCURR
     C                     KFLD           ANACNO
     C*****************************************************************
     C*          MAIN   PROGRAM
     C*****************************************************************
     C  N10                EXSR IN#01
     C   L1 10             EXSR DL#01
     C      10             EXSR DP#01
     CL1    10             EXSR TL#01
     CLR    10             EXSR TL#LR
     C**--------------------------------------------------**
     CSR         IN#01     BEGSR
     C                     Z-ADD0         AMTI   132
     C                     Z-ADD0         AMTT   132
     C                     Z-ADD0         VAMT   132
     C                     Z-ADD0         AMTTT  132
     C                     Z-ADD0         ITEM    30
     C                     Z-ADD0         ITEM1   30
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD60        TCNT    20
     C                     Z-ADD0         PAG     30
     C                     MOVEL*ALL'-'   ADASH 163
     C                     MOVEL*BLANK    VUNOG
     C*
     C           TXAR      IFEQ 'P'
     C                     SETON                     32
     C                     ELSE
     C                     SETOF                     32
     C                     ENDIF
     C*
     C                     MOVE *BLANK    KGRP
     C  N32      TXAR      LOKUPTAB1      TABR1          31
     C  N32 31             MOVELTABR1     KGRP
     C                     SETON                     10
     C***
     CSR                   ENDSR
     C**----------------------------------------------**
     C**    L1 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#01     BEGSR
     C   32                Z-ADD0         ITEM
     C  N32                Z-ADD1         ITEM
     C                     Z-ADD0         AMTT
     C                     Z-ADD0         AMTI
9008 C                     Z-ADD0         DYMD    80
     ****
     C                     EXSR @FDNO
     C                     MOVELVRNOX     VRNOY
     C           NO1       ADD  1         NO2
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**  FIND LAST NO                                **
     C**----------------------------------------------**
     CSR         @FDNO     BEGSR
     C                     MOVEL'I99999'  ACVRNO
     C                     Z-ADD999       ACITEM
     C                     SETOF                     95
     C           ACKEY1    SETLLACTRAN
     C                     READPACTRAN                   95
     C  N95                MOVELACVRNO    TYPE    1
     C  N95      TYPE      COMP 'I'                  9595
     C  N95                MOVELACVRNO    VRNON
     C  N95                ADD  1         NO3
     C  N95                MOVELVRNON     VRNOX
     C   95                MOVEL'I00001'  VRNOX
     CSR                   ENDSR
     C**----------------------------------------------**
     C**       DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     ADD  1         ITEM
     C**
     C                     ADD  RVRAMT    AMTTT
     C                     ADD  RVRAMT    AMTT
     C***
     C                     Z-ADDRVRAMT    VAMT   132
     C                     EXSR WR#01
     C                     EXSR PR#01
     C*
     C           RVKEY     CHAINRCVDTL               95
     C     N95             MOVELTXAR      VUNOG1
     C     N95             MOVELTXAR      VUNOG2
     C     N95             MOVELVRNOX     VUNOG3
     C     N95             MOVE VRNOX     VUNOG4
     C     N95             MOVELVUNOG     F#VNO2
     C     N95             MOVEL'C'       F#FLAG
     C     N95             MOVELTXAR      F#TXAR
     C     N95             Z-ADDUDATE     F#TXDT
     C     N95             UPDATRVREC1
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   L1 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#01     BEGSR
     C**
     C                     MOVELRVWRNO    RANO
     C                     Z-ADD0         RAITEM
     C           RAKEY     SETLLRCADTL               97
     C           *IN97     DOWEQ'0'
     C           RAKTY     READERCADTL                   97
     C   97                LEAVE
     C                     MOVELRAACNO    W#ACNO  9
     C           W#ACNO    CHAINBCNAME               31
     C   31                MOVEL*BLANK    ACNOM
     C  N31                MOVELACCHNM    ACNOM1
     C  N31                MOVE ''      ACNOM2
     C                     ADD  1         ITEM
     C           RAACTP    IFEQ 'C'
     C                     ADD  RAAAMT    AMTTT
     C                     ADD  RAAAMT    AMTT
     C                     ELSE
     C                     SUB  RAAAMT    AMTTT
     C                     SUB  RAAAMT    AMTT
     C                     ENDIF
     C***
     C           RAACTP    IFEQ 'D'
     C                     SETON                     18
     C                     ELSE
     C                     SETOF                     18
     C                     ENDIF
     C                     Z-ADDRAAAMT    VAMT
     C                     MOVELRARESV    BKNO   10
     C                     EXSR WR#011
     C                     EXSR PR#011
     C                     MOVELVUNOG     RAVUNO
     C                     MOVEL'C'       RAFLAG
     C                     MOVELTXAR      RATXAR
     C                     Z-ADDUDATE     RATXDT
     C                     UPDATRAREC
     C                     ENDDO
     C***
     C           TL#01A    TAG
     C***
     C   32                Z-ADDITEM      ITEM1   30
     C  N32                Z-ADD1         ITEM1
     C***
     C           DYMD      IFEQ 0
     C                     Z-ADDYMD       DYMD
     C                     ENDIF
     C****
     C   32                EXCPTSP#01
     C   32                ADD  1         LCNT
     C  N32                EXSR WR#03
     C  N32                EXSR PR#03
     CSR         TL#01Z    ENDSR
     C**----------------------------------**
     C**   LR BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXSR WR#02
     C                     EXSR PR#02
     C                     EXCPTET#LR
     CSR                   ENDSR
     C*****
     C**------------------------------------------**
     C**    CHECK   存入帳號 820101             **
     C**------------------------------------------**
     CSR         CK#SNO    BEGSR
     C           YMD       IFLT 820101
     C           ANKEY     CHAINBANKAC               97
     C  N97                MOVELF#OLID    RVSANO
     C                     ENDIF
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#01     BEGSR
     C****  WRITE  TO LOCAL
     C                     MOVE *BLANK    DSC0
     C                     CLEARRACVOUR
     C                     MOVEL'O'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXAR      ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           廠區
     C                     Z-ADDYM        ACYM             年月
     C                     Z-ADDYMD       ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM      ACITEM
     C                     MOVEL'C'       ACDC
     C                     MOVEL'1131'    ACNO
     C                     MOVEL'H'       ACTYCO
     C                     MOVE RVDUDT    ACDUE
     C                     MOVELRVCUNM    ACCSNM
     C                     MOVELRVPANO    DSC1
     C                     MOVELRVNTNO    ACRLNO
     C                     Z-ADDVAMT      ACAMT
     C                     MOVELRVWRNO    DSC2
     C                     MOVEL'A'       ACSEPT
     C                     MOVELDSC1      ACDSC1
     C                     MOVELDSC2      ACDSC2
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
     C                     MOVE UDATE     ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
     C                     MOVE UDATE     ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#01     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD10        LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    TOTAL  TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#011    BEGSR
     C****  WRITE  TO LOCAL
     C                     MOVE *BLANK    DSC0
     C                     CLEARRACVOUR
     C                     MOVEL'O'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXAR      ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           廠區
     C                     Z-ADDYM        ACYM             年月
     C*                    Z-ADDRVNTDT    ACYMD
     C                     Z-ADDYMD       ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM      ACITEM
     C                     MOVELRAACTP    ACDC
     C                     MOVELRAACNO    ACNO
     C                     MOVELRAWAYC    ACTYCO
     C                     MOVELRARLNO    ACRLNO
     C                     MOVELRAMUID    ACCSNM
     C                     MOVELRARESV    ACBKNO
     C*
     C           RADUDT    IFNE 0
     C                     MOVE RADUDT    ACDUE
     C                     ENDIF
     C                     Z-ADDVAMT      ACAMT
     C*
     C           RAWAYC    IFEQ 'B'
     C           RAWAYC    OREQ 'L'
     C           RAWAYC    OREQ 'M'
     C                     MOVELRADEPT    ACDPNO
     C                     ENDIF
     C                     MOVELRANO      DSC2
     C                     MOVEL'A'       ACSEPT
     C                     MOVELDSC1      ACDSC1
     C                     MOVELDSC2      ACDSC2
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
     C                     MOVE UDATE     ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
     C                     MOVE UDATE     ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#011    BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD10        LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#02
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C**------------------------------------------**
     C**    L1     TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#02     BEGSR
     C****  WRITE  TO TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'O'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           廠區
     C                     Z-ADDYM        ACYM             年月
     C*                    Z-ADDIVACDT    ACYMD
     C                     Z-ADDYMD       ACYMD
     C   32                MOVELVRNOX     ACVRNO
     C  N32                MOVELVRNOY     ACVRNO
     C                     ADD  1         ITEM1
     C                     Z-ADDITEM1     ACITEM
     C                     MOVEL'D'       ACDC
     C                     MOVEL'2159'    ACNO
     C                     MOVEL'D'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVE *BLANK    ACDUE
     C                     MOVEL*BLANK    ACCSNM
     C                     MOVELDYMD      ACDSC1
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
     C                     MOVE UDATE     ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
     C                     MOVE UDATE     ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     MOVEL*OFF      *IN60
     C           RVNO      SETLLRVREC1
     C           *IN60     DOWEQ*OFF
     C           RVNO      READERVREC1              N    60
     C*
     C   60                LEAVE
     C*
     C           F#RACN    IFNE '2159'
     C           D#RES1    ORNE '9'
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVELF#RLNO    ACRLNO
     C                     LEAVE
     C                     ENDDO
     C*
     C                     WRITERACVOUR
     C                     Z-ADD0         DYMD
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#02     BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD10        LCNT
     C   75                EXCPTEH#01
     C                     EXCPTET#01
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C**------------------------------------------**
     C**    L2     TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#03     BEGSR
     C*
     C****  WRITE  TO TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'O'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           廠區
     C                     Z-ADDYM        ACYM             年月
     C                     Z-ADDYMD       ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADD01        ACITEM
     C                     MOVEL'C'       ACDC
     C                     MOVEL'C'       DC1     1
     C                     Z-ADDAMTI      ACAMT
     C                     MOVELKACNO     ACNO
     C                     MOVEL'A'       ACTYCO
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
     C                     MOVE UDATE     ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
     C                     MOVE UDATE     ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C                     WRITERACVOUR
     C*
     C****  WRITE  TO NON-TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'O'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXAR      ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           廠區
     C                     Z-ADDYM        ACYM             年月
     C                     Z-ADDYMD       ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADD01        ACITEM
     C                     MOVEL'D'       ACDC
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'3511'    ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVE *BLANK    ACDUE
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
     C                     MOVE UDATE     ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
     C                     MOVE UDATE     ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#03     BEGSR
     C                     EXCPTET#03
     C                     ADD  3         LCNT
     CSR                   ENDSR
     O*****************************************************************
     OQPRINT  E  103           EH#01
     O                                   12 '報表編號：'
     O                                   18 'ARP119'
     O                                   83 '應收票據退回入帳清單'
     O                                  140 '列表日期:'
     O                         UDATE Y  148
     O        E  1             EH#01
     O                                   82 '********************'
     O                                  140 '頁　　數:'
     O                         PAG   Z  143
     O        E  1             EH#01
     O                                   73 '入帳日期:'
     O                         YMD       83 '    /  /  '
     O        E  1             EH#01
     O                                   24 '借'
     O        E  1             EH#01
     O                                   10 '退回單號'
     O                                   20 '客　戶'
     O                                   24 '貸'
     O                                   40 '會　計　科　目'
     O                                   60 '借方金額'
     O                                   80 '貸方金額'
     O                                   90 '到期日期'
     O                                  102 '票據號碼'
     O                                  124 '付款銀行'
     O                                  140 '票據帳號'
     O                                  154 '傳票編號'
     O                                  164 '部門'
     O        E  1             EH#01
     O                         ADASH    163
     O        E  1             ED#01
     O                         RVWRNO     9
     O*                                   8 '-'
     O*                        RVITEM    10
     O                         RVCUNM    20
     O                                   22 'C'
     O                                   27 '1131'
     O                                   37 '應收票據'
     O                                   38 'H'
     O                         VAMT  K   80
     O                         RVDUDT    89 '    /  /  '
     O                         RVNTNO   112
     O                         RVPBID   128
     O                         RVPANO   140
     O                         TXAR     146
     O                         TXAR     147
     O                         VRNOX    153
     O                                  154 '-'
     O                         ITEM     157
     O                         RVDPNO   163
     O        E  1             ED#02
     O                         RAACTP    22
     O                         RAACNO    27
     O                         ACNOM     37
     O                         RAWAYC    38
     O                 18      VAMT  K   60
     O                N18      VAMT  K   80
     O                         RADUDT    89 '    /  /  '
     O                         RARLNO   102
     O                         RAMUID   118
     O                         BKNO     130
     O                         TXAR     146
     O                         TXAR     147
     O                         VRNOX    153
     O                                  154 '-'
     O                         ITEM     157
     O                         RADEPT   163
     O        E  1             ET#01
     O                         RVCUNM    20
     O                         DYMD      89 '    /  /  '
     O                                   22 'D'
     O                                   38 '2159暫收其他D'
     O                         AMTT  K   60
     O*                        IVNO     130
     O                         RVRLNO    97
     O                         TXAR     146
     O                                  147 'P'
     O                    32   VRNOX    153
     O                   N32   VRNOY    153
     O                                  154 '-'
     O                         ITEM1    157
     O        E  1             SP#01
     O        E  1             ET#03
     O                                   22 'D'
     O                                   37 '3511公司往來'
     O                         AMTT  K   60
     O                         TXAR     146
     O                         TXAR     147
     O                         VRNOX    153
     O                                  157 '-001'
     O        E  2             ET#03
     O                                   32 'C'
     O                         KACNO     38
     O                         KACNON    48
     O                         AMTI  K   80
     O                         TXAR     146
     O                                  147 'P'
     O                         VRNOY    153
     O                                  157 '-001'
     O        E  1             ET#LR
     O                                   62 '應收票據　退回總額:'
     O                         AMTTT K   80
** TAB1
K3512 高雄往來K210
H3514 苗栗往來H800
T3513 桃園往來T380
