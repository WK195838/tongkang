     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARQ016R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     客戶暫收款查詢程式
     H*            5.DATE-WRITTEN  88/11/31
     H*            6.DATE-UPDATE   92/09/10  BY S02CSF
     H*                           新增財會切傳票金額欄位
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARQ016S CF  E                    WORKSTN
     F                                        RRN1  KSFILE AR016F1
     FAR2159D IF  E           K        DISK
     FCBCUST  IF  E           K        DISK
     E*************************************************************
     E                    T#MSG   1   2 70
     I*************************************************************
     I           UDS
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     C**************************************************************
     C*   檔案搜尋欄位組合
     C**************************************************************
     C           K#2159    KLIST                           暫收統計檔
     C                     KFLD           ADCUNO
     C                     KFLD           ADAREA
     C                     KFLD           ADORNO
     C**************************************************************
     C*   主程式開始
     C**************************************************************
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C           W#PRID    CASEQ'02'      SR2000           畫面二
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C*   副程式開始
     C**************************************************************
     C*----------------------------------------
     C*  宣告及初始變數
     C*----------------------------------------
     CSR         SR0000    BEGSR
     C                     Z-ADD10        N#PAG1  20       SF1筆數
     C                     MOVEL'01'      W#PRID  2        函式代號
     C                     MOVEL'F'       W#RTNV  1        函式返回值
     C                     MOVEL'F'       W#SFE1  1        SF1結束
     C                     Z-ADD0         RRN1    40
     C*
     C                     MOVEL*BLANK    S#INK1
     C                     EXSR SR1100                     初始畫面一
     C                     SETON                     51
     CSR                   ENDSR
     C*
     C************************************
     C*  畫面一:選擇客戶編號
     C************************************
     CSR         SR1000    BEGSR
     C                     WRITEAR016F1M
     C           S#SFN1    IFEQ 0
     C                     SETOF                     7273
     C                     SETON                     71
     C                     ELSE                            有資料
     C                     SETOF                     73
     C                     SETON                     7172
     C                     ENDIF
     C           W#SFE1    IFEQ 'T'
     C                     SETON                     74
     C                     ELSE
     C                     SETOF                     74
     C                     ENDIF
     C                     EXFMTAR016F1C
     C*
     C                     SETOF                     51
     C                     MOVEL*BLANK    S#MSG1
     C           S#CRN1    IFNE 0
     C                     Z-ADDS#CRN1    S#NBR1
     C                     ENDIF
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C           *IN12     OREQ '1'
     C                     MOVEL'00'      W#PRID
     C           *IN91     WHEQ '1'                        向下翻頁
     C                     EXSR SR1300
     C           *IN92     WHEQ '1'                        向上翻頁
     C                     EXSR SR1400
     C           *IN05     WHEQ '1'                        重新顯示
     C                     EXSR SR1500
     C*
     C                     OTHER                           執行鍵
     C                     EXSR SR1200
     C                     ENDSL
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  初始畫面一(指向指定的記錄)
     C*----------------------------------------
     CSR         SR1100    BEGSR
     C                     MOVELS#INK1    ADCUNO
     C           ADCUNO    SETLLRAR2159D             69
     C           *IN69     IFEQ '1'
     C           *HIVAL    SETGTRAR2159D             69
     C                     EXSR SR1130                     向前讀一頁
     C                     ENDIF
     C*
     C                     EXSR SR1110                     清除SF
     C                     EXSR SR1120                     讀入SF
     C*
     C                     Z-ADD1         S#NBR1           指向第一筆
     CSR         ES1100    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一清除SF
     C*----------------------------------------
     CSR         SR1110    BEGSR
     C                     MOVEL*BLANK    S#MSG1
     C                     SETOF                     7172
     C                     SETON                     73    清除 SF
     C                     WRITEAR016F1C
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一向下讀入SF (從目前的檔案位置)
     C*----------------------------------------
     CSR         SR1120    BEGSR
     C                     MOVEL'F'       W#SFE1
     C                     Z-ADD0         S#SFN1
     C                     Z-ADD0         RRN1
     C*
     C           1         DOWEQ1                          讀取迴圈
     C                     READ RAR2159D                 69
     C           *IN69     IFEQ '1'
     C                     MOVEL'T'       W#SFE1
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ADD  1         S#SFN1
     C           S#SFN1    IFGT N#PAG1                     已滿一頁
     C                     Z-ADDN#PAG1    S#SFN1
     C                     LEAVE
     C                     ENDIF
     C                     ADD  1         RRN1
     C                     CLEARAR016F1
     C                     EXSR SR1121                     搬移資料
     C                     WRITEAR016F1
     C                     ENDDO                           讀取迴圈
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一向讀入記錄之搬移資料
     C*----------------------------------------
     CSR         SR1121    BEGSR
     C                     MOVELADCUNO    S#CUNO
     C                     MOVEL*BLANK    S#CUNM
     C           S#CUNO    CHAINCBCUST               69
     C  N69                MOVELCBCUNM    S#CUNM
     C                     MOVELADAREA    S#AREA
     C                     MOVELADORNO    S#ORNO
     C*
     C                     Z-ADDADAMT1    S#AMT1
     C                     Z-ADDADAMT2    S#AMT2
     C                     Z-ADDADAMT3    S#AMT3
     C*
     C                     MOVELADRESV    S#RESV
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一向上移動記錄(從目前的檔案位置)
     C*----------------------------------------
     CSR         SR1130    BEGSR
     C                     Z-ADD0         S#SFN1
     C*
     C           1         DOWEQ1
     C                     READPRAR2159D                 69
     C           *IN69     IFEQ '1'
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ADD  1         S#SFN1
     C           S#SFN1    IFGT N#PAG1                     多一筆
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C*
     C           *IN69     IFEQ '1'
     C           *LOVAL    SETLLRAR2159D             69
     C                     MOVEL'T'       W#SFB1  1        已達檔頭
     C                     ELSE
     C                     MOVEL'F'       W#SFB1           未達檔頭
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一：執行
     C*----------------------------------------
     CSR         SR1200    BEGSR
     C           S#INK1    IFNE *BLANK
     C                     EXSR SR1100
     C                     MOVEL*BLANK    S#INK1
     C                     GOTO ES1200
     C                     ENDIF
     C*
     C           1         DO   S#SFN1    RRN1
     C           RRN1      CHAINAR016F1              69
     C                     SELEC
     C           S#OPT1    WHEQ '5'
     C                     Z-ADDRRN1      S#NBR1
     C                     MOVEL*BLANK    S#OPT1
     C                     UPDATAR016F1
     C                     MOVEL'02'      W#PRID
     C                     EXSR SR2100
     C                     GOTO ES1200
     C                     ENDSL
     C                     ENDDO
     C*
     C                     SETON                     51
     CSR         ES1200    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一：向下翻一頁
     C*----------------------------------------
     CSR         SR1300    BEGSR
     C           W#SFE1    IFEQ 'T'
     C                     MOVELT#MSG,1   S#MSG1           已達檔底
     C                     GOTO ES1300
     C                     ENDIF
     C*
     C           S#SFN1    IFNE 0                          本頁有資料
     C                     Z-ADDS#SFN1    RRN1
     C           RRN1      CHAINAR016F1              69
     C                     MOVELS#CUNO    ADCUNO
     C                     MOVELS#AREA    ADAREA
     C                     MOVELS#ORNO    ADORNO
     C           K#2159    CHAINRAR2159D             69    移至本頁尾
     C                     ENDIF                           本頁有資料
     C*
     C                     EXSR SR1110                     清除SF
     C                     EXSR SR1120                     讀入SF
     C           S#NBR1    IFGT S#SFN1
     C                     Z-ADD1         S#NBR1
     C                     ENDIF
     CSR         ES1300    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一：向上翻一頁
     C*----------------------------------------
     CSR         SR1400    BEGSR
     C           S#SFN1    IFNE 0                          本頁有資料
     C                     Z-ADD1         RRN1
     C           RRN1      CHAINAR016F1              69
     C                     MOVELS#CUNO    ADCUNO
     C                     MOVELS#AREA    ADAREA
     C                     MOVELS#ORNO    ADORNO
     C           K#2159    CHAINRAR2159D             69    移至本頁頭
     C                     ENDIF                           本頁有資料
     C*
     C                     EXSR SR1130                     向前移動
     C                     EXSR SR1110                     清除SF
     C                     EXSR SR1120                     讀入SF
     C*
     C           W#SFB1    IFEQ 'T'
     C                     MOVELT#MSG,2   S#MSG1           己達檔頭
     C                     ENDIF
     CSR         ES1400    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一：重新顯示
     C*----------------------------------------
     CSR         SR1500    BEGSR
     C           S#SFN1    IFEQ 0                          本頁無資料
     C           *HIVAL    SETGTRAR2159D
     C                     EXSR SR1130                     向前讀一頁
     C*
     C                     ELSE
     C                     Z-ADD1         RRN1
     C           RRN1      CHAINAR016F1              69
     C                     MOVELS#CUNO    ADCUNO
     C                     MOVELS#AREA    ADAREA
     C                     MOVELS#ORNO    ADORNO
     C           K#2159    CHAINRAR2159D             69    移至本頁頭
     C                     READPRAR2159D                 69前移一筆
     C           *IN69     IFEQ '1'
     C           *LOVAL    SETLLRAR2159D
     C                     ENDIF
     C                     ENDIF
     C*
     C                     EXSR SR1110                     清除SF
     C                     EXSR SR1120                     讀入SF
     C*
     C           S#NBR1    IFGT S#SFN1
     C                     Z-ADD1         S#NBR1
     C                     ENDIF
     CSR         ES1500    ENDSR
     C*
     C************************************
     C*  畫面二:編輯資料
     C************************************
     CSR         SR2000    BEGSR
     C                     EXFMTAR016F2                    螢幕輸入
     C*
     C                     MOVEL*BLANK    S#MSG2
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C                     MOVEL'00'      W#PRID           結束
     C           *IN12     WHEQ '1'
     C                     MOVEL'01'      W#PRID           回前畫面
     C           *IN91     WHEQ '1'
     C                     EXSR SR2400                     上筆
     C           *IN92     WHEQ '1'
     C                     EXSR SR2500                     下筆
     C*
     C                     OTHER
     C                     MOVEL'01'      W#PRID           檢核
     C                     ENDSL
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面二：初始
     C*----------------------------------------
     CSR         SR2100    BEGSR
     C           S#CUNO    CHAINCBCUST               69
     C  N69                MOVELCBCUNM    S#CUNM
     C*
     C                     MOVELS#CUNO    ADCUNO
     C                     MOVELS#AREA    ADAREA
     C                     MOVELS#ORNO    ADORNO
     C           K#2159    CHAINRAR2159D             69
     C                     Z-ADDADAMT1    S#AMT1           暫收已兌現
     C                     Z-ADDADAMT2    S#AMT2           暫收繳貨款
     C                     Z-ADDADAMT3    S#AMT3           暫收未兌現
     C                     Z-ADDADAMT4    S#AMT4           財會切傳票
     C           ADAMT1    ADD  ADAMT4    S#NBAL
     C                     SUB  ADAMT2    S#NBAL
     C                     MOVELADRESV    S#RESV
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面二：下筆
     C*----------------------------------------
     CSR         SR2400    BEGSR
     C                     MOVELS#CUNO    ADCUNO
     C                     MOVELS#AREA    ADAREA
     C                     MOVELS#ORNO    ADORNO
     C           K#2159    CHAINRAR2159D             69
     C                     READ RAR2159D                 69
     C           *IN69     IFEQ '0'
     C                     MOVELADCUNO    S#CUNO
     C                     MOVELADAREA    S#AREA
     C                     EXSR SR2100                     初始畫面
     C                     ELSE
     C                     MOVELT#MSG,1   S#MSG2
     C                     ENDIF
     CSR         ES2400    ENDSR
     C*
     C*----------------------------------------
     C*  畫面二：上筆
     C*----------------------------------------
     CSR         SR2500    BEGSR
     C                     MOVELS#CUNO    ADCUNO
     C                     MOVELS#AREA    ADAREA
     C                     MOVELS#ORNO    ADORNO
     C           K#2159    CHAINRAR2159D             69
     C                     READPRAR2159D                 69
     C           *IN69     IFEQ '0'
     C                     MOVELADCUNO    S#CUNO
     C                     MOVELADAREA    S#AREA
     C                     EXSR SR2100                     初始畫面
     C                     ELSE
     C                     MOVELT#MSG,2   S#MSG2
     C                     ENDIF
     CSR         ES2500    ENDSR
     C*
     C**************************************************************
** T#MSG
０１－已達檔底。
０２－已達檔頭。
