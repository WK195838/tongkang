     H*****************************************************************
     H*
     H*          >>  PROGRAM INTRODUCTION  <<
     H*          1.PROGRAM-ID    ARE052R
     H*          2.PROGRAM-TYPE  RPG/400
     H*          3.AUTHOR        S02CSF
     H*          4.FUNCTION     應收帳款承購發票結案　　　
     H*          5.DATE-WRITTEN  91/10/16
     H*          5.DATE-MODIFY
     H*
     H*****************************************************************
     H        1   Y                                     1
     FARE052S CF  E                    WORKSTN
     F                                        RRN1  KSFILE AR052F2
     FARINVM  IF  E           K        DISK
     FARINVY  UF  E           K        DISK
     F            RARINVM                           KRENAMERINVMY
     FARCVDTL1IF  E           K        DISK
     E                    ERR     1   4 70               *錯誤訊息
     I           UDS
     I                                     10011010 U#USID
     I                                     10111020 S#DEVN
     I                                     10211021 U#AREA
     C*****************************************************************
     C*          MAIN      PROGRAM
     C*****************************************************************
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C           W#PRID    CASEQ'02'      SR2000           畫面二
     C           W#PRID    CASEQ'03'      SR3000            READC
     C                     ENDCS
     C                     ENDDO
     C                     MOVE *ON       *INLR
     C*****************************************************************
     C           SR0000    BEGSR
     C*----------------------------------------------------------------
     C                     MOVE *ALL'='   S#LIN1
     C                     MOVE *ALL'='   S#LIN2
     C                     MOVEL'01'      W#PRID  2
     C                     MOVE *OFF      *IN99
     C                     MOVE *ON       *IN49
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR1000    BEGSR
     C*----------------------------------------------------------------
     C           W#PRID    DOWEQ'01'
     C*
     C                     WRITEAR052H
     C                     EXFMTAR052F1
     C*
     C                     SETOF                     404499
     C                     MOVEA'00'      *IN,30
     C                     MOVEL*BLANK    S#ERR
     C*-----------
     C*IN03 IN12 -
     C*-----------
     C*
     C           *IN03     IFEQ *ON
     C           *IN12     OREQ *ON
     C                     MOVE '00'      W#PRID
     C                     ITER
     C                     ENDIF
     C*----------------
     C*CHECK客戶代號-
     C*----------------
     C*
     C           S#CUNO    IFEQ *BLANK
     C                     SETON                     304099
     C                     MOVELERR,1     S#ERR
     C                     ENDIF
     C*----------------
     C*CHECK發票日期-
     C*----------------
     C  N99      S#IND1    IFNE *BLANK
     C*
     C                     MOVE S#IND1    W#IND1  80
     C                     MOVE W#IND1    P#PDAT
     C                     CALL 'UTS102R'
     C                     PARM           P#PDAT  8        日期
     C                     PARM '1'       P#MODE  1        民國年
     C                     PARM           P#MTL  24        月底日期
     C                     PARM           P#LEAP  1        閏年碼
     C                     PARM           P#ERR   1        錯誤碼
     C           P#ERR     IFNE '0'
     C                     MOVELERR,2     S#ERR
     C                     SETON                     3199
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     C  N99                MOVE '02'      W#PRID
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2000    BEGSR
     C*----------------------------------------------------------------
     C*W#PRID='01'--->ARR052S-2 畫面
     C*
     C                     Z-SUB13        S#NBR
     C                     Z-ADD0         RRN1    50
     C*
     C* CLEAR SUBFILE
     C                     MOVE *ON       *IN73            SFLCLR
     C                     WRITEAR052F2C
     C                     MOVE *OFF      *IN73
     C*
     C           S#KEY1    SETLLRARINVM
     C                     EXSR SR2100
     C*
     C           RRN1      IFEQ 0                          SFLDSP OFF
     C                     MOVE *ON       *IN72
     C                     MOVELERR,3     S#ERR2
     C                     ELSE
     C                     MOVE *OFF      *IN72
     C                     ENDIF
     C*
     C                     MOVE *OFF      *IN48
     C                     MOVEL*BLANK    S#KEY1
     C*顯示畫面
     C           W#PRID    DOWEQ'02'
     C*
     C   10 99             MOVELERR,4     S#ERR2
     C*
     C                     WRITEAR052H
     C                     WRITEAR052F2M
     C                     EXFMTAR052F2C
     C                     MOVEL*BLANK    S#ERR2
     C*結束作業
     C*
     C           *IN03     IFEQ *ON
     C                     MOVE '00'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*回上頁
     C           *IN12     IFEQ *ON
     C                     MOVE '01'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*
     C*REPOSITION
     C           S#KEY1    IFNE *BLANK
     C                     LEAVE
     C                     ENDIF
     C*
     C*F5=查詢全部與未結案
     C           *IN05     IFEQ *ON
     C*
     C           *IN49     IFEQ *ON
     C                     MOVE *OFF      *IN49
     C                     ELSE
     C                     MOVE *ON       *IN49
     C                     ENDIF
     C                     LEAVE
     C*
     C                     ENDIF
     C*F10確認結案
     C           *IN10     IFEQ *ON
     C                     MOVE '03'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2100    BEGSR                           讀入資料
     C*****************************************************************
     C                     MOVE *OFF      *IN42
     C           *IN42     DOWEQ*OFF
     C                     READ RARINVM                  42
     C   42                LEAVE
     C*
     C                     CLEARAR052F2
     C*檢核客戶編號或發票日期
     C*
     C                     MOVE AMINDT    W#INDT  6
     C           AMCUNO    IFNE S#CUNO                     客戶編號
     C           W#INDT    ORLT S#IND1                     發票日期
     C                     ITER
     C                     ENDIF
     C*
     C  N49      AMCLOC    IFEQ 'Y'
     C                     ITER
     C                     ENDIF
     C*
     C           S#APNO    IFNE *BLANK
     C           S#APNO    IFNE AMAPN1
     C                     ITER
     C                     ENDIF
     C                     ENDIF
     C*
     C                     MOVELAMINNO    S#INNO           發票號碼
     C                     Z-ADDAMINDT    S#INDT           發票日期
     C                     Z-ADDAMDAMT    S#DAMT           發票金額
     C                     Z-ADDAMFAMT    S#FAMT           繳款金額
     C           S#DAMT    SUB  S#FAMT    S#AMT            未繳金額
     C                     MOVELAMAPN1    S#APN1           承購單號
     C                     MOVELAMCLOC    S#CLOC           結案碼
     C           S#INNO    CHAINRARCVDT              50
     C  N50                MOVELATRCNO    S#RCNO           繳款單號
     C*
     C                     ADD  1         RRN1
     C                     WRITEAR052F2
     C*
     C                     ENDDO
     C*
     C           RRN1      IFGT 0
     C                     ADD  14        S#NBR
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR3000    BEGSR
     C*----------------------------------------------------------------
     C*READC-->AR052F1
     C                     Z-ADD1         RRN1
     C                     MOVE *OFF      *IN99
     C*
     C           W#PRID    DOWEQ'03'
     C*
     C                     READCAR052F2                  43
     C   43                MOVE '02'      W#PRID
     C   43                MOVE *ON       *IN99
     C   43                LEAVE
     C*
     C           S#CLOC    IFEQ *BLANK
     C                     ITER
     C                     ENDIF
     C*
     C  N99                EXSR SR3100
     C*
     C                     MOVE '02'      W#PRID
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR3100    BEGSR
     C*****************************************************************
     C*存檔
     C                     Z-ADD1         RRN1
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C           RRN1      CHAINAR052F2              90
     C   90                LEAVE
     C*
     C           S#CLOC    IFNE *BLANK                     結案碼不為空白
     C*
     C                     MOVELS#INNO    AMINNO           發票號碼
     C           AMINNO    CHAINRINVMY               45
     C  N45                MOVEL'C'       AMFLAG           結案碼
     C  N45                MOVEL'Y'       AMCLOC           結案碼
     C  N45                MOVE U#USID    AMCLOM           結案人員
     C  N45                MOVE UDATE     AMCLOD           結案日期
     C                     UPDATRINVMY
     C*
     C                     ENDIF
     C*
     C                     ADD  1         RRN1
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
**  ERR
01-客戶代號不可空白
02-日期輸入錯誤
03-無相關資料
04-請選擇欲結案之資料
