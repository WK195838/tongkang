     H****************************************************************
     H*
     H*            UPDAT  DATE  102/02/22  2013AR730  S00WCJ (0202A)
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FARR038S CF  E                    WORKSTN
     FSAMAST  IF  E           K        DISK
     E*************************************************************
     E                    ERR     1   7 70
     I*************************************************************
0202AI            DS
0202AI                                        1   6 D#ORNO
0202AI                                        1   1 D#ORN1
0202AI                                        2   60D#ORN2
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           *ENTRY    PLIST
     C           *IN03     PARM *IN03     IN03    1
     C                     PARM           AREAF   1
     C                     PARM           AREAT   1
     C                     PARM           CUNOF   6
     C                     PARM           CUNOT   6
     C                     PARM           SALEF   2
     C                     PARM           SALET   2
     C                     PARM           DATS    80
     C                     PARM           DATE    80
0202AC                     PARM           S#ORNS  6
0202AC                     PARM           S#ORNE  6
     C*
0202AC           KEY01     KLIST
0202AC                     KFLD           D#ORN1
0202AC                     KFLD           D#ORN2
     C**************************************************************
     C           *IN03     DOUEQ'1'
     C                     MOVELTXAR      AREA
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C                     EXSR CK#01
     C  N99                SETON                     LR
     C  N99                RETRN
     C                     ENDDO
     C******
     CSR         KC#01     BEGSR
     C                     MOVE '1'       *IN03
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C**************************
     C***                    ***
     C***  SCREEN 01 CHECK   ***
     C***                    ***
     C**************************
     CSR         CK#01     BEGSR
     C                     SETOF                     414299
     C                     SETOF                     43
     C                     MOVE *BLANK    ERRMSG
     C                     SELEC
     C           AREA      WHEQ ' '
     C           AREA      WHEQ 'P'
     C           AREA      WHEQ 'T'
     C           AREA      WHEQ 'K'
     C           AREA      WHEQ 'M'
     C           AREA      WHEQ 'H'
     C           AREA      WHEQ 'L'
     C                     OTHER
     C                     SETON                     4199
     C                     MOVELERR,1     ERRMSG
     C                     ENDSL
      **
     C           AREA      IFEQ *BLANK
     C                     MOVE ' '       AREAF
     C                     MOVE 'Z'       AREAT
     C                     ELSE
     C                     MOVE AREA      AREAF
     C                     MOVE AREA      AREAT
     C                     ENDIF
      **
     C           S#CUNO    IFEQ *BLANK
     C                     MOVE '      '  CUNOF
     C                     MOVE '999999'  CUNOT
     C                     ELSE
     C                     MOVE S#CUNO    CUNOF
     C                     MOVE S#CUNO    CUNOT
     C                     ENDIF
      **
     C           S#SALE    IFEQ *BLANK
     C                     MOVE '  '      SALEF
     C                     MOVE 'Z '      SALET
     C                     ELSE
     C                     MOVE S#SALE    SALEF
     C                     MOVE S#SALE    SALET
     C                     ENDIF
      **
     C           S#DATS    IFEQ 0
     C                     Z-ADD700101    S#DATS
     C                     ENDIF
     C*
     C                     MOVE S#DATS    P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           FLAG1   1
     C*
　 C*起始日期錯誤
     C*
     C           FLAG1     IFNE '0'
     C                     SETON                     9942
     C                     MOVELERR,4     ERRMSG
     C                     ENDIF
      **
     C           S#DATE    IFEQ 0
     C                     Z-ADD99991231  S#DATE
     C                     ENDIF
     C*
     C                     MOVE S#DATE    P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           FLAG1   1
     C*
　 C*終止日期錯誤
     C*
     C           FLAG1     IFNE '0'
     C                     SETON                     9943
     C                     MOVELERR,5     ERRMSG
     C                     ENDIF
      **
     C           S#DATS    IFGT S#DATE
     C                     SETON                     994243
     C                     MOVELERR,6     ERRMSG
     C                     ENDIF
      **
     C  N99                Z-ADDS#DATS    DATS
     C  N99                Z-ADDS#DATE    DATE
      **
     C*
0202AC           S#ORNO    IFNE *BLANKS
0202AC                     MOVELS#ORNO    D#ORNO
0202AC           KEY01     CHAINRSAMAST              44
0202AC   44                MOVELERR,7     ERRMSG
0202AC   44                SETON                     99
0202AC  N44                MOVELS#ORNO    S#ORNS
0202AC  N44                MOVELS#ORNO    S#ORNE
0202AC                     ENDIF
0202AC*
0202AC           S#ORNO    IFEQ *BLANKS
0202AC                     MOVEL*BLANKS   S#ORNS
0202AC                     MOVEL'999999'  S#ORNE
0202AC                     ENDIF
0202AC*
     CSR         CK#01Z    ENDSR
     C******
** ERR
１、廠區選擇錯誤
２、發票範圍錯誤
３、訂單範圍錯誤
４、起始日期錯誤
５、終止日期錯誤
６、起始日期不可大於終止日期
７、訂單編號錯誤
