     H*****************************************************************
     H*
     H*          >>  PROGRAM INTRODUCTION  <<
     H*          1.PROGRAM-ID    ARR115R
     H*          2.PROGRAM-TYPE  RPG/400
     H*          3.AUTHOR        S02CSF
     H*          4.FUNCTION     銷貨調整通知單
     H*          5.DATE-WRITTEN  93/01/16
     H*          6.DATE-MODIFY
     H*
     H*****************************************************************
     H        1   Y                                     1
     FTRNDTL  IP  E           K        DISK
     FARR115T O   E             66     PRINTER
     E                    TABA1   9   9  2   TABA2   4   *第一聯說明
     E                    TABB1   6   6  2   TABB2   4   *第二聯說明
     E*****************************************************************
     ITXREC
     I                                              TXNO  L1
     I           UDS
     I                                     10011010 U#USID
     I                                     10111020 S#DEVN
     I                                      300 307 U#DAT1
     I                                      308 315 U#DAT2
     I                                      316 316 U#AREA
     C*****************************************************************
     C           *ENTRY    PLIST
     C                     PARM           W#CNT   10
     C*****************************************************************
     C*         主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     MOVE *ON       *IN99
     C                     MOVE *ON       *IN66
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     MOVE *ON       *IN66
     C*
     C                     MOVEL*ALL'-'   T#LIN1
     C                     MOVEL*ALL'-'   T#LIN2
     C                     MOVEL*ALL'-'   T#LIN3
     C                     MOVEL*ALL'-'   T#LIN4
     C                     MOVEL*ALL'-'   T#LIN5
     C*HEAD
     C                     MOVELTXCODE    T#CODE           單據代號
     C                     MOVEL'N'       T#FLAG           處理代號
     C                     MOVELU#AREA    T#AREA           廠別
     C                     MOVELTXORNO    T#ORNO           訂單編號
     C                     MOVELTXCUNO    T#CUNO           客戶代號
     C                     MOVELTXCUNM    T#CUNM           客戶名稱
     C                     MOVELTXDATE    T#DATE           單據日期
     C                     MOVELTXVUNO    T#VUNO           磅單編號
     C                     MOVELTXNO      T#NO             本單編號
     C*
     C                     Z-ADD0         W#ITEM  20
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C*若調整單項次超過20筆，則換頁
     C           W#ITEM    IFGE 20
     C                     EXSR L1SR
     C                     EXSR L1CLR
     C                     ENDIF
     C*
     C                     CLEARAR115T1D
     C                     ADD  1         W#ITEM           項次
     C                     Z-ADDW#ITEM    T#ITEM           項次
     C                     MOVEL*ALL'-'   T#LIN6
     C*
     C                     MOVELTXACNT    T#ACNT           調整別
     C                     MOVELTXPDNM    T#PDNM           品名
     C                     Z-ADDTXQTY     T#QTY            數量
     C                     Z-ADDTXUPRC    T#PRC            單價
     C                     Z-ADDTXAMT     T#AMT            金額
     C                     MOVELTXPCNO    T#PCNO           磅單編號
     C*聯式說明
     C                     MOVELW#ITEM    W#WORD  2
     C           W#CNT     IFEQ 1
     C           W#WORD    LOKUPTABA1     TABA2          60
     C   60                MOVE TABA2     T#WORD
     C                     ELSE
     C           W#WORD    LOKUPTABB1     TABB2          60
     C   60                MOVE TABB2     T#WORD
     C                     ENDIF
     C*
     C   66                WRITEAR115T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR115T1D
     C*合計
     C                     ADD  T#QTY     T#QTYT
     C                     ADD  T#AMT     T#AMTT
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C*若調整項次未到20筆，則強迫寫至20筆
     C           W#ITEM    IFLT 20
     C           W#ITEM    DOUEQ20
     C                     CLEARAR115T1D
     C                     ADD  1         W#ITEM           項次
     C                     Z-ADDW#ITEM    T#ITEM           項次
     C                     MOVEL*ALL'-'   T#LIN6
     C*聯式說明
     C                     MOVELW#ITEM    W#WORD  2
     C           W#CNT     IFEQ 1
     C           W#WORD    LOKUPTABA1     TABA2          60
     C   60                MOVE TABA2     T#WORD
     C                     ELSE
     C           W#WORD    LOKUPTABB1     TABB2          60
     C   60                MOVE TABB2     T#WORD
     C                     ENDIF
     C                     WRITEAR115T1D
     C                     ENDDO
     C                     ENDIF
     C*
     C                     MOVEL*ALL'-'   T#LIN7
     C                     MOVEL*ALL'-'   T#LIN8
     C                     MOVEL*ALL'-'   T#LIN9
     C                     MOVEL*ALL'-'   T#LINA
     C                     MOVEL*ALL'-'   T#LINB
     C                     MOVELTXIVNO    T#INNO
     C*
     C           T#QTYT    IFNE 0
     C           T#AMTT    DIV  T#QTYT    T#PRCT
     C                     ENDIF
     C*
     C   66                WRITEAR115T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR115T1T
     C*
     C   66                WRITEAR115T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR115T1N
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C*
     C   66                WRITEAR115T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR115T1M
     C*
     C                     ENDSR
     C*****************************************************************
**  TABA1 TABA2
01一02、03業04務05部06門07↓08會09計
**  TABB1 TABB2
01二02、03業04務05部06門
