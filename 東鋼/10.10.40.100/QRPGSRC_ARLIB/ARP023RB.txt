     H****************************************************************
     H*
     H*            UPDATE  DATE  99/08/26  2010AR517 S00WCJ (9908A)
     H*                          99/11/09  U廠資料會被刪除，加LOG觀
     H*                          察是否為此程式所刪。
     H*                         104/09/23  2015AR953  S00WCJ (0409A)
     H*                          產品代碼3碼擴5碼，順便移除LOG
     H*                          檔
     H*
     H****************************************************************
     H            Y                                     1                 BR100
     FTRNDTLT IP  E                    DISK
     F            TXREC                             KRENAMETXRECD
     FTRNDTL  UF  E           K        DISK                      A
0409AF*TRNDTLG O   E                    DISK
0409AF*            TXREC                             KRENAMETXLOG
     FQPRINT  O   F     198     OV     PRINTER
     ITXRECD      01
     I              TXFLAG                          FLAG
     I              TXCODE                          CODE
     I              TXNO                            NO
     I              TXITEM                          ITEM
     I              TXACNT                          ACNT
     I              TXDATE                          DATE
     I              TXACDT                          ACDT
     I              TXCUNO                          CUNO
     I              TXCUNM                          CUNM
     I              TXORNO                          ORNO
     I              TXIVNO                          IVNO
     I              TXPCNO                          PCNO
     I              TXVUNO                          VUNO
     I              TXRVID                          RVID
     I              TXSALE                          SALE
     I              TXSATP                          SATP
     I              TXIVTP                          IVTP
     I              TXPDNM                          PDNM
     I              TXQTY                           QTY
     I              TXUPRC                          UPRC
     I              TXAMT                           AMT
     I              TXTAX                           TAX
     I              TXFL01                          FL01
     I              TXFL02                          FL02
     I              TXTXAR                          TXAR
     I              TXTXDT                          TXDT
     I              TXRESV                          RESV
0209AIAADS        DS
0209AI              '東和鋼鐵企業股份有-C         CON4                       X
0209AI              '限公司'
     I           UDS
     I                                       21  21 FAC
     C           KEY       KLIST
     C                     KFLD           CODE
     C                     KFLD           NO
     C                     KFLD           ITEM
9908AC           *DATE     SUB  19000000  U#SYSD  80
     C                     MOVEA'000'     *IN,11
     C           FLAG      COMP 'A'                      11
     C           FLAG      COMP 'C'                      12
     C           FLAG      COMP 'D'                      13
     C           KEY       CHAINTXREC                31
     C                     MOVEL*BLANK    TXFLAG
     C                     MOVE CODE      TXCODE
     C                     MOVE NO        TXNO
     C                     Z-ADDITEM      TXITEM
     C                     MOVE ACNT      TXACNT
     C                     Z-ADDDATE      TXDATE
     C                     Z-ADDACDT      TXACDT
     C                     MOVE CUNO      TXCUNO
     C                     MOVE CUNM      TXCUNM
9008 C                     MOVELORNO      TXORNO
     C                     MOVE IVNO      TXIVNO
     C                     MOVE PCNO      TXPCNO
     C                     MOVE VUNO      TXVUNO
     C                     MOVE RVID      TXRVID
     C                     MOVE SALE      TXSALE
     C                     MOVE SATP      TXSATP
     C                     MOVE IVTP      TXIVTP
0409AC                     MOVELPDNM      TXPDNM
     C                     Z-ADDQTY       TXQTY
     C                     Z-ADDUPRC      TXUPRC
     C                     Z-ADDAMT       TXAMT
     C                     Z-ADDTAX       TXTAX
     C                     MOVE FL01      TXFL01
     C                     MOVE FL02      TXFL02
     C                     MOVE TXAR      TXTXAR
     C                     Z-ADDTXDT      TXTXDT
     C                     MOVE RESV      TXRESV
     C   11 31
     COR 12 31             WRITETXREC
     C   11N31
     COR 12N31             UPDATTXREC
0409AC*   13                WRITETXLOG
     C   13N31             EXCPTDEL#A
     OTXREC   EDEL             DEL#A
     C*--------------------------------------------------------------
     OQPRINT  H  103   1P
     O       OR        OV
     O                                   15 'RPT.ID: AR023'
0209AO                         CON4     112
     O                                  180 '印表日期'
     O                                  181 ':'
9908AO                         UDATE   +  1 '01  /  /  '
     O        H  1     1P
     O       OR        OV
     O                                    9 'FROM :'
     O                         FAC     +  1
     O                                  111 '銷貨明細檔傳輸核對清單'
     O                                  180 '頁　　數'
     O                                  181 ':'
     O                         PAGE     194
     O        H 11     1P
     O       OR        OV
     O                                    7 '處理'
     O                                 +  1 '單據'
     O                                 +  1 '單據'
     O                                 +  1 '項'
     O                                 +  1 '原因'
     O                                 +  1 '單據'
     O                                 +  1 '入帳'
     O                                 +  1 '客戶碼'
     O                                 +  1 '客戶名稱'
     O                                 +  1 '訂單碼'
     O                                 +  1 '發票碼'
     O                                 +  1 '磅單碼'
     O                                 +  1 '傳票碼'
     O                                 +  3 '收出'
     O                                 +  1 '銷售'
     O                                 +  1 '發票'
     O                                 +  1 '品名'
     O                                 +  1 '數　量'
     O                                 +  1 '單價'
     O                                 +  1 '金　　額'
     O                                 +  1 '稅　　額'
     O                                 +  1 '折'
     O                                 +  1 '過'
     O                                 +  1 '異'
     O                                  196 '異動日'
     O        H  1     1P
     O       OR        OV
     O                                    7 '代號'
     O                                 +  1 '代號'
     O                                 +  1 '號碼'
     O                                 +  1 '次'
     O                                 +  1 '別　'
     O                                 +  1 '日期'
     O                                 +  1 '日期'
     O                                  113 '款貨'
     O                                  120 '　別'
     O                                  127 '　別'
     O                                  177 '讓'
     O                                  182 '票'
     O                                  187 '廠'
     O        H  1     1P
     O       OR        OV
     O                                   21 '===================='
     O                                   41 '===================='
     O                                   61 '===================='
     O                                   81 '===================='
     O                                  101 '===================='
     O                                  121 '===================='
     O                                  141 '===================='
     O                                  161 '===================='
     O                                  181 '===================='
     O                                  195 '=============='
     O        D  1     01
     O                         TXFLAG     5
     O                         TXCODE    13
     O                         TXNO      22
     O                         TXITEM    25
     O                         TXACNT    29
     O                         TXDATEY   39
     O                         TXACDTY   48
     O                         TXCUNO    55
     O                         TXCUNM    66
     O                         TXORNO    77
     O                         TXIVNO    88
     O                         TXPCNO    98
     O                         TXVUNO   108
     O                         TXRVID   110
     O                         TXSALE   112
     O                         TXSATP   119
     O                         TXIVTP   126
0409AO                         TXPDNM  +  0
     O                         TXQTY 3  142
     O                         TXUPRC3  149
     O                         TXAMT 3  160
     O                         TXTAX 3  171
     O                         TXFL01   176
     O                         TXFL02   181
     O                         TXTXAR   186
     O                         TXTXDTY  195
     O        D  1     01
