     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR150R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION     对そqXf干基t参p
     H*            5.DATE-WRITTEN  107/01/29 2017AR1126
     H*            6.UPDATE  DATE
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR150F IP  E           K        DISK
     FARR150F1IF  E           K        DISK
     FARR150T O   E             69     PRINTER
     F*
     IRAR150F     01
     I                                              A1ORNOL2
     I                                              A1STNOL1
     I                                              A1PDNML1
     I           UDS
     I                                        1   80T#DATS
     I                                       11  180T#DATE
     I                                     10011010 T#USID
     I                                     10211021 U#AREA
     C*****************************************************************
     C*              MAIN  PROGRAM
     C*****************************************************************
     C*
     C           *IN99     IFEQ *OFF
     C                     EXSR INISR
     C                     MOVE *ON       *IN99
     C                     ENDIF
     C   L2                EXSR L2CLR
     C   01                EXSR DTLSR
     CL1 99                EXSR L1SR
     CL2 99                EXSR L2SR
     CLRN99                EXSR MSGSR
     CLR 99                EXSR LRSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C*
     C                     MOVE *ALL'='   T#LIN2
     C                     Z-ADD0         T1QTY
     C                     Z-ADD0         T1AAMT
     C                     Z-ADD0         T2QTY
     C                     Z-ADD0         T2AAMT
     C                     MOVELT#ORNO    W#ORNO  6
     C                     MOVELT#CUNO    W#CUNO  6
     C                     MOVEL*OFF      *IN60
     C                     MOVEL*OFF      *IN61
     C                     WRITEAR150TH
     C*
     C                     ENDSR
     C*****************************************************************
     C           L2CLR     BEGSR
     C*****************************************************************
     C*q虫pp
     C                     Z-ADD0         T1QTY
     C                     Z-ADD0         T1AAMT
     C                     MOVEL*BLANKS   T1SAUE
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C*
     C                     MOVELA1CUNO    T#CUNO
     C                     MOVELA1CUNM    T#CUNM
     C                     MOVELA1ORNO    T#ORNO
     C                     MOVELA1STNO    T#STNO
     C                     MOVELA1PDNM    T#PDNM
     C                     ADD  A1QTY     T#QTY
     C                     Z-ADDA1SAMT    T#SAMT
     C                     Z-ADDT#SAMT    W#SAMT  42
     C*                    ADD  A1AAMT    T#AAMT
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C*
     C                     ADD  T#QTY     T1QTY
     C           T#QTY     MULT T#SAMT    T#AAMT    H
     C                     ADD  T#AAMT    T1AAMT
     C*
     C           T#ORNO    IFNE W#ORNO
     C                     MOVEL*OFF      *IN61
     C                     MOVELT#ORNO    W#ORNO
     C                     ENDIF
     C           T#CUNO    IFNE W#CUNO
     C                     MOVEL*OFF      *IN60
     C                     MOVELT#CUNO    W#CUNO
     C                     ENDIF
     C                     MOVEL*ALL'-'   T#LIN1
     C   69                WRITEAR150TH
     C   69                MOVEL*OFF      *IN60
     C   69                MOVEL*OFF      *IN61
     C                     MOVE *OFF      *IN69
     C                     WRITEAR150T1
     C                     CLEARAR150T1
     C                     MOVEL*ON       *IN60
     C                     MOVEL*ON       *IN61
     C*
     C                     ENDSR
     C*****************************************************************
     C           L2SR      BEGSR
     C*****************************************************************
     C*
     C                     ADD  T1QTY     T2QTY
     C                     ADD  T1AAMT    T2AAMT
     C           W#ORNO    CHAINRAR150F1             45
     C                     MOVEL*BLANKS   T1SAUE
     C                     MOVELA0SAUE    T1SAUE
     C   69                WRITEAR150TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR150T2
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C   69                WRITEAR150TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR150TR
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C   69                WRITEAR150TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR150T3
     C                     WRITEAR150T4
     C                     ENDSR
     C****************************************************************
