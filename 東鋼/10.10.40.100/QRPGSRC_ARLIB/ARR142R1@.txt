     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR142R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION     折讓開立分攤未確認檢核表
     H*            5.DATE-WRITTEN  105/12/13 2016AR931
     H*            6.UPDATE  DATE
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR142F IP  E           K        DISK
     FARR142T O   E             69     PRINTER
     F*
     IRAR142F     01
     I                                              A4DXNOL2
     I                                              A4IVNOL1
     I                                              A4PDNML1
     I                                              A4METLL1
     I                                              A4SPE1L1
     I                                              A4SPE2L1
     I           UDS
     I                                      501 506 T#TXN1
     I                                      507 512 T#TXN2
     I                                      521 521 T#CHSE
     I                                     10011010 T#USID
     I                                     10211021 U#AREA
     C*****************************************************************
     C*              MAIN  PROGRAM
     C*****************************************************************
     C*
     C           *IN99     IFEQ *OFF
     C                     EXSR INISR
     C                     MOVE *ON       *IN99
     C                     ENDIF
     C   L2                EXSR L2CLR
     C   L1                EXSR L1CLR
     C   01                EXSR DTLSR
     CL1 99                EXSR L1SR
     CL2 99                EXSR L2SR
     CLRN99                EXSR MSGSR
     CLR 99                EXSR LRSR
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C*
     C                     MOVE *ALL'='   T#LIN1
     C                     MOVE *ALL'='   T#LIN2
     C                     Z-ADD0         T#QTY4
     C                     Z-ADD0         T#AMT4
     C                     Z-ADD0         T#TAX4
     C                     MOVEL*ON       *IN36
     C                     WRITEAR142TH
     C*
     C                     ENDSR
     C*****************************************************************
     C           L2CLR     BEGSR
     C*****************************************************************
     C*折讓單小計
     C                     Z-ADD0         T#QTY1
     C                     Z-ADD0         T#AMT1
     C                     Z-ADD0         T#TAX1
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C*
     C                     CLEARAR142T1
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C*
     C                     MOVELA4DXNO    T#TXNO           折讓單號
     C                     MOVELA4CUNO    T#CUNO           客戶編號
     C                     MOVELA4ORNO    T#ORNO           訂單編號
     C                     MOVELA4IVNO    T#IVNO           發票編號
     C                     MOVELA4PDNM    T#PDNM           品名
     C                     MOVELA4METL    T#METL           鋼種
     C                     MOVELA4SPE1    T#SPE1            H/W/D
     C                     MOVELA4SPE2    T#SPE2            B/T
     C                     ADD  A4QTY     T#QTY            數量
     C                     Z-ADDA4UPRC    T#UPRC           單價
     C                     ADD  A4AMT     T#AMT            金額
     C                     ADD  A4TAX     T#TAX            稅額
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C*
     C   69                WRITEAR142TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR142T1
     C                     ADD  T#QTY     T#QTY1
     C                     ADD  T#AMT     T#AMT1
     C                     ADD  T#TAX     T#TAX1
     C*
     C                     ENDSR
     C*****************************************************************
     C           L2SR      BEGSR
     C*****************************************************************
     C*
     C                     MOVEL*ALL'-'   T#LIN2
     C   69                WRITEAR142TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR142T2
     C                     MOVEL*ON       *IN36
     C*
     C                     ADD  T#QTY1    T#QTY4
     C                     ADD  T#AMT1    T#AMT4
     C                     ADD  T#TAX1    T#TAX4
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C   69                WRITEAR142TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR142T6
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C*
     C   69                WRITEAR142TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR142T5
     C   69                WRITEAR142TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR142T7
     C*
     C                     ENDSR
     C*****************************************************************
