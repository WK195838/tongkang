     A*****************************************************************
     A*
     A*            *****   PROGRAM INTRODUCTION   *****
     A*
     A*            1.PROGRAM-ID    ARP138RC
     A*            2.PROGRAM-TYPE  RPG
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     台北INEDTLT-->桃園、苗栗、高雄
     A*            5.DATE-WRITTEN  107/12/06 2018AR00031
     A*            6.UPDATE  DATE  108/05/09 S00WCJ (0805A)
     A*                            增加寫入電子介面LOG檔
     A*
     A*****************************************************************
     H            Y                                     1                 BR100
     FINEDTLT UP  E           K        DISK
     F            RINEDTL                           KRENAMEDTLECD
     FINEDTL  UF  E           K        DISK                      A
0805AFINEDTLG O   E           K        DISK
0805AF            RINEDTL                           KRENAMEDTLG
     IDTLECD      01
     I              EDFLAG                          FLAG
     I              EDAREA                          AREA
     I              EDMSTE                          MSTE
     I              EDNO                            NO
     I              EDINDT                          INDT
     I              EDINTT                          INTT
     I              EDPDCD                          PDCD
     I              EDQTY                           QTY
     I              EDUNIT                          UNIT
     I              EDUPRC                          UPRC
     I              EDAMT                           AMT
     I              EDSENR                          SENR
     I              EDREMK                          REMK
     I              EDRENR                          RENR
     I              EDTMDT                          TMDT
     I              EDTMTM                          TMTM
     I              EDTMOP                          TMOP
     I              EDTAWS                          TAWS
     I              EDSTCL                          STCL
     I              EDXPDD                          XPDD
     I              EDXPDT                          XPDT
     I              EDXUSR                          XUSR
     I              EDMAR1                          MAR1
     I              EDPROD                          PROD
     I              EDPRNM                          PRNM
     I              EDPTYN                          PTYN
     I           UDS
     I                                        6   6 AREA
0805AI                                     10011010 U#USID
     C           KEY       KLIST
     C                     KFLD           NO
     C                     KFLD           SENR
     C*
     C                     MOVEA'0000'    *IN,11
     C           FLAG      COMP 'A'                      11
     C           FLAG      COMP 'C'                      12
     C           FLAG      COMP 'D'                      13
     C*
     C           KEY       CHAINRINEDTL              31
     C                     EXSR MOVE1
     C           *IN31     IFEQ '1'
     C           *IN11     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     WRITERINEDTL
0805AC                     EXSR MOVE2
0805AC                     WRITEDTLG
     C                     END
     C                     END
     C           *IN31     IFEQ '0'
     C           *IN11     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     UPDATRINEDTL
0805AC                     EXSR MOVE2
0805AC                     WRITEDTLG
     C                     END
     C                     END
     C*
     C                     DELETDTLECD
     C*
     C****************************************************************
     C           MOVE1     BEGSR
     C****************************************************************
     C                     Z-ADDTMDT      EDTMDT
     C                     Z-ADDTMTM      EDTMTM
     C                     MOVELTMOP      EDTMOP
     C                     MOVELTAWS      EDTAWS
     C                     MOVELSTCL      EDSTCL
     C                     ENDSR
     C**************************************************************
0805AC           MOVE2     BEGSR
     C**************************************************************
0805AC                     MOVELFLAG      EDFLAG
0805AC                     MOVELAREA      EDAREA
0805AC                     MOVELMSTE      EDMSTE
0805AC                     MOVELNO        EDNO
0805AC                     MOVELINDT      EDINDT
0805AC                     MOVELINTT      EDINTT
0805AC                     MOVELPDCD      EDPDCD
0805AC                     Z-ADDQTY       EDQTY
0805AC                     MOVELUNIT      EDUNIT
0805AC                     Z-ADDUPRC      EDUPRC
0805AC                     Z-ADDAMT       EDAMT
0805AC                     MOVELSENR      EDSENR
0805AC                     MOVELREMK      EDREMK
0805AC                     MOVELRENR      EDRENR
0805AC                     Z-ADDTMDT      EDTMDT
0805AC                     Z-ADDTMTM      EDTMTM
0805AC                     MOVELTMOP      EDTMOP
0805AC                     MOVELTAWS      EDTAWS
0805AC                     MOVELSTCL      EDSTCL
0805AC                     Z-ADDXPDD      EDXPDD
0805AC                     Z-ADDXPDT      EDXPDT
0805AC                     MOVELXUSR      EDXUSR
0805AC                     MOVELMAR1      EDMAR1
0805AC                     MOVELPROD      EDPROD
0805AC                     MOVELPRNM      EDPRNM
0805AC                     MOVELPTYN      EDPTYN
0805AC                     MOVEL'ARP138RC'EDAPNM
0805AC           *DATE     SUB  19000000  EDCHDD
0805AC                     TIME           EDCHDT
0805AC                     MOVELU#USID    EDCUSR
0805AC                     ENDSR
