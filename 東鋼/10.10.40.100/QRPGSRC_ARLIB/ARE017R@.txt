     H        1   Y                                     1                 BR003
     FARE017S CF  E                    WORKSTN
     F                                        RR1   KSFILE SF1
     FRCVDTLL4UF  E           K        DISK
     F            RVREC                             KRENAMERVREC4
     FRCVDTL  UF  E           K        DISK
     FRCVDTLL5UF  E           K        DISK
     F            RVREC                             KRENAMERVREC5
     FCBCUST  IF  E           K        DISK
     FAR2159D UF  E           K        DISK
     FAR2159F UF  E           K        DISK
     FGENSEQ  UF  E           K        DISK                      A
     E*************************************************************
     E                    ARRO       99 10
     E                    R@         36  6
     E                    ERR     1  42 70
     I*************************************************************
     IRVREC
     I              RVCUNO                          F#CUNO
     I              RVACTP                          F#ACTP
     I              RVACNO                          F#ACNO
     I              RVFLAG                          F#FLAG
     I              RVDECD                          F#DECD
     I              RVRAMT                          F#RAMT
     I              RVRLNO                          F#RLNO
     I              RVRESV                          F#RESV
     I*
     IAADS        DS
     I                                        1   5 W1CUNO
     I                                        1   1 S1KIND
     I                                        2   2 S1CUN1
     I                                        3   5 S1CUN2
9008 I                                       11  16 ORNO1
 .   I                                       11  11 ORNO11
 .   I                                       12  160ORNO12
     I                                       21  30 SBID
     I                                       21  23 SBID1
     I                                       24  27 SBID2
     I                                       28  30 SBID3
     I                                       31  40 SANO
     I                                       31  31 SANO1
     I                                       32  32 SANO2
     I                                       33  40 SANO3
     IBBDS        DS
     I                                        1  10 GRP
     I                                        1   1 GE1
     I                                        2  10 GE2
     I                                       11  16 NOG
     I                                       11  11 NOA
     I                                       12  160NOB
     I            DS
     I                                        1  12 F#RESV
     I                                       11  11 D#RES1
     I                                       12  12 D#RESV
     I            DS
     I                                        1  12 RVRESV
     I                                       11  11 D#RE11
     I           UDS
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVN
     I                                     10211021 D#TXAR
     C*****************************************************************
     C*          KEY   LIST
     C*****************************************************************
     C           GEKEY     KLIST
     C                     KFLD           GEKIND
     C                     KFLD           GEPRIN
     C*
     C           CBKEY     KLIST
     C                     KFLD           CBCUNO
     C*
     C           RV4KY     KLIST
     C                     KFLD           RVWRNO
     C                     KFLD           RVNTNO
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C*
     C           RV5KY     KLIST
     C                     KFLD           RVNTNO
     C                     KFLD           RVCUNO
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C*
     C           RV5TKY    KLIST
     C                     KFLD           RVNTNO
     C                     KFLD           RVCUNO
     C*
     C           K#A59D    KLIST
     C                     KFLD           K#CUNO  6        客戶編號
     C                     KFLD           K#AREA  1        出納廠區
     C                     KFLD           K#ORNO  6        訂單編號
     C*
     C           K#A59F    KLIST
     C                     KFLD           K#CUNO  6        客戶編號
     C                     KFLD           K#AREA  1        出納廠區
     C*****************************************************************
     C*          MAIN  PROGRAM
     C*****************************************************************
     C  N90                EXSR INZ#01
     C           *INKC     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C*****************************************************************
     CSR         INZ#01    BEGSR
     C*****************************************************************
     C  N90                MOVE '1'       SCRN    1
     C  N90                Z-ADD36        R#L1    30
     C  N90                Z-ADD10        R#L2    30
     C  N90                Z-ADD99        R#L3    30
     C  N90                MOVE '1'       *IN,90
     C*
     CSR                   ENDSR
     C*****----------------------*****
     C***** SCREEN 01 MAIN LOGIC *****
     C*****----------------------*****
     CSR         SR#01     BEGSR
     C*
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C                     EXSR CK#01
     C  N99                EXSR PR#02
     C  N99                MOVE '2'       SCRN
     C*
     CSR         SR#01Z    ENDSR
     C*****----------------------*****
     C***** SCREEN 02 MAIN LOGIC *****
     C*****----------------------*****
     CSR         SR#02     BEGSR
     C*
     C                     WRITEUND01
     C                     EXFMTSFCTL1
     C   KC                EXSR KC#01
     C   KC                GOTO SR#02Z
     C   KL                MOVE *BLANK    S#ERR
     C   KL                MOVE '1'       SCRN
     C   KL                GOTO SR#02Z
     C   KJ                EXSR KJ#02
     C   KJ                GOTO SR#02Z
     C*
     C           S#ACT     IFEQ '1'
     C           S#ACT     OREQ '2'
     C                     EXSR CK#02
     C                     ENDIF
     C*
     CSR         SR#02Z    ENDSR
     C*****---------------------*****
     C*****                     *****
     C*****  PF03  EXIT         *****
     C*****                     *****
     C*****---------------------*****
     CSR         KC#01     BEGSR
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
     CSR                   ENDSR
     C*****---------------------*****
     C*****                     *****
     C*****  PF10  SAVE         *****
     C*****                     *****
     C*****---------------------*****
     CSR         KJ#02     BEGSR
     C*
     C           S#ACT     IFEQ '1'
     C           S#ACT     OREQ '2'
     C                     EXSR CK#02
     C                     ENDIF
     C*
     C  N99      S#ACT     IFNE '4'
     C                     EXSR FL#02
     C                     ENDIF
     C  N99                MOVE '1'       SCRN
     C*
     CSR         KJ#02Z    ENDSR
     C*****-----------------------*****
     C*****   SCREEN 01 CHECK     *****
     C*****-----------------------*****
     CSR         CK#01     BEGSR
     C*
     C                     SETOF                     414299
     C                     SETOF                     4647
     C                     MOVE *BLANK    S#ERR
     C                     EXSR ACTCTL
     C*
     C                     SELEC
     C           S#ACT     WHEQ ' '
     C                     SETON                     4199
     C                     MOVE ERR,1     S#ERR
     C           S#ACT     WHEQ '1'
     C                     MOVE '新增'  S#MOD
     C           S#ACT     WHEQ '2'
     C                     MOVE '修改'  S#MOD
     C           S#ACT     WHEQ '3'
     C                     MOVE '刪除'  S#MOD
     C           S#ACT     WHEQ '4'
     C                     MOVE '查詢'  S#MOD
     C                     OTHER
     C                     SETON                     4199
     C                     MOVE ERR,2     S#ERR
     C                     ENDSL
     C******
     C           S#ACT     IFNE '1'
     C           S#WRNO    ANDEQ*BLANK                     退回單號
     C                     SETON                     4299
     C                     MOVE ERR,3     S#ERR
     C                     ENDIF
     C**
     C           S#ACT     IFEQ '1'
     C           S#WRNO    ANDNE*BLANK                     退回單號
     C                     SETON                     4299
     C                     MOVE ERR,18    S#ERR
     C                     ENDIF
     C**
     C           S#ACT     IFEQ '1'
     C           S#CUN     IFNE *BLANK                     客戶代號
     C           S#RNO     ANDEQ*BLANK                     繳款單號
     C                     SETON                     4799
     C                     MOVE ERR,32    S#ERR
     C                     ENDIF
     C*
     C           S#RNO     IFNE *BLANK                     繳款單號
     C           S#CUN     ANDEQ*BLANK                     客戶代號
     C                     SETON                     4699
     C                     MOVE ERR,32    S#ERR
     C                     ENDIF
     C                     ENDIF
     C**
     C**
     C           *IN99     IFEQ '0'
     C                     MOVELS#WRNO    RVWRNO           退回單號
     C                     MOVE *BLANK    RVNTNO           票據號碼
     C                     MOVE *BLANK    RVNO             繳款單號
     C                     MOVE *BLANK    RVITEM           項次
     C*
     C           RV4KY     SETLLRCVDTLL4             97
     C  N97      S#WRNO    READERCVDTLL4            N    97
     C                     SELEC
     C           *IN97     WHEQ '0'
     C           S#ACT     COMP '1'                      31
     C   31                SETON                     4299
     C   31                MOVE ERR,4     S#ERR
     C           *IN97     WHEQ '1'
     C           S#ACT     COMP '2'                      31
     C  N31      S#ACT     COMP '3'                      31
     C  N31      S#ACT     COMP '4'                      31
     C   31                SETON                     4299
     C   31                MOVE ERR,5     S#ERR
     C                     ENDSL
     C                     ENDIF
     C*
     C                     Z-ADD0         W#CON1  20       計算應收票
     C                     Z-ADD0         W#CON2  20       算貸方暫收
     C                     Z-ADD0         W#CON3  20       算未兌暫收
     C                     Z-ADD0         W#CON4  20       算暫收訂單
     C                     Z-ADD0         W#TAMT 110
     C*
     C                     MOVEL*OFF      *IN97
     C           S#RNO     SETLLRCVDTL
     C  N99      *IN97     DOWEQ*OFF
     C           S#RNO     READERCVDTL              N    97
     C*
     C   97                LEAVE
     C*
     C           S#CUN     IFNE F#CUNO
     C                     SETON                     464799
     C                     MOVE ERR,34    S#ERR
     C                     LEAVE
     C                     ENDIF
     C*
     C           F#FLAG    IFEQ 'D'
     C           F#DECD    OREQ 'D'
     C                     SETON                     464799
     C                     MOVE ERR,38    S#ERR
     C                     LEAVE
     C                     ENDIF
     C*
     C           F#ACTP    IFEQ 'D'
     C           F#ACNO    ANDEQ'1131'
     C                     ADD  1         W#CON1
     C                     ENDIF
     C*
     C           F#ACTP    IFEQ 'C'
     C           F#ACNO    ANDEQ'2159'
     C                     ADD  1         W#CON2
     C*
     C           D#RESV    IFEQ '*'
     C                     ADD  1         W#CON3
     C                     ELSE
     C                     ADD  F#RAMT    W#TAMT
     C                     ENDIF
     C*
     C           S#ORNO    IFEQ F#RLNO
     C                     ADD  1         W#CON4
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
     C*
     C  N99      W#CON1    IFEQ 0
     C                     SETON                     4799
     C                     MOVE ERR,39    S#ERR
     C                     ENDIF
     C*
     C  N99      W#CON2    IFEQ 0
     C                     SETON                     4799
     C                     MOVE ERR,35    S#ERR
     C                     ENDIF
     C*
     C  N99      W#CON3    IFEQ W#CON2
     C                     SETON                     4799
     C                     MOVE ERR,40    S#ERR
     C                     ENDIF
     C*
     C  N99      W#CON4    IFEQ 0
     C                     SETON                     4899
     C                     MOVE ERR,42    S#ERR
     C                     ENDIF
     C*
     CSR         CK#01Z    ENDSR
      ********
      ********
      ********
     CSR         ACTCTL    BEGSR
     C*
     C                     SETOF                     7071
     C                     SELEC
     C           S#ACT     WHEQ '1'                        *新增
     C                     SETON                     71
     C           S#ACT     WHEQ '3'                        *刪除
     C                     SETON                     7071
     C           S#ACT     WHEQ '4'                        *查詢
     C                     SETON                     7071
     C                     ENDSL
     C*
     CSR                   ENDSR
     C******
     C*****----------------------*****
     C*****  SCREEN 02 PREPARE   *****
     C*****----------------------*****
     CSR         PR#02     BEGSR
     C**
     C** SCREEN TOP
     C**
     C                     MOVE *BLANK    S#ERR
     C                     SETON                     80
     C                     WRITESFCTL1
     C                     SETOF                     80
     C                     MOVEA'00000'   *IN,43
     C                     MOVE *BLANK    S#CUNO
     C                     MOVE *BLANK    S#CUNM
     C                     Z-ADDUDATE     S#DATE
     C                     Z-ADD0         S#TAMT           合計金額
     C                     Z-ADD0         RR1     20
     C*
     C** SCREEN
     C           S#ACT     CASEQ'1'       P2#ADD
     C                     CAS            P2#OTH
     C                     ENDCS
     C*
     CSR         PR#02Z    ENDSR
     C*****------------------------*****
     C***** SCREEN 02 TOP FOR ADD  *****
     C*****------------------------*****
     CSR         P2#ADD    BEGSR
     C*
     C                     EXSR @GETNO
     C                     CLEARSF1
     C                     EXSR ACTCTL
     C           RR1       DOWLTR#L1
     C                     ADD  1         RR1
     C                     Z-ADDRR1       S#SRL1           項次
     C                     WRITESF1
     C                     ENDDO
     C                     SETON                     10
     C*
     CSR         P2#ADZ    ENDSR
     C*****-----------------------*****
     C*****  NO. AUTO-CODE        *****
     C*****-----------------------*****
     CSR         @GETNO    BEGSR
     C*
     C                     MOVEL'07'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVE *BLANK    GRP
     C                     MOVELD#TXAR    GE1
     C                     MOVELGRP       GEPRIN
     C*
     C           GEKEY     CHAINGENSEQ              N97
     C   97                Z-ADD0         GECUNO
     C                     MOVELD#TXAR    NOA
     C           GECUNO    ADD  1         NOB
     C                     MOVELNOG       S#WRNO
      *
     C           S#CUN     IFNE *BLANK
     C                     MOVE S#CUN     S#CUNO
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*****------------------------*****
     C*****
     C***** SCREEN 02 PREPARE(U,D,I)****
     C*****    TOP
     C*****------------------------*****
     CSR         P2#OTH    BEGSR
     C*
     C                     MOVELS#WRNO    RVWRNO
     C                     MOVE *BLANK    RVNTNO
     C                     Z-ADD0         RVDUDT
     C*
     C                     SETOF                     97
     C           RV4KY     SETLLRCVDTLL4             97
     ****
     C           *IN97     DOWNE'1'
     C           S#WRNO    READERCVDTLL4                 97
     C   97                LEAVE
     C           RVFLAG    COMP 'D'                      31
     C   31                ITER
     C*
     C           RVNTCD    IFEQ '2'
     C                     MOVELRVFLAG    S#ACD1
     C                     ADD  RVRAMT    S#TAMT           合計金額
     C                     ELSE
     C                     MOVEL'D'       S#ACD1
     C                     ENDIF
     C*
     C                     MOVELRVNTNO    S#NTNO           票據號碼
     C                     MOVELRVPBID    S#PBID           付款銀行
     C                     MOVELRVPANO    S#PANO           付款帳號
     C                     MOVELRVNTTP    S#NTTP           票據別
     C                     MOVELRVUSTP    S#USTP           款項別
     C                     MOVELRVPLAC    S#PLAC           付款地
     C                     MOVELRVRLNO    S#RLNO           相關號碼
     C                     Z-ADDRVRAMT    S#RAMT           收款金額
     C                     MOVELRVDPNO    S#DPNO           部門代號
     C                     MOVELRVSANO    S#SANO           存入帳號
     C                     Z-ADDRVDUDT    S#DUDT           到期日
     C                     MOVELRVAMCD    S#AMCD           資金代號
     C                     ADD  1         RR1
     C                     Z-ADDRR1       S#SRL1           項次
     C                     WRITESF1
     C                     ENDDO
     C****
     C                     MOVELRVCUNO    S#CUNO
     C                     MOVELS#CUNO    CBCUNO
     C           CBKEY     CHAINCBCUST               97
     C  N97                MOVELCBCUNM    S#CUNM
     C   97                MOVE *BLANK    S#CUNM
     C                     Z-ADDRVNTDT    S#DATE
      *
     C           S#ACT     IFEQ '1'
     C           S#ACT     OREQ '2'
     C           RR1       OREQ 0
     C                     CLEARSF1
     C                     EXSR ACTCTL
     C           RR1       DOWLTR#L1
     C                     ADD  1         RR1
     C                     Z-ADDRR1       S#SRL1
     C                     WRITESF1
     C                     ENDDO
     C                     ENDIF
     C****
     C                     SETON                     10
     C*
     CSR         P2#OTZ    ENDSR
     C*****----------------------*****
     C***** SCREEN 02 CHECK      *****
     C*****----------------------*****
     CSR         CK#02     BEGSR
     C*
     C                     SETOF                     99
     C                     MOVEA'000000'  *IN,43
     C                     MOVEA*BLANK    R@
     C                     MOVE *BLANK    S#ERR
     C                     MOVE *BLANK    S#CUNM
     C                     Z-ADD0         S#TAMT
     C***
     C           S#CUNO    IFNE *BLANK
     C                     MOVELS#CUNO    CBCUNO
     C           CBKEY     CHAINCBCUST               97
     C  N97                MOVELCBCUNM    S#CUNM
     C   97                MOVE ERR,8     S#ERR
     C   97                SETON                     9943
     C   97                GOTO CK#02Z
     C                     ENDIF
     C***
     C                     SELEC
     C           S#DATE    WHEQ 0
     C                     SETON                     9944
     C                     MOVELERR,13    S#ERR
     C                     GOTO CK#02Z
     C                     OTHER
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE S#DATE    P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           FLAG1   1
     C           FLAG1     IFNE '0'
     C                     SETON                     9945
     C                     MOVELERR,14    S#ERR
     C                     GOTO CK#02Z
     C                     ENDIF
     C                     ENDSL
      **
     C                     Z-ADD0         RR1
     C           RR1       DOWLTR#L1
     C                     ADD  1         RR1
     C           RR1       CHAINSF1                  97
     C   97                LEAVE
     C                     MOVEA'00000000'*IN,51
     C                     MOVEA'0000000' *IN,59
     C***
     C           S#ACD1    IFEQ 'D'
     C                     UPDATSF1
     C                     ITER
     C                     ENDIF
     C***
     C           S#NTNO    IFEQ *BLANK
     C                     CLEARSF1
     C                     EXSR ACTCTL
     C                     Z-ADDRR1       S#SRL1
     C                     UPDATSF1
     C                     ITER
     C                     END
      *
     C           S#ACD1    IFEQ *BLANK
     C                     MOVEL'C'       S#ACD1
     C                     ENDIF
      *
     C                     MOVELS#NTNO    RVNTNO
     C                     MOVELS#CUNO    RVCUNO
     C                     MOVE *BLANK    RVNO
     C                     MOVE *BLANK    RVITEM
     C*
     C           RV5KY     SETLLRCVDTLL5             31
     C           CK#02A    TAG
     C  N31      S#NTNO    READERCVDTLL5            N    31
     C   31                SETON                     4599
     C   31                MOVELERR,21    S#ERR
     C   31                UPDATSF1
     C   31                ITER
     C           RVFLAG    COMP 'D'                      31
     C   31                GOTO CK#02A
     C           S#CUNO    IFNE RVCUNO
     C                     SETON                     4599
     C                     MOVELERR,26    S#ERR
     C                     UPDATSF1
     C                     ITER
     C                     ENDIF
      ***
     C*                    MOVELRVNO      TXARW   1
     C                     MOVELRVAREA    TXARW   1
     C           D#TXAR    COMP TXARW                3131
     C   31                SETON                     4599
     C   31                MOVELERR,25    S#ERR
     C   31                UPDATSF1
     C   31                ITER
      ***
     C           RVWRNO    COMP *BLANK               3131
     C   31      RVWRNO    COMP S#WRNO               3131
     C   31                SETON                     4599
     C   31                MOVELERR,24    S#ERR
     C   31                UPDATSF1
     C   31                ITER
     C***
     C           RVNO      IFNE S#RNO
     C                     SETON                     4599
     C                     MOVELERR,36    S#ERR
     C                     UPDATSF1
     C                     ITER
     C                     ENDIF
     C***
     C                     MOVE RVRESV    W#RESV  1
     C           RVVNO2    IFNE *BLANK
     C           W#RESV    OREQ '*'
     C                     SETON                     4599
     C                     MOVELERR,37    S#ERR
     C                     UPDATSF1
     C                     ITER
     C                     ENDIF
     C***
     C                     MOVELRVNO      R@,RR1
     C***
     C                     ADD  RVRAMT    S#TAMT
     C                     MOVELRVPBID    S#PBID
     C                     MOVELRVPANO    S#PANO
     C                     MOVELRVNTTP    S#NTTP
     C                     MOVELRVUSTP    S#USTP
     C                     MOVELRVPLAC    S#PLAC
     C                     MOVELRVRLNO    S#RLNO
     C                     Z-ADDRVRAMT    S#RAMT
     C                     MOVELRVDPNO    S#DPNO
     C                     MOVELRVSANO    S#SANO
     C                     Z-ADDRVDUDT    S#DUDT
     C                     MOVELRVAMCD    S#AMCD
     C***
     C           RVACNO    IFNE '1131'
     C           RVACNO    ANDNE'1544'
     C                     SETON                     4599
     C                     MOVE ERR,22    S#ERR
     C                     END
     C                     UPDATSF1
     C***
     C                     ENDDO
     C***
     C  N99      S#TAMT    IFGT W#TAMT
     C                     SETON                     99
     C                     MOVE ERR,41    S#ERR
     C                     ENDIF
     C***
     C* N99      S#TAMT    IFEQ 0
     C*                    SETON                     99
     C*                    MOVELERR,16    S#ERR
     C*                    ENDIF
     C***
     CSR         CK#02Z    ENDSR
     C******
     C******
     CSR         FL#02     BEGSR
     C*
     C           S#ACT     CASEQ'3'       DL#02
     C           S#ACT     CASEQ'2'       UP#02
     C           S#ACT     CASEQ'1'       WR#02
     C                     ENDCS
     C*
     CSR         FL#02Z    ENDSR
      *****
      *****
      *****
     CSR         WR#02     BEGSR
     C*
     C                     CLEARGEREC
     C                     MOVEL'07'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVE *BLANK    GRP
     C                     MOVELD#TXAR    GE1
     C                     MOVELGRP       GEPRIN
     C*
     C           GEKEY     CHAINGENSEQ               97
     C   97                Z-ADD1         GECUNO
     C   97                WRITEGEREC
     C  N97                ADD  1         GECUNO
     C  N97                UPDATGEREC
     C                     MOVELD#TXAR    NOA
     C                     Z-ADDGECUNO    NOB
     C                     MOVELNOG       S#WRNO
      ***
     C                     EXSR DL#RV
     C                     EXSR UP#RV
     CSR         WR#02Z    ENDSR
     C***
     C***
     C***
     CSR         DL#02     BEGSR
     C*
     C                     EXSR DL#RV
     C*
     CSR         DL#02Z    ENDSR
     C***
     C***
     C***
     CSR         UP#02     BEGSR
     C                     EXSR DL#RV
     C                     EXSR UP#RV
     CSR         UP#02Z    ENDSR
     C***
     C***
     C***
     CSR         UP#RV     BEGSR
     C*
     C                     Z-ADD0         RR1
     C                     MOVEL*BLANK    DEPT    4
     C                     SETOF                     97
     C           *IN97     DOWEQ'0'
     C                     ADD  1         RR1
     C           RR1       CHAINSF1                  97
     C   97                LEAVE
      *
     C           S#NTNO    IFEQ *BLANK
     C           S#ACD1    OREQ 'D'
     C                     ITER
     C                     ENDIF
      *
     C                     MOVELS#NTNO    RVNTNO
     C                     MOVELS#CUNO    RVCUNO
     C                     MOVE *BLANK    RVNO
     C                     MOVE *BLANK    RVITEM
     C           RV5KY     SETLLRCVDTLL5             31
     C  N31      RV5TKY    READERCVDTLL5                 31
     C   31                ITER
     C                     MOVELRVDPNO    DEPT    4
     C                     MOVEL'C'       RVFLAG
     C                     MOVEL'2'       RVNTCD
     C                     Z-ADDS#DATE    RVNTDT
     C                     MOVELS#WRNO    RVWRNO
     C                     MOVELD#TXAR    RVTXAR
     C                     MOVEL'9'       D#RE11           暫收退記號
     C                     Z-ADDUDATE     RVTXDT
     C                     UPDATRVREC5
     C*
     C                     EXSR UP#AR
     C                     ENDDO
     C*
     C                     MOVEL*OFF      *IN60
     C           S#RNO     SETLLRCVDTL
     C           *IN60     DOWEQ*OFF
     C           S#RNO     READERCVDTL                   60
     C*
     C   60                LEAVE
     C*
     C           F#ACNO    IFNE '2159'
     C           F#RLNO    ORNE S#ORNO
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVEL'9'       D#RES1           暫收退記錄
     C                     UPDATRVREC
     C                     ENDDO
     C*
     CSR         UP#RVZ    ENDSR
     C***
     C***
     C***
     CSR         UP#AR     BEGSR
     C*
     C                     MOVELS#CUN     K#CUNO           客戶編號
     C                     MOVELS#RNO     K#AREA           出納廠區
     C                     MOVELS#ORNO    K#ORNO           訂單編號
     C*
     C                     SETOF                     97
     C           K#A59D    CHAINRAR2159D             97
     C           *IN97     DOWNE'1'
     C                     MOVEL'C'       ADFLAG           異動代碼
     C                     SUB  S#RAMT    ADAMT3           暫收未兌現
     C                     UPDATRAR2159D
     C                     LEAVE
     C                     ENDDO
     C*
     C                     SETOF                     97
     C           K#A59F    CHAINARREC                97
     C           *IN97     DOWNE'1'
     C                     MOVEL'C'       ARFLAG           異動代碼
     C                     SUB  S#RAMT    ARAMT3           暫收未兌現
     C                     UPDATARREC
     C                     LEAVE
     C                     ENDDO
     C*
     CSR         UP#ARZ    ENDSR
     C*****----------------------------*****
     C***** DELETE RV DATA (LOGICAL)   *****
     C*****----------------------------*****
     CSR         DL#RV     BEGSR
     C*
     C                     MOVELS#WRNO    RVWRNO
     C                     MOVE *BLANK    RVNTNO
     C                     MOVE *BLANK    RVNO
     C                     MOVE *BLANK    RVITEM
     C*
     C                     SETOF                     97
     C           RV4KY     SETLLRCVDTLL4             97
     C           *IN97     DOWNE'1'
     C           S#WRNO    READERCVDTLL4                 97
     C   97                LEAVE
      *
     C           RVNTCD    IFNE '2'
     C                     ITER
     C                     END
      *
     C                     MOVEL'C'       RVFLAG
     C                     MOVE *BLANK    RVNTCD
     C                     MOVELD#TXAR    RVTXAR
     C                     MOVEL*BLANK    D#RE11           暫收退記號
     C                     Z-ADDUDATE     RVTXDT
     C                     UPDATRVREC4
      *
     C                     EXSR DL#AR
     C                     ENDDO
     C*
     C                     MOVEL*OFF      *IN60
     C           S#RNO     SETLLRCVDTL
     C           *IN60     DOWEQ*OFF
     C           S#RNO     READERCVDTL                   60
     C*
     C   60                LEAVE
     C*
     C           F#ACNO    IFNE '2159'
     C           F#RLNO    ORNE S#ORNO
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVEL*BLANK    D#RES1           暫收退記錄
     C                     UPDATRVREC
     C                     ENDDO
     C*
     CSR         DL#RVZ    ENDSR
     C*****----------------------------*****
     C***** DELETE AR DATA (LOGICAL)   *****
     C*****----------------------------*****
     CSR         DL#AR     BEGSR
     C*
     C                     MOVELS#CUN     K#CUNO           客戶編號
     C                     MOVELS#RNO     K#AREA           出納廠區
     C                     MOVELS#ORNO    K#ORNO           訂單編號
     C*
     C                     SETOF                     97
     C           K#A59D    CHAINRAR2159D             97
     C           *IN97     DOWNE'1'
     C                     MOVEL'C'       ADFLAG           異動代碼
     C                     ADD  S#RAMT    ADAMT3           暫收未兌現
     C                     UPDATRAR2159D
     C                     LEAVE
     C                     ENDDO
     C*
     C                     SETOF                     97
     C           K#A59F    CHAINARREC                97
     C           *IN97     DOWNE'1'
     C                     MOVEL'C'       ARFLAG           異動代碼
     C                     ADD  S#RAMT    ARAMT3           暫收未兌現
     C                     UPDATARREC
     C                     LEAVE
     C                     ENDDO
     C*
     CSR         DL#ARZ    ENDSR
      *******************************************************
** ERR
01功能代碼必須輸入．
02功能代碼必須為１，２，３，４
03非新增時，退回單號必須輸入　　　　
04退回單號已存在，不可新增
05退回單號不存在
06繳款單已作廢，不可再處理
07此張發票已作廢，不可再處理
08客戶編號不存在
09發票號碼不存在　　　　
10沖銷總金額不等於退回總金額
11此張發票已繳完款，不可再沖銷
12發票號碼重複
13申請日期必須輸入
14申請日期錯誤
15支票號碼必須輸入
16退回金額不可為零
17訂單編號不存在
18新增時，退回單號不可輸入　　　　
19發票收款金額比退回金額還小，不可沖銷
20發票之客戶編號不等於繳款客戶編號
21票據號碼不存在
22此筆資料非為票據資料
23發票之繳款單號不存在於票據資料中
24此筆票據已退回不可再退回
25此筆票據為不同廠區之資料不可處理
26票據號碼非為此客戶之繳款資料
27借貸方必須為苠疿虳恛苠捑
28會計科目必須輸入
29沖帳方式必須輸入
30到期日期錯誤
31金額必須輸入
32以繳款單帶出發票時,繳款單及客戶代號不得空白　　　　
33會計科目輸入錯誤
34此繳款單號不屬於該客戶所有，請查核！
35此繳款單號貸方沒有２１５９之科目，不可退票，請查核！
36此票據號碼非隸屬此繳款單，請查核！
37此繳款書之票據已兌現，不可退票！
38此客戶此張繳款書已作廢，不可退票！
39此繳款書中沒有應收票據供退票，請查核!
40此繳款書中之２１５９科目皆已兌現，不可退票，請查核！
41退票申請單中之票據金額大於可退金額，請查核！
42欲退票之暫收訂單號碼不存在，請查核！
