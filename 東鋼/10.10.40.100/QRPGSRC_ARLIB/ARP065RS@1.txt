     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP065RS
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     發票每日開立作業（畫面）
     H*            5.DATE-WRITTEN  88/01/31
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARP065S CF  E                    WORKSTN
     FGENSEQ  UF  E           K        DISK
     E*************************************************************
     E                    T#TB1   1   5  1
     E                    T#MSG   1   8 70
     I*************************************************************
     I           UDS
     I                                        1  10 X#GEPR
     I                                       11  180X#DATE
     I                                       31  380X#NO1
     I                                       41  480X#NO2
     I                                       51  580X#NO3
     I*
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 S#USID
     I                                     10211021 U#AREA
     I            DS
     I                                        1   60U#XYM
     I                                        1   10U#X1
     I                                        1   20U#X2
9806 I                                        1   6 U#CX1
     I                                        3   6 U#CX2
     C**************************************************************
     C*   程式參數接收區
     C**************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P#IN03  1        結束旗標
     C**************************************************************
     C*   檔案搜尋欄位組合
     C**************************************************************
     C           K#GE      KLIST                           編號檔
     C                     KFLD           GEKIND
     C                     KFLD           GEPRIN
     C**************************************************************
     C*   主程式開始
     C**************************************************************
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C*   副程式開始
     C**************************************************************
     C*----------------------------------------
     C*  宣告及初始變數
     C*----------------------------------------
     CSR         SR0000    BEGSR
     C                     MOVEL'01'      W#PRID  2
     C                     EXSR SR1100                     初始畫面一
     C                     SETON                     52
     C*
     C                     Z-ADD0         X#NO1
     C                     Z-ADD0         X#NO2
     C                     Z-ADD0         X#NO3
     CSR                   ENDSR
     C*
     C************************************
     C*  畫面一:輸入廠區及發票開立號碼
     C************************************
     CSR         SR1000    BEGSR
     C                     EXFMTAR065F1
     C                     MOVEL*BLANK    S#MSG1
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C           *IN12     OREQ '1'
     C                     MOVEL'1'       P#IN03
     C                     MOVEL'00'      W#PRID
     C*
     C           *IN10     WHEQ '1'
     C                     EXSR SR1300                     存檔執行
     C*
     C                     OTHER
     C                     EXSR SR1200                     檢核
     C                     ENDSL
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  初始畫面一
     C*----------------------------------------
     CSR         SR1100    BEGSR
     C                     MOVELU#AREA    S#AREA
     C*
     C                     MOVEL'99'      GEKIND           已開日期
     C                     MOVELS#AREA    GEPRIN
     C           K#GE      CHAINGEREC               N69
     C                     Z-ADDGECUNO    S#XDAT
     C                     Z-ADDS#XDAT    S#DATE
     C*
     C                     EXSR SR1110                     設欄值
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一：設編號欄值並檢核之
     C*----------------------------------------
     CSR         SR1110    BEGSR
     C                     MOVEL'T'       W#RTNV
     C                     SETOF                     6263
     C*
     C                     MOVEL*BLANK    GEPRIN
     C           S#DATE    DIV  100       U#XYM            設GEPRIN
9806 C*          U#X2      IFEQ 0
 .   C*          S#AREA    CAT  U#CX2:0   GEPRIN
9806 C*                    ELSE
     C           S#AREA    CAT  U#CX1:0   GEPRIN
9806 C*                    ENDIF
     C*
     C                     MOVEL'01'      GEKIND           二聯式
     C           K#GE      CHAINGEREC               N69
     C           *IN69     IFEQ '1'
     C                     MOVEL'F'       W#RTNV
     C                     SETON                     5253
     C                     SETON                     62
     C                     ELSE
     C                     MOVELGEPRE     S#PRE2
     C                     Z-ADDGESTNO    S#STN2
     C                     Z-ADDGEENNO    S#ENN2
     C                     Z-ADDGECUNO    S#CUN2
     C           S#ENN2    SUB  S#CUN2    S#LEV2
     C                     ENDIF
     C*
     C                     MOVEL'02'      GEKIND           三聯式
     C           K#GE      CHAINGEREC               N69
     C           *IN69     IFEQ '1'
     C                     MOVEL'F'       W#RTNV
     C                     SETON                     5253
     C                     SETON                     63
     C                     ELSE
     C                     MOVELGEPRE     S#PRE3
     C                     Z-ADDGESTNO    S#STN3
     C                     Z-ADDGEENNO    S#ENN3
     C                     Z-ADDGECUNO    S#CUN3
     C           S#ENN3    SUB  S#CUN3    S#LEV3
     C                     ENDIF
     C*
     C   62N63             MOVELT#MSG,5   S#MSG1
     C  N62 63             MOVELT#MSG,6   S#MSG1
     C   62 63             MOVELT#MSG,7   S#MSG1
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一：檢核
     C*----------------------------------------
     CSR         SR1200    BEGSR
     C                     MOVEL'F'       W#RTNV  1
     C                     SETOF                     5153
     C*
     C           S#AREA    LOKUPT#TB1                    69
     C           *IN69     IFEQ '0'
     C                     MOVELT#MSG,8   S#MSG1           廠區錯誤
     C                     SETON                     51
     C                     GOTO ES1200
     C                     ENDIF
     C*
     C                     MOVEL'99'      GEKIND           已開日期
     C                     MOVEL*BLANK    GEPRIN
     C                     MOVELS#AREA    GEPRIN
     C           K#GE      CHAINGEREC               N69
     C                     Z-ADDGECUNO    S#XDAT
     C*                                                    檢核日期
     C                     MOVE S#DATE    P#DATE  8
     C                     CALL 'UTS104R'
     C                     PARM           P#DATE
     C                     PARM '1'       P#MOD1  1
     C                     PARM '2'       P#MOD2  1
     C                     PARM '0000'    P#DAYS  4
     C                     PARM *BLANK    P#RTND  8
     C                     PARM *BLANK    P#ERRC  1
     C*
     C           P#ERRC    IFNE '0'
     C                     MOVELT#MSG,1   S#MSG1           日期錯誤
     C                     SETON                     5253
     C                     GOTO ES1200
     C                     ENDIF
     C*
     C           S#DATE    IFGT UDATE
     C                     MOVELT#MSG,2   S#MSG1           大於系統日
     C                     SETON                     5253
     C                     GOTO ES1200
     C                     ENDIF
     C*
     C                     Z-ADDUDATE     W#LMDT  80       找上月底
     C                     MOVE '01'      W#LMDT
     C                     MOVE W#LMDT    P#DATE
     C                     CALL 'UTS104R'
     C                     PARM           P#DATE
     C                     PARM '1'       P#MOD1
     C                     PARM '1'       P#MOD2
     C                     PARM '0001'    P#DAYS
     C                     PARM *BLANK    P#RTND
     C                     PARM *BLANK    P#ERRC
     C                     MOVE P#RTND    W#LMDT           上月底
     C*
     C           S#DATE    IFLT S#XDAT
     C           S#DATE    ANDNEW#LMDT
     C                     MOVELT#MSG,3   S#MSG1           小於已開日
     C                     SETON                     5253
     C                     GOTO ES1200
     C                     ENDIF
     C*
     C                     Z-ADDUDATE     W#CRDT  80
     C                     MOVE '03'      W#CRDT           本月三日
     C           S#DATE    IFLT S#XDAT
     C           S#DATE    ANDEQW#LMDT                     上月底
     C           UDATE     ANDGTW#CRDT
     C                     MOVELT#MSG,4   S#MSG1
     C                     SETON                     5253
     C                     GOTO ES1200
     C                     ENDIF
     C*
     C                     EXSR SR1110                     設定欄值
     CSR         ES1200    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一：存檔執行
     C*----------------------------------------
     CSR         SR1300    BEGSR
     C                     EXSR SR1200
     C           W#RTNV    IFEQ 'F'
     C                     GOTO ES1300
     C                     ENDIF
     C*
     C                     MOVELGEPRIN    X#GEPR           存參數
     C                     MOVE S#DATE    X#DATE
     C                     MOVEL'0'       P#IN03
     C*
     C           S#DATE    IFGE S#XDAT                     非上月最後
     C                     MOVEL'99'      GEKIND           已開日期
     C                     MOVEL*BLANK    GEPRIN
     C                     MOVELS#AREA    GEPRIN
     C           K#GE      CHAINGEREC                69
     C                     Z-ADDS#DATE    GECUNO
     C                     UPDATGEREC                      存檔
     C                     ENDIF
     C*
     C                     MOVEL'00'      W#PRID           結束
     CSR         ES1300    ENDSR
     C*
     C**************************************************************
** T#TB1
P
M
T
H
K
** T#MSG
０１日期錯誤！
０２日期不可大於系統日期！
０３日期不可小於已開立日期！
０４本月已超過五號，不可重開上月底的發票！
０５該年月之二聯式字軌尚未輸入！
０６該年月之三聯式字軌尚未輸入！
０７該年月之字軌尚未輸入！
０８輸入之廠區不合法！
