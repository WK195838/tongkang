     H        1   Y                                     1                 BR003
     FARQ004S CF  E                    WORKSTN
     F                                        RRN   KSFILE SF1
     FCURCPRL1IF  E           K        DISK
     E*************************************************************
     E                    ERR     1   3 70
     I*************************************************************
     IAADS        DS
     I                                       21  260YMD
     I                                       21  220YY
     I                                       23  240MM
     I                                       25  260DD
     IBBDS        DS
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           CUKEY     KLIST
     C                     KFLD           CUDATE
     C********************************
     C*****                     ******
     C*****  INITIAL VALUES     ******
     C*****                     ******
     C********************************
     C  N90                MOVE '1'       SCRN    1
     C  N90                MOVE '1'       *IN,90
     C********************************
     C*****                     ******
     C*****     MAIN LOGIC      ******
     C*****                     ******
     C********************************
     C           *IN03     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C********************************
     C*****                     ******
     C***** SCREEN 01 MAIN CTRL ******
     C*****                     ******
     C********************************
     CSR         SR#01     BEGSR
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C   KC                GOTO SR#01Z
     C                     EXSR CK#01
     C  N99                EXSR PR#02
     C  N99                MOVE '2'       SCRN
     C   99                MOVE '1'       SCRN
     CSR         SR#01Z    ENDSR
     C********************************
     C*****                     ******
     C***** SCREEN 02 MAIN CTRL ******
     C*****                     ******
     C********************************
     CSR         SR#02     BEGSR
     C                     WRITEUND01
     C                     EXFMTSFCTL1
     C   KC                EXSR KC#01
     C   KC                GOTO SR#02Z
     C   KL                EXSR KL#02
     C   KL                GOTO SR#02Z
     CSR         SR#02Z    ENDSR
     C********************************
     C*****                     ******
     C*****   CF03 EXIT         ******
     C*****                     ******
     C********************************
     CSR         KC#01     BEGSR
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C********************************
     C*****                     ******
     C*****   CF12 PREVIOUS     ******
     C*****                     ******
     C********************************
     CSR         KL#02     BEGSR
     C                     MOVE *BLANK    ERRMSG
     C                     MOVE '1'       SCRN
     CSR         KL#02Z    ENDSR
     C********************************
     C*****                     ******
     C*****   SCREEN 01 CKECK   ******
     C*****                     ******
     C********************************
     CSR         CK#01     BEGSR
     C                     SETOF                     414299
     C                     SETOF                     434044
     C                     MOVE *BLANK    ERRMSG
      **
     C           DATE1     IFEQ 0
     C                     Z-ADDUDATE     DATE1
     C                     ENDIF
     C**
     C*                    CALL 'C01'
     C*                    PARM           DATE1
     C*                    PARM           FLAGC   1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DATE1     P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM ' '       FLAGC   1
     C           FLAGC     IFNE '0'
     C                     SETON                     9941
     C                     MOVELERR,1     ERRMSG
     C                     ENDIF
     C**
     C           AREA      IFNE ' '
     C           AREA      ANDNE'P'
     C           AREA      ANDNE'T'
     C           AREA      ANDNE'K'
     C           AREA      ANDNE'M'
     C           AREA      ANDNE'H'
     C                     SETON                     9942
     C                     MOVELERR,2     ERRMSG
     C                     ENDIF
     C****
     C           RVID2     IFEQ *BLANK
     C                     MOVEL'99'      RVID2
     C                     ENDIF
     C****
     C           RVID1     IFGT RVID2
     C                     SETON                     994344
     C                     MOVELERR,3     ERRMSG
     C                     ENDIF
     CSR         CK#01Z    ENDSR
     C********************************
     C*****                      *****
     C*****   SCREEN 02 PREPARE  *****
     C*****                      *****
     C********************************
     CSR         PR#02     BEGSR
     C                     MOVE *BLANK    ERRMSG
     C                     SETON                     80
     C                     WRITESFCTL1
     C                     SETOF                     801051
     C                     SETOF                     97
     C                     Z-ADD0         AMT
     C                     Z-ADD0         RRN     30
     C                     Z-ADDDATE1     CUDATE
     C           CUKEY     SETLLCURCPRL1             97
     C           *IN97     DOWEQ'0'
     C           CUKEY     READECURCPRL1                 97
     C   97                LEAVE
     C           AREA      IFNE *BLANK
     C           CUAREA    ANDNEAREA
     C                     ITER
     C                     ENDIF
     C           CURVID    IFLT RVID1
     C           CURVID    ORGT RVID2
     C                     ITER
     C                     ENDIF
     C                     MOVELCUCUNO    W01
     C                     MOVELCUCUNM    W02
     C                     MOVELCUORNO    W03
     C                     MOVELCURVID    W04
     C                     Z-ADDCURAMT    W05
     C                     Z-ADDCULAMT    W051
     C                     MOVELCUFL01    W06
     C                     MOVELCUFL02    W07
     C                     ADD  1         RRN
     C                     WRITESF1
     C                     ADD  CURAMT    AMT
     C                     ADD  CULAMT    AMT1
     C                     ENDDO
     C           RRN       IFEQ 0
     C                     CLEARSF1
     C                     MOVEL'END DATA'W02
     C                     ADD  1         RRN
     C                     SETON                     51
     C                     WRITESF1
     C                     SETOF                     51
     C                     ENDIF
     C                     SETON                     10
     CSR         PR#02Z    ENDSR
      ******************************* END PROGRAM *****************
** ERR
日期不合理
地區別錯誤
業務員範圍錯誤
