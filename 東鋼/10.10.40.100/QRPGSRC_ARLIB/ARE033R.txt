     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARE033R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     應收例外處理登錄程式
     H*            5.DATE-WRITTEN  86/02/20
     H*            6.UPDATE  DATE  99/08/23  2010AR517  S00WCJ (9908A)
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARE033S CF  E                    WORKSTN
     FARALOG  O   E           K        DISK
     E*************************************************************
     E                    T#ERR   1   1 70
     I*************************************************************
     I           UDS
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     I            DS
     I                                        1  140W#SYST
     I                                        1   60D#ST
     I                                        7  100D#SY
     I                                       11  140D#SMD
     C**************************************************************
     C*   程式參數接收區
     C**************************************************************
     C           *ENTRY    PLIST
     C                     PARM           W#TRID  3        異動代碼
     C                     PARM           W#PGID 10        相關號碼
     C                     PARM           W#RTNV  1        返回值
     C**************************************************************
     C*   主程式開始
     C**************************************************************
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C*   副程式開始
     C**************************************************************
     C*----------------------------------------
     C*  宣告及初始變數
     C*----------------------------------------
     CSR         SR0000    BEGSR
     C                     MOVELW#PGID    S#PGID
     C                     MOVEL'01'      W#PRID  2
     CSR                   ENDSR
     C*
     C************************************
     C*  畫面一:輸入密碼
     C************************************
     CSR         SR1000    BEGSR
     C                     WRITEAR033F1M
     C                     EXFMTAR033F1
     C                     MOVEL'F'       W#RTNV           返回值為假
     C                     MOVEL'00'      W#PRID           結束畫面
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C           *IN12     WHEQ '1'
     C                     OTHER                           執行鍵
     C           S#CGCA    IFEQ *BLANK
     C                     MOVELT#ERR,1   S#MSG1
     C                     MOVEL'01'      W#PRID
     C*
     C                     ELSE
     C           1         DOWEQ1
     C                     EXFMTAR033F1N
     C           *IN03     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     LEAVE
     C                     ENDIF
     C*
     C           *IN10     IFEQ '1'                        再確認一次
     C                     MOVEL'T'       W#RTNV
     C                     CLEARRARALOG
9908AC*                    Z-ADDUDATE     AGDATE           日期
9908AC           *DATE     SUB  19000000  AGDATE           日期
     C                     TIME           W#SYST
     C                     Z-ADDD#ST      AGTIME           時間
     C                     MOVELU#USID    AGURID           使用者
     C                     MOVELW#TRID    AGTRID           異動代號
     C                     MOVELW#PGID    AGRENO           相關號碼
     C                     MOVELS#CGCA    AGCGCA           修改原因
     C                     WRITERARALOG
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C                     ENDIF
     C*
     C                     ENDSL
     CSR                   ENDSR
     C*
     C**************************************************************
** T#ERR
０１－異動原因必須輸入
