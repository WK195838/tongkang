     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR079RS
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     應收帳款客戶帳齡分析表－畫面
     H*            5.DATE-WRITTEN  88/07/20
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR079S CF  E                    WORKSTN
     E*************************************************************
     E                    T#MSG   1   3 70
     I*************************************************************
     I           UDS
     I                                        1   4 U#DAY1
     I                                        5   8 U#DAY2
     I                                        9  12 U#DAY3
     I                                       13  16 U#DAY4
     I                                       17  17 U#TYPE
     I*
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     C**************************************************************
     C*   程式參數接收區
     C**************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P#IN03  1        結束旗標
     C**************************************************************
     C*   主程式開始
     C**************************************************************
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C*   副程式開始
     C**************************************************************
     C*----------------------------------------
     C*  宣告及初始變數
     C*----------------------------------------
     CSR         SR0000    BEGSR
     C                     Z-ADD61        S#DAY1
     C                     Z-ADD91        S#DAY2
     C                     Z-ADD121       S#DAY3
     C                     Z-ADD151       S#DAY4
     C                     MOVEL'1'       S#TYPE
     C*
     C                     MOVEL'01'      W#PRID  2
     CSR                   ENDSR
     C*
     C*
     C************************************
     C*  畫面一:選擇要列印的營業員
     C************************************
     CSR         SR1000    BEGSR
     C                     EXFMTAR079F1                    螢幕輸入
     C*
     C                     SETOF                     515253
     C                     MOVEL*BLANK    S#MSG1
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C           *IN12     OREQ '1'
     C                     MOVEL'00'      W#PRID
     C                     MOVEL'1'       P#IN03
     C*
     C                     OTHER
     C                     EXSR SR1100                     檢核
     C                     ENDSL
     CSR                   ENDSR
     C*
     C*-------------------------------
     C*  畫面一：執行檢核
     C*-------------------------------
     CSR         SR1100    BEGSR
     C           S#DAY1    IFGE S#DAY2
     C                     MOVELT#MSG,1   S#MSG1
     C                     SETON                     51
     C                     GOTO ES1100
     C                     ENDIF
     C*
     C           S#DAY2    IFGE S#DAY3
     C                     MOVELT#MSG,2   S#MSG1
     C                     SETON                     52
     C                     GOTO ES1100
     C                     ENDIF
     C*
     C           S#DAY3    IFGE S#DAY4
     C                     MOVELT#MSG,3   S#MSG1
     C                     SETON                     53
     C                     GOTO ES1100
     C                     ENDIF
     C*
     C                     MOVE S#DAY1    U#DAY1
     C                     MOVE S#DAY2    U#DAY2
     C                     MOVE S#DAY3    U#DAY3
     C                     MOVE S#DAY4    U#DAY4
     C                     MOVE S#TYPE    U#TYPE
     C                     MOVEL'0'       P#IN03
     C                     MOVEL'00'      W#PRID           存檔結束
     CSR         ES1100    ENDSR
     C*
     C*****************************************************************
** T#MSG
０１－統計區間日數２不得小於統計區間日數１。
０２－統計區間日數３不得小於統計區間日數２。
０３－統計區間日數４不得小於統計區間日數３。
