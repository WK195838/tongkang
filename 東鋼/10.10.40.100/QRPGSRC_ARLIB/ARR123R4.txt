     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR123R4
     H*            2.PROGRAM-TYPE  RPG
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     客戶銷售排行榜（財會用）－折扣
     H*            5.DATE-WRITTEN  95/01/11
     H*            6.DATE-MODIFY
     H*
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FINVMST  IP  E           K        DISK
     FINVDTL  IF  E           K        DISK
     FARR123F UF  E           K        DISK                      A
     E*****************************************************************
     IINREC
     I                                              INCUNML1
     I           UDS
     I                                      300 3050U#YM1
     I                                      306 3110U#YM2
     I                                      312 3140U#RAK1
     I                                      315 3170U#RAK2
     I                                      318 3280U#AMT
     I                                     10011010 U#USID
     C*****************************************************************
     C*             KEY   LIST
     C*****************************************************************
     C*FILE==>INVDTL(發票明細檔)
     C           K#INVD    KLIST
     C                     KFLD           IVNO
     C                     KFLD           IVACNT
     C                     KFLD           IVITEM
     C*****************************************************************
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     MOVE *ON       *IN99
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     Z-ADD0         W#QTY4 120       折扣數量
     C                     Z-ADD0         W#AMT4 120       折扣金額
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     MOVELINNO      IVNO
     C                     MOVEL'3'       IVACNT
     C                     Z-ADD0         IVITEM
     C           K#INVD    SETLLIVREC
     C                     MOVE *OFF      *IN40
     C           *IN40     DOWEQ*OFF
     C                     READ IVREC                    40
     C   40                LEAVE
     C*
     C           IVNO      IFNE INNO
     C           IVACNT    ORNE '3'
     C                     LEAVE
     C                     ENDIF
     C*
     C           IVDECD    IFNE *BLANK
     C                     ITER
     C                     ENDIF
     C*
     C           IVFL03    IFEQ 'K'
     C                     ADD  IVQTY     W#QTY4
     C                     ADD  IVAMT     W#AMT4
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C           INCUNM    CHAINRAR123F              41
     C           *IN41     IFEQ *ON
     C                     CLEARRAR123F
     C                     MOVELINCUNO    A1CUNO
     C                     MOVELINCUNM    A1CUNM
     C                     Z-ADDW#QTY4    A1QTY4
     C                     Z-ADDW#AMT4    A1AMT4
     C                     WRITERAR123F
     C                     ELSE
     C                     Z-ADDW#QTY4    A1QTY4
     C                     Z-ADDW#AMT4    A1AMT4
     C                     UPDATRAR123F
     C                     ENDIF
     C*
     C                     ENDSR
