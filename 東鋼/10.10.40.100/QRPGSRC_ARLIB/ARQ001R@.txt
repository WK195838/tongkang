     H        1   Y                                     1                 BR003
     FARQ001S CF  E                    WORKSTN
     F                                        RRN   KSFILE SF1
     F                                        RRN2  KSFILE SFLREC
     FINVMSTL2IF  E           K        DISK
     FINVDTL  IF  E           K        DISK
     E*************************************************************
     E                    ERR     1   8 70
     I*************************************************************
     IAADS        DS
     I                                       21  260YMD
     I                                       21  220YY
     I                                       23  240MM
     I                                       25  260DD
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           INKEY     KLIST
     C                     KFLD           INORNO
     C                     KFLD           INNO
     C           IVKEY     KLIST
     C                     KFLD           IVNO
     C                     KFLD           IVACNT
     C                     KFLD           IVITEM
     C********************************
     C*****                     ******
     C*****  INITIAL VALUES     ******
     C*****                     ******
     C********************************
     C  N90                MOVE *BLANK    W0101
     C  N90                MOVE *BLANK    W0102
     C  N90                MOVE *ALL'9'   W0103
     C  N90                Z-ADD1         NBR
     C  N90                MOVE '1'       SCRN    1
     C  N90                MOVE '1'       *IN,90
     C********************************
     C*****                     ******
     C*****     MAIN LOGIC      ******
     C*****                     ******
     C********************************
     C           *IN03     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
     C           SCRN      CASEQ'3'       SR#03
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C********************************
     C*****                     ******
     C***** SCREEN 01 MAIN CTRL ******
     C*****                     ******
     C********************************
     CSR         SR#01     BEGSR
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C   KC                GOTO SR#01Z
     C                     EXSR CK#01
     C  N99                EXSR PR#02
     C  N99                MOVE '2'       SCRN
     C   99                MOVE '1'       SCRN
     CSR         SR#01Z    ENDSR
     C********************************
     C*****                     ******
     C***** SCREEN 02 MAIN CTRL ******
     C*****                     ******
     C********************************
     CSR         SR#02     BEGSR
     C                     WRITEUND01
     C                     EXFMTSFCTL1
     C   KC                EXSR KC#01
     C   KC                GOTO SR#02Z
     C   KL                EXSR KL#02
     C   KL                GOTO SR#02Z
     C                     EXSR IUD02
     C  N99                MOVE '3'       SCRN
     C   99                MOVE '2'       SCRN
     CSR         SR#02Z    ENDSR
     C********************************
     C*****                     ******
     C***** SCREEN 03 MAIN CTRL ******
     C*****                     ******
     C********************************
     CSR         SR#03     BEGSR
     C                     WRITEERRFMT
     C                     EXFMTSFLCTL
     C   KC                EXSR KC#01
     C   KC                GOTO SR#03Z
     C   KL                EXSR KL#03
     C   KL                GOTO SR#03Z
     C                     EXSR IUD02
     C  N99                MOVE '3'       SCRN
     C   99                MOVE '2'       SCRN
     CSR         SR#03Z    ENDSR
     C********************************
     C*****                     ******
     C*****   ACTION            ******
     C*****                     ******
     C********************************
     CSR         IUD02     BEGSR
     C                     SETOF                     99
     C           *IN99     DOWNE'1'
     C                     READCSF1                      99
     C   99                LEAVE
     C           ACT       IFEQ *BLANK
     C                     ITER
     C                     ENDIF
     C                     MOVE *BLANK    ACT
     C                     Z-ADDRRN       NBR
     C                     UPDATSF1
     C                     EXSR PR#03
     C                     LEAVE
     C                     ENDDO
     CSR         IUD02Z    ENDSR
     C********************************
     C*****                     ******
     C*****   CF03 EXIT         ******
     C*****                     ******
     C********************************
     CSR         KC#01     BEGSR
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C********************************
     C*****                     ******
     C*****   CF12 PREVIOUS     ******
     C*****                     ******
     C********************************
     CSR         KL#02     BEGSR
     C                     MOVE *BLANK    ERRMSG
     C                     MOVE '1'       SCRN
     C                     Z-ADD1         NBR
     CSR         KL#02Z    ENDSR
     C********************************
     C*****                     ******
     C*****   CF12 PREVIOUS     ******
     C*****                     ******
     C********************************
     CSR         KL#03     BEGSR
     C                     MOVE *BLANK    ERRMSG
     C                     MOVE '2'       SCRN
     CSR         KL#03Z    ENDSR
     C********************************
     C*****                     ******
     C*****   SCREEN 01 CKECK   ******
     C*****                     ******
     C********************************
     CSR         CK#01     BEGSR
     C                     SETOF                     414299
     C                     SETOF                     434044
     C                     MOVE *BLANK    ERRMSG
      **
     C                     MOVELW0101     INORNO
     C                     MOVELW0102     INNO
     C           INKEY     SETLLINVMSTL2             31
     C  N31      W0101     READEINVMSTL2                 31
     C  N31      INNO      COMP W0103                31
     C   31                MOVELERR,1     ERRMSG
     C   31                SETON                     4199
      **
     C           W0103     IFEQ *BLANK
     C                     MOVE *ALL'9'   W0103
     C                     ENDIF
      **
     C           W0102     IFGT W0103
     C                     SETON                     4299
     C                     MOVELERR,2     ERRMSG
     C                     ENDIF
     CSR         CK#01Z    ENDSR
     C********************************
     C*****                      *****
     C*****   SCREEN 02 PREPARE  *****
     C*****                      *****
     C********************************
     CSR         PR#02     BEGSR
     C                     Z-ADD0         T03
     C                     Z-ADD0         T04
     C                     Z-ADD0         T05
     C                     Z-ADD0         T06
     C                     Z-ADD0         T07
     C                     MOVE *BLANK    ERRMSG
     C                     SETON                     80
     C                     WRITESFCTL1
     C                     SETOF                     8010
     C                     SETOF                     97
CLJ  C                     Z-ADD0         RRN     40
     C                     MOVELW0101     INORNO
     C                     MOVELW0102     INNO
     C           INKEY     SETLLINVMSTL2             97
     C           *IN97     DOWEQ'0'
     C           W0101     READEINVMSTL2                 97
     C  N97      INNO      COMP W0103                97
     C   97                LEAVE
     C           INTYPE    COMP '1'                  3131
     C   31      INTYPE    COMP '2'                  3131
     C   31      INTYPE    COMP '3'                  3131
     C   31                ITER
     C                     MOVELINNO      W01
     C                     Z-ADDININDT    W02
     C                     Z-ADDINCAMT    W04
     C           INTYPE    IFEQ '2'
     C                     Z-ADD0         W03
 9403C*          INKIND    IFEQ '2'
 9403C*          INAAMT    DIV  1.05      W05       H
 9403C*                    ELSE
     C                     Z-ADDINAAMT    W05
 9403C*                    ENDIF
     C                     ELSE
     C                     Z-ADDINBAMT    W05
     C                     ADD  INDAMT    W05
     C                     Z-ADDINAAMT    W03
     C                     ENDIF
     C           INATAX    ADD  INCTAX    W06
     C                     ADD  INDTAX    W06
     C                     Z-ADDINNBAL    W07
      *
     C           INDECD    IFEQ 'D'
     C                     MOVEL'V'       W08
     C                     ELSE
     C                     ADD  W03       T03
     C                     ADD  W04       T04
     C                     ADD  W05       T05
     C                     ADD  W06       T06
     C                     ADD  W07       T07
     C                     MOVEL' '       W08
     C                     ENDIF
      *
     C                     ADD  1         RRN
     C                     WRITESF1
     C                     ENDDO
     C           RRN       IFEQ 0
     C                     CLEARSF1
     C                     MOVEL'END DATA'W01
     C                     ADD  1         RRN
     C                     WRITESF1
     C                     ENDIF
     C                     SETON                     10
     CSR         PR#02Z    ENDSR
     C********************************
     C*****                      *****
     C*****   SCREEN 03 PREPARE  *****
     C*****                      *****
     C********************************
     CSR         PR#03     BEGSR
     C                     SETON                     80
     C                     WRITESFLCTL
     C                     SETOF                     8010
     C                     Z-ADD0         TTLAMT
 CLJ C                     Z-ADD0         RRN2    40
     C                     MOVELW0101     INORNO
     C                     MOVELW01       INNO
     C           INKEY     CHAININVMSTL2             31
     C   31                CLEARINREC
     C                     MOVELINAPNO    APNO1
     C                     MOVELINCUNO    CUNO
     C                     MOVELINCUNM    CUNM
     C                     MOVELINSATP    TYPE
     C                     MOVELINORNO    ORNO
     C                     MOVELINSALE    SALE
     C                     MOVELINKIND    CHK
     C                     MOVELINRVID    RVID
     C                     MOVELINTYPE    TYP1
     C                     MOVELINAREA    AREA
     C           INDECD    COMP 'D'                      74
     C**
     C                     MOVELW01       IVNO
     C                     MOVE *BLANK    IVACNT
     C                     Z-ADD0         IVITEM
     C           IVKEY     SETLLINVDTL               97
     C           RRN2      DOWLT190
     C           *IN97     ANDEQ'0'
     C           W01       READEINVDTL                   97
     C   97                LEAVE
     C           IVFLAG    IFNE 'D'
     C                     ADD  1         RRN2
     C                     Z-ADDIVITEM    ITEM
     C                     MOVELIVACNT    ACNT
     C                     MOVELIVPDCD    PDNM
     C                     Z-ADDIVQTY     QTY
     C                     Z-ADDIVUPRC    UPRC
     C                     Z-ADDIVAMT     AMT
     C                     Z-ADDIVACDT    ACDT
     C                     MOVELIVAPNO    APNO
     C                     MOVELIVACNO    ACNO
 9201C                     MOVELIVDECD    DECD
     C           IVDECD    IFNE 'D'
     C                     ADD  IVAMT     TTLAMT
     C                     ENDIF
     C                     WRITESFLREC
     C                     ENDIF
     C                     ENDDO
     C***
     C           RRN2      IFEQ 0
     C                     ADD  1         RRN2
     C                     Z-ADDRRN2      ITEM
     C                     MOVEL*BLANK    ACNT
     C                     MOVEL*BLANK    PDNM
     C                     MOVEL'END DATA'PDNM
     C                     Z-ADD0         QTY
     C                     Z-ADD0         UPRC
     C                     Z-ADD0         AMT
     C                     Z-ADD0         ACDT
     C                     MOVEL*BLANK    APNO
     C                     MOVEL*BLANK    ACNO
     C                     WRITESFLREC
     C                     ENDIF
     C                     SETON                     10
     CSR         PR#03Z    ENDSR
     C******
      ******************************* END PROGRAM *****************
** ERR
無符合資料
發票範圍錯誤
