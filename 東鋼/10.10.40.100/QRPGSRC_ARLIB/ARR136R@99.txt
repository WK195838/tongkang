     H*****************************************************************
     H*
     H*            *****   PROGRAM INTRODUCTION   *****
     H*
     H*            1.PROGRAM-ID    ARR136R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION     銷貨發票開立方式一覽表
     H*            5.DATE-WRITTEN  98/12/18  2009OD64
     H*            6.UPDATE-DATE
     H*
     H*****************************************************************
     FTRNDTLN IP  E           K        DISK
     FSAMAST  IF  E           K        DISK
     FARR136T O   E             60     PRINTER
     E                    TAB1    3   3  2   TAB2    6   *開立方式
     ITNREC
     I                                              TNMTHDL3
     I                                              TNSALEL2
     I                                              TNCUNOL1
     I            DS
     I                                        1   6 TNORN5
     I                                        1   1 D#AREA
     I                                        2   60D#ORNO
     I           UDS
     I                                        1   80R#DATS
     I                                        9  160R#DATE
     I                                       19  19 S#INVS
     I                                      951 985 COMP
     I                                     10011010 U#USID
     I                                     10111020 DEVNM
     C*
     C*****************************************************************
     C*          KEY       LIST
     C*****************************************************************
     C           KEY01     KLIST
     C                     KFLD           D#AREA
     C                     KFLD           D#ORNO
     C*****************************************************************
     C*          Main      Program
     C*****************************************************************
     C           W#DATA    IFEQ 'Y'
     C   L2 10             WRITEAR136T1M
     C                     ENDIF
     C  N10                EXSR SR0000
     C   L1                MOVEL*ON       *IN53
     C   L2                MOVEL*ON       *IN52
     C   L3                MOVEL*ON       *IN51
     C                     EXSR SR1000
     C   60                EXSR SR2000
     CLR                   EXSR SR3000
     C*****************************************************************
     C           SR0000    BEGSR
     C*****************************************************************
     C* 印表頭
     C                     MOVEL*BLANKS   W#ORN5
     C                     MOVEL'N'       W#DATA
     C                     MOVEL*ON       *IN10
     C                     WRITEAR136T1H
     C                     MOVEL*OFF      *IN60
     C                     ENDSR
     C*****************************************************************
     C           SR1000    BEGSR
     C*****************************************************************
     C*
     C           S#INVS    IFEQ '1'                        *隨車
     C           TNMTHD    ANDNE'00'
     C                     GOTO END100
     C                     ENDIF
     C*
     C           S#INVS    IFEQ '2'                        *區間
     C           TNMTHD    ANDNE'03'
     C                     GOTO END100
     C                     ENDIF
     C*
     C           S#INVS    IFEQ '3'                        *指定
     C           TNMTHD    ANDNE'04'
     C                     GOTO END100
     C                     ENDIF
     C*
     C           S#INVS    IFEQ *BLANKS
     C           TNMTHD    ANDEQ'01'                       *每日隨訂
     C           S#INVS    OREQ *BLANKS
     C           TNMTHD    ANDEQ'02'                       *每月隨訂
     C           S#INVS    OREQ *BLANKS
     C           TNMTHD    ANDEQ'05'                       *人工開立
     C                     GOTO END100
     C                     ENDIF
     C*
     C           TNORN5    IFEQ W#ORN5
     C                     GOTO END100
     C                     ENDIF
     C*
     C           TNMTHD    LOKUPTAB1      TAB2           50*EQ
     C   50                MOVELTAB2      R#MTHD
     C  N50                MOVEL*ALL'*'   R#MTHD
     C                     MOVELTNSALE    R#SALE
     C                     MOVELTNCUNO    R#CUNO
     C                     MOVELTNCUNM    R#CUNM
     C                     MOVELTNORN5    R#ORNO
     C           KEY01     CHAINSAMAST               69
     C     N69             MOVELS1CTNO    R#CTNO
     C                     MOVELTNTXAR    R#VREA
     C                     WRITEAR136T1D
     C                     MOVELTNORN5    W#ORN5  6
     C                     MOVEL*OFF      *IN51
     C                     MOVEL*OFF      *IN52
     C                     MOVEL*OFF      *IN53
     C                     MOVEL'Y'       W#DATA  1
     C           END100    ENDSR
     C*****************************************************************
     C           SR2000    BEGSR
     C*****************************************************************
     C*
     C                     WRITEAR136T1X
     C                     WRITEAR136T1H
     C                     SETOF                     60
     C                     MOVEL*ON       *IN51
     C                     MOVEL*ON       *IN52
     C                     MOVEL*ON       *IN53
     C                     ENDSR
     C*
     C*****************************************************************
     C           SR3000    BEGSR
     C*****************************************************************
     C*
     C                     WRITEAR136T1T
     C                     ENDSR
**  TAB1 TAB2
00隨車03區間04指定
