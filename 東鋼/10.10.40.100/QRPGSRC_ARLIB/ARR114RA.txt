     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR114RA
     H*            2.PROGRAM-TYPE  RPG
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     折讓單統計表（含銷折／銷退／預收）
     H*                            ***明細表***
     H*            5.DATE-WRITTEN  93/01/08
     H*            6.DATE-MODIFY
     H*
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR114F IP  E           K        DISK
     FARR114TAO   E             69     PRINTER
     E                    TABA1   1   5  1   TABA2  10
     E*****************************************************************
     IRAR114F
     I                                              A4ACNTL1
     I           UDS
     I                                      300 300 U#AREA
     I                                      301 3060U#YYMM
     I                                      307 307 U#KIND
     I                                     10011010 U#USID
     I*****************************************************************
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     SETON                     6999
     C*
     C                     MOVEL*ALL'='   T#LIN1
     C                     MOVEL*ALL'='   T#LIN2
     C*
     C                     MOVELU#AREA    T#AREA
     C                     Z-ADDU#YYMM    T#YYMM
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR114T1S
     C                     MOVE *ON       *IN69
     C                     MOVEL*BLANK    W#NO    6        折讓單號
     C                     MOVEL*BLANK    W#ORNO  6        訂單編號
     C*
     C           A4ACNT    LOKUPTABA1     TABA2          60
     C   60                MOVE TABA2     T#ACNT
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     CLEARAR114T1D
     C                     MOVELA4NO      T#NO             折讓單號
     C                     Z-ADDA4ACDT    T#ACDT           確認日期
     C                     MOVELA4CUNM    T#CUNM           客戶名稱
     C                     MOVELA4ORNO    T#ORNO           訂單編號
     C                     MOVELA4INNO    T#INNO           發票號碼
     C                     Z-ADDA4AMT     T#AMT            未稅
     C                     Z-ADDA4TAX     T#TAX            含稅
     C                     Z-ADDA4TOT     T#TOT            金額
     C*
     C*折讓單號相同則省略不印
     C           A4NO      IFEQ W#NO
     C                     MOVE *ON       *IN50
     C                     ELSE
     C                     MOVE *OFF      *IN50
     C                     MOVELA4NO      W#NO             折讓單號
     C                     ENDIF
     C*
     C   69                WRITEAR114T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR114T1D
     C*合計
     C                     ADD  T#AMT     T#AMTS           未稅
     C                     ADD  T#TAX     T#TAXS           含稅
     C                     ADD  T#TOT     T#TOTS           金額
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C           T#TOTS    IFNE 0
     C   69                WRITEAR114T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR114T1S
     C                     ENDIF
     C*總計
     C                     ADD  T#AMTS    T#AMTT           未稅
     C                     ADD  T#TAXS    T#TAXT           含稅
     C                     ADD  T#TOTS    T#TOTT           金額
     C*
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C           T#TOTT    IFNE 0
     C   69                WRITEAR114T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR114T1T
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C*
     C   69                WRITEAR114T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR114T1M
     C*
     C                     ENDSR
**  TABA1 TABA2
2銷貨退回
3銷貨折讓
4預收折讓
5扣預沖回
6預折手切
