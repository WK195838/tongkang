     H****************************************************************
     H*
     H*            UPDATE  DATE  99/09/01  2010AR517  S00WCJ (9909A)
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FRCVDTL  IP  E                    DISK
     F*ANKID  IF  E           K        DISK
     FQPRINT  O   F     158            PRINTER
      *************************************************************
     E                    TAB1    1  10  1   TABR1  10
     I*************************************************************
     IRVREC
     I                                              RVDPNOL2
     I                                              RVNO  L1
     I****
     IAADS        DS
     I              'もも收款明細表もも'C         CON3                       X
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     IBBDS        DS
     I                                        1  10 XXXX
     I                                        1   3 BAID1
     I                                        4   7 BAID2
      *
     I                                       21  260YMD
     I                                       21  240YM
     I                                       21  220YY
     I                                       23  240MM
     I                                       25  260DD
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
9909AC           *DATE     SUB  19000000  U#SYSD  80
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C           *ENTRY    PLIST
     C                     PARM           DATE1   80
     C                     PARM           DATE2   80
     C*          BKKEY     KLIST
     C*                    KFLD           BAID1
     C*                    KFLD           BAID2
      *
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD56        TCNT    30
     C                     MOVE *ALL'='   ALS   141
     C                     MOVE *ALL'-'   ALQ   121
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD8         LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         TAMT   110
     C                     Z-ADDTCNT      LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         AAMT   110
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
0110AC                     MOVEL*BLANKS   R#ACNO  6
0110AC                     MOVELRVACNO    R#ACNO
     C                     MOVE *BLANK    CHAR10 10
     C           RVNTTP    LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     CHAR10
     C  N31                MOVEL'其他'  CHAR10
      *
     C                     MOVE *BLANK    CHAR6   6
     C           RVPLAC    COMP 'D'                      31
     C   31                MOVEL'本埠'  CHAR6
     C           RVPLAC    COMP 'E'                      31
     C   31                MOVEL'外埠'  CHAR6
      *
     C           RVACTP    COMP 'C'                      31
     C   31                MOVEL'C'       DC      1
     C   31                SUB  RVRAMT    AAMT
     C   31                SUB  RVRAMT    TAMT
     C  N31                MOVEL'D'       DC
     C  N31                ADD  RVRAMT    AAMT
     C  N31                ADD  RVRAMT    TAMT
      *
     C                     Z-ADDRVRAMT    DEC9   100
      *
     C                     MOVEL*BLANK    R#NTNO 20
     C                     MOVELRVNTNO    R#NTNO
      *
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#01
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C                     EXCPTET#L1
     C                     ADD  3         LCNT
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C                     EXCPTET#L2
     C                     ADD  3         LCNT
     CSR         TL#L2Z    ENDSR
      ***********************************************************
      ****   DEFINE OUPUT FORMAT                             ****
      ***********************************************************
     OQPRINT  E   02           EH#01
     O                                   18 '報表編號: ARR041'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
9909AO                         U#SYSDZ  132
     O        E 1              EH#01
     O                         CON3      71                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 2              EH#01
     O                                   11 '繳款部門:'
     O                         RVDPNO  +  1
LYW  O                                   58 '繳款確認日期:'
     O                         DATE1   +  1 '    /  /  '
     O                                 +  1 '-'
     O                         DATE2   +  1 '    /  /  '
     O        E 1              EH#01
     O                         ALS      142
     O        E 1              EH#01
     O                                   10 '繳款單號'
     O                                   20 '客戶編號'
     O                                   30 '客戶名稱'
     O                                   38 '款項別'
     O                                   46 '票款別'
     O                                   58 '會計科目'
     O                                   68 '付款銀行'
     O                                   82 '付款帳號'
     O                                   93 '票據號碼'
     O                                  115 '到期日期'
     O                                  123 '付款地'
     O                                  137 '金　額'
     O                                  143 '借貸'
     O        E 1              EH#01
     O                         ALS      142
     O        E 1              ED#01
     O                 L1      RVNO       7
     O                 L1      RVCUNO    17
     O                 L1      RVCUNM    30
     O                         RVUSTP    36
     O                         CHAR10    48
0110AO*                        RVACNO    55
0110AO                         R#ACNO    55
     O                         RVPBID    72
     O                         RVPANO    83
     O                         R#NTNO   104
9008 O                         RVDUDT   114 '    /  /  '
     O                         CHAR6    122
     O                         DEC9  J  137
     O                         DC       141
     O        E 1              ET#L1
     O                         ALQ      142
     O        E 1              ET#L1
     O                                  107 '繳款單小計:'
     O                         AAMT  J  137
     O        E 1              ET#L1
     O        E 1              ET#L2
     O                         ALS      142
     O        E 1              ET#L2
     O                                  107 '部門合計:'
     O                         TAMT  J  137
     O        E 1              ET#L2
     O                         ALS      142
     O*****************************************************************
** TAB1
A支票
B背書支票
C本票
D背書本票
E銀行本票
F匯票
G匯款
H現金
I信用狀
J銀行存款
