     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR069R4
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02LYW
     H*            4.FUNCTION     訂單別出貨及發票明細表FOR DOWNLOAD
     H*            5.DATE-WRITTEN  91/07/12
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTL  IP  E           K        DISK
     FINVMST  IF  E           K        DISK
     FARR069F1UF  E           K        DISK                      A
     I*****************************************************************
     ITXREC
     I                                              TXVUNOL1
     I                                              TXCUNOL1
     I                                              TXRESVL1
     I            DS
     I                                        1   4 D#YYMM
     I                                        1   2 D#YY
     I                                        3   4 D#MM
     C*****************************************************************
     C*        MAIN  PROGRAM
     C*****************************************************************
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1                   EXSR L1SR
     CLR                   RETRN
     C*****************************************************************
     CSR         L1CLR     BEGSR
     C*****************************************************************
     C                     Z-ADD0         W#QTYA  90
     C                     Z-ADD0         W#AMTA 110
     C                     Z-ADD0         W#AR06 110
     C                     ENDSR
     C*****************************************************************
     CSR         DTLSR     BEGSR
     C*****************************************************************
     C           TXIVNO    CHAININREC                40
     C           INKIND    IFEQ '2'
     C                     DIV  1.05      TXAMT     H
     C                     ENDIF
     C  N40      TXACNT    IFEQ '3'
     C           INDECD    ANDNE'D'
     C           INTYPE    ANDEQ'1'
     C                     ADD  TXAMT     W#AR06           折讓金額
     C                     ELSE
     C                     ADD  TXQTY     W#QTYA           銷售量
     C                     ADD  TXAMT     W#AMTA           銷售金額
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     CSR         L1SR      BEGSR
     C*****************************************************************
     C           K#69F1    KLIST
     C                     KFLD           W1YY
     C                     KFLD           W1MM
     C                     KFLD           W1FACT
     C                     KFLD           W1CUNO
     C                     KFLD           W1ORNO
     C*
     C                     MOVELTXRESV    D#YYMM
     C                     MOVE D#YY      W1YY
     C                     MOVE D#MM      W1MM
     C                     MOVELTXNO      W1FACT
     C                     MOVELTXCUNO    W1CUNO
     C                     MOVELTXORNO    W1ORNO
     C           K#69F1    CHAINW1REC                41
     C                     Z-ADDW#QTYA    W1QTYA
     C                     Z-ADDW#AMTA    W1AMTA
     C                     Z-ADDW#AR06    W1AR06
     C  N41                UPDATW1REC
     C   41                WRITEW1REC
     C*
     C                     ENDSR
