     H        1   Y                                     1                 BR003
     FINVMST  IP  E           K        DISK
     FINVDTL  IF  E           K        DISK
     FRCVDTLL3IF  E           K        DISK
     FARSAID  IF  E           K        DISK
     FQPRINT  O   F     176            PRINTER
     I*************************************************************
     IINREC
     I                                              INRVIDL4
     I                                              INCUNML3
     I                                              INCUNOL2
     I                                              INAPNOL1
     I****
     IAADS        DS
     I              'も應收帳款及未兌現-C         CON3                       X
     I              '票據明細表も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     I           UDS
     I                                        1   2 START
     I                                        3   4 END
     I                                        5   5 AREA
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           IVKEY     KLIST
     C                     KFLD           IVNO
     C                     KFLD           IVACNT
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L4                EXSR DL#L4
     C   L3                EXSR DL#L3
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C                     EXSR SL#01
     C      95             EXSR DP#01
     CL1    96 70          EXSR TL#L1
     CL2    96 70          EXSR TL#L2
     CL3    96 70          EXSR TL#L3
     CL4    96 70          EXSR TL#L4
     CLR                   EXSR TL#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD57        TCNT    20
     C                     Z-ADD0         CNT
     C                     MOVE *ALL'='   ALS   166
     C                     MOVE *ALL'-'   ALQ    92
     C                     SELEC
     C           AREA      WHEQ ' '
     C                     MOVEL'全公司'AREAC   8
     C           AREA      WHEQ 'P'
     C                     MOVEL'台　北'AREAC   8
     C           AREA      WHEQ 'T'
     C                     MOVEL'桃　園'AREAC   8
     C           AREA      WHEQ 'M'
     C                     MOVEL'台　中'AREAC   8
     C           AREA      WHEQ 'K'
     C                     MOVEL'高　雄'AREAC   8
     C           AREA      WHEQ 'H'
     C                     MOVEL'苗　栗'AREAC   8
     C                     OTHER
     C                     MOVEL'　　　'AREAC   8
     C                     ENDSL
     C                     Z-ADD0         GAMT1  100
     C                     Z-ADD0         GAMT2  100
     C                     Z-ADD0         GAMT3  100
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     SETOF                     8870
     C                     Z-ADD0         TAMTA   90
     CSR                   ENDSR
     C*
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     SETOF                     747370
     CSR                   ENDSR
     C*
     C**----------------------------------------------**
     C* DETAIL PROCESS L3                             **
     C**----------------------------------------------**
     CSR         DL#L3     BEGSR
     C                     SETOF                     9670
     C                     Z-ADD0         TAMT1   90
     C                     Z-ADD0         TAMT2   90
     C                     Z-ADD0         TAMT3   90
     CSR                   ENDSR
     C*
     C**----------------------------------------------**
     C* DETAIL PROCESS L4                             **
     C**----------------------------------------------**
     CSR         DL#L4     BEGSR
     C                     Z-ADD0         TAMT4   90
     C                     Z-ADD0         TAMT5   90
     C                     Z-ADD0         TAMT6   90
     CSR                   ENDSR
     C*
     C**-------------------------------------------**
     C**   SELECT AREA SALE CODE                   **
     C**-------------------------------------------**
     CSR         SL#01     BEGSR
     C                     SETOF                     95
     C*
     C           AREA      IFEQ ' '                        選定全部
     C           INRVID    IFGE START
     C           INRVID    ANDLEEND
     C                     SETON                     9570
     C                     ENDIF
     C                     GOTO ESSL01
     C                     ENDIF
     C*          -------------------------------           選定一廠區
     C           INRVID    CHAINARSAID               41
     C           *IN41     IFEQ '0'                        找到該員
     C           SALOCA    IFEQ AREA
     C                     SETON                     9570
     C                     ENDIF
     C                     ENDIF
     C*          -------------------------------           例外處理
     C           AREA      IFEQ 'M'
     C           *IN95     ANDEQ'0'
     C           INSALE    CHAINARSAID               41
     C           *IN41     IFEQ '0'
     C           SALOCA    ANDEQ'M'
     C                     SETON                     9570
     C                     ENDIF
     C                     ENDIF
     C*
     CSR         ESSL01    ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     Z-ADDINNBAL    AMT2    90
     C                     Z-ADDINNBAL    AMT1    90
     C                     SUB  INEAMT    AMT1
     C                     SUB  INFAMT    AMT1
     C**:
     C                     MOVEL*BLANK    PCNO    8
     C                     Z-ADD0         QTY1    70
     C                     MOVELINNO      IVNO
     C                     MOVEL'1'       IVACNT
     C           IVKEY     SETLLINVDTL               97
     C           *IN97     DOWEQ'0'
     C           IVKEY     READEINVDTL                   97
     C   97                LEAVE
     C                     MOVELIVAPNO    PCNO
     C                     ADD  IVQTY     QTY1
     C                     ENDDO
     C****
     C           INNBAL    IFNE 0
     C                     EXSR PR#01
     C                     ENDIF
     C                     SETON                     96
     CSR         DP#01Z    ENDSR
     C*******
     CSR         PR#01     BEGSR
     C                     SETON                     74
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD10        LCNT
     C                     EXCPTED#01
     C                     SETON                     7473
     C                     ADD  1         LCNT
     C                     ADD  1         CNT     30
     C                     ADD  AMT2      TAMT2
     C                     ADD  AMT1      TAMT1
     C           INAPNO    IFNE *BLANK
     C                     SETON                     88
     C                     ADD  AMT2      TAMTA
     C                     END
     CSR         PR#01Z    ENDSR
     C*******
     CSR         PR#02     BEGSR
     C                     MOVEL*BLANK    R#NTNO 20
     C                     MOVELRVNTNO    R#NTNO
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD10        LCNT
     C  N88                EXCPTED#02
     C  N88                ADD  1         LCNT
     C   88                EXCPTET#L1
     C   88                ADD  1         LCNT
     C                     SETON                     74
     CSR         PR#02Z    ENDSR
     C*
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C           *IN88     IFEQ '1'
     C                     EXSR PR#02
     C                     SETOF                     88
     C                     END
     C                     SETON                     75
     CSR         TL#L1Z    ENDSR
     C***
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C           INCUNO    SETLLRCVDTLL3             98
     C           *IN98     DOWEQ'0'
     C           INCUNO    READERCVDTLL3                 98
     C   98                LEAVE
     C*
 9404C*例外處理：避免因更改客戶名稱造成報表資料重複EX:DP273
  .  C           RVCUNM    IFNE INCUNM
  .  C                     ITER
  .  C                     ENDIF
 9404C*
     C           RVACNO    IFNE '1131'
     C                     ITER
     C                     ENDIF
     C**** 5/18 UPDATE
     C*          RVDUDT    IFLT UDATE
     C*                    ITER
     C*                    ENDIF
     C           RVNTCD    IFNE ' '
     C                     ITER
     C                     ENDIF
     C*          TXAR      IFEQ 'P'
     C           RVRVID    IFNE INRVID
     C                     ITER
     C                     ENDIF
     C                     Z-ADDRVRAMT    AMT3    90
     C                     ADD  AMT3      TAMT3
     C                     EXSR PR#02
     C                     ENDDO
     C                     SETON                     75
     CSR         TL#L2Z    ENDSR
     C***
     C**-----------------------------------------**
     C**  TOTAL TIME L3 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L3     BEGSR
     C           TAMT2     ADD  TAMT3     TTAMT  100
     C           TAMT2     IFEQ 0
     C           TAMT3     ANDEQ0
     C                     GOTO TL#L3Z
     C                     ENDIF
     C                     EXCPTET#L3
     C                     ADD  3         LCNT
     C                     ADD  TAMT1     TAMT4
     C                     ADD  TAMT2     TAMT5
     C                     ADD  TAMT3     TAMT6
     CSR         TL#L3Z    ENDSR
     C***
     C**-----------------------------------------**
     C**  TOTAL TIME L4 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L4     BEGSR
     C           TAMT5     ADD  TAMT6     TTAMT  100
     C           TTAMT     IFEQ 0
     C                     GOTO TL#L4Z
     C                     ENDIF
     C                     EXCPTET#L4
     C                     Z-ADD80        LCNT
     C                     ADD  TAMT4     GAMT1
     C                     ADD  TAMT5     GAMT2
     C                     ADD  TAMT6     GAMT3
     CSR         TL#L4Z    ENDSR
     C******
     C**-----------------------------------------**
     C**  TOTAL TIME LR PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#LR     BEGSR
     C           GAMT2     ADD  GAMT3     TTAMT  100
     C           TTAMT     IFEQ 0
     C                     GOTO TL#LRZ
     C                     ENDIF
     C                     EXCPTET#LR
     CSR         TL#LRZ    ENDSR
     C******
     OQPRINT  E   02           EH#01
     O                                   18 '報表編號:AR043'
     O                         CON4      88                                    X
     O                                  149 '製表日期'
     O                                  150 ':'
     O                         UDATE Y  158
     O        E 1              EH#01
     O                                   13 '地區別:'
     O                         AREA      14
     O                         AREAC     22
     O                         CON3      92                                    X
     O                                  149 '頁　　次'
     O                                  150 ':'
     O                         PAG   Z  153
     O        E 1              EH#01
     O                                   13 '業務員:'
     O                         INRVID    15
     O        E 1              EH#01
     O                                   13 '截止日期:'
     O                         UDATE Y   21
     O        E 1              EH#01
     O                         ALS      166
     O        E 1              EH#01
     O                                   45 '|**************'
     O                                   61 '出　　貨'
     O                                   82 '***************'
     O                                   90 '|******'
     O                                  102 '應收帳款'
     O                                  111 '******|'
     O                                  117 '****'
     O                                  126 '請　款'
     O                                  132 '****'
     O                                  142 '|********'
     O                                  154 '應收票據'
     O                                  165 '********|'
     O        E 1              EH#01
     O                                   31 '|'
     O                                   84 '|'
     O                                  111 '|'
     O                                  134 '|'
     O                                  165 '|'
     O        E 1              EH#01
     O                                   10 '客戶編號'
     O                                   20 '客戶名稱'
     O                                   30 '訂單號碼'
     O                                   31 '|'
     O                                   41 '交貨日期'
     O                                   51 '發票號碼'
     O                                   72 '磅單號碼'
     O                                   82 '重量(kg)'
     O                                   84 '|'
     O                                   96 '應收金額'
     O                                  110 '未收金額'
     O                                  111 '|'
     O                                  122 '請款單號'
     O                                  132 '請款日期'
     O                                  134 '|'
     O                                  144 '票據金額'
     O                                  154 '到期日期'
     O                                  164 '繳款單號'
     O                                  165 '|'
     O        E 1              EH#01
     O                         ALS      166
     O        E 1              ED#01
     O                N73      INCUNO    08
     O                N73      INCUNM    20
     O                 75      INCUNO    08
     O                 75      INCUNM    20
     O                         INORNO    27
     O                         ININDT    40 '    /  /  '
     O                         INNO      52
     O                         PCNO      71
     O                         QTY1  N   82
     O                         AMT1  N   95
     O                         AMT2  N  109
     O                         INAPNO   120
9008 O                         INAPDT   131 '    /  /  '
     O        E 1              ED#02
     O                N74      INCUNO    08
     O                N74      INCUNM    20
     O                 75      INCUNO    08
     O                 75      INCUNM    20
     O                         RVDATE    40 '    /  /  '
     O                         R#NTNO    62
     O                         AMT3  N  143
     O                         RVDUDT   153 '    /  /  '
     O                         RVNO     161
     O                                  162 '-'
     O                         RVITEM   164
     O        E 1      88      ET#L1
     O                                  143 '請款單號：'
     O                         INAPNO   150
     O                         TAMTA N  164
     O        E 2              ET#L3
     O                                   38 '=======================>'
     O                                   50 '客戶小計：'
     O                         TAMT1 N   95
     O                         TAMT2 N  109
     O                                  123 '＋'
     O                         TAMT3 N  143
     O                                  150 '＝'
     O                         TTAMT N  164
     O        E 1              ET#L3
     O                                  110 '-------------'
     O                                  144 '-------------'
     O                                  165 '-------------'
     O*****
     O        E 1              ET#L4
     O                                   36 '=======================>'
     O                                   50 '業務員合計：'
     O                         TAMT4 N   95
     O                         TAMT5 N  109
     O                                  123 '＋'
     O                         TAMT6 N  143
     O                                  150 '＝'
     O                         TTAMT N  164
     O        E 1              ET#L4
     O                                  110 '------------'
     O                                  144 '------------'
     O                                  165 '------------'
     O        E 2              ET#LR
     O                                   36 '=======================>'
     O                                   50 '總　合　計：'
     O                         GAMT1 N   95
     O                         GAMT2 N  109
     O                                  123 '＋'
     O                         GAMT3 N  143
     O                                  150 '＝'
     O                         TTAMT N  164
     O        E 1              ET#LR
     O                                  110 '------------'
     O                                  144 '------------'
     O                                  165 '------------'
     O*****************************************************************
