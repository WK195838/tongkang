     H*****************************************************************
     H*
     H*          >>  PROGRAM INTRODUCTION  <<
     H*          1.PROGRAM-ID    ARR103R1
     H*          2.PROGRAM-TYPE  RPG/400
     H*          3.AUTHOR        S02CSF
     H*          4.FUNCTION     跨年度發票作廢統計表表
     H*          5.DATE-WRITTEN  92/01/09
     H*          6.DATE-MODIFY
     H*
     H*****************************************************************
     H        1   Y                                     1
     FARR103F IP  E           K        DISK
     FARR103T O   E             66     PRINTER
     IRAR103F
     I                                              AFDEPTL2
     I                                              AFRVIDL1
     I           UDS
     I                                     10011010 U#USID
     I*****************************************************************
     C*         主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C   L2                EXSR L2CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CL2 99                EXSR L2SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C*          初始程式
     C*****************************************************************
     CSR         INISR     BEGSR
     C                     SETON                     6699
     C                     MOVE *ALL'='   T#LIN1
     C                     MOVE *ALL'='   T#LIN2
     C                     MOVE *ALL'-'   T#LIN3
     C                     MOVE *OFF      *IN31
     C                     MOVE *OFF      *IN32
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           L2CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR103T1T
     C                     Z-ADD0         W#AMTT 120       發票金額暫存
     C                     Z-ADD0         W#CNTT  30       發票張數暫存
     C                     MOVEL*BLANK    W#DEPT  4
     C                     MOVE *OFF      *IN32
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR103T1S
     C                     Z-ADD0         W#AMTS 100       發票金額暫存
     C                     Z-ADD0         W#CNTS  30       發票張數暫存
     C                     MOVEL*BLANK    W#RVID  1
     C                     MOVE *OFF      *IN31
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     CLEARAR103T1D
     C                     MOVELAFDEPT    T#DEPT           營業部門
     C                     MOVELAFNAME    T#NAME           業務員名稱
     C                     MOVELAFCUNO    T#CUNO           客戶代號
     C                     MOVELAFCUNM    T#CUNM           客戶姓名
     C                     MOVELAFORNO    T#ORNO           訂單號碼
     C                     MOVELAFINNO    T#IVNO           發票號碼
     C                     MOVELAFNO      T#NO             磅單編號
     C                     Z-ADDAFAMT1    T#AMT1           發票金額
     C*
     C           T#DEPT    IFEQ W#DEPT
     C                     MOVE *ON       *IN31
     C                     ELSE
     C                     MOVE *OFF      *IN31
     C                     MOVELT#DEPT    W#DEPT
     C                     ENDIF
     C*
     C           AFRVID    IFEQ W#RVID
     C                     MOVE *ON       *IN32
     C                     ELSE
     C                     MOVE *OFF      *IN32
     C                     MOVELAFRVID    W#RVID
     C                     MOVELAFNAME    T#NAME
     C                     ENDIF
     C*
     C   66                WRITEAR103T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR103T1D
     C*
     C                     ADD  AFAMT1    W#AMTS
     C                     ADD  1         W#CNTS
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C                     ADD  W#AMTS    T#AMTS
     C                     ADD  W#CNTS    T#CNTS
     C*
     C   66                WRITEAR103T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR103T1S
     C*
     C                     ADD  T#AMTS    W#AMTT
     C                     ADD  T#CNTS    W#CNTT
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           L2SR      BEGSR
     C*****************************************************************
     C                     ADD  W#AMTT    T#AMTT
     C                     ADD  W#CNTT    T#CNTT
     C*
     C   66                WRITEAR103T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR103T1T
     C                     WRITEAR103T1L
     C*
     C                     ADD  T#AMTT    T#AMTA
     C                     ADD  T#CNTT    T#CNTA
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C   66                WRITEAR103T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR103T1A
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C*****************************************************************
     C   66                WRITEAR103T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR103T1M
     C*
     CSR                   ENDSR
