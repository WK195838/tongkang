     H        1   Y                                     1                 BR003
     FINVMST  IP  E           K        DISK
     FQPRINT  O   F     132            PRINTER
     I*************************************************************
     IINREC
     I                                              INRVIDL2
     I                                              INCUNOL1
     I****
     IAADS        DS
     I              'も業務別未請款明細-C         CON3                       X
     I              '表も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     I           UDS
     I                                        1   2 START
     I                                        3   4 END
     I                                        5   5 AREA
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L1                EXSR DL#L1
     C   L2                EXSR DL#L2
     C                     EXSR SL#01
     C      95             EXSR DP#01
     CL1    95             EXSR TL#L1
     CL2    95             EXSR TL#L2
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD55        TCNT    20
     C                     Z-ADD0         CNT
     C                     MOVE *ALL'='   ALS   132
     C                     MOVE *ALL'-'   ALQ    92
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         AAMTT   80
     C                     Z-ADD0         BAMTT   80
     C                     Z-ADD0         CAMTT   80
     C                     Z-ADD0         DAMTT   80
     C                     Z-ADD0         EAMTT   80
     C                     Z-ADD0         FAMTT   80
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         AAMTTT  90
     C                     Z-ADD0         BAMTTT  90
     C                     Z-ADD0         CAMTTT  90
     C                     Z-ADD0         DAMTTT  90
     C                     Z-ADD0         EAMTTT  90
     C                     Z-ADD0         FAMTTT  90
     CSR                   ENDSR
     C**-------------------------------------------**
     C**   SELECT AREA SALE CODE                   **
     C**-------------------------------------------**
     CSR         SL#01     BEGSR
     C                     SETOF                     95
     C                     SELEC
     C           AREA      WHEQ ' '
     C           INRVID    IFGE START
     C           INRVID    ANDLEEND
     C                     SETON                     95
     C                     ENDIF
     C           AREA      WHEQ 'K'
     C           INRVID    IFEQ 'A '
     C           INRVID    OREQ 'B '
     C           INRVID    OREQ 'C '
     C           INRVID    OREQ 'D '
     C                     SETON                     95
     C                     ENDIF
     C           AREA      WHEQ 'T'
     C           INRVID    IFEQ 'J '
     C           INRVID    OREQ 'I '
     C                     SETON                     95
     C                     ENDIF
     C           AREA      WHEQ 'P'
     C           INRVID    IFEQ 'G '
     C           INRVID    OREQ 'Q '
     C           INRVID    OREQ 'N '
     C           INRVID    OREQ 'T '
     C           INRVID    OREQ 'P '
     C           INRVID    OREQ 'R '
     C                     SETON                     95
     C                     ENDIF
     C           AREA      WHEQ 'M '
     C           INRVID    IFEQ 'U '
     C           INRVID    OREQ 'V '
     C           INRVID    OREQ 'X '
     C                     SETON                     95
     C                     ENDIF
     C           AREA      WHEQ 'H '
     C           INRVID    IFEQ 'O '
     C           INRVID    OREQ 'F '
     C           INRVID    OREQ 'H '
     C                     SETON                     95
     C                     ENDIF
     C                     ENDSL
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     Z-ADDINAAMT    AAMT    80
     C                     Z-ADDINCAMT    BAMT    80
     C                     Z-ADDINBAMT    CAMT    80
     C                     ADD  INDAMT    CAMT
     C                     Z-ADDINATAX    DAMT    80
     C                     ADD  INCTAX    DAMT
     C                     ADD  INDTAX    DAMT
     C                     Z-ADDINEAMT    EAMT    80
     C                     ADD  INFAMT    EAMT
     C                     Z-ADDINNBAL    FAMT    80
     C                     ADD  AAMT      AAMTT   80
     C                     ADD  BAMT      BAMTT   80
     C                     ADD  CAMT      CAMTT   80
     C                     ADD  DAMT      DAMTT   80
     C                     ADD  EAMT      EAMTT   80
     C                     ADD  FAMT      FAMTT   80
     C                     ADD  AAMT      AAMTTT
     C                     ADD  BAMT      BAMTTT
     C                     ADD  CAMT      CAMTTT
     C                     ADD  DAMT      DAMTTT
     C                     ADD  EAMT      EAMTTT
     C                     ADD  FAMT      FAMTTT
     C*                    Z-ADDININDT    BDATE   60
     C*                    Z-ADDUDATE     EDATE   60
     C*                    Z-ADD0         WDAY    40
     C*                    CALL 'C04'
     C*                    PARM           BDATE
     C*                    PARM           EDATE
     C*                    PARM           WDAY
     C*                    FREE 'C04'
     C                     MOVE *ALL'0'   P#PDAT
     C                     MOVE ININDT    P#PDAT
     C                     MOVE *ALL'0'   P#EDAT
     C                     MOVE UDATE     P#EDAT
     C                     CALL 'UTS103R'
     C                     PARM           P#PDAT  8
     C                     PARM           P#EDAT  8
     C                     PARM '1'       P#MODE  1
     C                     PARM 0         DAY     50
     C                     PARM           P#ERR   1
     C                     Z-ADDDAY       WDAY    40
     C                     SELEC
     C           WDAY      WHLT 15
     C                     MOVE *BLANK    MARK    4
     C           WDAY      WHGE 15
     C           WDAY      ANDLT30
     C                     MOVE '*   '    MARK    4
     C           WDAY      WHGE 30
     C           WDAY      ANDLT45
     C                     MOVE '**  '    MARK    4
     C           WDAY      WHGE 45
     C                     MOVE '*** '    MARK    4
     C                     ENDSL
     C****
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD8         LCNT
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     C                     ADD  1         CNT     30
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C                     EXCPTET#L1
     C                     ADD  3         LCNT
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C                     EXCPTET#L2
     C                     Z-ADD80        LCNT
     CSR         TL#L2Z    ENDSR
     C******
     OQPRINT  E   02           EH#01
     O                                   17 '報表編號: AR002'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
     O                         UDATE Y  132
     O        E 1              EH#01
     O                         CON3      75                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 1              EH#01
     O                                   13 '收款業務員:'
     O                         INRVID  +  1
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              EH#01
     O                                   10 '客戶編號'
     O                                   20 '客戶名稱'
     O                                   30 '發票號碼'
     O                                   40 '發票日期'
     O                                   50 '訂單編號'
     O                                   64 '出貨金額'
     O                                   76 '折讓／銷退'
     O                                   88 '預收貨款'
     O                                  100 '銷項稅額'
     O                                  112 '已收金額'
     O                                  126 '未收金額'
     O                                  132 '備註'
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              ED#01
     O                 L1      INCUNO     8
     O                 L1      INCUNM    20
     O                 75      INCUNO     8
     O                 75      INCUNM    20
     O                         INNO      30
     O                         ININDT    40 '    /  /  '
     O                         INORNO    47
     O                         AAMT  J   64
     O                         BAMT  J   76
     O                         CAMT  J   88
     O                         DAMT  J  100
     O                         EAMT  J  112
     O                         FAMT  J  126
     O                         MARK     131
     O        E 1              ET#L1
     O                         ALQ      128
     O        E 1              ET#L1
     O                                   50 '客戶小計：'
     O                         AAMTT J   64
     O                         BAMTT J   76
     O                         CAMTT J   88
     O                         DAMTT J  100
     O                         EAMTT J  112
     O                         FAMTT J  126
     O        E 1              ET#L1
     O                         ALQ      128
     O*****
     O        E 1              ET#L2
     O                                   50 '業務員合計：'
     O                         AAMTTTJ   64
     O                         BAMTTTJ   76
     O                         CAMTTTJ   88
     O                         DAMTTTJ  100
     O                         EAMTTTJ  112
     O                         FAMTTTJ  126
