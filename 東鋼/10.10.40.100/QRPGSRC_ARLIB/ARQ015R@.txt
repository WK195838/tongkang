     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARQ015R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     客戶繳款應轉暫收額度計算程式
     H*            5.DATE-WRITTEN  88/11/12
     H*
     H* 說明：   1.應用程式傳入繳款書編號及項次。
     H*            2.本程式傳回該筆項次的繳款其應轉暫收的額度。
     H*            3.計算原則：借方金額優先扣抵貨款、次而其他貸方
     H*                        科目，最後才是暫收款。所有借方金額
     H*                        依兌現日期的先後及其項次的順序來處理。
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FRCVDTLL8IF  E           K        DISK
     E*************************************************************
     E                    ARY1      100 21
     I*************************************************************
     I            DS
     I                                        1  21 A#DATA
     I                                        1   80A@NTDT
     I                                        9  100A@ITEM
     I                                       11  210A@RAMT
     I           UDS
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     C**************************************************************
     C*   程式參數傳遞區
     C**************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P#RVNO  6
     C                     PARM           P#ITEM  20
     C                     PARM           P#RAMT 110
     C**************************************************************
     C*   搜尋關鍵
     C**************************************************************
     C           K#RV      KLIST
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C**************************************************************
     C*   主程式
     C**************************************************************
     C                     Z-ADD0         P#RAMT
     C*
     C                     MOVELP#RVNO    RVNO
     C                     Z-ADDP#ITEM    RVITEM
     C           K#RV      CHAINRVREC                69
     C           *IN69     IFEQ '1'                        不存在
     C           RVACTP    OREQ 'C'                        貸方
     C                     SETON                     LR
     C                     RETRN
     C                     ENDIF
     C*
     C                     Z-ADD0         I       20
     C                     Z-ADD0         ARY1L   20
     C                     MOVEL*ALL'9'   ARY1
     C                     Z-ADD0         W#2159 110
     C                     Z-ADD0         W#DAMT 110
     C                     Z-ADD0         W#CAMT 110
     C*
     C           RVNO      SETLLRVREC                69
 {   C           1         DOWEQ1
     C           RVNO      READERVREC                    69
     C           *IN69     IFEQ '1'
     C                     LEAVE
     C                     ENDIF
     C*
     C           RVACTP    IFEQ 'D'
     C                     ADD  RVRAMT    W#DAMT
     C                     EXSR SR1000                     兌現日期
     C                     Z-ADDW#NTDT    A@NTDT
     C                     Z-ADDRVITEM    A@ITEM
     C                     Z-ADDRVRAMT    A@RAMT
     C                     ADD  1         I
     C                     ADD  1         ARY1L
     C                     MOVELA#DATA    ARY1,I
     C                     ENDIF
     C*
     C           RVACTP    IFEQ 'C'
     C           RVACNO    ANDEQ'2159'
     C                     ADD  RVRAMT    W#2159
     C                     ENDIF
 }   C                     ENDDO
     C*
     C           W#DAMT    SUB  W#2159    W#CAMT
     C                     SORTAARY1
     C*
 {   C           1         DO   ARY1L     I
     C                     MOVELARY1,I    A#DATA
     C           W#CAMT    IFLE A@RAMT
     C                     SUB  W#CAMT    A@RAMT
     C                     Z-ADD0         W#CAMT
     C                     ELSE
     C                     SUB  A@RAMT    W#CAMT
     C                     Z-ADD0         A@RAMT
     C                     ENDIF
     C                     MOVELA#DATA    ARY1,I
 }   C                     ENDDO
     C*
 {   C           1         DO   ARY1L     I
     C                     MOVELARY1,I    A#DATA
     C           A@ITEM    IFEQ P#ITEM
     C                     Z-ADDA@RAMT    P#RAMT
     C                     LEAVE
     C                     ENDIF
 }   C                     ENDDO
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
     C************************************
     C*  求得該筆繳款明細項之兌現日期
     C************************************
     CSR         SR1000    BEGSR
     C                     Z-ADD0         W#NTDT  80       兌現日期
     C*
     C           RVNTTP    IFEQ 'F'                        匯票
     C           RVNTTP    OREQ 'G'                        匯款
     C           RVNTTP    OREQ 'H'                        現金
     C           RVNTTP    OREQ 'J'                        銀行存款
     C                     GOTO ES1000
     C                     ENDIF
     C*
     C           RVACNO    IFEQ '1131'
     C           RVNTCD    ANDEQ'1'
     C                     Z-ADDRVNTDT    W#NTDT
     C                     GOTO ES1000
     C                     ENDIF
     C*
     C           RVACNO    IFEQ '1131'
     C                     Z-ADDRVDUDT    W#NTDT
     C                     ENDIF
     CSR         ES1000    ENDSR
     C****************************************************************
