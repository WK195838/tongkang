     H                                                  1                 BR310
     FINVMST  IP  E                    DISK
     FQPRINT  O   F     132            PRINTER
     E                    TABA    1   5  1   TABB   10
     E                    TABC    1   4  1   TABD   10
     IINREC
     I                                              INAREAL1
     IAADS        DS
     I                                       11  160YMD
     I                                       11  120YY
     I                                       13  140MM
     I           UDS
     I                                        1   6 DATEA
     I                                        1   2 YY1
     I                                        3   4 MM1
     I                                      951 985 COMP
     C  N10                EXSR @INT
     C  N10                SETON                     10
     C   L1                EXSR @DL1
     C                     EXSR DP#R
     CL1                   EXSR @TL1
     CLR                   EXSR @TLR
     C***************
     C** DETAIL L1 **
     C***************
     CSR         @INT      BEGSR
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD55        TCNT    30
     C                     Z-ADD0         CNTA    30
     C                     Z-ADD0         TOT11   90
     C                     Z-ADD0         TOT21   90
     C                     Z-ADD0         TOT31   90
     C                     MOVEL*ALL'='   ALQ   132
     C                     MOVEL*ALL'-'   ALS   110
     CSR                   ENDSR
     C***************
     C** DETAIL L1 **
     C***************
     CSR         @DL1      BEGSR
     C                     Z-ADD0         CNT     30
     C                     Z-ADD0         TOT1    90
     C                     Z-ADD0         TOT2    90
     C                     Z-ADD0         TOT3    90
     C           INAREA    LOKUPTABA      TABB           31
     C   31                MOVELTABB      AREAN  10
     C  N31                MOVEL*BLANK    AREAN
     CSR                   ENDSR
     C***************
     C** DETAIL PR **
     C***************
     CSR         DP#R      BEGSR
     C                     ADD  1         LCNT
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD8         LCNT
     C           INTYPE    LOKUPTABC      TABD           31
     C   31                MOVELTABD      TYPEN  10
     C  N31                MOVEL*BLANK    TYPEN
     C*                    Z-ADD0         WDAY    40
     C*                    Z-ADDININDT    BDATE   60
     C*                    Z-ADDINDEDT    EDATE   60
     C*                    CALL 'C04'
     C*                    PARM           BDATE
     C*                    PARM           EDATE
     C*                    PARM           WDAY
     C*                    FREE 'C04'
     C                     MOVE *ALL'0'   P#PDAT
     C                     MOVE ININDT    P#PDAT
     C                     MOVE *ALL'0'   P#EDAT
     C                     MOVE INDEDT    P#EDAT
     C                     CALL 'UTS103R'
     C                     PARM           P#PDAT  8
     C                     PARM           P#EDAT  8
     C                     PARM '1'       P#MODE  1
     C                     PARM 0         DAY     50
     C                     PARM           P#ERR   1
     C                     Z-ADDDAY       WDAY    40
     C*
     C                     ADD  1         CNT
     C           INTYPE    IFEQ '2'
     C                     Z-ADD0         AAMT
     C                     Z-ADDINAAMT    BAMT
     C                     ELSE
     C                     Z-ADDINAAMT    AAMT    90
     C                     Z-ADDINBAMT    BAMT    90
     C                     ENDIF
     C                     Z-ADDINATAX    ATAX    90
     C                     ADD  INAAMT    TOT1
     C                     ADD  INBAMT    TOT2
     C                     ADD  INATAX    TOT3
     C                     Z-ADDINAAMT    NBAL    90
     C                     ADD  INBAMT    NBAL
     C                     ADD  INATAX    NBAL
     C                     EXCPTED#01
     CSR                   ENDSR
     C***************
     C** TOTAL L1  **
     C***************
     CSR         @TL1      BEGSR
     C                     ADD  CNT       CNTA
     C                     ADD  TOT1      TOT11
     C                     ADD  TOT2      TOT21
     C                     ADD  TOT3      TOT31
     C                     Z-ADDTOT1      NBAL    90
     C                     ADD  TOT2      NBAL
     C                     ADD  TOT3      NBAL
     C                     EXCPTET#L1
     C                     ADD  3         LCNT
     CSR                   ENDSR
     CSR         @TLR      BEGSR
     C                     Z-ADDTOT11     NBAL    90
     C                     ADD  TOT21     NBAL
     C                     ADD  TOT31     NBAL
     C                     EXCPTET#LR
     CSR                   ENDSR
     OQPRINT  E  202           EH#01
     O                                   20 '報表編號:AR044'
     O                         COMP      79                                    X
     O                                  117 '印表日期'
     O                                  118 ':'
     O                         UDATE Y  127
     O        E  2             EH#01
     O                         YY1       50
     O                                 +  0 '年'
     O                         MM1     +  0
     O                                 +  0 '月'
     O                                   76 '作廢發票統計表'
     O                                  117 '頁　　數'
     O                                  118 ':'
     O                         PAG   Z  126
     O        E  1             EH#01
     O                                   10 '開立廠區'
     O                                   20 '發票號碼'
     O                                   30 '發票類別'
     O                                   40 '客戶名稱'
     O                                   50 '訂單號碼'
     O                                   62 '銷售金額'
     O                                   74 '預收貨款'
     O                                   86 '銷項稅額'
     O                                   98 '發票總額'
     O                                  108 '開立日期'
     O                                  118 '作廢日期'
     O                                  128 '延遲天數'
     O        E  1             EH#01
     O                         ALQ      132
     O        E  1             ED#01
     O                 L1      AREAN     10
     O                 75      AREAN     10
     O                         INNO      20
     O                         TYPEN     30
     O                         INCUNM    40
     O                         INORNO    47
     O                         AAMT  N   61
     O                         BAMT  N   73
     O                         ATAX  N   85
     O                         NBAL  N   97
     O                         ININDTY  107
     O                         INDEDTY  117
     O                         WDAY  N  125
     O        E  1             ET#L1
     O                         ALS      132
     O        E  1             ET#L1
     O                                   36 '廠區合計：'
     O                                   40 '共'
     O                         CNT   N   44
     O                                   48 '張'
     O                         TOT1  N   61
     O                         TOT2  N   73
     O                         TOT3  N   85
     O                         NBAL  N   97
     O        E  1             ET#L1
     O                         ALS      132
     O        E  1             ET#LR
     O                                   36 '　總合計：'
     O                                   40 '共'
     O                         CNTA  N   44
     O                                   48 '張'
     O                         TOT11 N   61
     O                         TOT21 N   73
     O                         TOT31 N   85
     O                         NBAL  N   97
** TABA
K高　雄
L林　口
M台　中
P台　北
T桃　園
** TABC
1出　貨
2預　收
3調　整
9非營業
