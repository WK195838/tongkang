     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARE040R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     發票指定開立作業
     H*            5.DATE-WRITTEN  88/02/05
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARE040S CF  E                    WORKSTN
     F                                        RRN1  KSFILE AR040F1
     F                                        RRN2  KSFILE AR040F2
     FSAMAST  IF  E           K        DISK
     FARCUCT  IF  E           K        DISK
     FARODCT  IF  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FTRNDTLL4IF  E           K        DISK
     F            TXREC                             KRENAMETXRECL
     FTRNDTL  UF  E           K        DISK                      A
     FGENSEQ  UF  E           K        DISK
     FAFCBAL  UF  E           K        DISK
     FINVMST  O   E           K        DISK
     FINVDTL  O   E           K        DISK
     E*************************************************************
     E                    T#MSG   1  12 70
     E*------------------------------------------------------------
     E                    ARX1      100 35
     E                    ARY1      100 43
     I*************************************************************
     I           UDS
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 S#USID
     I                                     10211021 U#AREA
     I*-------------------------------------------------------------
     I            DS
9008 I                                        1   6 F#ORNO
LYW  I                                        1   1 S1OREA
 .   I                                        2   60S1ORNO
     I            DS
     I                                        1   6 F#CUNO
     I                                        1   1 S1KIND
     I                                        2   2 S1CUN1
     I                                        3   5 S1CUN2
9511 I                                        6   6 S1CD01
     I            DS
9008 I                                        1   6 S#INK1
LYW  I                                        2   6 D#ORNO
     I            DS
9008 I                                        1   9 A2ORNO
LYW  I                                        1   6 D#A2OR
     I            DS
     I                                        1   8 TXNO
     I                                        1   1 D#TXAR
     I*-------------------------------------------------------------
     I            DS
     I                                        1   60U#XYM
     I                                        1   10U#X1
     I                                        1   20U#X2
     I                                        2   6 U#CX1
     I                                        3   6 U#CX2
     I*
     I            DS
     I                                        1  35 X#DATA
     I                                        1   1 X#KEY
     I                                        2   4 X#PDCD
     I                                        5  153X#UPRC
     I                                       16  240X#QTY
     I                                       25  350X#AMT
     I*
     I            DS
     I                                        1  43 Y#DATA
     I                                        1   1 Y#KEY
     I                                        2   9 Y#NO
     I                                       10  12 Y#PDCD
     I                                       13  233Y#UPRC
     I                                       24  320Y#QTY
     I                                       33  430Y#AMT
     C**************************************************************
     C*   檔案搜尋欄位組合
     C**************************************************************
     C           K#S1      KLIST                           訂單主檔
     C                     KFLD           S1OREA
     C                     KFLD           S1ORNO
     C*
     C           K#A1      KLIST                           客戶管制
     C                     KFLD           A1CUNO
     C                     KFLD           A1CTKD
     C*
     C           K#A2      KLIST                           訂單管制
     C                     KFLD           A2ORNO
     C                     KFLD           A2CTKD
     C*
     C           K#GE      KLIST                           號碼管制
     C                     KFLD           GEKIND
     C                     KFLD           GEPRIN
     C*
     C           K#TX      KLIST                           銷貨明細
     C                     KFLD           TXCODE
     C                     KFLD           TXNO
     C                     KFLD           TXITEM
     C**************************************************************
     C*   主程式開始
     C**************************************************************
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C           W#PRID    CASEQ'02'      SR2000           畫面二
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C*   副程式開始
     C**************************************************************
     C*----------------------------------------
     C*  宣告及初始變數
     C*----------------------------------------
     CSR         SR0000    BEGSR
     C                     Z-ADD11        N#PAG1  20       SF1筆數
     C                     MOVEL'01'      W#PRID  2        函式代號
     C                     MOVEL'F'       W#RTNV  1        函式返回值
     C                     MOVEL'F'       W#SFE1  1        SF1結束
9008 C                     MOVEL*BLANK    W#ORNO  6        訂單號碼暫
     C                     Z-ADD0         RRN1    40
     C                     Z-ADD0         RRN2    40
     C*
     C                     MOVEL*BLANK    W#PDCD  3        品名（暫）
     C                     Z-ADD0         W#UPRC 113       單價（暫）
     C                     Z-ADD0         W#AMT  110       金額（暫）
     C*
     C                     MOVEL*BLANK    S#INK1
     C                     EXSR SR1100                     初始畫面一
     C                     SETON                     51    SET LOW
     CSR                   ENDSR
     C*
     C************************************
     C*  畫面一:選擇訂單號碼
     C************************************
     CSR         SR1000    BEGSR
     C                     WRITEAR040F1M
     C           S#SFN1    IFEQ 0
     C                     SETOF                     7273
     C                     SETON                     71
     C                     ELSE                            有資料
     C                     SETOF                     73
     C                     SETON                     7172
     C                     ENDIF
     C           W#SFE1    IFEQ 'T'
     C                     SETON                     74
     C                     ELSE
     C                     SETOF                     74
     C                     ENDIF
     C                     EXFMTAR040F1C
     C*
     C                     SETOF                     515253
     C                     MOVEL*BLANK    S#MSG1
     C           S#CRN1    IFNE 0
     C                     Z-ADDS#CRN1    S#NBR1
     C                     ENDIF
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C           *IN12     OREQ '1'
     C                     MOVEL'00'      W#PRID
     C           *IN91     WHEQ '1'                        向下翻頁
     C                     EXSR SR1300
     C           *IN92     WHEQ '1'                        向上翻頁
     C                     EXSR SR1400
     C*
     C                     OTHER                           執行鍵
     C                     EXSR SR1200
     C                     ENDSL
     CSR                   ENDSR
     C*
     C*================================================================
     C*  初始畫面一(指向指定的記錄)
     C*================================================================
     CSR         SR1100    BEGSR
     C           S#INK1    IFNE *BLANK                     訂單錯誤
     C                     TESTN          D#ORNO     3132
     C           *IN31     IFNE '1'
     C           *IN32     ANDNE'1'
     C                     MOVELT#MSG,1   S#MSG1           必須數值
     C                     SETON                     5152
     C                     GOTO ES1100
     C                     ENDIF
     C                     MOVELS#INK1    F#ORNO
     C           K#S1      SETLLRSAMAST              69
     C*
     C                     ELSE                            用空白找
     C           *LOVAL    SETLLRSAMAST              69
     C                     ENDIF
     C*
     C*
     C           *IN69     IFEQ '1'
     C           *HIVAL    SETGTRSAMAST              69
     C                     MOVEL*BLANK    F#ORNO
     C                     EXSR SR1130                     向前讀一頁
     C                     ENDIF
     C*
     C                     MOVEL*BLANK    F#ORNO
     C                     EXSR SR1110                     清除SF
     C                     EXSR SR1120                     讀入SF
     C*
     C                     Z-ADD1         S#NBR1           指向第一筆
     C                     MOVEL*BLANK    S#INK1
     CSR         ES1100    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一清除SF
     C*----------------------------------------
     CSR         SR1110    BEGSR
     C                     SETOF                     717251
     C                     SETON                     73    清除 SF
     C                     WRITEAR040F1C
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一向下讀入SF (從目前的檔案位置)
     C*----------------------------------------
     CSR         SR1120    BEGSR
     C                     MOVEL'F'       W#SFE1
     C                     MOVELF#ORNO    W#ORNO
     C                     Z-ADD0         S#SFN1
     C                     Z-ADD0         RRN1
     C*
     C           1         DOWEQ1                          讀取迴圈
     C                     READ RSAMAST                  69
     C           *IN69     IFEQ '1'
     C                     MOVEL'T'       W#SFE1
     C                     LEAVE
     C                     ENDIF
     C*
     C           F#ORNO    IFEQ W#ORNO
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVELF#ORNO    W#ORNO
     C                     ADD  1         S#SFN1
     C           S#SFN1    IFGT N#PAG1                     已滿一頁
     C                     Z-ADDN#PAG1    S#SFN1
     C                     LEAVE
     C                     ENDIF
     C                     ADD  1         RRN1
     C                     CLEARAR040F1
     C                     EXSR SR1121                     搬移資料
     C                     WRITEAR040F1
     C                     ENDDO                           讀取迴圈
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一向讀入記錄之搬移資料
     C*----------------------------------------
     CSR         SR1121    BEGSR
     C                     MOVELF#ORNO    S#ORNO
     C                     Z-ADDS1DATE    S#ORDT
     C                     MOVELS1CTNO    S#CTNO
     C                     MOVELF#CUNO    S#CUNO
     C                     MOVELS1CUNO    S#CUNM
     C*
     C                     Z-ADDS1PRAT    S#PRAT
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一向上移動記錄(從目前的檔案位置)
     C*----------------------------------------
     CSR         SR1130    BEGSR
     C                     MOVELF#ORNO    W#ORNO
     C                     Z-ADD0         S#SFN1
     C*
     C           1         DOWEQ1
     C                     READPRSAMAST                  69
     C           *IN69     IFEQ '1'
     C                     LEAVE
     C                     ENDIF
     C*
     C           F#ORNO    IFNE W#ORNO
     C                     MOVELF#ORNO    W#ORNO
     C                     ADD  1         S#SFN1
     C           S#SFN1    IFGT N#PAG1                     多一筆
     C                     LEAVE
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
     C*
     C           *IN69     IFEQ '1'
     C           *LOVAL    SETLLRSAMAST              69
     C                     MOVEL'T'       W#SFB1  1        已達檔頭
     C                     MOVEL*BLANK    F#ORNO
     C                     ELSE
     C                     MOVEL'F'       W#SFB1           未達檔頭
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*================================================================
     C*  畫面一：執行
     C*================================================================
     CSR         SR1200    BEGSR
     C           S#INK1    IFNE *BLANK
     C                     EXSR SR1100
     C                     GOTO ES1200
     C                     ENDIF
     C*
     C           1         DO   S#SFN1    RRN1
     C           RRN1      CHAINAR040F1              69
     C                     SELEC
     C           S#OPT1    WHEQ '1'
     C                     EXSR SR1210
     C           W#RTNV    IFEQ 'T'
     C                     MOVEL*BLANK    S#OPT1
     C                     MOVEL'02'      W#PRID           初始畫面二
     C                     EXSR SR2100
     C                     ELSE
     C                     MOVELT#MSG,4   S#MSG1           非指定類
     C                     SETON                     53
     C                     ENDIF
     C                     UPDATAR040F1
     C                     Z-ADDRRN1      S#NBR1
     C                     GOTO ES1200
     C           S#OPT1    WHEQ ' '
     C                     SETOF                     53
     C                     UPDATAR040F1
     C                     ENDSL
     C                     ENDDO
     C*
     C                     SETON                     51    SET LOW
     CSR         ES1200    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一：執行之訂單管制檢核
     C*----------------------------------------
     CSR         SR1210    BEGSR
     C                     MOVEL'F'       W#RTNV  1        返回值
     C                     MOVEL'F'       W#CUCT  1        客戶管制
     C                     MOVEL'F'       W#RFOR  1        參考訂單
     C*
     C                     MOVELS#CUNO    A1CUNO           客戶管制
     C                     MOVEL'04'      A1CTKD
     C           K#A1      CHAINRARCUCT              69
{    C           *IN69     IFEQ '0'                        客戶有管制
 {   C           A1MTHD    IFEQ '04'                       指定開立
     C                     MOVEL'T'       W#RTNV
  {  C           A1RFOR    IFEQ ' '                        不看訂單
     C                     MOVEL'T'       W#CUCT
  -  C                     ELSE
     C                     MOVEL'T'       W#RFOR
  }  C                     ENDIF
     C                     GOTO ES1210
 -   C                     ELSE                            非指定開立
  {  C           A1RFOR    IFEQ ' '                        不看訂單
     C                     GOTO ES1210
  }  C                     ENDIF
 }   C                     ENDIF
}    C                     ENDIF                           有管制客戶
     C*
     C*         ---------------------------------------
     C*
     C                     MOVEL*BLANK    A2ORNO           訂單管制
9008 C                     MOVE S#ORNO    A2ORNO
     C           A2ORNO    SETLLRARODCT              69
     C  N69                READ RARODCT                  69
{    C           *IN69     DOWEQ'0'
 {   C           D#A2OR    IFNE S#ORNO
     C                     SETON                     69
     C                     LEAVE
 -   C                     ELSE
  {  C           A2CTKD    IFEQ '04'
     C           A2MTHD    ANDEQ'04'
     C                     LEAVE
  }  C                     ENDIF
 }   C                     ENDIF
     C                     READ RARODCT                  69
}    C                     ENDDO
     C*
     C           *IN69     IFEQ '0'                        有訂單項次
     C                     MOVEL'T'       W#RTNV
     C                     ENDIF
     CSR         ES1210    ENDSR
     C*
     C*================================================================
     C*  畫面一：向下翻一頁
     C*================================================================
     CSR         SR1300    BEGSR
     C           W#SFE1    IFEQ 'T'
     C                     MOVELT#MSG,2   S#MSG1           已達檔底
     C                     GOTO ES1300
     C                     ENDIF
     C*
     C           S#SFN1    IFNE 0                          本頁有資料
     C                     Z-ADDS#SFN1    RRN1
     C           RRN1      CHAINAR040F1              69
     C                     MOVELS#ORNO    F#ORNO
     C           K#S1      SETLLRSAMAST              69    移至本頁尾
     C                     READ RSAMAST                  69
     C                     ENDIF                           本頁有資料
     C*
     C                     EXSR SR1110                     清除SF
     C                     EXSR SR1120                     讀入SF
     C           S#NBR1    IFGT S#SFN1
     C                     Z-ADD1         S#NBR1
     C                     ENDIF
     CSR         ES1300    ENDSR
     C*
     C*================================================================
     C*  畫面一：向上翻一頁
     C*================================================================
     CSR         SR1400    BEGSR
     C           S#SFN1    IFNE 0                          本頁有資料
     C                     Z-ADD1         RRN1
     C           RRN1      CHAINAR040F1              69
     C                     MOVELS#ORNO    F#ORNO
     C           K#S1      SETLLRSAMAST                    移至本頁頭
     C                     READ RSAMAST                  69
     C                     ENDIF                           本頁有資料
     C*
     C                     EXSR SR1130                     向前移動
     C                     EXSR SR1110                     清除SF
     C                     EXSR SR1120                     讀入SF
     C*
     C           W#SFB1    IFEQ 'T'
     C                     MOVELT#MSG,3   S#MSG1           己達檔頭
     C                     ENDIF
     CSR         ES1400    ENDSR
     C*
     C*
     C****************************************************************
     C*  畫面二:選擇銷貨開立發票
     C****************************************************************
     CSR         SR2000    BEGSR
     C                     WRITEAR040F2M
     C           S#SFN2    IFEQ 0
     C                     SETOF                     7273
     C                     SETON                     7174
     C                     ELSE                            有資料
     C                     SETOF                     73
     C                     SETON                     717274
     C                     ENDIF
     C                     EXFMTAR040F2C
     C*
     C                     MOVEL*BLANK    S#MSG2
     C           S#CRN2    IFNE 0
     C                     Z-ADDS#CRN2    S#NBR2
     C                     ENDIF
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'                        結束程式
     C                     MOVEL'00'      W#PRID
     C           *IN12     WHEQ '1'                        回前畫面
     C                     MOVEL'01'      W#PRID
     C*
     C           *IN10     WHEQ '1'                        存檔
     C                     EXSR SR2300
     C*
     C                     OTHER                           執行鍵
     C                     EXSR SR2200
     C                     ENDSL
     CSR                   ENDSR
     C*
     C*================================================================
     C*  初始畫面二
     C*================================================================
     CSR         SR2100    BEGSR
     C                     SETOF                     7172
     C                     SETON                     73    清除 SF
     C                     WRITEAR040F2C
     C*
     C                     Z-ADD0         S#AAMT
     C                     Z-ADD0         S#ATAX
     C                     Z-ADD0         S#BAMT
     C                     Z-ADD0         S#NBAL
     C           S#CUNO    CHAINCBCUST               69
     C                     MOVELCBIVCO    S#KIND           聯式
     C*
     C                     Z-ADD0         S#SFN2
     C                     Z-ADD0         RRN2
     C*
     C           S#ORNO    CHAINTXRECL               69
     C           *IN69     DOWEQ'0'
     C           TXFL02    IFEQ *BLANK                     未開立發票
     C           TXIVNO    ANDEQ*BLANK
     C           TXCODE    ANDEQ'SA04'                     銷貨類者
     C           D#TXAR    ANDEQU#AREA
     C*
     C                     EXSR SR2101                     訂單管控
     C           W#RTNV    IFEQ 'T'
     C                     EXSR SR2110                     搬移變數
     C                     ADD  1         RRN2
     C                     ADD  1         S#SFN2
     C                     WRITEAR040F2
     C                     ENDIF
     C*
     C                     ENDIF
     C           S#ORNO    READETXRECL                   69
     C                     ENDDO
     C*
     C                     Z-ADD1         S#NBR2
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面二：訂單管制檢核
     C*----------------------------------------
     CSR         SR2101    BEGSR
     C                     MOVEL'F'       W#RTNV
     C*
     C           W#CUCT    IFEQ 'T'                        客戶絕對
     C                     MOVEL'T'       W#RTNV
     C                     MOVELA1TYP1    W#TYP1  2
     C                     Z-ADDA1RAT1    W#RAT1 113
     C                     MOVELA1TYP2    W#TYP2  2
     C                     Z-ADDA1RAT2    W#RAT2 113
     C                     GOTO ES2101
     C                     ENDIF
     C*
     C                     MOVELTXORNO    A2ORNO
     C                     MOVEL'04'      A2CTKD
     C           K#A2      CHAINRARODCT              69
     C*
     C           *IN69     IFEQ '0'                        有管制
     C           A2MTHD    IFEQ '04'                       指定開立類
     C                     MOVEL'T'       W#RTNV
     C                     MOVELA2TYP1    W#TYP1
     C                     Z-ADDA2RAT1    W#RAT1
     C                     MOVELA2TYP2    W#TYP2
     C                     Z-ADDA2RAT2    W#RAT2
     C                     ENDIF
     C*
     C                     ELSE                            未管制
     C           W#RFOR    IFEQ 'T'                        參照客戶
     C                     MOVEL'T'       W#RTNV
     C                     MOVELA1TYP1    W#TYP1
     C                     Z-ADDA1RAT1    W#RAT1
     C                     MOVELA1TYP2    W#TYP2
     C                     Z-ADDA1RAT2    W#RAT2
     C                     ENDIF
     C                     ENDIF
     CSR         ES2101    ENDSR
     C*
     C*----------------------------------------
     C*  畫面二：讀入記錄之搬移資料
     C*----------------------------------------
     CSR         SR2110    BEGSR
     C                     MOVEL*BLANK    S#OPT2
     C                     MOVELTXCODE    S#TXCD
     C                     MOVELTXNO      S#TXNO
     C                     Z-ADDTXITEM    S#TXIT
     C                     Z-ADDTXDATE    S#TXDT
     C                     MOVELTXPDNM    S#TXPD
     C                     Z-ADDTXQTY     S#TXQT
     C                     Z-ADDTXUPRC    S#TXUP
     C                     Z-ADDTXAMT     S#TXAM
     C                     Z-ADDTXTAX     S#TXTA
     C                     MOVELTXORNO    S#TXOR
     C*
     C                     MOVELW#TYP1    S#TYP1
     C                     Z-ADDW#RAT1    S#RAT1
     C                     MOVELW#TYP2    S#TYP2
     C                     Z-ADDW#RAT2    S#RAT2
     C*
     C                     MOVELTXSATP    W#SATP  1
     C                     MOVELTXSALE    S#SALE
     C                     MOVELTXRVID    S#RVID
     CSR                   ENDSR
     C*
     C*================================================================
     C*  畫面二：執行（試算）
     C*================================================================
     CSR         SR2200    BEGSR
     C                     EXSR SR2201                     檢核
     C           W#RTNV    IFEQ 'F'
     C                     GOTO ES2200
     C                     ENDIF
     C*                                                    試算
     C                     MOVEL*ALL'9'   ARX1
     C                     MOVEL*ALL'9'   ARY1
     C                     Z-ADD0         ARX1L   30
     C                     Z-ADD0         ARY1L   30
     C*
     C                     Z-ADD0         S#AAMT
     C                     Z-ADD0         S#ATAX
     C                     Z-ADD0         S#BAMT
     C                     Z-ADD0         S#NBAL
     C*
     C           1         DO   S#SFN2    RRN2             迴圈開始
     C           RRN2      CHAINAR040F2              69
     C           S#OPT2    IFNE '1'
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVELS#TXPD    W#PDCD
     C                     Z-ADDS#TXUP    W#UPRC
     C                     EXSR SR2210                     銷貨發票
     C*
     C                     SELEC
     C           S#TYP1    WHEQ '01'
     C                     MOVEL'709'     W#PDCD
     C                     Z-ADDS#RAT1    W#UPRC
     C                     EXSR SR2210                     加工發票
     C           S#TYP1    WHEQ '02'
     C                     MOVEL'709'     W#PDCD
     C                     Z-ADDS#RAT1    W#UPRC
     C                     EXSR SR2220                     加工調整
     C                     ENDSL
     C*
     C                     SELEC
     C           S#TYP2    WHEQ '01'
     C                     MOVEL'714'     W#PDCD
     C                     Z-ADDS#RAT2    W#UPRC
     C                     EXSR SR2210                     廠租發票
     C           S#TYP2    WHEQ '02'
     C                     MOVEL'714'     W#PDCD
     C                     Z-ADDS#RAT2    W#UPRC
     C                     EXSR SR2220                     廠租調整
     C                     ENDSL
     C                     ENDDO                           迴圈結束
     C*
     C                     EXSR SR2230                     合計銷貨
     C                     EXSR SR2240                     合計扣預收
     C                     EXSR SR2250                     合計稅額
     C*
     C           S#AAMT    ADD  S#BAMT    S#NBAL           合計
     C                     ADD  S#ATAX    S#NBAL
     CSR         ES2200    ENDSR
     C*
     C*----------------------------------------
     C*  畫面二：執行之檢核
     C*----------------------------------------
     CSR         SR2201    BEGSR
     C                     MOVEL'F'       W#RTNV
     C                     SETOF                     54
     C*
     C                     MOVEL'99'      GEKIND           已開日期
     C                     MOVEL*BLANK    GEPRIN
     C                     MOVELU#AREA    GEPRIN
     C           K#GE      CHAINGEREC               N69
     C                     Z-ADDGECUNO    W#XDAT  80
     C*                                                    檢核日期
     C                     MOVE S#INDT    P#DATE  8
     C                     CALL 'UTS104R'
     C                     PARM           P#DATE
     C                     PARM '1'       P#MOD1  1
     C                     PARM '2'       P#MOD2  1
     C                     PARM '0000'    P#DAYS  4
     C                     PARM *BLANK    P#RTND  8
     C                     PARM *BLANK    P#ERRC  1
     C*
     C           P#ERRC    IFNE '0'
     C                     MOVELT#MSG,5   S#MSG2           日期錯誤
     C                     SETON                     54
     C                     GOTO ES2201
     C                     ENDIF
     C*
     C           S#INDT    IFGT UDATE
     C                     MOVELT#MSG,6   S#MSG2           大於系統日
     C                     SETON                     54
     C                     GOTO ES2201
     C                     ENDIF
     C*
     C                     Z-ADDUDATE     W#LMDT  80       找上月底
     C                     MOVE '01'      W#LMDT
     C                     MOVE W#LMDT    P#DATE
     C                     CALL 'UTS104R'
     C                     PARM           P#DATE
     C                     PARM '1'       P#MOD1
     C                     PARM '1'       P#MOD2
     C                     PARM '0001'    P#DAYS
     C                     PARM *BLANK    P#RTND
     C                     PARM *BLANK    P#ERRC
     C                     MOVE P#RTND    W#LMDT           上月底
     C*
     C           S#INDT    IFLT W#XDAT
     C           S#INDT    ANDNEW#LMDT
     C                     MOVELT#MSG,7   S#MSG2           小於已開日
     C                     SETON                     54
     C                     GOTO ES2201
     C                     ENDIF
     C*
     C                     Z-ADDUDATE     W#CRDT  80
     C                     MOVE '05'      W#CRDT           本月五日
     C           S#INDT    IFLT W#XDAT
     C           S#INDT    ANDEQW#LMDT                     上月底
     C           UDATE     ANDGTW#CRDT
     C                     MOVELT#MSG,8   S#MSG2
     C                     SETON                     54
     C                     GOTO ES2201
     C                     ENDIF
     C*
     C           S#KIND    IFEQ '2'
     C                     MOVEL'01'      GEKIND
     C                     ELSE
     C                     MOVEL'02'      GEKIND
     C                     ENDIF
     C*
     C                     MOVEL*BLANK    GEPRIN
     C           S#INDT    DIV  100       U#XYM            設GEPRIN
     C           U#X2      IFEQ 0
     C           U#AREA    CAT  U#CX2:0   GEPRIN
     C                     ELSE
     C           U#AREA    CAT  U#CX1:0   GEPRIN
     C                     ENDIF
     C                     MOVELGEPRIN    W#PRIN 10        記錄年月
     C*
     C           K#GE      CHAINGEREC               N69
     C           *IN69     IFEQ '1'
     C                     MOVELT#MSG,9   S#MSG2           字軌未設
     C                     GOTO ES2201
     C                     ENDIF
     C*
     C                     MOVEL'T'       W#RTNV
     CSR         ES2201    ENDSR
     C*
     C*
     C*----------------------------------------
     C* 銷貨、加工、廠租費計入陣列（合開一張發票）
     C*----------------------------------------
     CSR         SR2210    BEGSR
     C                     MOVEL'F'       W#FIND  1
     C           1         DO   ARX1L     I       20
     C                     MOVELARX1,I    X#DATA
     C           X#KEY     IFEQ '9'                        找不到
     C                     LEAVE
     C                     ENDIF
     C           W#PDCD    IFEQ X#PDCD                     同品名　　
     C           W#UPRC    ANDEQX#UPRC                     同單價
     C                     ADD  S#TXQT    X#QTY            合計數量
     C                     MOVELX#DATA    ARX1,I
     C                     MOVEL'T'       W#FIND
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C*
     C           W#FIND    IFEQ 'F'                        新增
     C                     ADD  1         ARX1L            指標加一
     C                     MOVEL*ALL'0'   X#DATA
     C                     MOVELW#PDCD    X#PDCD
     C                     Z-ADDW#UPRC    X#UPRC
     C                     Z-ADDS#TXQT    X#QTY
     C                     Z-ADDARX1L     I
     C                     MOVELX#DATA    ARX1,I
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*-------------------------------
     C* 加工、廠租費計入陣列（另開調整單）
     C*-------------------------------
     CSR         SR2220    BEGSR
     C                     MOVEL'F'       W#FIND  1
     C           1         DO   ARY1L     I       20
     C                     MOVELARY1,I    Y#DATA
     C           Y#KEY     IFEQ '9'                        找不到
     C                     LEAVE
     C                     ENDIF
     C           S#TXNO    IFEQ Y#NO                       同磅單
     C           W#PDCD    ANDEQY#PDCD                     同品名
     C           W#UPRC    ANDEQY#UPRC                     同單價
     C                     ADD  S#TXQT    Y#QTY            合計數量
     C                     MOVELY#DATA    ARY1,I
     C                     MOVEL'T'       W#FIND
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C*
     C           W#FIND    IFEQ 'F'                        新增
     C                     ADD  1         ARY1L            指標加一
     C                     MOVEL*ALL'0'   Y#DATA
     C                     MOVELS#TXNO    Y#NO
     C                     MOVELW#PDCD    Y#PDCD
     C                     Z-ADDW#UPRC    Y#UPRC
     C                     Z-ADDS#TXQT    Y#QTY
     C                     Z-ADDARY1L     I
     C                     MOVELY#DATA    ARY1,I
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C* 銷貨、加工、廠租合計銷貨金額
     C*----------------------------------------
     CSR         SR2230    BEGSR
     C           ARX1L     IFGT 0
     C                     SORTAARX1
     C           1         DO   ARX1L     I
     C                     MOVELARX1,I    X#DATA
     C           X#QTY     MULT X#UPRC    W#AMT     H
     C           S#KIND    IFEQ '2'
     C                     MULT 1.05      W#AMT     H
     C                     ENDIF
     C                     ADD  W#AMT     S#AAMT
     C                     ENDDO
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C* 合計扣預收金額
     C*----------------------------------------
     CSR         SR2240    BEGSR
     C           S#PRAT    IFEQ 0
     C           S#AAMT    OREQ 0
     C                     GOTO ES2240
     C                     ENDIF
     C*
     C           S#ORNO    CHAINAFREC                69
     C           *IN69     IFEQ '0'
     C           S#AAMT    MULT 10        W#AMT     H
     C                     MULT S#PRAT    W#AMT     H
     C           AFNBAL    IFLT W#AMT
     C                     Z-ADDAFNBAL    W#AMT
     C                     ENDIF
     C           W#AMT     IFLE 0
     C                     Z-SUB0         S#BAMT
     C                     ELSE
     C                     Z-SUBW#AMT     S#BAMT           （負）
     C                     ENDIF
     C                     ENDIF
     CSR         ES2240    ENDSR
     C*
     C*----------------------------------------
     C* 合計稅額
     C*----------------------------------------
     CSR         SR2250    BEGSR
     C           S#KIND    IFEQ '2'
     C                     GOTO ES2250
     C                     ENDIF
     C*
     C           S#AAMT    IFEQ 0
     C                     GOTO ES2250
     C                     ENDIF
     C*
     C           S#AAMT    ADD  S#BAMT    W#AMT
     C           W#AMT     MULT 0.05      S#ATAX    H
     CSR         ES2250    ENDSR
     C*
     C*================================================================
     C*  畫面二：存檔
     C*================================================================
     CSR         SR2300    BEGSR
     C                     EXSR SR2200                     試算
     C           W#RTNV    IFEQ 'F'
     C           S#AAMT    ORLE 0                          無金額不計
     C                     GOTO ES2300
     C                     ENDIF
     C*
     C                     EXSR SR2310                     初始發票主
     C           W#RTNV    IFEQ 'F'
     C                     GOTO ES2300
     C                     ENDIF
     C*
     C                     Z-ADD0         W#IVIT  20
     C                     EXSR SR2330                     寫發票明細
     C                     EXSR SR2340                     扣預收明細
     C                     EXSR SR2350                     寫稅額明細
     C*
     C                     MOVEL*BLANK    W#AR05  6
     C                     Z-ADD0         W#TXIT  20
     C                     EXSR SR2360                     寫調整明細
     C*
     C                     WRITEINREC                      寫發票主檔
     C                     EXSR SR2370                     改銷貨明細
     C                     EXSR SR2380                     存已開日期
     C*
     C                     MOVELT#MSG,11  S#MSG1
     C           S#MSG1    CAT  INNO:0    S#MSG1
     C           W#AR05    IFNE *BLANK
     C                     MOVELT#MSG,12  W#MSGT 70
     C           S#MSG1    CAT  W#MSGT:0  S#MSG1
     C           S#MSG1    CAT  W#AR05:0  S#MSG1
     C                     ENDIF
     C*
     C                     MOVEL'01'      W#PRID           返回畫面一
     CSR         ES2300    ENDSR
     C*
     C*-------------------------------
     C* 初始發票主檔
     C*-------------------------------
     CSR         SR2310    BEGSR
     C                     MOVEL'F'       W#RTNV
     C*
     C           INKIND    IFEQ '2'                        獲得發票號
     C                     MOVEL'01'      GEKIND
     C                     ELSE
     C                     MOVEL'02'      GEKIND
     C                     ENDIF
     C                     MOVELW#PRIN    GEPRIN
     C           K#GE      CHAINGEREC                69
     C*
     C           GECUNO    IFEQ GEENNO
     C                     MOVELT#MSG,10  S#MSG2           字軌已用完
     C                     GOTO ES2310
     C                     ENDIF
     C*
     C                     ADD  1         GECUNO
     C                     UPDATGEREC                      存回
     C*
     C                     CLEARINREC
     C                     MOVEL'A'       INFLAG           處理代碼
     C                     MOVEL'1'       INTYPE           發票類別
     C                     MOVELGEPRE     INNO
     C                     MOVE GECUNO    INNO             發票號碼
     C                     MOVELS#CUNO    INCUNO           客戶編號
     C                     MOVELS#CUNM    INCUNM           客戶名稱
     C                     MOVELS#ORNO    INORNO           訂單號碼
     C                     Z-ADDS#INDT    ININDT           發票日期
     C                     MOVELS#KIND    INKIND           發票聯式
     C                     MOVELS#RVID    INRVID           收款業務
     C                     MOVELS#SALE    INSALE           發貨業務
     C                     MOVELW#SATP    INSATP           銷售別
     C                     MOVELU#AREA    INAREA           開立廠區
     C                     MOVEL'1'       INTXTP           課稅別
     C                     MOVELU#AREA    INTXAR           異動廠區　
     C                     Z-ADDUDATE     INTXDT           異動日期
     C*
     C                     MOVEL'T'       W#RTNV
     CSR         ES2310    ENDSR
     C*
     C*-------------------------------
     C* 初始發票明細
     C*-------------------------------
     CSR         SR2320    BEGSR
     C                     CLEARIVREC
     C                     MOVEL'A'       IVFLAG           處理代碼
     C                     MOVELINNO      IVNO             發票號碼
     C                     MOVEL'1'       IVACNT           類別
     C                     ADD  1         W#IVIT
     C                     Z-ADDW#IVIT    IVITEM           項次
     C                     Z-ADDININDT    IVACDT           入帳日期
     C                     MOVELINORNO    IVORNO           訂單號碼
     C                     MOVELINORNO    IVAPNO           憑証編號
     C                     MOVEL'Y'       IVFL02           過發票碼
     C                     MOVEL'A'       IVFL03           類別碼
     C                     MOVELU#AREA    IVTXAR           異動廠區
     C                     Z-ADDUDATE     IVTXDT           異動日期
     CSR                   ENDSR
     C*
     C*-------------------------------
     C* 寫發票明細資料
     C*-------------------------------
     CSR         SR2330    BEGSR
     C                     SORTAARX1
     C           1         DO   ARX1L     I
     C                     MOVELARX1,I    X#DATA
     C*
     C           X#QTY     IFEQ 0                          數量零不計
     C                     ITER
     C                     ENDIF
     C*
     C                     EXSR SR2320                     初始明細
     C*
     C                     MOVELX#PDCD    IVPDCD           品名
     C                     Z-ADDX#QTY     IVQTY            數量
     C                     Z-ADDX#UPRC    IVUPRC           單價
     C           IVQTY     MULT IVUPRC    IVAMT     H      金額
     C*
     C           INKIND    IFEQ '2'                        二聯式
     C           IVAMT     MULT 1.05      IVAMT     H
     C           IVAMT     DIV  IVQTY     IVUPRC    H
     C                     ENDIF
     C*
     C                     WRITEIVREC                      寫出明細
     C                     ADD  IVAMT     INAAMT           出貨金額
     C                     ADD  IVAMT     INNBAL           發票餘額
     C                     ENDDO
     CSR                   ENDSR
     C*
     C*-------------------------------
     C* 寫扣預收明細
     C*-------------------------------
     CSR         SR2340    BEGSR
     C           S#PRAT    IFEQ 0                          無預收款
     C                     GOTO ES2340
     C                     ENDIF
     C*
     C           INAAMT    IFEQ 0                          無出貨？
     C                     GOTO ES2340
     C                     ENDIF
     C*
     C                     Z-ADD0         W#AMT            計算扣預收
     C           INORNO    CHAINAFREC                69
     C           *IN69     IFEQ '0'
     C           INAAMT    MULT 10        W#AMT     H      扣預比乘十
     C                     MULT S#PRAT    W#AMT     H
     C           AFNBAL    IFLT W#AMT
     C                     Z-ADDAFNBAL    W#AMT
     C                     ENDIF
     C                     ENDIF
     C*
     C           W#AMT     IFLE 0                          無可扣額
     C                     Z-ADD0         W#AMT
     C                     ELSE
     C                     ADD  W#AMT     AFEAMT           扣預收金額
     C                     SUB  W#AMT     AFNBAL           兌現餘額
     C                     Z-ADDUDATE     AFCHDT           異動日期
     C                     UPDATAFREC                      存回預收檔
     C                     ENDIF
     C*
     C                     EXSR SR2320                     初始明細
     C                     MOVEL'4'       IVACNT           類別
     C                     Z-ADD1         IVITEM           項次01
     C                     Z-SUBW#AMT     IVAMT            金額（負）
     C                     MOVEL'E'       IVFL03           類別碼
     C*
     C                     WRITEIVREC                      寫出明細
     C                     ADD  IVAMT     INBAMT           扣預收
     C                     ADD  IVAMT     INNBAL           發票餘額
     CSR         ES2340    ENDSR
     C*
     C*-------------------------------
     C* 寫稅額明細
     C*-------------------------------
     CSR         SR2350    BEGSR
     C           INKIND    IFEQ '2'                        二聯式
     C                     GOTO ES2350                     已計稅額
     C                     ENDIF
     C*
     C           INAAMT    IFEQ 0                          無出貨？
     C                     GOTO ES2350
     C                     ENDIF
     C*
     C           INAAMT    ADD  INBAMT    W#AMT
     C           W#AMT     MULT 0.05      W#AMT     H
     C*
     C                     EXSR SR2320
     C                     MOVEL'5'       IVACNT           稅額
     C                     Z-ADD1         IVITEM           項次01
     C                     Z-ADDW#AMT     IVAMT
     C*
     C                     WRITEIVREC
     C                     ADD  IVAMT     INATAX           稅額
     C                     ADD  IVAMT     INNBAL           發票餘額
     CSR         ES2350    ENDSR
     C*
     C*-------------------------------
     C* 加工、廠租費寫調整明細（含獲得調整單號（首次即可））
     C*-------------------------------
     CSR         SR2360    BEGSR
     C           1         DO   ARY1L     I
     C                     MOVELARY1,I    Y#DATA
     C*
     C           Y#QTY     IFEQ 0                          數量零不計
     C                     ITER
     C                     ENDIF
     C*
     C                     EXSR SR2361                     獲得調整單
     C                     EXSR SR2362                     初始調整單
     C*
     C                     MOVELY#NO      TXPCNO           磅單編號
     C                     MOVELY#PDCD    TXPDNM           品名
     C                     Z-ADDY#QTY     TXQTY            數量
     C                     Z-ADDY#UPRC    TXUPRC           單價
     C           TXQTY     MULT TXUPRC    TXAMT            金額
     C*
     C                     WRITETXREC                      寫出明細
     C                     ENDDO
     CSR                   ENDSR
     C*
     C*-------------------------------
     C* 新開調整單之單號獲得（同時存回已用號碼）
     C*-------------------------------
     CSR         SR2361    BEGSR
     C           W#AR05    IFNE *BLANK
     C                     GOTO ES2361
     C                     ENDIF
     C*
     C                     MOVEL'05'      GEKIND           調整單
     C                     MOVEL*BLANK    GEPRIN
     C                     MOVELU#AREA    GEPRIN
     C           K#GE      CHAINGEREC                69
     C                     Z-ADDGECUNO    W#GENO  50
     C*
     C           W#GENO    IFEQ 99999                      每年移出
     C                     Z-ADD1         W#GENO           號碼會輪
     C                     ELSE
     C                     ADD  1         W#GENO
     C                     ENDIF
     C*
     C                     Z-ADDW#GENO    GECUNO
     C                     UPDATGEREC                      存回號碼
     C*
     C                     MOVELU#AREA    W#AR05
     C                     MOVE W#GENO    W#AR05           調整單號
     CSR         ES2361    ENDSR
     C*
     C*-------------------------------
     C* 初始調整單明細
     C*-------------------------------
     CSR         SR2362    BEGSR
     C                     CLEARTXREC
     C                     MOVEL'A'       TXFLAG           處理代碼
     C                     MOVEL'AR05'    TXCODE           單據代碼
     C                     MOVELW#AR05    TXNO             調整單號
     C                     ADD  1         W#TXIT
     C                     Z-ADDW#TXIT    TXITEM           項次
     C                     Z-ADDININDT    TXDATE           單據日期
     C                     Z-ADDININDT    TXACDT           入帳日期
     C                     MOVELINCUNO    TXCUNO           客戶代號
     C                     MOVELINCUNM    TXCUNM           客戶簡稱
     C                     MOVELINORNO    TXORNO           訂單號碼
     C                     MOVELINRVID    TXRVID           收款員
     C                     MOVELINSALE    TXSALE           出貨員
     C                     MOVELINSATP    TXSATP           銷售別
     C                     MOVELINKIND    TXIVTP           發票聯式
     C                     MOVELU#AREA    TXTXAR           異動廠區
     C                     Z-ADDUDATE     TXTXDT           異動日期
     CSR                   ENDSR
     C*
     C*-------------------------------
     C* 改銷貨明細
     C*-------------------------------
     CSR         SR2370    BEGSR
     C           1         DO   S#SFN2    RRN2             迴圈開始
     C           RRN2      CHAINAR040F2              69
     C           S#OPT2    IFNE '1'
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVELS#TXCD    TXCODE
     C                     MOVELS#TXNO    TXNO
     C                     Z-ADDS#TXIT    TXITEM
     C           K#TX      CHAINTXREC                69
     C*
     C                     MOVEL'C'       TXFLAG
     C                     MOVELINNO      TXIVNO
     C                     MOVEL'Y'       TXFL02
     C                     MOVELU#AREA    TXTXAR
     C                     Z-ADDUDATE     TXTXDT
     C                     UPDATTXREC
     C                     ENDDO                           迴圈結束
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C* 存已開日期
     C*----------------------------------------
     CSR         SR2380    BEGSR
     C           ININDT    IFGE W#XDAT                     大於已開日
     C                     MOVEL'99'      GEKIND           已開日期
     C                     MOVEL*BLANK    GEPRIN
     C                     MOVELU#AREA    GEPRIN
     C           K#GE      CHAINGEREC                69
     C                     Z-ADDININDT    GECUNO
     C                     UPDATGEREC
     C                     ENDIF
     CSR                   ENDSR
     C*
     C**************************************************************
** T#MSG
０１－訂單編號不合法！
０２－已達檔底。
０３－已達檔頭。
０４－本訂單不含發票指定開立的功能！
０５－發票日期不合法！
０６－發票日期不得大於系統日期！
０７－發票日期不得小於已開立日期！
０８－本月份已超過五日，不得重開上月底之發票！
０９－該發票年月之字軌尚未輸入，請輸入後再進行發票開立作業。
１０－注意！！發票字軌已用完，無法再開立發票，請洽財會人員！
１１－發票開立成功，號碼為：
，調整單號為：
