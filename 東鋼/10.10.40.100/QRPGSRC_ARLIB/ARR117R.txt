     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR117R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     已確認未過入財會營業退款明細表
     H*            5.DATE-WRITTEN  93/07/28
     H*            6.DATE-UPDATE
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARRETN  UP  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FBCNAME  IF  E           K        DISK
     FARSAID  IF  E           K        DISK
     FARR117T O   E             66     PRINTER
     F*****************************************************************
     IRARRETN
     I                                              ANRTNOL1
     I*
     I            DS
     I                                        1  10 T#ACNM
     I                                       10  10 D#NOF
     I            DS
     I                                       11  12 W#OEOF
     I                                       11  11 D#OE
     I                                       12  12 D#OF
     I            DS
     I                                        1   8 D#ORNO
     I                                        1   1 D#ORN1
     I                                        2   7 D#ORN2
     I                                        8   8 D#ORN3
     I            DS
     I                                        1  10 ANRLNO
     I                                        1   6 D#RLNO
     I           UDS
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLRN99                EXSR MSGSR
     C*
     C*****************************************************************
     CSR         INISR     BEGSR
     C*****************************************************************
     C                     SETON                     6699
     C                     MOVE *ALL'-'   T#LIN1
     C                     MOVE *ALL'-'   T#LIN2
     C                     MOVE *ALL'-'   T#LIN3
     C                     MOVE *ALL'-'   T#LIN4
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         L1CLR     BEGSR                           換退款單號
     C*****************************************************************
     C                     SETON                     66    強迫換頁
     C                     Z-ADD0         W#ITEM  20
     C*
     C                     Z-ADDANRTDT    T#RTDT           退款日期
     C                     MOVELANDPNO    T#DPNO           退款部門
     C                     Z-ADDANCFDT    T#CFDT           退款確認日
     C           ANCUNO    CHAINCBREC                40
     C  N40                MOVELCBFNAM    T#FNAM           受款人
     CSR                   ENDSR
     C*
     C*****************************************************************
     CSR         DTLSR     BEGSR
     C*****************************************************************
     C                     CLEARAR117T1D
     C*
     C           *INL1     IFEQ '1'                        只印首筆
     C                     MOVELANRTNO    T#RTNO
     C                     ENDIF
     C*
     C                     MOVELANACNO    T#ACNO           會計科目
9707 C                     MOVELANACNO    W#ACNO  9
 .   C           W#ACNO    CHAINRACNAME              69
9707 C           W#ACNO    IFEQ ACNO
     C                     MOVE ''      W#OEOF
     C                     MOVELACCHNM    T#ACNM           會計名稱
     C                     MOVE ''      D#NOF
     C                     ENDIF
     C*
     C           ANSAID    IFNE *BLANK
     C           ANSAID    CHAINRARSAID              41
     C  N41                MOVELSANAME    T#SAID           領款人
     C                     ENDIF
     C                     MOVELANDC      T#DC             借貸
     C                     MOVELD#RLNO    T#RLNO           相關號碼
     C                     Z-ADDANDUDT    T#DUDT           到期日
     C                     MOVELANCSNM    T#CSNM           對象別
     C                     Z-ADDANAAMT    T#AAMT           金額
     C                     MOVELANDSC1    T#DSC1           輔助摘要
     C                     MOVELANDSC2    T#DSC2           摘要
     C                     MOVE *ALL'-'   T#LIN5
     C*
     C   66                WRITEAR117T1H
     C                     SETOF                     66
     C                     WRITEAR117T1D
     C                     ADD  1         W#ITEM
     C*
     C                     MOVEL'Y'       ANFL02           已列印
     C                     UPDATRARRETN                    存回
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         L1SR      BEGSR
     C*****************************************************************
     C           W#ITEM    DOWLE4
     C                     CLEARAR117T1D
     C                     MOVE *ALL'-'   T#LIN5
     C   66                WRITEAR117T1H
     C                     SETOF                     66
     C                     WRITEAR117T1D
     C                     ADD  1         W#ITEM
     C                     ENDDO
     C*
     C   66                WRITEAR117T1H
     C                     SETOF                     66
     C                     WRITEAR117T1X
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C*
     C   66                WRITEAR117T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR117T1M
     C*
     CSR                   ENDSR
