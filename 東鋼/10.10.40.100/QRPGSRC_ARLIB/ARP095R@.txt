     H*****************************************************************
     H*
     H*          >>  PROGRAM INTRODUCTION  <<
     H*          1.PROGRAM-ID    ARP095R
     H*          2.PROGRAM-TYPE  RPG/400
     H*          3.AUTHOR        S02CSF
     H*          4.FUNCTION     應收帳款承購發票自動結案　
     H*          5.DATE-WRITTEN  91/10/16
     H*
     H*****************************************************************
     H        1   Y                                     1
     FARINVML1IP  E           K        DISK
     F            RARINVM                           KRENAMERINVM
     FARCVDTL1IF  E           K        DISK
     FINVMST  IF  E           K        DISK
     FARCVMS  IF  E           K        DISK
     FARINVM  UF  E           K        DISK
     IRINVM       01
     I*****************************************************************
     C*         主程式開始
     C*****************************************************************
     C   01                EXSR DTLSR
     CLR                   RETRN
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     Z-ADD0         W#AMT3 120       總繳款金額
     C                     Z-ADD0         W#DAMT 120       總應繳金額
     C*
     C                     MOVELAMINNO    ATINNO           發票號碼
     C           ATINNO    SETLLRARCVDT
     C                     MOVE *OFF      *IN40
     C           *IN40     DOWEQ*OFF
     C                     READ RARCVDT             N    40
     C           ATINNO    IFNE AMINNO
     C           *IN40     OREQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C*當發票相同時，判斷該筆沖銷金額是否已確認 　　　　　　　　
     C*當已確認時才累加該筆發票沖銷金額
     C*
     C           ATRCNO    CHAINRARCVMS             N42
     C  N42      ASFL01    IFEQ 'Y'
     C                     ADD  ATAMT3    W#AMT3 120       總繳款金額
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C           W#AMT3    IFNE 0
     C*計算該筆發票折讓部分金額
     C           AMINNO    CHAININREC               N41
     C*
     C*總繳款金額＋折退金額＋折退稅額＝發票金額
     C  N41      W#AMT3    SUB  INCAMT    W#DAMT           折讓金額
     C  N41                SUB  INCTAX    W#DAMT           折讓稅額
     C  N41                SUB  AMDAMT    W#DAMT           發票金額
     C*
     C*當總繳款金額=總應繳金額時,結案碼上Y
     C           AMINNO    CHAINRARINVM              41
     C           W#DAMT    IFEQ 0
     C                     Z-ADDW#AMT3    AMFAMT
     C                     MOVEL'Y'       AMCLOC
     C                     MOVEL'SYSTEM'  AMCLOM
     C                     MOVE UDATE     AMCLOD
     C                     ENDIF
     C*UPDATE已沖銷金額
     C                     Z-ADDW#AMT3    AMFAMT           已沖銷金額
     C  N41                UPDATRARINVM
     C*
     C                     ENDIF
     C*
     C                     ENDSR
