     H        1   Y                                     1                 BR003
     FRCVDTLL1IP  E           K        DISK
     FRCVDTL  UF  E           K        DISK
     F            RVREC                             KRENAMERVREC1
     FBANKAC  IF  E           K        DISK
     FACTRAN  UF  E           K        DISK                      A
     FQPRINT  O   F     176            PRINTER
     ****
     IRVREC
     I                                              RVSANOL1
     I*
     IACREC
     I              ACTYPE                          F#TYPE
     I              ACCURR                          F#CURR
     I              ACACNO                          F#ACNO
     I              ACBNAM                          F#BNAM
     I              ACNAME                          F#NAME
     I              ACAREA                          F#AREA
     I              ACBAID                          F#BAID
     I              ACOLID                          F#OLID
     I              ACRESV                          F#RESV
     ****
     I            DS
     I                                        1   6 VRNOX
     I                                        2   60NO1
     I                                       11  16 VRNON
     I                                       12  160NO3
     I            DS
     I                                       11  17 VRNOG
     I                                       11  11 VRNOG1
     I                                       12  12 VRNOG2
     I                                       13  16 VRNOG3
     I                                       14  160VRNOG4
     I                                       17  17 VRNOG5
     I                                       21  30 RVSANO
     I                                       21  21 ANTYPE
     I                                       22  22 ANCURR
     I                                       23  30 ANACNO
     I            DS
     I                                       21  36 DSC0
     I                                       21  28 DSC1
     I                                       21  30 DSC11
     I                                       29  36 DSC2
     I                                       31  36 DSC21
     I*                                      41  47 VUNOG
     I*                                      41  41 VUNOG1
     I*                                      42  42 VUNOG2
     I*                                      43  46 VUNOG3
     I*                                      47  47 VUNOG4
     I                                       41  47 VUNOG
     I                                       41  41 VUNOG1
     I                                       42  47 VUNOG2
     I*                                      46  47 VUNOG3
     I           UDS
9008 I                                        1   80YMD
     I                                        1   60YM
     I                                     10011010 D#USER
     I                                     10211021 TXAR
     C*****************************************************************
     C*          KEY  LIST
     C*****************************************************************
     C           ACKEY1    KLIST
     C                     KFLD           TXAR
     C                     KFLD           YM
     C                     KFLD           ACVRNO
     C                     KFLD           ACITEM
     C           ACKEY     KLIST
     C                     KFLD           TXAR
     C                     KFLD           YM
     C                     KFLD           ACVRNO
     C           RVKEY     KLIST
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C           ANKEY     KLIST
     C                     KFLD           ANTYPE
     C                     KFLD           ANCURR
     C                     KFLD           ANACNO
     C*****************************************************************
     C*          MAIN   PROGRAM
     C*****************************************************************
     C  N10                EXSR IN#01
     C   L1 10             EXSR DL#01
     C      10             EXSR DP#01
     CL1    10             EXSR TL#01
     CLR    10             EXSR TL#LR
     C**--------------------------------------------------**
     CSR         IN#01     BEGSR
     C                     Z-ADD0         AMTI   132
     C                     Z-ADD0         AMTT   132
     C                     Z-ADD0         VAMT   132
     C                     Z-ADD0         AMTTT  132
     C                     Z-ADD0         ITEM    30
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD60        TCNT    20
     C                     Z-ADD0         PAG     30
     C                     MOVEL*ALL'-'   ADASH 150
     C                     MOVEL*BLANK    VUNOG
     C                     SETON                     10
     CSR                   ENDSR
     C***
     C**----------------------------------------------**
     C**    L1 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#01     BEGSR
     C                     Z-ADD0         ITEM
     C                     Z-ADD0         AMTT
     ****
     C                     EXSR @FDNO
     CSR                   ENDSR
     C**----------------------------------------------**
     C**  FIND LAST NO                                **
     C**----------------------------------------------**
     CSR         @FDNO     BEGSR
     C                     MOVEL'A99999'  ACVRNO
     C                     Z-ADD999       ACITEM
     C                     SETOF                     95
     C           ACKEY1    SETLLACTRAN
     C                     READPACTRAN                   95
     C  N95                MOVELACVRNO    TYPE    1
     C  N95      TYPE      COMP 'A'                  9595
     C  N95                MOVELACVRNO    VRNON
     C  N95                ADD  1         NO3
     C  N95                MOVELVRNON     VRNOX
     C   95                MOVEL'A00001'  VRNOX
     CSR                   ENDSR
     C**----------------------------------------------**
     C**       DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C**
     C                     ADD  RVRAMT    AMTTT
     C                     ADD  RVRAMT    AMTT
     C***
     C                     Z-ADDRVRAMT    VAMT   132
     C                     EXSR WR#01
     C                     EXSR PR#01
     C           RVKEY     CHAINRCVDTL               95
     C*    N95             MOVELTXAR      VUNOG1
     C*    N95             MOVELTXAR      VUNOG2
     C*    N95             MOVELVRNOX     VUNOG3
     C*    N95             MOVELVUNOG     RVVNO2
     C     N95             MOVELTXAR      VUNOG1
     C     N95             MOVELVRNOX     VUNOG2
     C*因欄位長度不夠放
     C*    N95             MOVELITEM      VUNOG3
     C     N95             MOVELVUNOG     RVVNO2
     C     N95             MOVEL'C'       RVFLAG
     C     N95             MOVELTXAR      RVTXAR
     C     N95             Z-ADDUDATE     RVTXDT
     C     N95             UPDATRVREC1
     CSR                   ENDSR
     C**----------------------------------**
     C**   L1 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#01     BEGSR
     C                     EXCPTSP#01
     C                     ADD  1         LCNT
     CSR         TL#01Z    ENDSR
     C**----------------------------------**
     C**   LR BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     CSR                   ENDSR
     C*****
     C**------------------------------------------**
     C**    CHECK   存入帳號 820101             **
     C**------------------------------------------**
     CSR         CK#SNO    BEGSR
     C           ANKEY     CHAINBANKAC               97
     C   97                MOVE *BLANK    F#NAME
     C           YMD       IFLT 820101
     C  N97                MOVELF#OLID    RVSANO
     C                     ENDIF
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#01     BEGSR
     C**** WRITE TO 1112
     C                     ADD  1         ITEM
     C                     CLEARRACVOUR
     C                     MOVEL'P'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXAR      ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     MOVELYMD       ACYM             年月
     C                     Z-ADDRVNTDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM      ACITEM
     C                     MOVEL'D'       ACDC
      *
     C                     MOVE *OFF      *IN88
     C                     MOVELRVSANO    W#SANO  2
     C           W#SANO    IFEQ 'BU'
     C                     MOVE *ON       *IN88
     C                     MOVEL'1114'    ACNO
     C                     ELSE
     C                     MOVEL'1112'    ACNO
     C                     ENDIF
      *
     C                     MOVEL'C'       ACTYCO
     C                     Z-ADDVAMT      ACAMT
     C                     EXSR CK#SNO
     C                     MOVELRVSANO    ACBKNO
     C                     MOVEL'A'       ACSEPT
     C                     MOVELRVNO      ACDSC1
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
     C                     MOVE UDATE     ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
     C                     MOVE UDATE     ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C                     WRITERACVOUR
     C                     Z-ADDITEM      SIT1    30
     C*
     C**** WRITE TO 1131
     C           ITEM      ADD  1         ITEM1   30
     C                     Z-ADDITEM1     ITEM
     C                     CLEARRACVOUR
     C                     MOVEL'P'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXAR      ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     MOVELYMD       ACYM             年月
     C                     Z-ADDRVNTDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM      ACITEM
     C                     MOVEL'C'       ACDC
     C                     MOVEL'1131'    ACNO
     C                     MOVEL'H'       ACTYCO
     C                     MOVELRVNTNO    ACRLNO
     C                     MOVE RVDUDT    ACDUE
     C                     MOVELRVCUNM    ACCSNM
     C                     Z-ADDVAMT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C                     MOVELRVNO      ACDSC1
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
     C                     MOVE UDATE     ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
     C                     MOVE UDATE     ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#01     BEGSR
     C*
     C                     MOVEL*BLANK    R#NTNO 20
     C                     MOVELRVNTNO    R#NTNO
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD10        LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#01
     C                     ADD  2         LCNT
     CSR                   ENDSR
     OQPRINT  E  103           EH#01
     O                                   12 '報表編號：'
     O                                   18 'ARP016'
     O                                   83 '應收票據兌現代開傳票'
     O                                  140 '列表日期:'
     O                         UDATE Y  148
     O        E  1             EH#01
     O                                   82 '********************'
     O                                  140 '頁　　數:'
     O                         PAG   Z  143
     O        E  1             EH#01
     O                                   73 '入帳日期:'
     O                         YMD       83 '    /  /  '
     O        E  1             EH#01
     O                                   14 '借'
     O        E  1             EH#01
     O*                                  10 '繳款編號'
     O                                   10 '客戶名稱'
     O                                   14 '貸'
     O                                   30 '會　計　科　目'
     O                                   47 '借方金額'
     O                                   67 '貸方金額'
     O                                   80 '到期日期'
     O                                   91 '票據號碼'
     O                                  104 '銀行名稱'
     O                                  128 '票據帳號'
     O                                  140 '存入帳號'
     O                                  153 '傳票編號'
     O                                  161 '部門'
     O        E  1             EH#01
     O                         ADASH    150
     O        E  1             ED#01
     O*                        RVNO       7
     O*                                   8 '-'
     O*                        RVITEM    10
     O                         RVCUNM    10
     O                                   12 'D'
     O                N88                17 '1112'
     O                 88                17 '1114'
     O                N88                27 '銀行存款'
     O                 88                27 '外幣存款'
     O                                   28 'C'
     O                         VAMT  K   50
     O                         F#NAME   116
     O                         RVSANO   141
     O                         TXAR     145
     O                         TXAR     146
     O                         VRNOX    152
     O                                  153 '-'
     O                         SIT1     156
     O                         RVDPNO   162
     O        E  1             ED#01
     O                                   12 'C'
     O                                   17 '1131'
     O                                   27 '應收票據'
     O                                   28 'H'
     O                         VAMT  K   70
9009 O                         RVDUDT    79 '    /  /  '
     O                         R#NTNO   101
     O                         RVPBID   117
     O                         RVPANO   129
     O                         TXAR     145
     O                         TXAR     146
     O                         VRNOX    152
     O                                  153 '-'
     O                         ITEM1    156
     O        E  1             SP#01
     O        E  1             ET#LR
     O                                   52 '應收票據　兌現總額:'
     O                         AMTTT K   70
     O        T   63   LR
     O                                   39 '協理:'
     O                                   80 '課長:'
     O                                  120 '製表:'
