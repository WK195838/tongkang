     H*****************************************************************
     H*
     H*            *****   PROGRAM INTRODUCTION   *****
     H*
     H*            1.PROGRAM-ID    ARP073RS
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02HYS
     H*            4.FUNCTION     外銷訂單單價先前處理
     H*            5.DATE-WRITTEN  89/05/12
     H*            6.UPDATE        89/05/12
     H*****************************************************************
     FARP073S CF  E                    WORKSTN
     FHSMAST  IF  E           K        DISK
     E                    ERR     1   3 70
     I           UDS
     I                                     10011010 S#USER
     I                                     10111020 S#DEVN
     I            DS
     I                                        1   6 W#ORNO
     I                                        1   1 W#AREA
     I                                        2   60W#ORN1
     C**************************************************************
     C*   程式參數接收區
     C**************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P#ORNO  6        *訂單編號
     C                     PARM           P#ORN1  6
     C                     PARM           IN03    1        *結束旗標
     C*****************************************************************
     C*          Key       List
     C*****************************************************************
     C*
     C*FILE => HSMAST
     C*
     C           K#MAST    KLIST
     C                     KFLD           S1OREA           *地區
     C                     KFLD           S1ORNO           *流水號
     C**************************************************************
     C*      MAIN         PROGRAM
     C**************************************************************
     C                     EXSR SR0000
     C           PRCID     DOUEQ'00'
     C           PRCID     CASEQ'01'      SR1000           畫面一
     C                     ENDCS
     C                     ENDDO
     C                     MOVE *ON       *INLR
     C**************************************************************
     C           SR0000    BEGSR
     C**************************************************************
     C                     Z-ADD1         S#ORN1
     C                     MOVEL'01'      PRCID   2
     C                     ENDSR
     C****************************************************************
     C           SR1000    BEGSR
     C****************************************************************
     C*
     C           PRCID     DOWEQ'01'
     C                     EXFMTARP073H
     C                     MOVEL*BLANK    S#ERR
     C           *IN03     IFEQ *ON
     C           *IN12     OREQ *ON
     C                     MOVE '1'       IN03
     C                     MOVE '00'      PRCID
     C                     ENDIF
     C           S#ORNO    IFEQ *BLANK
     C                     MOVEAERR,1     S#ERR
     C                     ITER
     C                     ENDIF
     C                     EXSR SR1100
     C           S#ERR     IFEQ *BLANK
     C                     MOVE '0'       IN03
     C                     MOVE '00'      PRCID
     C                     ENDIF
     C                     ENDDO
     C                     ENDSR
     C****************************************************************
     C           SR1100    BEGSR
     C****************************************************************
     C                     MOVE S#ORNO    W#ORNO
     C                     MOVE W#AREA    S1OREA
     C                     Z-ADDW#ORN1    S1ORNO
     C           K#MAST    CHAINRHSMAST              56
     C           *IN56     IFEQ *OFF
     C           S1KIND    IFNE 'E'
     C                     MOVEAERR,3     S#ERR
     C                     ENDIF
     C           S1KIND    IFEQ 'E'
     C                     MOVELS#ORNO    P#ORNO
     C                     MOVE S#ORN1    P#ORN1
     C                     ENDIF
     C                     ENDIF
     C           *IN56     IFEQ *ON
     C                     MOVEAERR,2     S#ERR
     C                     ENDIF
     C                     ENDSR
     C**************************************************************
** ERR
０１訂單號碼必須輸入!
０２訂單號碼不存在！
０３該客戶非外銷客戶！
