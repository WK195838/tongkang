     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR145R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION     外銷應收帳款查詢報表
     H*            5.DATE-WRITTEN  108/08/05 2019AR00053
     H*            6.UPDATE  DATE
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARSTATL1IP  E           K        DISK
     FSAMAST  IF  E           K        DISK
     FARR145T O   E             69     PRINTER
     F*
     IARREC       01
     I                                              ARCUNOL1
     I            DS
     I                                        1   6 ARORNO
     I                                        1   1 S1OREA
     I                                        2   60S1ORNO
     I           UDS
     I                                        1   1 T#PROD
     I                                     10011010 T#USID
     I                                     10211021 U#AREA
     C*****************************************************************
     C*              MAIN  PROGRAM
     C*****************************************************************
     C*
     C           KEY01     KLIST
     C                     KFLD           S1OREA
     C                     KFLD           S1ORNO
     C*
     C           *IN99     IFEQ *OFF
     C                     EXSR INISR
     C                     MOVE *ON       *IN99
     C                     ENDIF
     C   L1                EXSR L1CLR
     C   01                EXSR DTLSR
     CL1 99                EXSR L1SR
     CLRN99                EXSR MSGSR
     CLR 99                EXSR LRSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C*
     C                     MOVE *ALL'='   T#LIN2
     C                     Z-ADD0         T1AAMT
     C                     Z-ADD0         T1BAMT
     C                     Z-ADD0         T1EAMT
     C                     Z-ADD0         T1NBAL
     C                     Z-ADD0         T2AAMT
     C                     Z-ADD0         T2BAMT
     C                     Z-ADD0         T2EAMT
     C                     Z-ADD0         T2NBAL
     C                     MOVELT#ORNO    W#ORNO  6
     C                     MOVELT#CUNO    W#CUNO  6
     C                     MOVEL*OFF      *IN60
     C                     MOVEL*OFF      *IN61
     C                     WRITEAR145TH
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C*客戶小計
     C                     Z-ADD0         T1AAMT
     C                     Z-ADD0         T1BAMT
     C                     Z-ADD0         T1EAMT
     C                     Z-ADD0         T1NBAL
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C*
     C                     MOVELARCUNO    T#CUNO
     C                     MOVELARCUNM    T#CUNM
     C                     MOVELARORNO    T#ORNO
     C           KEY01     CHAINRSAMAST              50
     C  N50                MOVELS1CTNO    T#CTNO
     C   50                MOVEL*BLANKS   T#CTNO           購案號碼
     C                     Z-ADDARAAMT    T#AAMT           出貨金額
     C                     Z-ADD0         W#BAMT 120
     C                     Z-ADDARBAMT    W#BAMT
     C                     ADD  AREAMT    W#BAMT
     C                     ADD  ARGAMT    W#BAMT
     C                     ADD  ARHAMT    W#BAMT
     C                     Z-ADDW#BAMT    T#BAMT           預收貨款
     C                     Z-ADD0         W#EAMT 120
     C                     Z-ADDARIAMT    W#EAMT
     C                     ADD  ARJAMT    W#EAMT
     C                     Z-ADDW#EAMT    T#EAMT           繳款金額
     C                     Z-ADDARNBAL    T#NBAL           應收餘額
     C*
     C                     ADD  T#AAMT    T1AAMT
     C                     ADD  T#AAMT    T2AAMT
     C                     ADD  T#BAMT    T1BAMT
     C                     ADD  T#BAMT    T2BAMT
     C                     ADD  T#EAMT    T1EAMT
     C                     ADD  T#EAMT    T2EAMT
     C                     ADD  T#NBAL    T1NBAL
     C                     ADD  T#NBAL    T2NBAL
     C*
     C           T#ORNO    IFNE W#ORNO
     C                     MOVEL*OFF      *IN61
     C                     MOVELT#ORNO    W#ORNO
     C                     ENDIF
     C           T#CUNO    IFNE W#CUNO
     C                     MOVEL*OFF      *IN60
     C                     MOVELT#CUNO    W#CUNO
     C                     ENDIF
     C                     MOVEL*ALL'-'   T#LIN1
     C   69                WRITEAR145TH
     C   69                MOVEL*OFF      *IN60
     C   69                MOVEL*OFF      *IN61
     C                     MOVE *OFF      *IN69
     C                     WRITEAR145T1
     C                     CLEARAR145T1
     C                     MOVEL*ON       *IN60
     C                     MOVEL*ON       *IN61
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C*
     C   69                WRITEAR145TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR145T2
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C   69                WRITEAR145TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR145TR
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C   69                WRITEAR145TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR145T3
     C                     ENDSR
     C****************************************************************
