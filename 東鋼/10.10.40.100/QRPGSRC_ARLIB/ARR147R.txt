     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR147R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION     訂單別出貨表─交貨地點
     H*            5.DATE-WRITTEN  113/04/26 2024AR00015
     H*            6.DATE-UPDATED
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTL  IP  E           K        DISK
     FSAVOURL5IF  E           K        DISK
     FSADLVY  IF  E           K        DISK
     FINVMST  IF  E           K        DISK
     FINVTRLL1IF  E           K        DISK
     FARR147F O   E           K        DISK
     F*****************************************************************
     I            DS
     I                                        1   8 TXNO
     I                                        1   1 D#AREA
     I                                        2   3 D#PCOD
     I                                        4   8 D#NO
     I           UDS
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     C****************************************************************
     C*          KEY       LIST
     C****************************************************************
     C           KEY01     KLIST
     C                     KFLD           K#VREA  1
     C                     KFLD           K#PCOD  2        進出別
     C                     KFLD           K#VRNO  50       磅單編號
     C                     KFLD           K#VRTM  20       磅單序號
     C*
     C           KEY02     KLIST
     C                     KFLD           K#VREA  1        地區別
     C                     KFLD           K#PCOD  2        進出別
     C                     KFLD           K#VRNO  50       磅單編號
     C*
     C           KEY03     KLIST
     C                     KFLD           K#AREA  1        填單廠區
     C                     KFLD           K#DVNO  50       流水號
     C                     KFLD           K#ITEM  30       項次
     C*
     C           KEY04     KLIST
     C                     KFLD           TXNO
     C                     KFLD           TXITEM
     C*
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C*
     C                     CLEARWFREC
     C                     MOVELTXCUNO    WFCUNO
     C                     MOVELTXORNO    WFORNO
     C                     Z-ADDTXDATE    WFDATE           日期
     C                     MOVELTXNO      WFWTNO           磅單編號
     C                     Z-ADDTXITEM    WFVRTM
     C                     MOVELTXPDNM    WFPDNM           品名
     C                     Z-ADDTXQTY     WFQTY
     C                     Z-ADDTXAMT     WFIVAM
     C                     MOVELTXIVNO    WFIVNO
     C*
     C*聯絡人資料及工地地點
     C*
     C                     MOVELD#AREA    K#VREA
     C                     MOVELD#PCOD    K#PCOD
     C                     MOVE D#NO      K#VRNO
     C                     Z-ADD0         K#VRTM
     C*
     C                     MOVEL*OFF      *IN40
     C           KEY01     SETLLRSRVOUR
     C           *IN40     DOWEQ*OFF
     C           KEY02     READERSRVOUR             N    40
     C   40                LEAVE
     C           TXDATE    IFNE S4DATE
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVELS4AREA    K#AREA
     C                     Z-ADDS4DVNO    K#DVNO
     C                     Z-ADDS4ITEM    K#ITEM
     C                     MOVEL*OFF      *IN41
     C           KEY03     CHAINRSADLVY             N41
     C  N41                MOVELS2PLE1    WFPLE1           工地地點
     C  N41                MOVELS2MSMN    WFMSMN           聯絡人
     C   41                MOVEL*BLANKS   WFPLE1
     C   41                MOVEL*BLANKS   WFMSMN
     C                     LEAVE
     C                     ENDDO
     C*
     C*試算單號
     C*
     C                     MOVEL*OFF      *IN41
     C           KEY04     SETLLRINVTRL
     C           *IN41     DOWEQ*OFF
     C           KEY04     READERINVTRL             N    41
     C   41                LEAVE
     C           TXDATE    IFNE ITTXDE
     C                     ITER
     C                     ENDIF
     C                     MOVELITNO      WFVTNO
     C                     ENDDO
     C*
     C*請款單號
     C*
     C           TXIVNO    IFNE *BLANKS
     C           TXIVNO    CHAININREC               N41
     C  N41                MOVELINAPNO    WFAPNO
     C   41                MOVEL*BLANKS   WFAPNO
     C                     ENDIF
     C*
     C                     WRITEWFREC
     C*
