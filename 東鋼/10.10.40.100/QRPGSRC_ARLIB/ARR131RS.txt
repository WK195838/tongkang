     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR131RS
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02LBU
     H*            4.FUNCTION     外銷出口報單核對明細表
     H*            5.DATE-WRITTEN  97/02/19
     H*            6.DATE-MODIFY
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR131S CF  E                    WORKSTN
     FHSCINV  IF  E           K        DISK
     FCBCUSTL1IF  E           K        DISK
     E*----------------------------------------------------------------
     E                    ERR     1   9 70
     I*****************************************************************
     I            DS
     I                                        1   80D#DATS
     I                                        1   60D#YMS
     I                                        7   80D#DAYS
     I            DS
     I                                        1   80D#DATE
     I                                        1   60D#YME
     I                                        7   80D#DAYE
     I            DS
     I                                        1   60D#SYM
     I                                        1   40D#YYS
     I                                        5   60D#MMS
     I            DS
     I                                        1   60D#EYM
     I                                        1   40D#YYE
     I                                        5   60D#MME
     I           UDS
     I                                     10011010 S#USER
     I                                     10111020 S#DEVN
     C*****************************************************************
     C*          參數接收
     C*****************************************************************
     C           *ENTRY    PLIST
     C           *IN03     PARM *IN03     IN03    1
     C                     PARM           BDATS   80
     C                     PARM           EDATS   80
     C                     PARM           ORNO    6
     C                     PARM           CUNM   10
     C*****************************************************************
     C*          主程式開始
     C*****************************************************************
     C           *IN03     DOUEQ'1'
     C                     EXFMTAR131F1
     C   KC                EXSR KC#01
     C                     EXSR CK#01
     C  N99                SETON                     LR
     C  N99                RETRN
     C                     ENDDO
     C*****************************************************************
     CSR         KC#01     BEGSR
     C*****************************************************************
     C                     MOVE '1'       *IN03
     C                     SETON                     LR
     C                     RETRN
     C*
     CSR                   ENDSR
     C*****************************************************************
     C*          SCREEN 01 CHECK
     C*****************************************************************
     CSR         CK#01     BEGSR
     C*
     C                     MOVEA'000'     *IN,31
     C                     MOVEA'0000'    *IN,41
     C                     SETOF                     99
     C                     MOVEL*BLANK    S#ERR
     C*
     C           S#ORNO    IFNE *BLANK
     C           S#YMS     IFNE 0
     C           S#YME     ORNE 0
     C           S#CUNM    ORNE *BLANK
     C                     SETON                     99
     C                     SETON                     414344
     C                     MOVELERR,8     S#ERR
     C                     GOTO CK#01Z
     C                     ENDIF
     C*
     C* CHECK訂單號碼
     C*
     C           S#ORNO    CHAINRHSCINV              31
     C           *IN31     IFEQ *ON
     C                     SETON                     9942
     C                     MOVELERR,1     S#ERR
     C                     GOTO CK#01Z
     C                     ENDIF
     C*
     C                     ELSE
     C           S#YMS     IFEQ 0
     C           S#YME     OREQ 0
     C*          S#CUNM    OREQ *BLANK
     C                     SETON                     99
     C                     SETON                     4143
     C                     MOVELERR,9     S#ERR
     C                     GOTO CK#01Z
     C                     ENDIF
     C*
     C* CHECK起始年月
     C*
     C           S#YMS     IFEQ 0
     C                     SETON                     9941
     C                     MOVELERR,5     S#ERR
     C                     GOTO CK#01Z
     C                     ENDIF
     C*
     C                     Z-ADDS#YMS     D#SYM
     C           D#YYS     IFLE 0
     C           D#MMS     ORLE 0
     C           D#MMS     ORGT 12
     C                     SETON                     9941
     C                     MOVELERR,2     S#ERR
     C                     GOTO CK#01Z
     C                     ENDIF
     C*
     C* CHECK終止年月
     C*
     C           S#YME     IFEQ 0
     C                     SETON                     9943
     C                     MOVELERR,6     S#ERR
     C                     GOTO CK#01Z
     C                     ENDIF
     C*
     C                     Z-ADDS#YME     D#EYM
     C           D#YYE     IFLE 0
     C           D#MME     ORLE 0
     C           D#MME     ORGT 12
     C                     SETON                     9943
     C                     MOVELERR,3     S#ERR
     C                     GOTO CK#01Z
     C                     ENDIF
     C*
     C*起始年月不可大於終止年月
     C*
     C           S#YMS     IFGT S#YME
     C                     SETON                     994143
     C                     MOVELERR,4     S#ERR
     C                     GOTO CK#01Z
     C                     ENDIF
     C*
     C* CHECK客戶名稱
     C*
     C           S#CUNM    IFNE *BLANK
     C           S#CUNM    CHAINCBREC                31
     C           *IN31     IFEQ *ON
     C                     SETON                     9944
     C                     MOVELERR,7     S#ERR
     C                     GOTO CK#01Z
     C                     ENDIF
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     C           S#YMS     IFNE 0
     C                     Z-ADDS#YMS     D#YMS
     C                     Z-ADD1         D#DAYS
     C                     ELSE
     C                     Z-ADD0         D#DATS
     C                     ENDIF
     C*
     C           S#YME     IFNE 0
     C                     Z-ADDS#YME     D#YME
     C                     Z-ADD31        D#DAYE
     C                     ELSE
     C                     Z-ADD0         D#DATE
     C                     ENDIF
     C*
     C                     Z-ADDD#DATS    BDATS
     C                     Z-ADDD#DATE    EDATS
     C                     MOVELS#ORNO    ORNO
     C                     MOVELS#CUNM    CUNM
     C*
     CSR         CK#01Z    ENDSR
     C*****************************************************************
** ERR
訂單號碼錯誤．
起始年月錯誤．
終止年月錯誤．
日期範圍錯誤．
起始年月不可空白．
終止年月不可空白．
客戶名稱錯誤．
訂單號碼不為空白時，其它欄位值不可輸入．
訂單號碼為空白時，其它欄位值必需輸入．
