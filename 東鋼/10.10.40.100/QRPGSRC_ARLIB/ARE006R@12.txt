     H        1   Y                                     1                 BR003
     FARE006S CF  E                    WORKSTN
     F                                        RRN   KSFILE SF1
9802 F                                        RRN2  KSFILE SF2
     FTRNDTL  UF  E           K        DISK                      A
     FINVMST  UF  E           K        DISK
     FINVMSTL2IF  E           K        DISK
     F            INREC                             KRENAMEINREC2
     FINVDTL  UF  E           K        DISK                      A
     FINVDTLL2IF  E           K        DISK
     F            IVREC                             KRENAMEIVREC2
     FHIPROD  IF  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FGENSEQ  UF  E           K        DISK                      A
     E*************************************************************
     E                    ERR     1  40 70
     I*************************************************************
     IIVREC2
     I              IVDECD                          F#DECD
     I              IVACDT                          F#ACDT
     I              IVACNT                          F#ACNT
     I              IVNO                            F#NO
     I              IVPDCD                          F#PDCD
     I              IVQTY                           F#QTY
     I              IVAMT                           F#AMT
     I*
     IINREC2
     I              INCUNO                          F#CUNO
     I              INCUNM                          F#CUNM
     I*
     I            DS
     I                                        1   6 CUST
     I                                        1   5 CUST1
     I                                        6   6 CUST2
     I                                       11  20 GRP
     I                                       11  11 GE1
     I                                       11  20 GE2
     I                                       21  26 NOG
     I                                       21  21 NOA
     I                                       22  260NOB
     IBBDS        DS
     I                                        1  15 TXRESV
9710 I                                        1   6 TXACYM
     I                                       15  15 TXWHY
     I                                       21  25 IVRESV
9710 I                                       21  25 IVACYM
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           GEKEY     KLIST
     C                     KFLD           GEKIND
     C                     KFLD           GEPRIN
     C           TXKEY     KLIST
     C                     KFLD           TXCODE
     C                     KFLD           TXNO
     C                     KFLD           TXITEM
     C           TX1KEY    KLIST
     C                     KFLD           TXCODE
     C                     KFLD           TXNO
     C           IV2KEY    KLIST
     C                     KFLD           IVNO
     C                     KFLD           IVACNT
     C           IVKEY     KLIST
     C                     KFLD           IVNO
     C                     KFLD           IVACNT
     C                     KFLD           IVITEM
     C           CBKEY     KLIST
     C                     KFLD           CUST
     C**************************************************************
     C  N90                MOVE '1'       SCRN    1
     C  N90                MOVE '1'       *IN,90
     C**************************************************************
     C           *IN03     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
9802 C           SCRN      CASEQ'3'       SR#03
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     CSR         SR#01     BEGSR
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C                     EXSR CK#01
     C  N99      CODE      IFEQ '6'
     C                     CALL 'ARR033P'
     C                     PARM           NO
     C                     FREE 'ARR033P'
     C                     MOVEL'1'       SCRN
     C                     MOVELERR,17    ERRMSG
     C                     ELSE
     C                     MOVE '2'       SCRN
     C                     EXSR PR#02
     C                     ENDIF
     C   99                MOVE '1'       SCRN
     CSR         SR#01Z    ENDSR
     C******
     CSR         SR#02     BEGSR
     C*
     C                     WRITEUND01
     C                     EXFMTSFCTL1
     C*
     C   KC                EXSR KC#01
     C   KL                MOVE '1'       SCRN
     C   KL                GOTO SR#02Z
     C   KJ                EXSR KJ#02
     C   KJ                GOTO SR#02Z
     C   08                EXSR KH#02                      載入
     C   08                GOTO SR#02Z
     C*
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C*
     C                     ELSE
     C           CODE      IFEQ '5'
     C                     EXSR CK#05
     C                     ENDIF
     C*
     C           CODE      IFEQ '4'
     C                     MOVE '1'       SCRN
     C                     ENDIF
     C                     ENDIF
     C*
     CSR         SR#02Z    ENDSR
     C*****************************************************************
     CSR         KC#01     BEGSR
     C*****************************************************************
     C                     SETON                     LR
     C                     RETRN
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         KJ#02     BEGSR
     C*****************************************************************
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C                     ENDIF
     C           CODE      IFEQ '5'
     C                     EXSR CK#05
     C                     ENDIF
     C  N99                EXSR FL#02
     C  N99                MOVE '1'       SCRN
     C*
     CSR         KJ#02Z    ENDSR
     C*****************************************************************
     CSR         KH#02     BEGSR
     C*****************************************************************
     C                     MOVEL'3'       SCRN
     C                     EXSR SR3100                     初始畫面三
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         SR#03     BEGSR
     C*****************************************************************
     C           S#SFN2    IFEQ 0
     C                     SETOF                     81
     C                     SETON                     8274
     C                     MOVELERR,31    S#ERR
     C                     ELSE                            有資料
     C                     SETOF                     8182
     C                     SETON                     74
     C                     ENDIF
     C*
     C                     WRITEUND02
     C                     EXFMTSFCTL2                     螢幕輸入
     C*
     C                     MOVE *BLANK    S#ERR
     C           S#CRN2    IFNE 0
     C                     Z-ADDS#CRN2    S#NBR2           記錄位置
     C                     ENDIF
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C                     EXSR KC#01                      結束
     C           *IN12     WHEQ '1'
     C                     MOVE '2'       SCRN             回前畫面
     C                     EXSR SR3110                     清挑選條件
     C           *IN05     WHEQ '1'
     C                     EXSR SR3400                     全部挑選
     C           *IN10     WHEQ '1'
     C                     EXSR SR3220                     重新合計
     C                     EXSR SR3200                     存檔
     C                     EXSR SR3110                     清挑選條件
     C                     OTHER
     C                     EXSR SR3300                     執行
     C                     ENDSL
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         SR3100    BEGSR
     C*****************************************************************
     C*INISR畫面三
     C                     MOVEL*BLANK    S#ERR
     C                     Z-ADD1         S#NBR2
     C                     SETON                     8182
     C                     WRITESFCTL2
     C*
     C                     Z-ADD0         S#SFN2
     C                     Z-ADD0         RRN2
     C                     Z-ADD0         S#SQTY
     C                     Z-ADD0         S#SAMT
     C                     Z-ADD0         S#STAX
     C                     MOVEL*BLANK    S#OPT
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         SR3110    BEGSR
     C**************************************************************
     C*清空畫面三挑選條件資料
     C*
     C                     MOVEL*BLANK    S#ORNO           訂單號碼
     C                     MOVEL*BLANK    W#ORNO
     C                     MOVEL*BLANK    S#ACNT           種類
     C                     MOVEL*BLANK    S#RESV           原因
     C                     MOVEL*BLANK    W#FLAG
     C                     Z-ADD0         S#UPRC
     C                     Z-ADD0         S#ACDS
     C                     Z-ADD0         W#ACDS
     C                     Z-ADD0         S#ACDE
     C                     Z-ADD0         W#ACDE
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         SR3200    BEGSR
     C**************************************************************
     C                     Z-ADD0         RRN2    30
     C                     Z-ADD0         RRN
     C                     Z-ADD0         W#SVN2  20
     C*
     C                     DO   S#SFN2
     C                     ADD  1         RRN2
     C           RRN2      CHAINSF2                  30
     C*
     C           S#OPT     IFEQ ' '
     C                     ITER
     C                     ENDIF
     C*
     C           1         DOWEQ1
     C                     ADD  1         RRN
     C*
     C           RRN       IFGT 10
     C                     MOVELERR,32    ERRMSG           項次超過
     C                     MOVEL'2'       SCRN
     C                     GOTO EN3200
     C                     ENDIF
     C*
     C           RRN       CHAINSF1                  30
     C           INVOX     IFEQ *BLANK
     C                     EXSR SR3210                     轉換內容
     C                     ADD  1         W#SVN2
     C                     UPDATSF1
     C                     LEAVE
     C                     ELSE
     C                     EXSR SR3211                     金額重新算
     C                     UPDATSF1
     C                     ENDIF
     C                     ENDDO
     C*
     C                     ENDDO
     C                     MOVELERR,33    ERRMSG
     C                     MOVELW#SVN2    W#SVC2  2
     C           ERRMSG    CAT  W#SVC2:0  ERRMSG
     C           ES3200    TAG
     C*
     C                     MOVEL'2'       SCRN
     C*
     CSR         EN3200    ENDSR
     C**************************************************************
     CSR         SR3210    BEGSR
     C**************************************************************
     C*畫面三存檔搬移變數
     C*
     C                     Z-ADDRRN       ITEM             項次
     C                     Z-ADDS#QTY     QTY              數量
     C                     Z-ADDS#UPRC    UPRC             單價
     C                     MOVELS#INNO    INVOX            發票號碼
     C                     MOVELS#ACNT    ACNT             種類別
     C                     MOVELS#PDNO    PDNM             折讓品名
     C                     MOVELS#RESV    WHY              原因
     C                     MOVEL'A'       FLAG              FLAG碼
     C*
     C           UPRC      MULT QTY       AMT       H      金額
     C           AMT       MULT 0.05      TAX       H      稅額
     C*
     C                     ADD  AMT       TTLAMT
     C                     ADD  TAX       TTLTAX
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         SR3211    BEGSR
     C**************************************************************
     C           UPRC      MULT QTY       AMT       H      金額
     C           AMT       MULT 0.05      TAX       H      稅額
     C*
     C           RRN       IFEQ 1
     C                     Z-ADD0         TTLAMT
     C                     Z-ADD0         TTLTAX
     C                     ENDIF
     C*
     C                     ADD  AMT       TTLAMT
     C                     ADD  TAX       TTLTAX
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         SR3220    BEGSR
     C**************************************************************
     C                     Z-ADD1         RRN
     C                     Z-ADD1         RRN2
     C                     Z-ADD0         TTLTAX
     C                     Z-ADD0         TTLAMT
     C*
     C*重新合計畫面二金額、稅額
     C*
     C           1         DOWEQ1
     C           RRN       CHAINSF1                  31
     C*
     C   31                LEAVE
     C*
     C                     ADD  1         RRN
     C                     ADD  AMT       TTLAMT
     C                     ADD  TAX       TTLTAX
     C                     ENDDO
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         SR3300    BEGSR
     C**************************************************************
     C*畫面三執行
     C*
     C*訂單或起訖日期不同時，則重新讀取資料
     C*
     C           S#ORNO    IFNE W#ORNO
     C           S#ACDS    ORNE W#ACDS
     C           S#ACDE    ORNE W#ACDE
     C                     MOVEL' '       W#FLAG  1
     C                     MOVELS#ORNO    W#ORNO  6
     C                     Z-ADDS#ACDS    W#ACDS  80
     C                     Z-ADDS#ACDE    W#ACDE  80
     C                     ELSE
     C                     MOVEL'X'       W#FLAG
     C                     ENDIF
     C*
     C           S#ORNO    IFEQ *BLANK                     訂單號碼
     C           S#ACNT    OREQ *BLANK                     種類別
     C           S#RESV    OREQ *BLANK                     原因
     C           S#ACDS    OREQ 0                          發票起日
     C           S#ACDE    OREQ 0                          發票止日
     C           S#UPRC    OREQ 0                          平均單價
     C           W#FLAG    OREQ 'X'
     C                     GOTO EN3300
     C                     ELSE
     C                     EXSR SR3100
     C                     ENDIF
     C*
     C           S#ORNO    SETLLIVREC2
     C           1         DOWEQ1
     C           S#ORNO    READEIVREC2              N    30
     C   30                LEAVE
     C*
     C           F#ACDT    IFLT S#ACDS
     C           F#ACDT    ORGT S#ACDE
     C           F#DECD    OREQ 'D'
     C           F#ACNT    ORNE '1'
     C                     ITER
     C                     ENDIF
     C*
     C                     ADD  1         S#SFN2
     C                     ADD  1         RRN2
     C                     Z-ADDRRN2      S#ITEM           項次
     C                     MOVELF#NO      S#INNO           發票號碼
     C                     MOVELF#PDCD    S#PDNO           品名
     C                     Z-ADDF#QTY     S#QTY            數量
     C                     Z-ADDF#AMT     S#AMT            金額
     C           F#AMT     MULT 0.05      S#TAX     H      稅額
     C*
     C                     WRITESF2
     C                     ADD  S#QTY     S#SQTY           數量小計
     C                     ADD  S#AMT     S#SAMT           金額小計
     C                     ADD  S#TAX     S#STAX           稅額小計
     C                     ENDDO
     C*
     C                     MOVELS#ORNO    ORNO             訂單編號
     C                     MOVEL*OFF      *IN30
     C           S#ORNO    CHAININREC2              N30
     C  N30                MOVELF#CUNO    CUNO             客戶編號
     C  N30                MOVELF#CUNM    CUNM             客戶名稱
     C*
     CSR         EN3300    ENDSR
     C**************************************************************
     CSR         SR3400    BEGSR
     C**************************************************************
     C*畫面三發票資料全部挑選
     C*
     C                     Z-ADD0         RRN2
     C                     MOVE *OFF      *IN34
     C*
     C           1         DOWEQ1
     C                     ADD  1         RRN2
     C           RRN2      CHAINSF2                  34
     C*
     C   34                LEAVE
     C*
     C                     MOVEL'Y'       S#OPT
     C                     UPDATSF2
     C                     ENDDO
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         CK#01     BEGSR
     C*****************************************************************
     C                     SETOF                     414299
     C                     SETOF                     4369
     C                     MOVE *BLANK    ERRMSG
     C*
     C                     SELEC
     C           CODE      WHEQ ' '
     C                     SETON                     4199
     C                     MOVE ERR,1     ERRMSG
     C           CODE      WHEQ '1'
     C                     MOVE '新增'  MOD
     C                     MOVEA'01'      *IN,71
     C           CODE      WHEQ '2'
     C                     MOVE '更正'  MOD
     C                     MOVEA'01'      *IN,71
     C           CODE      WHEQ '3'
     C                     MOVE '刪除'  MOD
     C                     MOVEA'11'      *IN,71
     C                     MOVE '1'       *IN69
     C           CODE      WHEQ '4'
     C                     MOVE '查詢'  MOD
     C                     MOVEA'11'      *IN,71
     C                     MOVE '1'       *IN69
     C           CODE      WHEQ '5'
     C                     MOVE '確認'  MOD
     C                     MOVEA'10'      *IN,71
     C                     Z-ADDUDATE     ACDT
     C           CODE      WHEQ '6'
     C                     MOVE '列印'  MOD
     C                     MOVEA'11'      *IN,71
     C                     OTHER
     C                     SETON                     4199
     C                     MOVE ERR,2     ERRMSG
     C                     ENDSL
     C******
     C           CODE      IFNE '1'
     C           NO        IFEQ *BLANK
     C                     SETON                     4299
     C                     MOVE ERR,3     ERRMSG
     C                     ENDIF
     C                     ELSE
     C           NO        IFNE *BLANK
     C                     SETON                     4299
     C                     MOVE ERR,18    ERRMSG
     C                     ENDIF
     C                     ENDIF
     C**
     C           CODE      IFEQ '2'
     C           CODE      OREQ '3'
     C           CODE      OREQ '5'
     C           CODE      OREQ '6'
     C                     MOVELNO        NOAREA  1
     C           NOAREA    IFNE TXAR
     C                     SETON                     4299
     C                     MOVE ERR,23    ERRMSG
     C                     ENDIF
     C                     ENDIF
     C**
     C           *IN99     IFEQ '0'
     C                     MOVEL'AR06'    TXCODE
     C                     MOVELNO        TXNO
     C                     Z-ADD1         TXITEM
9007 C*          TXKEY     CHAINTXREC               N97
YSH  C           TXKEY     SETLLTXREC                97
 .   C                     READ TXREC               N    97
 .   C           TXNO      IFNE NO
 .   C                     SETON                     97
 .   C                     ENDIF
     C                     SELEC
     C           CODE      WHEQ '1'
     C           *IN97     ANDEQ'0'
     C                     SETON                     4299
     C                     MOVE ERR,4     ERRMSG
     C           CODE      WHEQ '2'
     C           *IN97     ANDEQ'0'
     C           TXFL01    ANDNE' '
     C                     SETON                     4299
     C                     MOVE ERR,5     ERRMSG
     C           CODE      WHEQ '3'
     C           *IN97     ANDEQ'0'
     C           TXFL01    ANDNE' '
     C                     SETON                     4299
     C                     MOVE ERR,6     ERRMSG
     C           CODE      WHEQ '5'
     C           *IN97     ANDEQ'0'
     C           TXFL01    ANDEQ'Y'
     C                     SETON                     4299
     C                     MOVE ERR,10    ERRMSG
     C           CODE      WHNE '1'
     C           *IN97     ANDEQ'1'
     C                     SETON                     4299
     C                     MOVE ERR,7     ERRMSG
     C                     ENDSL
     C                     ENDIF
     C*
     CSR         CK#01Z    ENDSR
     C*****************************************************************
     CSR         PR#02     BEGSR
     C*****************************************************************
     C                     SETON                     80
     C                     WRITESFCTL1
     C                     SETOF                     80
     C                     Z-ADD0         RRN     20
     C                     MOVE *BLANK    CUNO
     C                     MOVE *BLANK    CUNM
 9401C                     MOVE *BLANK    ORNO
     C                     Z-ADDUDATE     DATE
     C                     Z-ADD0         TTLAMT
     C                     Z-ADD0         TTLTAX
     C**
     C           CODE      CASEQ'1'       PR#ADD
     C                     CAS            PR#OTH
     C                     ENDCS
     CSR         PR#02Z    ENDSR
     C******
     CSR         PR#ADD    BEGSR
     C                     EXSR @GETNO
     C                     CLEARSF1
     C           RRN       DOWLT10
     C                     ADD  1         RRN
     C                     Z-ADDRRN       ITEM
     C                     WRITESF1
     C                     ENDDO
     C                     SETON                     10
     CSR         PR#ADZ    ENDSR
     C******
     CSR         @GETNO    BEGSR
     C                     MOVEL'06'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVE *BLANK    GRP
     C                     MOVELTXAR      GE1
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGEREC               N97
     C   97                Z-ADD0         GECUNO
     C                     MOVELTXAR      NOA
     C           GECUNO    IFEQ 99999
     C                     Z-ADD1         NOB
     C                     ELSE
     C           GECUNO    ADD  1         NOB
     C                     ENDIF
     C                     MOVELNOG       NO
     CSR                   ENDSR
     C***
     CSR         PR#OTH    BEGSR
     C                     MOVELTXCUNO    CUNO
     C                     MOVELTXCUNM    CUNM
 9401C                     MOVELTXORNO    ORNO
     C                     Z-ADDTXDATE    DATE
     C                     Z-ADDTXACDT    ACDT
     C           TXACYM    IFNE *BLANK
9708 C                     MOVE TXACYM    ACYM
     C                     ENDIF
     C           CODE      IFEQ '5'
     C                     Z-ADDUDATE     ACDT
     C                     ENDIF
     C                     MOVEL'AR06'    TXCODE
     C                     MOVELNO        TXNO
     C                     Z-ADD0         TXITEM
     C           TXKEY     SETLLTXREC                97
     C  N97      TX1KEY    READETXREC                    97
     C           *IN97     DOWEQ'0'
     C                     CLEARSF1
     C   71                SETON                     70
     C                     ADD  1         RRN
     C           RRN       IFNE TXITEM
     C           RRN       DOWLTTXITEM
     C                     Z-ADDRRN       ITEM
     C                     WRITESF1
     C                     ADD  1         RRN
     C                     ENDDO
     C                     ENDIF
     C                     Z-ADDTXITEM    ITEM
     C                     MOVELTXACNT    ACNT
9803 C           TXFLAG    IFNE 'D'
     C                     Z-ADDTXACDT    ACDT
     C                     MOVELTXORNO    ORNO
9803 C                     MOVELTXORNO    H#ORNO
9803 C                     ENDIF
     C                     MOVELTXPDNM    PDNM
     C                     MOVELTXIVNO    INVOX
     C                     Z-ADDTXQTY     QTY
     C                     Z-ADDTXUPRC    UPRC
     C                     Z-ADDTXAMT     AMT
     C                     Z-ADDTXTAX     TAX
     C                     MOVELTXRVID    RVID
     C                     MOVELTXSALE    SALE
     C                     MOVELTXSATP    SATP
     C                     MOVELTXIVTP    KIND
     C                     MOVELTXFLAG    FLAG
     C                     MOVELTXWHY     WHY
     C           TXFLAG    IFNE 'D'
     C                     ADD  TXAMT     TTLAMT
     C                     ADD  TXTAX     TTLTAX
     C                     ENDIF
     C                     WRITESF1
     C           TX1KEY    READETXREC                    97
     C                     ENDDO
     C****
     C           CODE      IFEQ '2'
     C                     CLEARSF1
     C           RRN       DOWLE9
     C                     ADD  1         RRN
     C                     Z-ADDRRN       ITEM
     C                     WRITESF1
     C                     ENDDO
     C                     ENDIF
     C                     SETON                     10
     CSR         PR#OTZ    ENDSR
     C******
     CSR         CK#02     BEGSR
     C                     SETOF                     99
     C                     MOVEA'00'      *IN,43
     C                     MOVE *BLANK    ERRMSG
     C                     Z-ADD0         TTLAMT
     C                     Z-ADD0         TTLTAX
9803 C                     Z-ADD0         W#CONT  30
9803 C                     MOVEL*BLANK    W1ORNO  6
     C***
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DATE      P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           FLAG1   1
　 C*開立日期錯誤
     C           FLAG1     IFNE '0'
     C                     SETON                     9944
     C                     MOVELERR,19    ERRMSG
     C                     ENDIF
     C***
     C*客戶編號不存在
     C                     MOVELCUNO      CUST
     C           CBKEY     CHAINCBREC                97
     C   97                SETON                     43
     C   97                MOVELERR,13    ERRMSG
     C  N97                MOVELCBCUNM    CUNM
     C  N97                MOVELCBIVCO    IVCO    1
     C***
     C*
     C                     MOVEL*BLANK    W#AREA  1        發票廠區
     C*
     C                     MOVEL*BLANK    INVOT  10
     C                     Z-ADD0         RRN
     C                     MOVE '0'       *IN95
     C           *IN95     DOWEQ'0'
     C                     MOVEA'00000000'*IN,51
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  95
     C   95                LEAVE
     C   21                MOVELINVOT     INVOX
     C                     MOVELINVOX     INVOT
     C           INVOX     IFEQ *BLANK
     C           ACNT      ANDEQ*BLANK
     C           FLAG      OREQ 'D'
     C                     UPDATSF1
     C                     ITER
     C                     ENDIF
     C***
     C                     SELEC
     C*種類別必須輸入
     C           ACNT      WHEQ ' '
     C                     SETON                     9952
     C                     MOVE ERR,11    ERRMSG
     C*種類別必須為２，３，４，５，６
     C           ACNT      WHNE '2'
     C           ACNT      ANDNE'3'
     C           ACNT      ANDNE'4'
     C           ACNT      ANDNE'5'
     C           ACNT      ANDNE'6'
     C                     SETON                     9952
     C                     MOVE ERR,12    ERRMSG
     C                     ENDSL
     C***
     C*發票號碼必須輸入
     C           INVOX     IFEQ *BLANK
     C                     SETON                     9951
     C                     MOVE ERR,8     ERRMSG
     C                     ENDIF
     C***
     C*此發票號碼不存在
     C           INVOX     CHAININREC               N94
     C   94                SETON                     9951
     C   94                MOVELERR,15    ERRMSG
     C*此發票號碼非屬此客戶
     C  N94      INCUNO    IFNE CUNO
     C                     SETON                     9951
     C                     MOVE ERR,14    ERRMSG
     C                     END
     C*此發票號碼已作廢
     C  N94      INDECD    IFNE ' '
     C                     SETON                     9951
     C                     MOVE ERR,16    ERRMSG
     C                     END
     C*
 9406C*若不為鋼筋類發票，在未押匯情況下不允許開立
  .  C  N94      INNBAL    IFNE 0
  .  C                     MOVELPDNM      W#PDNM  1
  .  C           W#PDNM    IFEQ '3'
  .  C           W#PDNM    OREQ '4'
  .  C           W#PDNM    OREQ '5'
  .  C*                    SETON                     9951
  .  C                     MOVE ERR,30    ERRMSG
  .  C                     ENDIF
 9406C                     ENDIF
     C*
     C*    ----- 檢核同一張折退單內之發票統一編號必須一致-----
     C*
 {   C           W#AREA    IFEQ *BLANK                     第一筆
     C                     MOVELINAREA    W#AREA
     C*
     C                     ELSE
     C                     MOVEL'T'       W#RTNV  1
  {  C                     SELEC
     C           W#AREA    WHEQ 'P'
     C           W#AREA    OREQ 'M'
     C           W#AREA    OREQ 'T'
   { C           INAREA    IFNE 'P'
     C           INAREA    ANDNE'M'
     C           INAREA    ANDNE'T'
     C                     MOVEL'F'       W#RTNV
  }} C                     ENDIF
  {  C           W#AREA    WHEQ 'H'
   { C           INAREA    IFNE 'H'
     C                     MOVEL'F'       W#RTNV
   } C                     ENDIF
  {  C           W#AREA    WHEQ 'K'
   { C           INAREA    IFNE 'K'
     C                     MOVEL'F'       W#RTNV
   } C                     ENDIF
  }  C                     ENDSL
     C*
     C           W#RTNV    IFEQ 'F'                        非同廠區
     C                     SETON                     9951
     C                     MOVELERR,26    ERRMSG
     C                     ENDIF
 }   C                     ENDIF
     C*
     C***
     C* N94      INNBAL    IFLE 0
     C*                    SETON                     9951
     C*                    MOVE ERR,21    ERRMSG
     C*                    END
     C  N94                MOVELINORNO    ORNO
9803 C  N94                MOVELINORNO    H#ORNO
     C  N94                MOVELINRVID    RVID
     C  N94                MOVELINSALE    SALE
     C  N94                MOVELINSATP    SATP
9010 C  N94                MOVE INKIND    KIND
9405 C*
 .   C           ACNT      IFEQ '4'
 .   C           AMT       ANDGT0
 .   C           PDNM      ANDNE*BLANK
 .   C                     SETON                     9958
 .   C                     MOVE ERR,29    ERRMSG
 .   C                     ENDIF
9405 C*
     C***
     C*品名代號不存在
     C           PDNM      IFNE *BLANK
     C           PDNM      CHAINRHIPROD              97
     C   97                SETON                     9958
     C   97                MOVE ERR,9     ERRMSG
     C                     ENDIF
     C***計算金額
     C           AMT       IFEQ 0
     C           QTY       MULT UPRC      AMT       H
     C                     ENDIF
     C***計算稅額
 9305C                     MOVELCUNO      W#CUNO  1
   . C           W#CUNO    IFEQ 'D'
   . C           TAX       ANDEQ0
   . C           AMT       MULT 0.05      TAX       H
 9305C                     ENDIF
     C***
     C                     ADD  AMT       TTLAMT
     C                     ADD  TAX       TTLTAX
     C***
     C*金額不可為０
     C           AMT       IFEQ 0
     C                     SETON                     9955
     C                     MOVELERR,22    ERRMSG
     C                     ENDIF
     C***
     C           INKIND    IFEQ '3'
     C           AMT       MULT 0.05      TMTAX  110
     C           TAX       SUB  TMTAX     DIFAMT 110
     C           DIFAMT    IFGT 2
     C           DIFAMT    ORLT -2
     C                     SETON                     995556
     C                     MOVELERR,25    ERRMSG
     C                     ENDIF
     C                     ENDIF
 9203C******
  .  C*此張發票之折讓金額不可大於發票金額　
  .  C           AMT       IFGT INAAMT
  .  C                     SETON                     995556
  .  C                     MOVELERR,28    ERRMSG
 9203C                     ENDIF
 9803C*
  .  C           H#ORNO    IFNE W1ORNO
  .  C           W1ORNO    IFNE *BLANK
  .  C                     ADD  1         W#CONT
  .  C                     ENDIF
  .  C                     MOVELH#ORNO    W1ORNO
  .  C                     ENDIF
  .  C*
  .  C  N99      W#CONT    IFGT 0
  .  C                     SETON                     9951
  .  C                     MOVELERR,34    ERRMSG
 9803C                     ENDIF
     C******
     C                     UPDATSF1
     C***
     C                     ENDDO
     C******
9101 C*原因詳見CNV013R#X
9101 C  N94      INKIND    IFEQ '2'                        預收發票
9101 C           INRCNO    ANDEQ*BLANK                     預收發票
9101 C           INNBAL    ANDEQTTLAMT
9101 C                     SETON                     9951
9101 C                     MOVE ERR,27    ERRMSG
9101 C                     END
     CSR         CK#02Z    ENDSR
     C***
     CSR         CK#05     BEGSR
     C                     SETOF                     99
     C                     MOVEA'00'      *IN,45
     C                     MOVE *BLANK    ERRMSG
9710 C           ACDT      IFNE 99999999
     C*                    CALL 'C01'
     C*                    PARM           ACDT
     C*                    PARM           FLAG1   1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE ACDT      P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE
     C                     PARM '1'       P#MODE
     C                     PARM           P#MTL
     C                     PARM           P#LY
     C                     PARM           FLAG1
　 C*確認日期錯誤
     C           FLAG1     IFNE '0'
     C                     SETON                     9945
     C                     MOVELERR,20    ERRMSG
     C                     ENDIF
     C                     ENDIF
     C***
　 C*折讓確認日期為９９９９９９９９時，則入帳年月必須輸入
9710 C           ACDT      IFEQ 99999999
     C           ACYM      IFEQ 0
9711 C           ACYM      OREQ 9999
     C                     SETON                     9946
     C                     MOVELERR,24    ERRMSG
     C                     ENDIF
     C                     ELSE
     C                     MOVELACDT      ACYM
     C                     ENDIF
     CSR         CK#05Z    ENDSR
     C******
     CSR         FL#02     BEGSR
     C           CODE      CASEQ'3'       DL#02
     C           CODE      CASEQ'1'       WR#02
     C           CODE      CASEQ'2'       UP#02
     C           CODE      CASEQ'5'       UP#05
     C                     ENDCS
     CSR         FL#02Z    ENDSR
     C****
     CSR         DL#02     BEGSR
     C***
     C                     MOVEL'AR06'    TXCODE
     C                     MOVELNO        TXNO
     C                     Z-ADD0         TXITEM
     C           TXKEY     SETLLTXREC                97
     C  N97      TX1KEY    READETXREC                    97
     C           *IN97     DOWEQ'0'
     C                     MOVEL'D'       TXFLAG
     C                     MOVELTXAR      TXTXAR
     C                     Z-ADDUDATE     TXTXDT
     C                     UPDATTXREC
     C           TX1KEY    READETXREC                    97
     C                     ENDDO
     CSR         DL#02Z    ENDSR
     *****
     CSR         WR#02     BEGSR
     C                     CLEARGEREC
     C                     MOVEL'06'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVE *BLANK    GRP
     C                     MOVELTXAR      GE1
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGEREC                97
     C   97                Z-ADD1         GECUNO
     C   97                WRITEGEREC
     C           *IN97     IFEQ '0'
     C           GECUNO    IFEQ 99999
     C                     Z-ADD1         GECUNO
     C                     ELSE
     C                     ADD  1         GECUNO
     C                     ENDIF
     C                     UPDATGEREC
     C                     ENDIF
     C                     MOVELTXAR      NOA
     C                     Z-ADDGECUNO    NOB
     C                     MOVELNOG       NO
     C***
     C                     SETOF                     95
     C                     Z-ADD0         RRN
     C           *IN95     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  95
     C   95                LEAVE
     C***
     C           INVOX     IFEQ *BLANK
     C           ACNT      ANDEQ*BLANK
     C           FLAG      OREQ 'D'
     C                     ITER
     C                     ENDIF
     C****
     C                     EXSR WR#TX
     C***
     C                     ENDDO
     C***
     CSR         WR#02Z    ENDSR
     *****
     CSR         UP#02     BEGSR
     C***
     C                     Z-ADD0         RRN
     C                     SETOF                     95
     C           *IN95     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  95
     C   95                LEAVE
     C                     MOVEL'AR06'    TXCODE
     C                     MOVELNO        TXNO
     C                     Z-ADDRRN       TXITEM
     C           TXKEY     CHAINTXREC                96
     C           INVOX     IFEQ *BLANK
     C           PDNM      ANDEQ*BLANK
     C           FLAG      OREQ 'D'
     C   96                ITER
     C                     MOVE 'D'       TXFLAG
     C                     MOVE TXAR      TXTXAR
     C                     Z-ADDUDATE     TXTXDT
     C                     UPDATTXREC
     C                     ELSE
     C  N96                EXSR UP#TX
     C   96                EXSR WR#TX
     C                     ENDIF
     C***
     C                     ENDDO
     C***
     CSR         UP#02Z    ENDSR
     C***
     CSR         UP#05     BEGSR
     C                     Z-ADD0         RRN
     C                     SETOF                     95
     C           *IN95     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  95
     C   95                LEAVE
     C                     MOVEL'AR06'    TXCODE
     C                     MOVELNO        TXNO
     C                     Z-ADDRRN       TXITEM
     C           TXKEY     CHAINTXREC                97
     C           *IN97     IFEQ '1'
     C           TXFLAG    OREQ 'D'
     C                     ITER
     C                     ENDIF
     C  N97                MOVE 'C'       TXFLAG
     C  N97                MOVE 'Y'       TXFL01
     C  N97                MOVE 'Y'       TXFL02
     C  N97                Z-ADDACDT      TXACDT
9708 C  N97                MOVELACYM      TXACYM
     C  N97                MOVELTXAR      TXTXAR
     C  N97                Z-ADDUDATE     TXTXDT
     C  N97                UPDATTXREC
     C***
     C                     SETOF                     9192
     C                     MOVELINVOX     IVNO
     C                     MOVELACNT      IVACNT
     C                     Z-ADD0         IVITEM
     C           IV2KEY    SETLLIVREC                91
     C           *IN91     DOWEQ'0'
     C  N91      IV2KEY    READEIVREC               N    91
     C                     ENDDO
     C                     Z-ADDIVITEM    IVIT@   20
     C                     ADD  1         IVIT@
     C                     MOVELACNT      ACNT@   1
     C           AMT       MULT -1        IVAMT@ 110
     C***
     C                     CLEARIVREC
     C                     MOVEL'A'       IVFLAG
     C                     MOVELINVOX     IVNO
     C                     Z-ADDIVIT@     IVITEM
     C                     MOVELACNT@     IVACNT
     C                     Z-ADDACDT      IVACDT
     C                     MOVELORNO      IVORNO
     C                     MOVELPDNM      IVPDCD
     C                     Z-SUBQTY       IVQTY
     C                     Z-ADDUPRC      IVUPRC
     C                     Z-ADDIVAMT@    IVAMT
     C                     MOVELNO        IVAPNO
     C           ACNT      IFEQ '2'
     C                     MOVEL'C'       IVFL03
     C                     END
     C           ACNT      IFEQ '3'
     C                     MOVEL'D'       IVFL03
     C                     END
     C           ACNT      IFEQ '4'
     C           AMT       IFLT 0
     C                     MOVE 'H'       IVFL03
     C                     ELSE
     C                     MOVE 'G'       IVFL03
     C                     END
     C                     END
     C                     MOVEL'Y'       IVFL02
     C                     Z-ADDUDATE     IVTXDT
     C                     MOVELTXAR      IVTXAR
9708 C                     MOVE ACYM      IVACYM
     C                     WRITEIVREC
     C****
     C*9112新增判斷該發票之課稅別
9112 C           INVOX     CHAININREC               N91
9112 C           INTXTP    IFEQ '1'                        應稅
     C*
     C                     MOVELINVOX     IVNO
     C                     MOVEL'5'       IVACNT
     C                     Z-ADD0         IVITEM
     C           IV2KEY    SETLLIVREC                91
     C           *IN91     DOWEQ'0'
     C  N91      IV2KEY    READEIVREC               N    91
     C                     ENDDO
     C                     Z-ADDIVITEM    IVIT@   20
     C                     ADD  1         IVIT@
     C                     MOVEL'5'       ACNT@   1
     C           TAX       MULT -1        IVAMT@ 110
     C                     CLEARIVREC
     C                     MOVEL'A'       IVFLAG
     C                     MOVELINVOX     IVNO
     C                     Z-ADDIVIT@     IVITEM
     C                     MOVELACNT@     IVACNT
     C                     Z-ADDACDT      IVACDT
     C                     MOVELORNO      IVORNO
     C                     Z-ADD0         IVQTY
     C                     Z-ADD0         IVUPRC
     C                     Z-ADDIVAMT@    IVAMT
     C                     MOVELNO        IVAPNO
     C           ACNT      IFEQ '2'
     C                     MOVEL'B'       IVPDCD
     C                     MOVEL'C'       IVFL03
     C                     END
     C           ACNT      IFEQ '3'
     C                     MOVEL'C'       IVPDCD
     C                     MOVEL'D'       IVFL03
     C                     END
     C           ACNT      IFEQ '4'
     C           AMT       IFLT 0
     C                     MOVEL'A'       IVPDCD
     C                     MOVE 'H'       IVFL03
     C                     ELSE
     C                     MOVEL'C'       IVPDCD
     C                     MOVE 'G'       IVFL03
     C                     END
     C                     END
     C                     MOVEL'Y'       IVFL02
     C                     Z-ADDUDATE     IVTXDT
     C                     MOVELTXAR      IVTXAR
9708 C                     MOVE ACYM      IVACYM
     C                     WRITEIVREC
9112 C                     ELSE
9112 C                     Z-ADD0         TAX
9112 C                     ENDIF
     C****
     C***
     C           INVOX     CHAININREC                93
     C           *IN93     IFEQ '0'
     C           ACNT      IFEQ '2'
     C           AMT       MULT -1        AMTY   110
     C           TAX       MULT -1        TAXY   110
     C                     ADD  AMTY      INCAMT
     C                     ADD  TAXY      INCTAX
     C                     END
     C           ACNT      IFEQ '3'
     C           AMT       MULT -1        AMTZ   110
     C           TAX       MULT -1        TAXZ   110
     C                     ADD  AMTZ      INCAMT
     C                     ADD  TAXZ      INCTAX
     C                     END
     C           ACNT      IFEQ '4'
     C           AMT       MULT -1        AMTR   110
     C           TAX       MULT -1        TAXR   110
     C           INTYPE    IFNE '2'
     C                     ADD  AMTR      INDAMT
     C                     ADD  TAXR      INDTAX
     C                     ELSE
     C                     ADD  AMTR      INCAMT
     C                     ADD  TAXR      INCTAX
     C                     ENDIF
     C                     ENDIF
     C           INAAMT    ADD  INATAX    INNBAL
     C                     ADD  INBAMT    INNBAL
     C                     ADD  INCAMT    INNBAL
     C                     ADD  INCTAX    INNBAL
     C                     ADD  INDAMT    INNBAL
     C                     ADD  INDTAX    INNBAL
     C                     ADD  INEAMT    INNBAL
     C                     ADD  INFAMT    INNBAL
     C           INNBAL    IFEQ 0
     C                     MOVE 'Y'       INFL01
     C                     ENDIF
     C                     MOVE 'C'       INFLAG
     C                     MOVELTXAR      INTXAR
     C                     Z-ADDUDATE     INTXDT
     C                     UPDATINREC
     C                     ENDIF
     C                     ENDDO
     CSR         UP#05Z    ENDSR
     C***************************
     C***  WRITE  TO  TRNDTL  ***
     C***************************
     CSR         WR#TX     BEGSR
     C                     CLEARTXREC
     C                     MOVEL'AR06'    TXCODE
     C                     MOVELNO        TXNO
     C                     MOVEL'A'       TXFLAG
     C                     Z-ADDRRN       TXITEM
     C                     MOVELACNT      TXACNT
     C                     MOVELORNO      TXORNO
     C                     MOVELRVID      TXRVID
     C                     MOVELSALE      TXSALE
     C                     MOVELKIND      TXIVTP
     C                     MOVELSATP      TXSATP
     C                     Z-ADDDATE      TXDATE
     C                     MOVELCUNO      TXCUNO
     C                     MOVELCUNM      TXCUNM
     C                     MOVELIVCO      TXIVTP
     C                     MOVELINVOX     TXIVNO
     C                     MOVELPDNM      TXPDNM
     C                     Z-ADDQTY       TXQTY
     C                     Z-ADDUPRC      TXUPRC
     C                     Z-ADDAMT       TXAMT
     C                     Z-ADDTAX       TXTAX
     C                     MOVELTXAR      TXTXAR
     C                     Z-ADDUDATE     TXTXDT
     C                     MOVELWHY       TXWHY
     C                     WRITETXREC
     CSR         WR#TXZ    ENDSR
     C***************************
     C***  UPDATE TO  TRNDTL  ***
     C***************************
     CSR         UP#TX     BEGSR
     C                     MOVEL'C'       TXFLAG
     C                     MOVELACNT      TXACNT
     C                     Z-ADDDATE      TXDATE
     C                     MOVELORNO      TXORNO
     C                     MOVELRVID      TXRVID
     C                     MOVELSALE      TXSALE
     C                     MOVELKIND      TXIVTP
     C                     MOVELSATP      TXSATP
     C                     MOVELCUNO      TXCUNO
     C                     MOVELCUNM      TXCUNM
     C                     MOVELPDNM      TXPDNM
     C                     MOVELINVOX     TXIVNO
     C                     Z-ADDQTY       TXQTY
     C                     Z-ADDUPRC      TXUPRC
     C                     Z-ADDAMT       TXAMT
     C                     Z-ADDTAX       TXTAX
     C                     MOVELTXAR      TXTXAR
     C                     Z-ADDUDATE     TXTXDT
     C                     MOVELWHY       TXWHY
     C                     UPDATTXREC
     CSR         UP#TXZ    ENDSR
** ERR
功能代碼必須輸入．
功能代碼必須為１，２，３，４，５，６
非新增時，折讓單號必須輸入
此張折讓單已存在
此張折讓單已確認，不可再修改
此張折讓單已確認，不可再刪除
此張折讓單不存在
發票號碼必須輸入
品名代號不存在
此張折讓單已確認
種類別必須輸入
種類別必須為２，３，４
客戶編號不存在
此發票號碼非屬此客戶
此發票號碼不存在
此發票號碼已作廢
折讓證明單已列印完畢．
新增時折讓單號不可輸入
開立日期錯誤
確認日期錯誤
發票餘額已等於０，不可再有折讓
金額不可為０
此張折讓單非屬本廠區，不可更改，刪除，確認及列印
折讓確認日期為９９９９９９９９時，則入帳年月必須輸入
此張發票之折讓稅額與折讓金額計算出之稅額相差２元以上，不合理，請查核
２６－折讓退回證明單內含有不同的統一編號發票。
２７－此預收發票無繳款紀錄,不得折讓,請查核！
２８－此張發票之折讓金額不可大於發票金額！
２９－該張預收發票折讓不須打品名！
３０－此張發票尚未押匯，不可開立折讓單！
３１－資料不存在
３２－載入之發票筆數多於折讓畫面內空白的項次，載入不完整
３３－載入之發票已存回折讓畫面，合計存回筆數：
３４－載入之發票不屬於同一張訂單，請查核
