     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR132R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     訂單別出貨及發票明細表（磅單）
     H*            5.DATE-WRITTEN  86/04/26
     H*            6.DATE-UPDATED  89/06/30 CJAIN HIPRTB
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FWTMASPJ1IP  E           K        DISK
     FHIPRTB  IF  E           K        DISK
     FARR132F O   E           K        DISK
     F*****************************************************************
     IRWTMASPJ
     I                                              W6ENTDL6
     I                                              W7AREAL5
     I                                              W7VNO2L4
     I                                              W7NAMEL3
     I                                              W7QULTL2
     I                                              W7SPE1L1
     I*------------------------------------------------------
     I           UDS
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     I            DS
     I                                        1   2 W6TRCD
     I                                        1   1 W#IO
     I            DS
     I                                        1   8 W#WTNO
     I                                        1   1 W7AREA
     I                                        2   60W7VNO2
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*
     C*********************************
     C*  初始程式
     C*********************************
     CSR         INISR     BEGSR
     C                     Z-ADD0         W#PDWT 120       地磅重量
     C*
     CSR                   ENDSR
     C*********************************
     C*  L1清除函式
     C*********************************
     CSR         L1CLR     BEGSR
     C                     Z-ADD0         W#PDWT 120       地磅重量
     C*
     CSR                   ENDSR
     C*********************************
     C*  DETAIL TIME 函式
     C*********************************
     CSR         DTLSR     BEGSR
     C*
     C           W#IO      IFEQ '1'                        出貨類
     C           W6TYPE    IFEQ '0'
     C                     ADD  W7STDW    W#PDWT           理論重
     C                     ELSE
     C                     ADD  W7NWGT    W#PDWT           實重
     C                     ENDIF
     C*
     C                     ELSE                            退貨類
     C           W6TYPE    IFEQ '0'
     C                     SUB  W7STDW    W#PDWT           理論重
     C                     ELSE
     C                     SUB  W7NWGT    W#PDWT           實重
     C                     ENDIF
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*********************************
     C*  L1 函式  存檔
     C*********************************
     CSR         L1SR      BEGSR
     C*
     C                     CLEARWFREC
     C                     MOVE W6ORNO    WFORNO
     C                     MOVE W6CUNO    WFCUNO
     C                     Z-ADDW6ENTD    WFDATE           日期
     C                     MOVELW#WTNO    WFWTNO           磅單編號
     C                     MOVELW7NAME    WFPDNM           品名
     C*
8906 C* CHAIN HIPRTB START.......
  .  C*
  .  C                     MOVELW7QULT    W#QULT  5
  .  C           W#QULT    CHAINHIPRTBR              50
  .  C  N50                MOVELH1KINN    WFQULT
  .  C   50                MOVELW7QULT    WFQULT
  .  C*
8906 C* CHAIN HIPRTB END  .......
     C*
     C                     MOVELW7SPE1    WFSPE1           徑度
     C                     Z-ADDW#PDWT    WFQTY            數量
     C           W7VNO2    IFNE 0
     C                     WRITEWFREC
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*********************************
     C*  LR 函式  印核覆
     C*********************************
     CSR         LRSR      BEGSR
     C*
     CSR                   ENDSR
     C*********************************
     C*  查無資料函式
     C*********************************
     CSR         MSGSR     BEGSR
     C*
     CSR                   ENDSR
     C*********************************
