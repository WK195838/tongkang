     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR036RN
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02YSH
     H*            4.FUNCTION     未開立發票明細表
     H*            5.DATE-WRITTEN  89/04/06
     H*
     H*  說明：發票號碼為空白或雖已開立發票但發票開立日期大於所選定
     H*        之月份;但若作廢日期在報稅日期之後則不納入
     H*        若廠區為台北,則需寫入ARR036F
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTLX IP  E           K        DISK
     F            TXREC                             KRENAMETXRECL
     FTRNDTL  IF  E           K        DISK
     FINVMST  IF  E           K        DISK
     FARBTAXL2IF  E           K        DISK
     FARR036F IF  E           K        DISK                      A
     FARR036T O   E             69     PRINTER
     ITXRECL      01
     I              TXFL01                          F#FL01
     I              TXCODE                          F#CODE
     I              TXCUNO                          F#CUNO
     I              TXPCNO                          F#PCNO
     I              TXNO                            F#NO
     I              TXIVNO                          F#IVNO
     I              TXORNO                          F#ORNO
     I              TXAMT                           F#AMT
     I              TXDATE                          F#DATE
     I              TXACDT                          F#ACDT
     I                                              F#FL01L4
     I                                              F#CUNOL3
     I                                              F#PCNOL2
     I                                              F#NO  L1
     I            DS
     I                                        1   60D#DEDT
     I                                        1   20D#YY
     I                                        3   40D#MM
     I                                        5   60D#DD
     I            DS
     I                                        1  10 D#IVNO
     I                                        1   1 D#IVN1
     I                                        1   6 D#TXIV
     I            DS
9010 I                                        1   80F#DATE
LYW  I                                        1   60D#TXDT
     I            DS
9010 I                                        1   80F#ACDT
LYW  I                                        1   60D#ACDT
     I           UDS
     I                                      506 5110D#DATE
     I                                      506 5090T#YYMM
     I                                      512 512 D#AREA
     I                                     10011010 T#USID
     I                                     10211021 U#AREA
     C*****************************************************************
     C*              MAIN  PROGRAM
     C*****************************************************************
     C           *IN99     IFEQ *OFF
     C                     EXSR INISR
     C                     MOVE *ON       *IN99
     C                     ENDIF
     C   L4                EXSR L4CLR
     C   L3                EXSR L3CLR
     C   L2                EXSR L2CLR
     C   L1                EXSR L1CLR
     C   01                EXSR DTLSR
     CL1 99                EXSR L1SR
     CL2 99                EXSR L2SR
     CL3 99                EXSR L3SR
     CL4 99                EXSR L4SR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     SELEC
     C           D#AREA    WHEQ 'T'
     C                     MOVE '桃園'  T#BARA
     C           D#AREA    WHEQ 'H'
     C                     MOVE '苗栗'  T#BARA
     C           D#AREA    WHEQ 'M'
     C                     MOVE '台中'  T#BARA
     C           D#AREA    WHEQ 'K'
     C                     MOVE '高雄'  T#BARA
     C                     OTHER
     C                     MOVE '全部'  T#BARA
     C                     ENDSL
     C*
     C                     MOVE *ALL'='   T#LIN1
     C                     MOVE *ALL'='   T#LIN2
     C                     MOVE *ALL'0'   W#IVNO  6
     C*
     C           K#DTL     KLIST
     C                     KFLD           W#CODE  4
     C                     KFLD           W#NO    8
     C                     KFLD           W#ITEM  20
     C*
     C           K#BTAX    KLIST
     C                     KFLD           AXCODE
     C                     KFLD           AXVNNO
     C                     ENDSR
     C           K#036F    KLIST
     C                     KFLD           ARWTNO
     C                     KFLD           ARWTIT
     C*****************************************************************
     C           L4CLR     BEGSR
     C*****************************************************************
     C*廠區別小計
     C                     Z-ADD0         T#QTY4
     C                     Z-ADD0         T#AMT4
     C                     MOVE *ON       *IN69
     C                     ENDSR
     C*****************************************************************
     C           L3CLR     BEGSR
     C*****************************************************************
     C*客戶別小計
     C                     Z-ADD0         T#QTY3
     C                     Z-ADD0         T#AMT3
     C                     MOVE *OFF      *IN41
     C                     ENDSR
     C*****************************************************************
     C           L2CLR     BEGSR
     C*****************************************************************
     C*訂單別小計
     C                     Z-ADD0         T#QTY2
     C                     Z-ADD0         T#AMT2
     C                     MOVE *OFF      *IN42
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C*磅單別小計
     C                     Z-ADD0         T#QTY1
     C                     Z-ADD0         T#AMT1
     C                     MOVE *OFF      *IN43
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     MOVE F#IVNO    D#IVNO
TEST C           D#ACDT    IFEQ 8912
 .   C                     Z-ADD0         W#TEST  10
 .   C                     ENDIF
     C*                    EXSR SR1000                     作廢日期
     C           F#IVNO    IFEQ *BLANK                     未開發票
     C           F#IVNO    OREQ W#IVNO
     C           D#IVN1    OREQ '*'                        暫不開立
     C           D#IVN1    OREQ '#'                        永不開立
     C                     EXSR SR1000                     作廢日期
     C  N90                EXSR SR2000                     先前判斷
     C*
     C                     ELSE
     C*
     C           F#IVNO    CHAININREC                40
     C           *IN40     IFEQ *OFF
     C*
     C           ININDT    IFGT D#DATE                     開立日期
     C                     EXSR SR1000                     作廢日期
     C  N90                EXSR SR2000                     先前判斷
     C                     ENDIF
     C*
     C                     ENDIF                            *IN40
     C*
     C                     ENDIF                            TXIVNO
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C           T#QTY1    IFNE 0
     C           T#AMT1    ANDNE0
     C   69                WRITEAR036TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR036T2
     C*
     C                     ADD  T#QTY1    T#QTY2
     C                     ADD  T#AMT1    T#AMT2
     C                     ENDIF
     C                     ENDSR
     C*****************************************************************
     C           L2SR      BEGSR
     C*****************************************************************
     C           T#QTY2    IFNE 0
     C           T#AMT2    ANDNE0
     C   69                WRITEAR036TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR036T3
     C*
     C                     ADD  T#QTY2    T#QTY3
     C                     ADD  T#AMT2    T#AMT3
     C                     ENDIF
     C                     ENDSR
     C*****************************************************************
     C           L3SR      BEGSR
     C*****************************************************************
     C           T#QTY3    IFNE 0
     C           T#AMT3    ANDNE0
     C   69                WRITEAR036TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR036T4
     C*
     C                     ADD  T#QTY3    T#QTY4
     C                     ADD  T#AMT3    T#AMT4
     C                     ENDIF
     C                     ENDSR
     C*****************************************************************
     C           L4SR      BEGSR
     C*****************************************************************
     C           T#QTY4    IFNE 0
     C           T#AMT4    ANDNE0
     C   69                WRITEAR036TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR036T5
     C                     ENDIF
     C                     ENDSR
     C*****************************************************************
     C           WTRSR     BEGSR
     C*****************************************************************
     C*印明細
     C   41                MOVE *BLANK    T#CUNO           客戶編號
     C   41                MOVE *BLANK    T#CUNM           客戶名稱
     C   42                MOVE *BLANK    T#ORNO           訂單編號
     C   43                MOVE *BLANK    T#IVNO           磅單編號
     C   43                Z-ADD0         T#DATE           出貨日期
     C  N41                MOVE TXCUNO    T#CUNO           客戶編號
     C  N41                MOVE TXCUNM    T#CUNM           客戶名稱
     C  N42                MOVELTXPCNO    T#ORNO           訂單編號
     C  N43                MOVE TXNO      T#IVNO           磅單編號
     C  N43                Z-ADDTXDATE    T#DATE           出貨日期
     C                     MOVE TXPDNM    T#PDNM           品名
     C                     Z-ADDTXUPRC    T#UPRC           單價
     C                     Z-ADDTXQTY     T#QTY            數量
     C                     Z-ADDTXAMT     T#AMT            金額
     C                     MOVE TXVUNO    T#VUNO           傳票號碼
     C                     MOVE *BLANK    T#IVTP
     C*入帳日必須與單據日同月
     C           TXACDT    IFLE D#DATE
     C   69                WRITEAR036TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR036T1
     C                     MOVEA'111'     *IN,41
     C*
     C                     ADD  T#QTY     T#QTY1
     C                     ADD  T#AMT     T#AMT1
8907 C           U#AREA    IFEQ 'P'
 .   C                     EXSR SR3000
 .   C                     ENDIF
     C                     ENDIF
     C                     ENDSR
     C*****************************************************************
     C           SR1000    BEGSR
     C*****************************************************************
     C                     MOVE *OFF      *IN90
     C*
     C           F#CODE    IFEQ 'SA04'
     C                     MOVE '1'       AXCODE           單據別
     C                     ENDIF
     C*
     C           F#CODE    IFEQ 'AR05'
     C                     MOVE '2'       AXCODE           單據別
     C                     ENDIF
     C*
     C                     MOVE F#NO      AXVNNO           單據號碼
     C           K#BTAX    CHAINRARBTAX              52
     C           *IN52     IFEQ *OFF
     C*取得原作廢之發票號碼
     C           AXIVNO    CHAININREC                52
     C*
     C           INDECD    IFEQ 'D'                        已作廢
     C                     Z-ADDININDT    D#DEDT           開立日期
     C*計算報稅日期
     C                     SELEC
     C           D#MM      WHEQ 1
     C           D#MM      OREQ 2
     C                     Z-ADD3         D#MM
     C           D#MM      WHEQ 3
     C           D#MM      OREQ 4
     C                     Z-ADD5         D#MM
     C           D#MM      WHEQ 5
     C           D#MM      OREQ 6
     C                     Z-ADD7         D#MM
     C           D#MM      WHEQ 7
     C           D#MM      OREQ 8
     C                     Z-ADD9         D#MM
     C           D#MM      WHEQ 9
     C           D#MM      OREQ 10
     C                     Z-ADD11        D#MM
     C           D#MM      WHEQ 11
     C           D#MM      OREQ 12
     C                     Z-ADD1         D#MM
     C                     ADD  1         D#YY
     C                     ENDSL
     C                     Z-ADD15        D#DD
     C*
     C           INDEDT    IFGE D#DEDT
     C                     MOVE *ON       *IN90
     C                     ENDIF
     C*
     C                     ENDIF                            INDECD
     C*
     C                     ENDIF
     C                     ENDSR
     C*****************************************************************
     C           SR2000    BEGSR
     C*****************************************************************
     C*先前判斷是否經一正一負處理,若是則以一正一負中正數資料為準
     C           F#NO      IFNE W#NOX
     C                     MOVEA'00'      *IN,50
     C*
     C                     MOVELF#CODE    W#CODE
     C                     MOVE F#NO      W#NO
     C                     Z-ADD0         W#ITEM
     C           K#DTL     SETLLTXREC
     C           *IN50     DOWEQ*OFF
     C                     READ TXREC                    50
     C*
     C           *IN50     IFEQ *ON
     C           TXNO      ORNE F#NO
     C                     LEAVE
     C                     ENDIF
     C*
TEST C           TXNO      IFEQ 'K1108757'
 .   C                     Z-ADD0         W#TEST  10
 .   C                     ENDIF
     C*
     C                     MOVE TXIVNO    D#IVNO
     C           D#TXIV    IFEQ W#IVNO                     一正一負
0622 C*          D#IVN1    OREQ '#'
     C*
0622 C*          TXAMT     IFGT 0
     C                     EXSR WTRSR
     C                     MOVE *ON       *IN51
0622 C*                    ENDIF
     C*
     C                     ELSE
     C* IN51為判斷是否列印過一正一負之資料
     C*若列印過,則已開立之發票資料不予列印
     C  N51                EXSR WTRSR
     C                     ENDIF
     C                     ENDDO
     C                     MOVE F#NO      W#NOX   8
     C                     ENDIF
     C                     ENDSR
     C*****************************************************************
     C           SR3000    BEGSR
     C*****************************************************************
     C                     MOVE TXNO      ARWTNO           磅單編號
     C                     Z-ADDTXITEM    ARWTIT
     C           K#036F    CHAINRARR036F             40
     C                     MOVE TXCUNO    ARCUNO           客戶編號
     C                     MOVE TXCUNM    ARCUNM           客戶名稱
     C                     MOVELTXPCNO    ARORNO           訂單編號
     C                     MOVE TXNO      ARWTNO           磅單編號
     C                     Z-ADDTXITEM    ARWTIT
     C                     Z-ADDTXDATE    ARDATE           出貨日期
     C                     MOVE TXPDNM    ARPDNM           品名
     C                     Z-ADDTXUPRC    ARUPRC           單價
     C                     Z-ADDTXQTY     ARQTY            數量
     C                     Z-ADDTXAMT     ARAMT            金額
     C                     MOVE TXSALE    ARSALE
     C                     WRITERARR036F
     C                     ENDSR
