     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARQ017R2
     H*            2.PROGRAM-TYPE  RPG
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     發票別出貨明細查詢─ＦＯＲ型鋼（出表）
     H*            5.DATE-WRITTEN  93/08/19
     H*            6.DATE-MODIFY
     H*
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARQ017F IP  E           K        DISK
     FARQ017T O   E             69     PRINTER
     E*****************************************************************
     IRAQ017F
     I                                              A7INNOL2
     I                                              A7PDNML1
     I            DS
     I                                        1   5 A7SPE1
     I                                        1   40D#SPE1
     I            DS
     I                                        1   5 A7SPE2
     I                                        1   40D#SPE2
     I           UDS
     I                                      300 309 T#INNO
     I                                     10011010 U#USID
     I*****************************************************************
     C  N99                EXSR INISR
     C   L2                EXSR L2CLR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CL2 99                EXSR L2SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     MOVE *ON       *IN99
     C                     MOVE *ON       *IN69
     C*
     C                     MOVEL*ALL'='   T#LIN1
     C                     MOVEL*ALL'='   T#LIN2
     C*
     C                     ENDSR
     C*****************************************************************
     C           L2CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR017T1T
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR017T1S
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     CLEARAR017T1D
     C*
     C                     MOVELA7ORNO    T#ORNO           訂單號碼
     C                     Z-ADDA7ORTM    T#ORTM           訂單項次
     C                     MOVELA7VRNO    T#VRNO           磅單編號
     C                     MOVELA7VRTM    T#VRTM           磅單項次
     C                     MOVELA7STND    T#STND           規範別
     C                     MOVELA7QULT    T#QULT           鋼種
     C                     MOVELA7PDNM    T#PDNM           品名
     C                     Z-ADDD#SPE1    T#SPE1            H/T/D
     C           A7SPE2    IFEQ *BLANK
     C           A7SPE2    OREQ '00000'
     C                     MOVE *ON       *IN53
     C                     ELSE
     C                     MOVE *OFF      *IN53
     C                     ENDIF
     C                     Z-ADDD#SPE2    T#SPE2            B/W
     C           A7SPE3    IFEQ *BLANK
     C           A7SPE3    OREQ '000  '
     C                     MOVE *ON       *IN51
     C                     ELSE
     C                     MOVE *OFF      *IN51
     C                     ENDIF
     C                     MOVE A7SPE3    T#SPE3            T1
     C           A7SPE4    IFEQ *BLANK
     C           A7SPE4    OREQ '000  '
     C                     MOVE *ON       *IN52
     C                     ELSE
     C                     MOVE *OFF      *IN52
     C                     ENDIF
     C                     MOVE A7SPE4    T#SPE4            T2
     C                     Z-ADDA7SPE5    T#SPE5           長度
     C                     Z-ADDA7PNUM    T#PNUM           支數
     C                     Z-ADDA7WGHT    T#QTY            重量
     C                     Z-ADDA7UPRC    T#UPRC           單價
     C                     Z-ADDA7AMT     T#AMT            金額
     C                     MOVELA7PLE1    T#PLE1           交貨地點
     C                     MOVELA7NOTE    T#NOTE           備註
     C*
     C   69                WRITEAR017T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR017T1D
     C*品名小計
     C                     ADD  T#QTY     T#QTYS
     C                     ADD  T#AMT     T#AMTS
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C   69                WRITEAR017T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR017T1S
     C*發票合計
     C                     ADD  T#QTYS    T#QTYT
     C                     ADD  T#AMTS    T#AMTT
     C*
     C                     ENDSR
     C*****************************************************************
     C           L2SR      BEGSR
     C*****************************************************************
     C   69                WRITEAR017T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR017T1T
     C*
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C*
     C   69                WRITEAR017T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR017T1M
     C*
     C                     ENDSR
