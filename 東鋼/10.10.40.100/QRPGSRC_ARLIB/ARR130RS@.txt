     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR130RS
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02LBU
     H*            4.FUNCTION     已開發票押匯狀況彙總表
     H*            5.DATE-WRITTEN  96/11/14
     H*            6.DATE-MODIFY
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR130S CF  E                    WORKSTN
     FARSAID  IF  E           K        DISK
     E*----------------------------------------------------------------
     E                    ERR     1   5 70
     I*****************************************************************
     I            DS
     I                                       19  260YMD
     I                                       21  240YM
     I                                       21  220YY
     I                                       23  240MM
     I                                       25  260DD
     I           UDS
     I                                     10111020 S#DEVN
     I                                     10211021 D#TXAR
     C*****************************************************************
     C*          參數接收
     C*****************************************************************
     C           *ENTRY    PLIST
     C           *IN03     PARM *IN03     IN03    1
     C                     PARM           BDATS   80
     C                     PARM           EDATS   80
     C                     PARM           RVID    2
     C                     Z-ADD0         RRN     30
     C*****************************************************************
     C*          主程式開始
     C*****************************************************************
     C           *IN03     DOUEQ'1'
     C                     EXFMTAR130F1
     C   KC                EXSR KC#01
     C                     EXSR CK#01
     C  N99                SETON                     LR
     C  N99                RETRN
     C                     ENDDO
     C*****************************************************************
     CSR         KC#01     BEGSR
     C*****************************************************************
     C                     MOVE '1'       *IN03
     C                     SETON                     LR
     C                     RETRN
     C*
     CSR                   ENDSR
     C*****************************************************************
     C*          SCREEN 01 CHECK
     C*****************************************************************
     CSR         CK#01     BEGSR
     C*
     C                     SETOF                     99
     C                     SETOF                     313233
     C                     MOVEL*BLANK    S#ERR
     C*
     C* CHECK業務代號
     C*
     C                     MOVE *OFF      *IN30
     C           S#RVID    CHAINARSAID               30
     C*
     C           *IN30     IFEQ '1'
     C           S#RVID    ANDNE*BLANK
     C                     SETON                     9931
     C                     MOVELERR,1     S#ERR
     C                     GOTO CK#01Z
     C                     ENDIF
     C*
     C* CHECK起始日期
     C*
     C           S#DATS    IFNE 0
     C                     MOVEL*ALL'0'   P#DATE
     C                     MOVE S#DATS    P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           FLAGC   1
     C*
     C           FLAGC     IFNE '0'
     C                     SETON                     9932
     C                     MOVELERR,2     S#ERR
     C                     GOTO CK#01Z
     C                     ENDIF
     C*
     C                     ELSE
     C                     SETON                     9932
     C                     MOVELERR,4     S#ERR
     C                     GOTO CK#01Z
     C                     ENDIF
     C*
     C* CHECK終止日期
     C*
     C           S#DATE    IFNE 0
     C                     MOVEL*ALL'0'   P#DATE
     C                     MOVE S#DATE    P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE
     C                     PARM '1'       P#MODE
     C                     PARM           P#MTL
     C                     PARM           P#LY
     C                     PARM           FLAGC
     C*
     C           FLAGC     IFNE '0'
     C                     SETON                     9933
     C                     MOVELERR,2     S#ERR
     C                     GOTO CK#01Z
     C                     ENDIF
     C*
     C                     ELSE
     C                     SETON                     9933
     C                     MOVELERR,5     S#ERR
     C                     GOTO CK#01Z
     C                     ENDIF
     C*
     C*起始日不可大於終止日
     C*
     C           S#DATS    IFGT S#DATE
     C                     SETON                     993233
     C                     MOVELERR,3     S#ERR
     C                     GOTO CK#01Z
     C                     ENDIF
     C*
     C                     Z-ADDS#DATS    BDATS
     C                     Z-ADDS#DATE    EDATS
     C                     MOVELS#RVID    RVID
     C*
     CSR         CK#01Z    ENDSR
     C*****************************************************************
** ERR
業務代號錯誤．
日期不合邏輯．
日期範圍錯誤．
起始日期不可空白．
終止日期不可空白．
