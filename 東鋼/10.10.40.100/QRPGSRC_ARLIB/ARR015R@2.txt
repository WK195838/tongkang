     H        1   Y                                     1                 BR003
     FRCVDTL  IP  E                    DISK
     FQPRINT  O   F     158            PRINTER
      *************************************************************
     E                    TAB1    1   6  1   TABR1  10
     E                    TAB2    1   4  1   TABR2   6
     I*************************************************************
     IRVREC
     I                                              RVCUNOL2
     I                                              RVNO  L1
     I****
     IAADS        DS
     I              'も保證票據明細表も'C         CON3                       X
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     IBBDS        DS
     I                                        1  10 RVSBID
     I                                        1   3 BAID1
     I                                        4   7 BAID2
      *
     I                                       11  20 RVSANO
     I                                       11  11 ACTYPE
     I                                       12  12 ACCURR
     I                                       13  20 ACACNO
      *
9008 I                                       21  280YMD
LYW  I                                       21  260YM
 .   I                                       21  240YY
 .   I                                       25  260MM
 .   I                                       27  280DD
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C           *ENTRY    PLIST
9008 C                     PARM           BDATE1  80
LYW  C                     PARM           EDATE2  80
     C           BKKEY     KLIST
     C                     KFLD           BAID1
     C                     KFLD           BAID2
     C           ACKEY     KLIST
     C                     KFLD           ACTYPE
     C                     KFLD           ACCURR
     C                     KFLD           ACACNO
      *
     C                     Z-ADDBDATE1    YMD
9008 C                     Z-ADDYM        DATE1   60
     C                     Z-ADDEDATE2    YMD
9008 C                     Z-ADDYM        DATE2   60
      *
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD55        TCNT    30
     C                     MOVE *ALL'='   ALS   145
     C                     MOVE *ALL'-'   ALQ    92
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD8         LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         AAMT   110
     C                     Z-ADD0         AQTY    50
     C                     Z-ADDTCNT      LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     MOVE *BLANK    CHAR10 10
     C           RVNTTP    LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     CHAR10
     C  N31                MOVEL'其他'  CHAR10
      *
     C                     MOVE *BLANK    CHAR6   6
     C           RVNTCD    LOKUPTAB2      TABR2          31
     C   31                MOVELTABR2     CHAR6
      *
     C           RVPLAC    COMP 'D'                      31
     C   31                MOVEL'本埠'  CHAR6X  6
     C  N31                MOVEL'外埠'  CHAR6X
      *
     C                     MOVEL*BLANK    R#NTNO 20
     C                     MOVELRVNTNO    R#NTNO
      *
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#01
     C                     ADD  1         AQTY
     C                     ADD  RVRAMT    AAMT
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C                     EXCPTET#L2
     C                     ADD  3         LCNT
     CSR         TL#L2Z    ENDSR
      ***********************************************************
      ****   DEFINE OUPUT FORMAT                             ****
      ***********************************************************
     OQPRINT  E   02           EH#01
     O                                   17 '報表編號: AR015'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
     O                         UDATE Y  132
     O        E 1              EH#01
     O                         CON3      71                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 2              EH#01
     O                                   11 '收款年月:'
     O                         DATE1   +  1 '    /  '
     O                                 +  1 '-'
     O                         DATE2   +  1 '    /  '
     O        E 1              EH#01
     O                         ALS      145
     O        E 1              EH#01
     O                                   10 '客戶編號'
     O                                   23 '客戶名稱'
     O                                   35 '繳款單號'
     O                                   44 '款項別'
     O                                   53 '票據別'
     O                                   67 '付款銀行'
     O                                   81 '票據號碼'
     O                                  100 '付款地'
     O                                  111 '到期日期'
     O                                  126 '金　額'
     O                                  134 '異動別'
     O                                  145 '異動日期'
     O        E 1              EH#01
     O                         ALS      145
     O        E 1              ED#01
     O                 L1      RVCUNO     7
     O                 L1      RVCUNM    23
     O                         RVNO      32
     O                         RVUSTP    41
     O                         CHAR10    55
     O                         RVPBID    71
     O                         R#NTNO    92
     O                         CHAR6X    99
     O                         RVDUDT   110 '    /  /  '
     O                         RVRAMTJ  126
     O                         CHAR6    133
     O                         RVNTDT   144 '    /  /  '
     O        E 1              ET#L2
     O                         ALQ      145
     O        E 1              ET#L2
     O                                   70 '客戶小計：　共'
     O                         AQTY  J   80
     O                                   84 '張'
     O                                  101 '總金額:'
     O                         AAMT  J  126
     O        E 1              ET#L2
     O                         ALQ      145
     O*****************************************************************
** TAB1
A支票
B背書支票
C本票
D背書本票
E銀行本票
I信用狀　
** TAB2
1兌現
2退票
3換票
4延票
