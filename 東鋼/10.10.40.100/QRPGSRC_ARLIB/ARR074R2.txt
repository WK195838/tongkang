     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR074R4
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     鋼筋型鋼廠別應收明細表列表
     H*            5.DATE-WRITTEN  87/09/10
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR074F IP  E           K        DISK
     FARR074T O   E             66     PRINTER
     F*****************************************************************
     IRARREC
     I                                              ARCUNOL1
     I*------------------------------------------------------
     I           UDS
     I                                        1   6 U#YYMM
     I                                       10  10 U#TYPE
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*
     C*********************************
     C*  初始程式
     C*********************************
     CSR         INISR     BEGSR
     C           U#TYPE    IFEQ '2'
     C                     SETOF                     92    鋼筋
     C                     ELSE
     C                     SETON                     92    型鋼
     C                     ENDIF
     C*
     C                     MOVELU#YYMM    R#YYMM
     C                     SETON                     66
     C*
     C                     CLEARAR074T1T
     CSR                   ENDSR
     C*
     C*********************************
     C*  L1清除函式換客戶
     C*********************************
     CSR         L1CLR     BEGSR
     C                     CLEARAR074T1S
     C                     Z-ADD0         W#RCNT  60
     CSR                   ENDSR
     C*
     C*********************************
     C*  DETAIL TIME 函式
     C*********************************
     CSR         DTLSR     BEGSR
     C                     CLEARAR074T1D
     C   L1                MOVELARCUNO    R#CUNO
     C   L1                MOVELARCUNM    R#CUNM
     C                     MOVELARORNO    R#ORNO
     C                     Z-ADDARDAMP    R#DAMP
     C                     Z-ADDARDAMM    R#DAMM
     C                     Z-ADDARDAMT    R#DAMT
     C                     Z-ADDARDAMH    R#DAMH
     C                     Z-ADDARDAMK    R#DAMK
     C*
     C           R#DAMP    ADD  R#DAMM    R#DAMX
     C                     ADD  R#DAMT    R#DAMX
     C                     ADD  R#DAMH    R#DAMX
     C                     ADD  R#DAMK    R#DAMX
     C*
     C           *IN66     IFEQ '1'
     C                     SETOF                     66
     C                     WRITEAR074T1H
     C                     ENDIF
     C                     WRITEAR074T1D
     C                     ADD  1         W#RCNT           印出一筆
     C*
     C*                                                    小計
     C                     ADD  R#DAMP    R#SAMP
     C                     ADD  R#DAMM    R#SAMM
     C                     ADD  R#DAMT    R#SAMT
     C                     ADD  R#DAMH    R#SAMH
     C                     ADD  R#DAMK    R#SAMK
     C                     ADD  R#DAMX    R#SAMX
     CSR                   ENDSR
     C*
     C*********************************
     C*  L1 函式  客戶更改
     C*********************************
     CSR         L1SR      BEGSR
     C           W#RCNT    IFGT 1                          一筆以上
     C           *IN66     IFEQ '1'
     C                     SETOF                     66
     C                     WRITEAR074T1H
     C                     ENDIF
     C                     WRITEAR074T1S
     C                     ENDIF
     C*
     C*                                                    合計
     C                     ADD  R#SAMP    R#TAMP
     C                     ADD  R#SAMM    R#TAMM
     C                     ADD  R#SAMT    R#TAMT
     C                     ADD  R#SAMH    R#TAMH
     C                     ADD  R#SAMK    R#TAMK
     C                     ADD  R#SAMX    R#TAMX
     CSR                   ENDSR
     C*
     C*********************************
     C*  LR 函式  (NOTHING)
     C*********************************
     CSR         LRSR      BEGSR
     C           *IN66     IFEQ '1'
     C                     SETOF                     66
     C                     WRITEAR074T1H
     C                     ENDIF
     C                     WRITEAR074T1T
     CSR                   ENDSR
     C*
     C*********************************
     C*  查無資料函式
     C*********************************
     CSR         MSGSR     BEGSR
     C                     WRITEAR074T1H
     C                     WRITEAR074T1M
     CSR                   ENDSR
     C*
