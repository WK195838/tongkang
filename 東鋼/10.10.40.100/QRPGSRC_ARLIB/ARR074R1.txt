     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR074R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     鋼筋型鋼廠別應收明細表合計帳款
     H*            5.DATE-WRITTEN  87/09/10
     H*            6.UPDATE  DATE  103/11/27 2014AR887 S00WCJ (0311A)
     H*                            配合營業鋼筋產品代碼擴檔修改
     H*                            104/10/05 2015AR953 S00WCJ (0409A)
     H*                             產品代碼3碼擴5碼
     H*
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARSTWF  IP  E           K        DISK
     FSAMAST  IF  E           K        DISK
     FINVMST  IF  E           K        DISK
     FARR074F O   E           K        DISK
     F*****************************************************************
     IASREC
     I                                              ASORNOL1
     I*----------------------------------------------------------------
     I           UDS
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     I*
     I            DS
     I                                        1  10 ASIVNO
     I                                        1   1 W#TXAR
     I                                        9  10 D#IVNO
     I            DS
9008 I                                        1   6 ASORNO
LYW  I                                        1   1 S1OREA
 .   I                                        2   60S1ORNO
     I            DS
0311AI                                        1   5 S1PDNO
     I                                        1   1 W#PDNO
0409AI                                        1   2 D#PDN2
0409AI                                        4   4 D#PDN4
     C*****************************************************************
     C*   檔案搜尋欄位組合
     C*****************************************************************
     C           K#SA      KLIST
     C                     KFLD           S1OREA
     C                     KFLD           S1ORNO
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*
     C*********************************
     C*  初始程式
     C*********************************
     CSR         INISR     BEGSR
     CSR                   ENDSR
     C*
     C*********************************
     C*  L1清除函式換訂單
     C*********************************
     CSR         L1CLR     BEGSR
     C                     CLEARRARREC
     C*
9011 C           ASORNO    IFNE *BLANK
     C           K#SA      CHAINSAMAST               69
     C           *IN69     IFEQ '1'
     C                     MOVEL*BLANKS   ARPDTP
0409AC                     ELSE
0409AC           D#PDN4    IFEQ ' '
0409AC                     MOVELW#PDNO    ARPDTP           品名別
0409AC                     ENDIF
0409AC           D#PDN4    IFNE ' '
0409AC                     MOVELD#PDN2    ARPDTP
0409AC                     ENDIF
     C                     ENDIF
9011 C                     ELSE
0409AC                     MOVEL*BLANKS   ARPDTP
 .   C                     ENDIF
     C*
     C                     MOVELASCUNO    ARCUNO           客戶名稱
     C                     MOVELASCUNM    ARCUNM           客戶簡稱
     C                     MOVELASORNO    ARORNO           訂單編號
0409AC*                    MOVELW#PDNO    ARPDTP           品名別
     CSR                   ENDSR
     C*
     C*********************************
     C*  DETAIL TIME 函式
     C*********************************
     CSR         DTLSR     BEGSR
     C           D#IVNO    IFEQ *BLANK                     為磅單
     C                     MOVELW#TXAR    W#AREA  1
     C                     ELSE                            為發票
     C           ASIVNO    CHAININREC                69
     C                     MOVELINAREA    W#AREA
     C                     ENDIF
     C*
     C                     SELEC
     C           W#AREA    WHEQ 'P'
     C                     ADD  ASHAMT    ARDAMP
     C           W#AREA    WHEQ 'M'
     C                     ADD  ASHAMT    ARDAMM
     C           W#AREA    WHEQ 'T'
     C                     ADD  ASHAMT    ARDAMT
     C           W#AREA    WHEQ 'H'
     C                     ADD  ASHAMT    ARDAMH
     C           W#AREA    WHEQ 'K'
     C                     ADD  ASHAMT    ARDAMK
     C                     ENDSL
     CSR                   ENDSR
     C*
     C*********************************
     C*  L1 函式  寫入一筆記錄
     C*********************************
     CSR         L1SR      BEGSR
     C                     WRITERARREC
     CSR                   ENDSR
     C*
     C*********************************
     C*  LR 函式  (NOTHING)
     C*********************************
     CSR         LRSR      BEGSR
     CSR                   ENDSR
     C*
     C*********************************
     C*  查無資料函式
     C*********************************
     CSR         MSGSR     BEGSR
     CSR                   ENDSR
     C*
