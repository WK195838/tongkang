     H****************************************************************
     H*
     H*            UPDATE  DATE  99/05/25 2009AR406 S00WCJ (9905A)
     H*                          99/08/25 2010AR517 S00WCJ (9908A)
     H*                         100/07/04 S00WCJ
     H*                         100/11/28  2011AR634 S00WCJ (0011A)
     H*                          配合IFRS修改
     H*                         101/09/14 2012AR681 S00WCJ (0109A)
     H*                          磅單X開頭，廠區為U
     H*                         101/09/20 2012AR682 S00WCJ (0109B)
     H*                         101/10/25 2012AR695 S00WCJ (0110A)
     H*                          磅單R開頭，廠區為U
     H*                         101/11/29 2012AR703 S00WCJ (0111A)
     H*                          磅單J開頭，廠區為U
     H*                         101/12/05 2012AR705 S00WCJ (0112A)
     H*                          磅單N開頭，廠區為U
     H*                         102/01/22 2013AR726 S00WCJ (0201A)
     H*                          磅單V開頭，廠區為U
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
0007AFTRNDTLZ IP  E                    DISK
     FTRNDTLUPUF  E           K        DISK
     F            TXREC                             KRENAMETXW
     FACTRAN  UF  E           K        DISK                      A
     FHIPROD  IF  E           K        DISK
0007AFQPRINT  O   F     176            PRINTER
     ******************************************************************
9905AE*                   TAB1    1   5  1   TABR1  18
9905AE                    TAB1    1   6  1   TABR1  18
0109BE                    TAB2    1   6  1   TABR2  21
     E                    ARY         6 14
     ******************************************************************
     ITXREC
     I                                              TXACDTL4
     I                                              TXFL02L3
     I                                              TXIVNOL2
     I                                              TXCUNOL2
     I                                              TXORNOL2
     I                                              TXRESVL1
     I                                              TXUPRCL1
0007AI                                              TXSTNOL1
     I*----------------------------------------------------------------
     ITXW
     I              TXFLAG                          TWFLAG
     I              TXCODE                          TWCODE
     I              TXNO                            TWNO
     I              TXITEM                          TWITEM
     I              TXACNT                          TWACNT
     I              TXDATE                          TWDATE
     I              TXACDT                          TWACDT
     I              TXCUNO                          TWCUNO
     I              TXCUNM                          TWCUNM
     I              TXORNO                          TWORNO
     I              TXIVNO                          TWIVNO
     I              TXPCNO                          TWPCNO
     I              TXVUNO                          TWVUNO
     I              TXRVID                          TWRVID
     I              TXSALE                          TWSALE
     I              TXSATP                          TWSATP
     I              TXIVTP                          TWIVTP
     I              TXPDNM                          TWPDNM
     I              TXQTY                           TWQTY
     I              TXUPRC                          TWUPRC
     I              TXAMT                           TWAMT
     I              TXTAX                           TWTAX
     I              TXFL01                          TWFL01
     I              TXFL02                          TWFL02
     I              TXTXAR                          TWTXAR
     I              TXTXDT                          TWTXDT
     I              TXRESV                          TWRESV
     I*----------------------------------------------------------------
     I            DS
     I                                        1  18 KGRP
     I                                        1   4 KACNO
     I                                        5  14 KACNON
     I                                       15  18 KDPNO
0109BI            DS
0109BI                                        1  21 K#GRP
0109BI                                        1   7 K#ACNO
0109BI                                        8  17 K#ACNN
0109BI                                       18  21 K#DPNO
     I            DS
     I                                        1   6 VRNOX
     I                                        2   60NO1
     I                                       11  16 VRNON
     I                                       12  160NO3
     I            DS
     I                                        1   6 VRNOY
     I                                        2   60NO2
     I            DS
     I                                        1   7 TWVUNO
     I                                        1   1 TTT1
     I                                        2   2 TTT2
     I                                        3   3 TTT3
     I                                        4   7 TTT4
CLJ  I*
     I            DS
     I                                        1   2 W#OEOF
     I                                        1   1 D#OE
     I                                        2   2 D#OF
     I            DS
     I                                        1  12 NAME1
     I                                       12  12 D#NOF
     I            DS
     I                                        1  14 D#ARY
     I                                        1   1 D#AREA
     I                                        2  142D#AMT
---  I*
     I           UDS
9008 I                                        1   60U#YYMM
LYW  I                                        1   8 YMD
     I                                        9   9 TXAR
0011AI                                      201 206 D#VNO1
0011AI                                      211 216 D#VNO2
     I                                     10011010 D#USER
0109BIIFRS       UDS                             50
0109BI                                        1   60D#IFYM
0109BI                                        1   80D#IFRS
     C*****************************************************************
     C*          KEY  LIST
     C*****************************************************************
     C           ACKEY1    KLIST
     C                     KFLD           TXAR
     C                     KFLD           U#YYMM
     C                     KFLD           ACVRNO
     C                     KFLD           ACITEM
     C*
     C           TXKEY     KLIST
     C                     KFLD           TXCODE
     C                     KFLD           TXNO
     C                     KFLD           TXITEM
     C*****************************************************************
     C*          MAIN  PROGRAM
     C*****************************************************************
9908AC           *DATE     SUB  19000000  U#SYSD  80
0109BC           *NAMVAR   DEFN ARIFRSCTL IFRS
0109BC           U#YYMM    IFLT D#IFYM
0109BC                     MOVEL*ON       *IN77
0109BC                     ELSE
0109BC                     MOVEL*OFF      *IN77
0109BC                     ENDIF
0109BC                     UNLCKIFRS
     C  N10                EXSR IN#01
     C   L3                EXSR DL#03
     C   L2                EXSR DL#02
     C   L1                EXSR DL#01
     C                     EXSR DP#01
     CL1                   EXSR TL#01
     CL2                   EXSR TL#02
     CL3                   EXSR TL#L3
     CLR                   EXSR TL#LR
     C**--------------------------------------------------**
     CSR         IN#01     BEGSR
     C*                    MOVE YMD       DATEA   80
9008 C                     Z-ADDU#YYMM    DATEA   60
     C                     Z-ADD0         AMTT   132
     C                     Z-ADD0         AMTTT  132
     C                     Z-ADD0         ARAMT  132
     C                     Z-ADD0         ARTMT  132
     C                     Z-ADD1         ITEM1   30
     C                     Z-ADD1         ITEM2   30
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD58        TCNT    20
     C                     Z-ADD0         PAG     30
     C                     Z-ADD0         COUNT   40
     C                     MOVE *BLANK    AREA    1
     C                     MOVE *BLANK    KGRP
0109BC                     MOVEL*BLANKS   K#GRP
0109BC   77      TXAR      LOKUPTAB1      TABR1          31
0109BC   31 77             MOVELTABR1     KGRP
0109BC  N77      TXAR      LOKUPTAB2      TABR2          31
0109BC   31N77             MOVELTABR2     K#GRP
     C                     MOVE *ALL'9'   ARY
     C                     SETON                     10
0011AC                     MOVEL*BLANKS   D#VNO1
0011AC                     MOVEL*BLANKS   D#VNO2
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**  FIND LAST NO                                **
     C**----------------------------------------------**
     CSR         @FDNO     BEGSR
     C*
     C                     MOVEL'D99999'  ACVRNO
     C                     Z-ADD999       ACITEM
     C                     SETOF                     95
     C           ACKEY1    SETGTACTRAN
     C                     READPACTRAN                   95
     C  N95                MOVELACVRNO    TYPE    1
     C  N95      TYPE      COMP 'D'                  9595
     C  N95                MOVELACVRNO    VRNON
     C  N95                ADD  1         NO3
     C  N95                MOVELVRNON     VRNOX
     C   95                MOVEL'D00001'  VRNOX
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**    L3 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#03     BEGSR
      *
      * INITIAL NEXT VOUCHER NO
      *
     C                     EXSR @FDNO
     C                     MOVELVRNOX     VRNOY
0011AC           D#VNO1    IFEQ *BLANKS
0011AC                     MOVELVRNOY     D#VNO1
0011AC                     MOVELVRNOY     D#VNO2
0011AC                     ENDIF
     C*
     C           NO1       ADD  1         NO2
     C                     Z-ADD1         ITEM1   30
     C                     Z-ADD1         ITEM2   30
     C                     Z-ADD0         AMTT   132
     C*
0011AC           D#VNO2    IFLT VRNOY
0011AC                     MOVELVRNOY     D#VNO2
0011AC                     ENDIF
     CSR                   ENDSR
     C**----------------------------------------------**
     C**    L2 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#02     BEGSR
     C*
     C                     Z-ADD0         AMT2   132
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**    L1 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#01     BEGSR
     C*
     C           TXPDNM    CHAINHIPROD               31
CLJ  C   31                MOVE *BLANK    NAME1
     C           *IN31     IFEQ '0'
     C                     MOVE ''      W#OEOF
     C                     MOVELF4CHIN    NAME1
     C                     MOVE D#OF      D#NOF
---  C                     ENDIF
     C                     Z-ADD0         AMT1   132
     C                     Z-ADD0         QTY1    80
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**       DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
TEST C           TXNO      IFEQ 'P00598'
  .  C                     Z-ADD0         W#TEST  10
  .  C                     ENDIF
     C*
     C                     ADD  TXAMT     AMT1
     C                     ADD  TXAMT     AMT2
     C                     ADD  TXQTY     QTY1
     C***
     C                     ADD  TXAMT     AMTT
     C                     ADD  TXAMT     AMTTT
     C                     ADD  1         COUNT
      *
     C           TXKEY     CHAINTRNDTLUP             31
CLJ  C  N31                MOVELTXAR      TTT1
CLJ  C  N31                MOVELTWPCNO    TX@     1
     C  N31                MOVELTX@       TTT2
     C  N31                MOVELVRNOY     TTT3
     C  N31                MOVE VRNOY     TTT4
     C  N31                UPDATTXW
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   L1 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#01     BEGSR
     C*
     C           AMT1      IFNE 0
     C                     ADD  1         ITEM1
     C           AMT1      IFLT 0
     C                     Z-SUBAMT1      AMT1
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXSR WR#01
     C                     EXSR PR#01
     C                     ENDIF
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   L2 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#02     BEGSR
     C*
     C           AMT2      IFNE 0
     C                     ADD  1         ITEM2
     C           AMT2      IFLT 0
     C                     Z-SUBAMT2      AMT2
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXSR WR#02
     C                     EXSR PR#02
     C                     ENDIF
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   L3 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#L3     BEGSR
     C*
     C           AMTT      IFNE 0
     C           AMTT      IFLT 0
     C                     Z-SUBAMTT      AMTT
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXSR WR#03
     C                     EXSR PR#03
     C                     EXSR WR#04
     C                     EXSR PR#04
     C                     ENDIF
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   LR BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#LR     BEGSR
     C*
     C           AMTTT     IFNE 0
     C                     EXCPTET#LR
     C*
     C           TXAR      IFEQ 'P'
     C           TXAR      OREQ 'M'
     C                     EXSR SR1100
     C                     ENDIF
     C*
     C                     EXCPTET#LRR
     C                     ENDIF
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    TOTAL  TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#01     BEGSR
     C*
     C****  WRITE  TO NON-TAIPEI "4111" 銷貨收入
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXPCNO    TX@     1
0007AC           TX@       IFEQ 'D'
0109AC           TX@       OREQ 'X'
0110AC           TX@       OREQ 'R'
0111AC           TX@       OREQ 'J'
0112AC           TX@       OREQ 'N'
0201AC           TX@       OREQ 'V'
0007AC                     MOVEL'U'       TX@
0007AC                     ENDIF
     C                     MOVELTX@       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDU#YYMM    ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADDITEM1     ACITEM
     C   51                MOVEL'C'       ACDC
     C  N51                MOVEL'D'       ACDC
0109BC   77                MOVEL'4111'    ACNO
0109BC  N77                MOVEL'410101'  ACNO
     C                     MOVEL'L'       ACTYCO
     C                     MOVE *BLANK    KGRP
0109BC   77      TX@       LOKUPTAB1      TABR1          31
0109BC   31 77             MOVELTABR1     KGRP
0109BC  N77      TX@       LOKUPTAB2      TABR2          31
0109BC   31N77             MOVELTABR2     K#GRP
0109BC   77                MOVELKDPNO     ACDPNO
0109BC     N77             MOVELK#DPNO    ACDPNO
     C                     MOVELACDPNO    W#DPNO  4
     C                     MOVELTXORNO    KORNO   6
     C                     MOVELKORNO     ACRLNO
     C                     MOVELTXCUNM    ACCSNM
     C                     Z-ADDAMT1      ACAMT
CLJ  C*                    MOVELTXPDNM    ACDSC1           移至下面
     C*
     C           TXACNT    IFNE '2'
     C                     Z-ADD0         QTY1
     C                     ENDIF
     C                     MOVELQTY1      ACDSC2
     C                     MOVEL'A'       ACSEPT
CLJ  C                     EXSR SR8000                     設ACDSC1
     C*
0007AC                     MOVELTXSTNO    ACDSC3           摘要三
     C                     MOVELD#USER    ACENTM           建檔人員
9908AC*                    MOVE UDATE     ACENTD           建檔日期
9908AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9908AC*                    MOVE UDATE     ACUPDD           異動日期
9908AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     C           TXAR      IFEQ 'P'
     C           TXAR      OREQ 'M'
     C                     EXSR SR1000                     各廠調整額
     C                     ENDIF
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#02     BEGSR
     C*
     C****  WRITE  TO TAIPEI  "1134"應收帳款
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDU#YYMM    ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM2     ACITEM
     C  N51                MOVEL'C'       ACDC
     C   51                MOVEL'D'       ACDC
0109BC   77                MOVEL'1134'    ACNO
0109BC  N77                MOVEL'111101'  ACNO
     C                     MOVEL'G'       ACTYCO
     C                     MOVELKORNO     ACRLNO
     C                     MOVELTXCUNM    ACCSNM
     C                     Z-ADDAMT2      ACAMT
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9908AC*                    MOVE UDATE     ACENTD           建檔日期
9908AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9908AC*                    MOVE UDATE     ACUPDD           異動日期
9908AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#03     BEGSR
     C*
     C****  WRITE  TO TAIPEI  "3511"公司往來
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXPCNO    TX@     1
0007AC           TX@       IFEQ 'D'
0109AC           TX@       OREQ 'X'
0110AC           TX@       OREQ 'R'
0111AC           TX@       OREQ 'J'
0112AC           TX@       OREQ 'N'
0201AC           TX@       OREQ 'V'
0007AC                     MOVEL'U'       TX@
0007AC                     ENDIF
     C                     MOVELTX@       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDU#YYMM    ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADD1         ACITEM
     C  N51                MOVEL'C'       ACDC
     C   51                MOVEL'D'       ACDC
0109BC   77                MOVEL'3511'    ACNO
0109BC  N77                MOVEL'390101'  ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9908AC*                    MOVE UDATE     ACENTD           建檔日期
9908AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9908AC*                    MOVE UDATE     ACUPDD           異動日期
9908AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#04     BEGSR
     C*
     C****  WRITE  TO TAIPEI  "3512-6"廠區往來
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDU#YYMM    ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADD1         ACITEM
     C   51                MOVEL'C'       ACDC
     C  N51                MOVEL'D'       ACDC
     C                     MOVE *BLANK    KGRP
0109BC                     MOVEL*BLANKS   K#GRP
0109BC   77      TX@       LOKUPTAB1      TABR1          31
0109BC   31 77             MOVELTABR1     KGRP
0109BC  N77      TX@       LOKUPTAB2      TABR2          31
0109BC   31N77             MOVELTABR2     K#GRP
0109BC   77                MOVELKACNO     ACNO
0109BC  N77                MOVELK#ACNO    ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9908AC*                    MOVE UDATE     ACENTD           建檔日期
9908AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9908AC*                    MOVE UDATE     ACUPDD           異動日期
9908AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#01     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#02     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#02
     C                     ADD  1         LCNT
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#03     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#03
     C                     ADD  1         LCNT
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#04     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#04
     C                     ADD  2         LCNT
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         SR1000    BEGSR
     C*****************************************************************
0109BC   77      ACNO      IFNE '4111'
     C                     GOTO EN1000
     C                     ENDIF
0109BC  N77      ACNO      IFNE '410101'
0109BC                     GOTO EN1000
0109BC                     ENDIF
     C*
     C                     Z-ADD1         I       20
     C                     SORTAARY                        排序
     C           1         DO   30        I
     C                     MOVE ARY,I     D#ARY
     C*
     C           TX@       IFEQ D#AREA                     廠區
     C*
     C           ACDC      IFEQ 'C'
     C                     ADD  ACAMT     D#AMT            調整金額
     C                     ELSE
     C                     SUB  ACAMT     D#AMT            調整金額
     C                     ENDIF
     C*
     C                     MOVE D#ARY     ARY,I
     C                     LEAVE
     C                     ELSE
     C           D#AREA    IFEQ '9'
     C                     MOVELTX@       D#AREA           廠區
     C*
     C           ACDC      IFEQ 'C'
     C                     Z-ADDACAMT     D#AMT            調整金額
     C                     ELSE
     C                     Z-SUBACAMT     D#AMT            調整金額
     C                     ENDIF
     C*
     C                     MOVE D#ARY     ARY,I
     C                     LEAVE
     C                     ENDIF                           類別
     C                     ENDIF                            D#AREA
     C                     ENDDO
     C*
     CSR         EN1000    ENDSR
     C*****************************************************************
     CSR         SR1100    BEGSR
     C*****************************************************************
     C                     EXCPTET#LR1
     C*
     C                     Z-ADD1         I
     C                     SORTAARY
     C           1         DO   6         I
     C                     MOVE ARY,I     D#ARY
     C*
     C           D#AREA    IFEQ '9'
     C                     LEAVE
     C                     ENDIF
     C*
     C           D#AMT     IFEQ 0
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVELD#AREA    AREA             調整廠區
     C                     Z-ADDD#AMT     ARAMT            調整金額
     C                     ADD  D#AMT     ARTMT            調整額合計
     C                     EXCPTET#LR2
     C                     ENDDO
     C*
     C                     EXCPTET#LIN
     C                     EXCPTET#LR3
     C*
     CSR                   ENDSR
CLJB C*****************************************************************
     C*  更改摘要一之品名為磅單內銷貨數量最多之品名
     C*****************************************************************
     CSR         SR8000    BEGSR
     C           K#TX      KLIST
     C                     KFLD           F#CODE  4
     C                     KFLD           F#NO    8
     C*
     C                     MOVEL'SA04'    F#CODE
     C                     MOVELTXPCNO    F#NO
     C                     MOVEL*BLANK    W#PDNM  3
     C                     Z-ADD0         W#QTY   70
     C           K#TX      CHAINTXW                  69
     C           *IN69     DOWEQ'0'
     C           TWQTY     IFGT W#QTY
     C                     Z-ADDTWQTY     W#QTY
     C                     MOVELTWPDNM    W#PDNM           量多之品名
     C                     ENDIF
     C           TXRESV    IFNE *BLANK
     C           TXRESV    ANDEQTWPDNM
     C                     MOVELTWPDNM    W#PDNM           品不為空白
     C                     ENDIF
     C           K#TX      READETXW                      69
     C                     ENDDO
     C*
     C                     MOVELW#PDNM    ACDSC1           摘要一
     C*
     CSR                   ENDSR
---  C*****************************************************************
     OQPRINT  E   03           EH#01
     O                                   12 '報表編號：'
     O                                   18 'ARP012'
     O                                   85 '調整單入帳清單（整月）'
     O                                  140 '列表日期:'
9908AO*                        UDATE Y  148
9908AO                         U#SYSD   150 '    /  /  '
     O        E 1              EH#01
     O                                   82 '******************'
     O                                  140 '頁　　數:'
     O                         PAG   Z  143
     O        E 1              EH#01
     O                                   71 '年月:'
CLJ  O                         DATEA     80 '    /  '
     O        E 1              EH#01
     O                                   41 '借'
     O        E 1              EH#01
     O                                   10 '入帳日期'
     O                                   20 '磅單編號'
     O                                   26 '訂單'
     O                                   35 '客　戶'
     O                                   41 '貸'
     O                                   51 '會計科目'
0109BO                                   67 '品　名'
0109BO                                   89 '數量'
0109BO                                   99 '單價'
0109BO                                  116 '借方金額'
0109BO                                  133 '貸方金額'
0109BO                                  140 '部門'
0109BO                                  154 '傳票編號'
0109BO                                  167 '庫別代號'
     O        E 1              EH#01
     O                                   20 '--------------------'
     O                                   40 '--------------------'
     O                                   60 '--------------------'
     O                                   80 '--------------------'
     O                                  100 '--------------------'
     O                                  120 '--------------------'
     O                                  132 '--------------------'
     O                                  150 '--------------------'
     O                                  155 '-----'
0007AO                                  165 '----------'
     O        E 1              ED#01
     O                         ACYMD     10 '    /  /  '
     O                         TXPCNO    19
     O                         KORNO     26
     O                         ACCSNM    37
0109BO                 77 51             58 'C 4111  銷貨收入L'
0109BO                 77N51             58 'D 4111  銷貨收入L'
0109BO                N77 51             58 'C 410101銷貨收入L'
0109BO                N77N51             58 'D 410101銷貨收入L'
     O*                        TXPDNM    61
0109BO                         W#PDNM    63
     O                         NAME1   +  1
0109BO                         QTY1  K   89
0109BO                         TXUPRCK   99
0109BO                   N51   ACAMT K  116
0109BO                    51   ACAMT K  133
0109BO                         W#DPNO   143
0109BO                         TXAR     146
0109BO                         TX@      147
0109BO                         VRNOY    153
     O                                      '-'
0109BO                         ACITEM   157
0109BO                         ACDSC3   167
     O        E 1              ED#02
     O                         ACYMD     10 '    /  /  '
     O*                        TXPCNO    19
     O                         KORNO     26
     O                         ACCSNM    37
0109BO                 77N51             58 'C 1134  應收帳款G'
0109BO                 77 51             58 'D 1134  應收帳款G'
0109BO                N77N51             58 'C 111101應收帳款G'
0109BO                N77 51             58 'D 111101應收帳款G'
0109BO                    51   ACAMT K  116
0109BO                   N51   ACAMT K  133
0109BO                         TXAR     146
0109BO                                  147 'P'
0109BO                         VRNOX    153
     O                                      '-'
0109BO                         ACITEM   157
     O        E 1              ED#03
     O                         ACYMD     10 '    /  /  '
     O*                        TXPCNO    20
0109BO                 77N51             58 'C 3511  公司往來A'
0109BO                 77 51             58 'D 3511  公司往來A'
0109BO                N77N51             58 'C 390101公司往來A'
0109BO                N77 51             58 'D 390101公司往來A'
0109BO                    51   ACAMT K  116
0109BO                   N51   ACAMT K  133
0109BO                         TXAR     146
0109BO                         TX@      147
0109BO                         VRNOY    153
     O                                      '-'
0109BO                         ACITEM   157
     O        E 1              ED#04
     O                         ACYMD     10 '    /  /  '
     O*                        TXNO      20
     O                   N51             41 'D '
     O                    51             41 'C '
0109BO                 77      KACNO     46
0109BO                 77      KACNON    56
0109BO                N77      K#ACNO    47
0109BO                N77      K#ACNN    57
0109BO                                   58 'A'
0109BO                   N51   ACAMT K  116
0109BO                    51   ACAMT K  133
0109BO                         TXAR     146
0109BO                                  147 'P'
0109BO                         VRNOX    153
     O                                      '-'
0109BO                         ACITEM   157
     O        E 1              ED#04
     O        E 1              ET#LR
     O                                   60 '入帳筆數'
     O                         COUNT K +  1
0109BO                N51                98 '銷貨調整金額合計'
0109BO                N51      AMTTT K  116
0109BO                 51               115 '銷貨調整金額合計'
0109BO                 51      AMTTT K  133
     O        E 3              ET#LR1
0109BO                                   58 '廠別'
0109BO                                   95 '銷貨調整金額'
     O        E 1              ET#LR2
0109BO                         AREA      55
0109BO                         ARAMT J   95
     O        E 1              ET#LR3
0109BO                                   58 '合計'
0109BO                         ARTMT J   95
     O        E 1              ET#LIN
     O                                   53 '---'
     O                                   73 '--------------------'
     O                                   93 '--------------------'
     O        E   60           ET#LRR
     O                                   50 '經理（廠長）：'
     O                                   73 '複核：'
     O                                   96 '製票：'
** TAB1
K3512高雄往來K210
T3513桃園往來T380
H3514苗栗往來H800
M3515台中往來W040
L3516林口往來L080
U3518新桃往來U380
** TAB2
K390102 高雄往來K210
T390103 桃園往來T380
H390104 苗栗往來H800
M390105 台中往來W040
L390106 林口往來L080
U390108 新桃往來U380
