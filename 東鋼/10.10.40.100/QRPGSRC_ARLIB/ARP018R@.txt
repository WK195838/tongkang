     H        1   Y                                     1                 BR003
     FRCVWF   IP  E           K        DISK
     FRCVDTL  UF  E           K        DISK
     F            RVREC                             KRENAMERVREC1
     FINVDTLL1UF  E           K        DISK
     FINVMST  IF  E           K        DISK
     FBCNAME  IF  E           K        DISK
     FBANKAC  IF  E           K        DISK
     FACTRAN  UF  E           K        DISK                      A
     FQPRINT  O   F     158            PRINTER
     ****
     E                    TAB1    1   3  1   TABR1  18
     IRVREC
     I                                              RVNO  L1
     ****
     I            DS
     I                                        1  18 KGRP
     I                                        1   4 KACNO
     I                                        5  14 KACNON
     I                                       15  18 KDPNO
     I            DS
     I                                        1   4 VRNOX
     I                                        2   40NO1
     I                                       11  14 VRNON
     I                                       12  140NO3
     I            DS
     I                                        1   4 VRNOY
     I                                        2   40NO2
     I                                       11  17 VRNOG
     I                                       11  11 VRNOG1
     I                                       12  12 VRNOG2
     I                                       13  16 VRNOG3
     I                                       14  160VRNOG4
     I                                       17  17 VRNOG5
     I                                       21  30 RVSANO
     I                                       21  21 ANTYPE
     I                                       22  22 ANCURR
     I                                       23  30 ANACNO
     I            DS
     I                                        1  10 ACNOM
     I                                        1   9 ACNOM1
     I                                       10  10 ACNOM2
     I                                       21  36 DSC0
     I                                       21  28 DSC1
     I                                       21  30 DSC11
     I                                       29  36 DSC2
     I                                       31  36 DSC21
     I                                       41  47 VUNOG
     I                                       41  41 VUNOG1
     I                                       42  42 VUNOG2
     I                                       43  46 VUNOG3
     I                                       47  47 VUNOG4
     I           UDS
     I                                        1   60YMD
     I                                     10211021 TXAR
     C*****
     C           IVKEY     KLIST
     C                     KFLD           IVACNT
     C                     KFLD           IVAPNO
     C           RVKEY     KLIST
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C           ACKEY1    KLIST
     C                     KFLD           ACVRNO
     C                     KFLD           ACITEM
     C           ACKEY     KLIST
     C                     KFLD           ACVRNO
     C           ANKEY     KLIST
     C                     KFLD           ANTYPE
     C                     KFLD           ANCURR
     C                     KFLD           ANACNO
     C***
     C  N10                EXSR IN#01
     C   L1 10             EXSR DL#01
     C      10             EXSR DP#01
     CL1    10             EXSR TL#01
     CLR    10             EXSR TL#LR
     C**--------------------------------------------------**
     CSR         IN#01     BEGSR
     C                     Z-ADD0         AMTI   132
     C                     Z-ADD0         AMTF   132
     C                     Z-ADD0         AMTT   132
     C                     Z-ADD0         VAMT   132
     C                     Z-ADD0         AMTTT  132
     C                     Z-ADD0         ITEM    20
     C                     Z-ADD0         ITEM1   20
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD60        TCNT    20
     C                     Z-ADD0         PAG     30
     C                     MOVEL*ALL'-'   ADASH 158
     C                     MOVEL*BLANK    VUNOG
     C           TXAR      IFEQ 'P'
     C                     SETON                     32
     C                     MOVEL'P'       TXAR1
     C                     ELSE
     C                     SETOF                     32
     C                     ENDIF
     C                     MOVE *BLANK    KGRP
     C  N32      TXAR      LOKUPTAB1      TABR1          31
     C  N32 31             MOVELTABR1     KGRP
     C                     SETON                     10
     CSR                   ENDSR
     C***
     C**----------------------------------------------**
     C**    L1 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#01     BEGSR
     C   32                Z-ADD0         ITEM
     C  N32                Z-ADD1         ITEM
     C  N32                Z-ADD1         ITEM1
     C                     Z-ADD0         AMTT
     C                     Z-ADD0         AMTI
     C                     Z-ADD0         AMTF
     C                     Z-ADD0         DYMD    60
     ****
     C           RVVNO1    IFNE *BLANK
     C                     EXSR @DLNO
     C                     ELSE
     C                     EXSR @FDNO
     C                     ENDIF
     C                     MOVELVRNOX     VRNOY
     C           NO1       ADD  1         NO2
     CSR                   ENDSR
     C**----------------------------------------------**
     C**  DELETE ACTRAN  (IF ALREADY HAVE NO)         **
     C**----------------------------------------------**
     CSR         @DLNO     BEGSR
     C                     MOVELRVVNO1    VRNOG
     C                     MOVELVRNOG3    VRNOX
     C           *IN32     IFEQ '0'
     C           NO1       DIV  2         A       30
     C                     MVR            B       30
     C           B         IFEQ 0
     C                     SUB  1         NO1
     C                     ENDIF
     C                     ENDIF
     C                     MOVELVRNOX     VRNOG3
     C                     MOVE *BLANK    ACVRNO
     C                     MOVELVRNOG3    ACVRNO
     C           ACKEY     SETLLACTRAN               95
     C           *IN95     DOWEQ'0'
     C  N95      ACKEY     READEACTRAN                   95
     C   95                LEAVE
     C*                    MOVELACDSC2    DSC2
     C*          DSC21     IFNE RVNO
     C*                    LEAVE
     C*                    ENDIF
     C                     DELETRACVOUR
     C                     ENDDO
     C***
     C           *IN32     IFEQ '0'
     C                     ADD  1         VRNOG4
     C                     MOVE *BLANK    ACVRNO
     C                     MOVELVRNOG3    ACVRNO
     C           ACKEY     SETLLACTRAN               95
     C           *IN95     DOWEQ'0'
     C  N95      ACKEY     READEACTRAN                   95
     C   95                LEAVE
     C                     DELETRACVOUR
     C                     ENDDO
     C                     ENDIF
     CSR                   ENDSR
     C**----------------------------------------------**
     C**  FIND LAST NO                                **
     C**----------------------------------------------**
     CSR         @FDNO     BEGSR
     C                     MOVEL'H999'    ACVRNO
     C                     Z-ADD99        ACITEM
     C                     SETOF                     95
     C           ACKEY1    SETLLACTRAN
     C                     READPACTRAN                   95
     C  N95                MOVELACVRNO    TYPE    1
     C  N95      TYPE      COMP 'H'                  9595
     C  N95                MOVELACVRNO    VRNON
     C  N95                ADD  1         NO3
     C  N95                MOVELVRNON     VRNOX
     C   95                MOVEL'H001'    VRNOX
     CSR                   ENDSR
     C**----------------------------------------------**
     C**       DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C**
     C                     SELEC
     C           RVNTTP    WHGE 'A'
     C           RVNTTP    ANDLE'E'
     C                     MOVEL'1131'    ACNN    4
     C                     MOVEL'H'       ACTP    1
     C           RVNTTP    WHEQ 'I'
     C                     MOVEL'1131'    ACNN    4
     C                     MOVEL'H'       ACTP    1
     C           RVDUDT    IFGT DYMD
     C                     Z-ADDRVDUDT    DYMD
     C                     ENDIF
     C           RVNTTP    WHEQ 'F'
     C           RVNTTP    OREQ 'G'
     C           RVNTTP    OREQ 'J'
     C                     MOVELRVACNO    ACNN
     C                     MOVEL'C'       ACTP
     C                     EXSR CK#SNO
     C           RVNTTP    WHEQ 'H'
     C                     MOVEL'1111'    ACNN
     C                     MOVEL'D'       ACTP
     C                     MOVEL'NT'      RVRLNO
     C*          RVACNO    WHEQ '2221'
     C*                    MOVEL'2221'    ACNN
     C*                    MOVEL'G'       ACTP
     C*          RVACNO    WHEQ '1512'
     C*                    MOVEL'1512'    ACNN
     C*                    MOVEL'E'       ACTP
     C*          RVACNO    WHEQ '1138'
     C*                    MOVEL'1138'    ACNN
     C*                    MOVEL'E'       ACTP
     C*          RVACNO    WHEQ '2131'
     C*                    MOVEL'2131'    ACNN
     C*                    MOVEL'G'       ACTP
     C*          RVACNO    WHEQ '2121'
     C*                    MOVEL'2121'    ACNN
     C*                    MOVEL'K'       ACTP
     C*          RVACNO    WHEQ '2129'
     C*                    MOVEL'2129'    ACNN
     C*                    MOVEL'E'       ACTP
     C*          RVACNO    WHEQ '2122'
     C*                    MOVEL'2122'    ACNN
     C*                    MOVEL'G'       ACTP
     C*          RVACNO    WHEQ '1137'
     C*                    MOVEL'1137'    ACNN
     C*                    MOVEL'A'       ACTP
     C*          RVACNO    WHEQ '2159'
     C*                    MOVEL'2159'    ACNN
     C*                    MOVEL'D'       ACTP
     C*          RVACNO    WHEQ '1169'
     C*                    MOVEL'1169'    ACNN
     C*                    MOVEL'D'       ACTP
     C*          RVACNO    WHEQ '1114'
     C*                    MOVEL'1114'    ACNN
     C*                    MOVEL'C'       ACTP
     C*          RVACNO    WHEQ '1111'
     C*                    MOVEL'1111'    ACNN
     C*                    MOVEL'D'       ACTP
     C*                    MOVEL'NT'      RVRLNO
     C*          RVACNO    WHEQ '8111'
     C*                    MOVEL'8111'    ACNN
     C*                    MOVEL'G'       ACTP
     C*          RVACNO    WHEQ '8149'
     C*          RVACNO    OREQ '8249'
     C*          RVACNO    OREQ '7112'
     C*          RVACNO    OREQ '7114'
     C*          RVACNO    OREQ '7111'
     C*                    MOVELRVACNO    ACNN
     C*                    MOVEL'M'       ACTP
     C*          RVACNO    WHEQ '8211'
     C*                    MOVELRVACNO    ACNN
     C*                    MOVEL'D'       ACTP
     C                     OTHER
     C                     MOVELRVACNO    ACNN
     C*                    MOVEL'B'       ACTP
     C                     ENDSL
     C***
     C                     MOVELACNN      ACNN3   3
     C           ACNN3     IFEQ '711'
     C                     MOVEL'M'       ACTP
     C                     ENDIF
     C***
     C                     MOVELACNN      ACNN2   2
     C           ACNN2     IFEQ '35'
     C                     MOVEL'A'       ACTP
     C                     ENDIF
     C***
     C           RVACTP    IFEQ ' '
     C                     MOVEL'D'       RVACTP
     C                     ENDIF
     C***
     C                     SETOF                     70
     C                     MOVELACNN      ACNN1   1
     C  N32      ACNN2     IFEQ '71'
     C           ACNN1     OREQ '8'
     C           ACNN      OREQ '2221'
     C                     SETON                     70
     C                     MOVEL'P'       TXAR1   1
     C                     ELSE
     C                     SETOF                     70
     C                     MOVELTXAR      TXAR1
     C                     ENDIF
     C****
     C           RVACTP    IFEQ 'D'
     C                     ADD  RVRAMT    AMTTT
     C  N70                ADD  RVRAMT    AMTT
     C   70                ADD  RVRAMT    AMTF
     C                     ELSE
     C                     SUB  RVRAMT    AMTTT
     C  N70                SUB  RVRAMT    AMTT
     C   70                SUB  RVRAMT    AMTF
     C                     ENDIF
     ***
     C           ACNN      CHAINBCNAME               97
     C   97                MOVEL*BLANK    ACNOM
     C   97                MOVEL*BLANK    ACTP
     C  N97                MOVELACCHNM    ACNOM1
     C  N97                MOVE ''      ACNOM2
     C  N97                MOVELACDM06    ACTP
     C***
     C                     Z-ADDRVRAMT    VAMT   132
     C  N70                EXSR WR#01
     C   70                EXSR WR#011
     C                     EXSR PR#01
     C           RVKEY     CHAINRCVDTL               95
     C      95             GOTO D#PRZ
     C                     MOVELTXAR      VUNOG1
     C                     MOVELTXAR1     VUNOG2
     C  N70                MOVELVRNOX     VUNOG3
     C   70                MOVELVRNOY     VUNOG3
     C                     MOVELVUNOG     RVVNO1
     C                     MOVEL'C'       RVFLAG
     C                     MOVELTXAR      RVTXAR
     C                     Z-ADDUDATE     RVTXDT
     C                     UPDATRVREC1
     CSR         D#PRZ     ENDSR
     C**----------------------------------**
     C**   L1 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#01     BEGSR
     C   32                Z-ADDITEM      ITEM1   20
     C***
     C           DYMD      IFEQ 0
     C                     Z-ADDYMD       DYMD
     C                     ENDIF
     C***
     C                     MOVEL'6'       IVACNT
     C                     MOVELRVNO      IVAPNO
     C           IVKEY     SETLLINVDTLL1             95
     C           *IN95     DOWEQ'0'
     C  N95      IVKEY     READEINVDTLL1                 95
     C   95                LEAVE
     C           IVFLAG    IFEQ 'D'
     C                     ITER
     C                     ENDIF
     C           IVNO      CHAININVMST               96
     C           IVAMT     IFGE 0
     C                     MOVEL'D'       DC      1
     C                     Z-ADDIVAMT     VAMT
     C                     ELSE
     C                     MOVEL'C'       DC
     C                     Z-SUBIVAMT     VAMT
     C                     ENDIF
     C                     ADD  IVAMT     AMTI
     C                     ADD  1         ITEM1
     C                     EXSR WR#02
     C                     EXSR PR#02
     C                     MOVELTXAR      VUNOG1
     C                     MOVEL'P'       VUNOG2
     C   32                MOVELVRNOX     VUNOG3
     C  N32                MOVELVRNOY     VUNOG3
     C                     MOVELVUNOG     IVACNO
     C                     MOVEL'C'       IVFLAG
     C                     MOVELTXAR      IVTXAR
     C                     Z-ADDUDATE     IVTXDT
     C                     UPDATIVREC
     C                     ENDDO
     C****
     C   32                EXCPTSP#01
     C   32                ADD  1         LCNT
     C  N32                EXSR WR#03
     C  N32                EXSR PR#03
     CSR         TL#01Z    ENDSR
     C**----------------------------------**
     C**   LR BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     CSR                   ENDSR
     C*****
     C**------------------------------------------**
     C**    CHECK   存入帳號 820101             **
     C**------------------------------------------**
     CSR         CK#SNO    BEGSR
     C           YMD       IFLT 820101
     C           ANKEY     CHAINBANKAC               97
     C  N97                MOVELACOLID    RVSANO
     C                     ENDIF
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#01     BEGSR
     C****  WRITE  TO LOCAL
     C                     ADD  1         ITEM
     C                     MOVE *BLANK    DSC0
     C                     CLEARRACVOUR
     C                     MOVEL'O'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXAR      ACCARD
     C                     MOVEL'A'       ACKIND
     C                     Z-ADDRVDATE    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM      ACITEM
     C                     MOVELRVACTP    ACDC
     C                     MOVELACNN      ACNO
     C                     MOVELACTP      ACTYCO
     C                     SELEC
     C           ACTP      WHEQ 'B'
     C                     MOVELRVDPNO    ACDPNO
     C*                    MOVELRVRLNO    ACRLNO
     C           ACTP      WHEQ 'M'
     C           ACNN      IFEQ '8149'
     C           ACNN      OREQ '8249'
     C                     MOVELRVDPNO    ACDPNO
     C                     MOVELRVDPNO    ACRLNO
     C                     ELSE
     C                     MOVELRVDPNO    ACDPNO
     C                     MOVELRVRLNO    ACRLNO
     C                     ENDIF
     C           ACTP      WHEQ 'C'
     C                     MOVELRVSANO    ACBKNO
     C           ACTP      WHEQ 'K'
     C                     MOVELRVSANO    ACBKNO
     C                     MOVELRVNTNO    ACRLNO
     C                     MOVELRVDUDT    ACDUE
     C                     MOVELRVCUNM    ACCSNM
     C           ACTP      WHEQ 'H'
     C                     MOVELRVDUDT    ACDUE
     C                     MOVELRVCUNM    ACCSNM
     C                     MOVELRVPANO    DSC11
     C                     MOVELRVNTNO    ACRLNO
     C           ACTP      WHEQ 'D'
     C                     MOVELRVRLNO    ACRLNO
     C           ACTP      WHEQ 'E'
     C           ACNN      IFEQ '2125'
     C                     MOVELRVRESV    ACCSNM
     C                     ELSE
     C                     MOVELRVCUNM    ACCSNM
     C                     ENDIF
     C           ACTP      WHEQ 'G'
     C                     MOVELRVRLNO    ACRLNO
     C                     MOVELRVCUNM    ACCSNM
     C                     ENDSL
     C                     Z-ADDVAMT      ACAMT
     C                     MOVELRVNO      DSC21
     C                     MOVEL'A'       ACSEPT
     C                     MOVELDSC1      ACDSC1
     C                     MOVELDSC2      ACDSC2
     C                     WRITERACVOUR
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#011    BEGSR
     C****  WRITE  TO LOCAL
     C                     ADD  1         ITEM1
     C                     MOVE *BLANK    DSC0
     C                     CLEARRACVOUR
     C                     MOVEL'O'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXAR1     ACCARD
     C                     MOVEL'A'       ACKIND
     C                     Z-ADDRVDATE    ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADDITEM1     ACITEM
     C                     MOVELRVACTP    ACDC
     C                     MOVELACNN      ACNO
     C                     MOVELACTP      ACTYCO
     C                     SELEC
     C           ACTP      WHEQ 'B'
     C                     MOVELRVDPNO    ACDPNO
     C*                    MOVELRVRLNO    ACRLNO
     C           ACTP      WHEQ 'M'
     C                     MOVELRVDPNO    ACDPNO
     C                     MOVELRVRLNO    ACRLNO
     C           ACTP      WHEQ 'C'
     C                     MOVELRVSANO    ACBKNO
     C           ACTP      WHEQ 'K'
     C                     MOVELRVSANO    ACBKNO
     C                     MOVELRVNTNO    ACRLNO
     C                     MOVELRVDUDT    ACDUE
     C                     MOVELRVCUNM    ACCSNM
     C           ACTP      WHEQ 'H'
     C                     MOVELRVDUDT    ACDUE
     C                     MOVELRVCUNM    ACCSNM
     C                     MOVELRVPANO    DSC11
     C                     MOVELRVNTNO    ACRLNO
     C           ACTP      WHEQ 'D'
     C                     MOVELRVRLNO    ACRLNO
     C           ACTP      WHEQ 'E'
     C           ACNN      IFEQ '2125'
     C                     MOVELRVRESV    ACCSNM
     C                     ELSE
     C                     MOVELRVCUNM    ACCSNM
     C                     ENDIF
     C           ACTP      WHEQ 'G'
     C                     MOVELRVRLNO    ACRLNO
     C                     MOVELRVCUNM    ACCSNM
     C                     ENDSL
     C                     Z-ADDVAMT      ACAMT
     C                     MOVELRVNO      DSC21
     C                     MOVEL'A'       ACSEPT
     C                     MOVELDSC1      ACDSC1
     C                     MOVELDSC2      ACDSC2
     C                     WRITERACVOUR
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#01     BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD10        LCNT
     C   75                EXCPTEH#01
     C           RVACTP    IFEQ 'D'
     C                     SETON                     41
     C                     ELSE
     C                     SETOF                     41
     C                     ENDIF
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C**------------------------------------------**
     C**    L1     TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#02     BEGSR
     C****  WRITE  TO TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'O'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     Z-ADDIVACDT    ACYMD
     C   32                MOVELVRNOX     ACVRNO
     C  N32                MOVELVRNOY     ACVRNO
     C                     Z-ADDITEM1     ACITEM
     C                     MOVELDC        ACDC
     C                     MOVEL'1134'    ACNO
     C                     MOVEL'G'       ACTYCO
     C                     Z-ADDVAMT      ACAMT
     C  N96                MOVELINORNO    ACRLNO
     C  N96                MOVELINCUNM    ACCSNM
     C*                    MOVELDYMD      ACDSC1
     C                     MOVEL'A'       ACSEPT
     C                     WRITERACVOUR
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#02     BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD10        LCNT
     C   75                EXCPTEH#01
     C           DC        IFEQ 'D'
     C                     SETON                     42
     C                     ELSE
     C                     SETOF                     42
     C                     ENDIF
     C                     EXCPTET#01
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C**------------------------------------------**
     C**    L2     TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#03     BEGSR
     C****  WRITE  TO TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'O'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     Z-ADDYMD       ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADD01        ACITEM
     C                     ADD  AMTF      AMTI
     C           AMTI      IFGE 0
     C                     MOVEL'C'       ACDC
     C                     MOVEL'C'       DC1     1
     C                     ELSE
     C                     MOVEL'D'       ACDC
     C                     MOVEL'D'       DC1
     C                     Z-SUBAMTI      AMTI
     C                     ENDIF
     C                     Z-ADDAMTI      ACAMT
     C                     MOVELKACNO     ACNO
     C                     MOVEL'A'       ACTYCO
     C                     MOVEL'A'       ACSEPT
     C                     WRITERACVOUR
     C****  WRITE  TO NON-TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'O'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXAR      ACCARD
     C                     MOVEL'A'       ACKIND
     C                     Z-ADDYMD       ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADD01        ACITEM
     C           AMTT      IFGT 0
     C                     MOVEL'C'       ACDC
     C                     MOVEL'C'       DC2     1
     C                     ELSE
     C                     MOVEL'D'       ACDC
     C                     MOVEL'D'       DC2
     C                     Z-SUBAMTT      AMTT
     C                     ENDIF
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'3511'    ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C                     WRITERACVOUR
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#03     BEGSR
     C           DC1       IFEQ 'D'
     C                     SETON                     43
     C                     ELSE
     C                     SETOF                     43
     C                     ENDIF
     C           DC2       IFEQ 'D'
     C                     SETON                     44
     C                     ELSE
     C                     SETOF                     44
     C                     ENDIF
     C                     EXCPTET#03
     C                     ADD  3         LCNT
     CSR                   ENDSR
     OQPRINT  E  103           EH#01
     O                                   12 '報表編號：'
     O                                   18 'ARP018'
     O                                   81 '繳款收入入帳清單'
     O                                  140 '列表日期:'
     O                         UDATE Y  148
     O        E  1             EH#01
     O                                   82 '********************'
     O                                  140 '頁　　數:'
     O                         PAG   Z  143
     O        E  1             EH#01
     O                                   73 '入帳日期:'
     O                         YMD   Y   81
     O        E  1             EH#01
     O                                   24 '借'
     O        E  1             EH#01
     O*                                  10 '入帳日期'
     O                                   10 '繳款編號'
     O                                   20 '客　戶'
     O                                   24 '貸'
     O                                   40 '會　計　科　目'
     O                                   60 '借方金額'
     O                                   80 '貸方金額'
     O                                   90 '到期日期'
     O                                  100 '票據號碼'
     O                                  110 '付款銀行'
     O                                  128 '票據帳號'
     O                                  140 '存入帳號'
     O                                  152 '傳票編號'
     O                                  158 '部門'
     O        E  1             EH#01
     O                         ADASH    158
     O        E  1             ED#01
     O*                        RVDATEY    9
     O                         RVNO       7
     O                                    8 '-'
     O                         RVITEM    10
     O                         RVCUNM    20
     O                         RVACTP    22
     O                         ACNN      27
     O                         ACNOM     37
     O                         ACTP      38
     O                 41      VAMT  K   60
     O                N41      VAMT  K   80
     O                         RVDUDT    89 '  /  /  '
     O                         ACRLNO   100
     O                         RVPBID   114
     O                         RVPANO   128
     O                         RVSANO   140
     O                         TXAR     144
     O                         TXAR1    145
     O                N70      VRNOX    149
     O                 70      VRNOY    149
     O                                  150 '-'
     O                N70      ITEM     152
     O                 70      ITEM1    152
     O                         RVDPNO   157
     O        E  1             ET#01
     O                         INCUNM    20
     O                         DYMD      89 '  /  /  '
     O                         DC        22
     O                                   38 '1134應收帳款G'
     O                 42      VAMT  K   60
     O                N42      VAMT  K   80
     O                         IVNO     128
     O                         INORNO    97
     O                         TXAR     144
     O                                  145 'P'
     O                    32   VRNOX    149
     O                   N32   VRNOY    149
     O                                  150 '-'
     O                         ITEM1    152
     O        E  1             SP#01
     O        E  1             ET#03
     O                         DC2       22
     O                                   37 '3511公司往來'
     O                 44      AMTT  K   60
     O                N44      AMTT  K   80
     O                         TXAR     144
     O                         TXAR     145
     O                         VRNOX    149
     O                                  152 '-01'
     O        E  2             ET#03
     O                         DC1       22
     O                         KACNO     27
     O                         KACNON    37
     O                 43      AMTI  K   80
     O                N43      AMTI  K   60
     O                         TXAR     144
     O                                  145 'P'
     O                         VRNOY    149
     O                                  152 '-01'
     O        E  1             ET#LR
     O                                   62 '應收帳款　沖銷總額:'
     O                         AMTTT K   80
** TAB1
K3512高雄往來K210
H3514苗栗往來H800
T3513桃園往來T380
