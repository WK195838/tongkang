     H        1   Y                                     1                 BR003
     FINVMST  IP  E           K        DISK
     FINVDTL  IF  E           K        DISK
     FQPRINT  O   F     132            PRINTER
      *************************************************************
     E                    TAB3    6   6  1   TAB4    6   *地區名稱
     IINREC
     I                                              INTYPEL4
     I                                              INKINDL3
     I*************************************************************
     I****
     IAADS        DS
     I              'も每月開立發票彙總-C         CON3                       X
     I              '表も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
      *
     I           UDS
     I                                        1   6 NO1
     I                                        7  12 NO2
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C*****************************************************************
     C*          Key       List
     C*****************************************************************
     C*
     C*FILE => INVDTL KEY01=發票號碼+類別+項次
     C*
     C           KEY01     KLIST
     C                     KFLD           IVNO
     C                     KFLD           IVACNT
     C                     KFLD           IVITEM
     C           KEY02     KLIST
     C                     KFLD           IVNO
     C                     KFLD           IVACNT
     C**************************************************************
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L4                EXSR DD#01
     C   L3                Z-ADD0         WQTY    90
     C   L3                Z-ADD0         WAAMT  110
     C   L3                Z-ADD0         WATAX  110
     C   L3                Z-ADD0         WTOT   110
     C                     EXSR DP#01
     CL3                   EXSR DT#01
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
      *
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD60        TCNT    30
     C                     MOVE *ALL'='   ALS    80
     C                     MOVE *ALL'-'   ALQ    94
     C*
     C*讀取廠區名稱
     C*
     C           TXAR      LOKUPTAB3      TAB4           55*EQ
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS  L4                            **
     C**----------------------------------------------**
     CSR         DD#01     BEGSR
     C***
     C                     Z-ADD60        LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD9         LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     ADD  INAAMT    WAAMT
     C                     ADD  INATAX    WATAX
     C           INAAMT    ADD  INATAX    WAMT   110
     C                     ADD  WAMT      WTOT
     C***
     C                     MOVELINNO      IVNO
     C                     MOVEL'1'       IVACNT
     C                     Z-ADD0         IVITEM
     C           KEY01     SETLLIVREC                60
     C           *IN60     DOWEQ*OFF
     C           KEY02     READEIVREC               N    60
     C   60                LEAVE
     C           IVFLAG    COMP 'D'                      61
     C   61                ITER
     C           IVACNT    IFEQ '1'
     C                     ADD  IVQTY     WQTY
     C                     ENDIF
     C                     ENDDO
     CSR         DP#01Z    ENDSR
     C**----------------------------------------------**
     C* TLTAL  PROCESS  L3                            **
     C**----------------------------------------------**
     CSR         DT#01     BEGSR
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTET#01
     CSR         DT#01Z    ENDSR
      ***********************************************************
      ****   DEFINE OUPUT FORMAT                             ****
      ***********************************************************
     OQPRINT  E   02           EH#01
     O                                   18 '報表編號: ARR062'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
     O                         UDATE Y  132
     O        E 1              EH#01
     O                         CON3      75                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 1              EH#01
     O                                   13 '廠　　區：'
     O                         TXAR    +  1
     O                         TAB4    +  1
     O        E 1              EH#01
     O                                   13 '發票類別：'
     O                         INTYPE  +  1
     O        E 1              EH#01
     O                         ALS       80
     O        E 1              EH#01
     O                                   11 '發票聯式'
     O                                   21 '數　量'
     O                                   37 '稅　基'
     O                                   52 '稅　額'
     O                                   68 '合　計'
     O        E 1              EH#01
     O                         ALS       80
     O        E 1              ET#01
     O                         INKIND     6
     O                         WQTY  J   21
     O                         WAAMT J   37
     O                         WATAX J   52
     O                         WTOT  J   68
**  TAB3 TAB4
P台北T桃園M台中K高雄H苗栗L林口
