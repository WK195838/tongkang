     H            Y                                     1
     FSAVOUR  UP  E                    DISK
     FTRNDTL  IF  E           K        DISK                      A
     FSAMAST  IF  E           K        DISK
     FCBCUST  IF  E           K        DISK
 9112FARDLWT  UF  E           K        DISK                      A
 9112FSASLPD  IF  E           K        DISK
 9112FSADLVY  IF  E           K        DISK
 9112FWTMASPL2IF  E           K        DISK
     FPRINT   O   F     132            PRINTER
     FPRINT1  O   F     132            PRINTER
     IRSAVOUR
     I                                              S4VREAL2
     I                                              S4VRNOL2
     I                                              S4ORNOL1
     IAADS        DS
     I                                        1   6 W1CUNO
     I                                        1   6 CBKEY
     I                                        1   1 S1KIND
     I                                        2   2 S1CUN1
     I                                        3   5 S1CUN2
9008 I                                        6   6 S1CD01
LYW  I                                        7   7 S1CD02
 .   I                                        8   8 S1CD03
 .   I                                        9   9 S1CD04
 .   I                                       10  10 S1CD05
     I                                       11  18 S4KEY
     I                                       11  11 S4VREA
     I                                       12  13 S4PCOD
     I                                       14  180S4VRNO
     IBBDS        DS
9008 I                                        1   9 WORNO
LYW  I                                        1   1 WORNO1
 .   I                                        2   6 WORNO2
 .   I                                        7   9 WORNO3
CLJB I            DS
     I                                        1  15 TXRESV
CLJE I                                       10  10 D#TXRV
     I           UDS
9710 I                                        1   80YMD
9710 I                                        9  160YMD1
     I                                     10211021 TXAR
9112 I            DS
 .   I                                        1   6 D#OTNO
 .   I                                        1   1 D#VEAR
 .   I                                        2   6 D#VRNO
9112 I            DS
 .   I                                        1   6 D#DVNO
 .   I                                        1   1 D#DVN1
 .   I                                        2   60D#DVN2
9203 I            DS
 .   I                                        1  20 D#MARK
 .   I                                       17  17 D#MAR1
     C*----------------------------------------------------*
     C  N81                EXSR INZ#01
     C  N81                SETON                         81
     C*----------------------------------------------------*
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     CLR                   EXSR TL#LR
     C*----------------------------------------------------*
     CSR         INZ#01    BEGSR
     C                     MOVE 'SA04'    CDE1    4
     C                     MOVE *ALL'='   ALS   108
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD55        TCNT    20
     C                     Z-ADD0         PAG     30
     C                     Z-ADD0         WITEM   20
     C*----------------------------------------------------*
     C           KEY       KLIST
     C                     KFLD           CDE1
     C                     KFLD           TXNO
     C                     KFLD           TXITEM
     C           KEYT      KLIST
     C                     KFLD           CDE1
     C                     KFLD           TXNO
     C           S1KEY     KLIST
     C                     KFLD           S1OREA
     C                     KFLD           S1ORNO
     C                     KFLD           S1ORTM
 9112C*FILE==>SASLPD
  .  C           W#SLPD    KLIST
  .  C                     KFLD           S3VREA
  .  C                     KFLD           S3VRNO
  .  C                     KFLD           S3VRTM
  .  C*FILE==>SADLVY
  .  C           W#DLVY    KLIST
  .  C                     KFLD           S2AREA
  .  C                     KFLD           S2DVNO
  .  C                     KFLD           S2ITEM
  .  C*FILE==>ARDLWT
  .  C           W#DLWT    KLIST
  .  C                     KFLD           AWORNO
  .  C                     KFLD           AWWTNO
  .  C*FILE==>WTMASPL2
  .  C           W#MASP    KLIST
  .  C                     KFLD           W6AREA
  .  C                     KFLD           W6VNO1
 9112C                     KFLD           W6VNO2
     C*
     CSR                   ENDSR
     C*----------------------------------------------------*
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         WAMTT   90
     C                     Z-ADD0         WAMTP   90
     C                     Z-ADD0         TWQTY   70
     CSR                   ENDSR
     C*----------------------------------------------------*
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         WQTY    90
     CSR                   ENDSR
     C*----------------------------------------------------*
     CSR         DP#01     BEGSR
9008 C                     MOVELS4ORNO    WORNO
LYW  C                     MOVE WORNO1    S1OREA
 .   C                     MOVELWORNO2    S1ORNO
 .   C                     MOVELWORNO3    S1ORTM
     C           S1KEY     CHAINSAMAST               36
     C           *IN36     IFEQ '0'
     C           CBKEY     CHAINCBCUST               97
     C  N97                MOVELCBIVCO    S1CHK
     C           S1KIND    IFEQ 'D'
     C           S1KIND    OREQ 'G'
     C                     MOVE 'L'       WSK
     C                     ELSE
     C                     MOVE S1KIND    WSK     1
     C                     END
     C           S1UPRC    COMP 0                        35
     C                     END
     C           S4DATE    COMP YMD1                   37
     C                     SETOF                     90
     C   37
     COR 35
     COR 36                SETON                     90
     C   90                ADD  1         CNT0    40
     C   90      CNT0      COMP 1                        33
     C   90                EXCPT
     C                     SETOF                     333536
     C  N90                MOVE 'X'       S4CDE2
     C  N90                UPDATRSAVOUR
     C  N90                EXSR DP#02
     C                     ENDSR
     C*---------------------------------------------------*
     CSR         DP#02     BEGSR
     C                     ADD  S4NWGT    WQTY
     CSR                   ENDSR
     C*---------------------------------------------------*
     C*  TOTAL TIME  L1                                   *
     C*---------------------------------------------------*
     CSR         TL#L1     BEGSR
     C           WQTY      MULT S1UPRC    WAMT   123
     C                     Z-ADDWAMT      WAMTP     H
     C                     ADD  WAMTP     WAMTT
     C                     Z-ADDWAMTP     WAMTO  112     80
     C                     ADD  WQTY      TWQTY
     C                     ADD  WAMTP     WAMTTT  90
     C  N80                EXSR FITEM
     C  N80                EXSR WR#TN
 9112C  N80                EXSR WR#DW
     C  N80                EXSR OU#01
     CSR                   ENDSR
     C*---------------------------------------------------*
     C*  TOTAL TIME  L2                                   *
     C*---------------------------------------------------*
     CSR         TL#L2     BEGSR
     C           WAMTT     COMP 0                        17
     C  N17                EXCPTET#01
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C*---------------------------------------------------*
     C*  TOTAL TIME  LR                                   *
     C*---------------------------------------------------*
     CSR         TL#LR     BEGSR
     C                     Z-ADDWAMTTT    WAMTTR  90
     C                     EXCPTET#02
     CSR                   ENDSR
     C*---------------------------------------------------*
     C*  FIND LAST NUMBER                                 *
     C*---------------------------------------------------*
     CSR         FITEM     BEGSR
     C                     SETOF                     18
     C                     MOVE *BLANK    TXNO
     C                     MOVELS4KEY     TXNO
     C                     Z-ADD99        TXITEM
     C           KEY       SETGTTRNDTL
     C  N18      KEYT      REDPETRNDTL                   18
     C      18             Z-ADD1         WITEM
     C     N18   TXITEM    ADD  1         WITEM
     CSR                   ENDSR
     C*-----------------------------------------------------------*
     C** WRITE TO TRNDTL                                          *
     C*-----------------------------------------------------------*
     CSR         WR#TN     BEGSR
     C                     CLEARTXREC
     C                     MOVE 'A'       TXFLAG
     C                     MOVELCDE1      TXCODE
     C                     MOVE '1'       TXACNT
     C                     MOVE *BLANK    TXNO
     C                     MOVELS4KEY     TXNO
     C                     Z-ADDWITEM     TXITEM
     C                     Z-ADDS4DATE    TXDATE
     C                     Z-ADDS4DATE    TXACDT
CLJ  C           W1CUNO    CHAINCBCUST               69    改用客戶檔
     C  N69                MOVELCBCUNM    TXCUNM
---  C   69                MOVELS1CUNO    TXCUNM
     C                     MOVELW1CUNO    TXCUNO
     C                     MOVELS4ORNO    TXORNO
     C                     MOVELS1RECV    TXRVID
     C                     MOVELS1SND     TXSALE
     C                     MOVELS1KIND    TXSATP
     C                     MOVELS1CHK     TXIVTP
     C                     MOVELS4PDNM    TXPDNM
     C                     Z-ADDWQTY      TXQTY
     C           S1UPRC    MULT 100000    TPRC    70
     C                     Z-ADDS1UPRC    TXUPRC
     C                     Z-ADDWAMTO     TXAMT
     C                     MOVELTXAR      TXTXAR
     C                     Z-ADDUDATE     TXTXDT
     C                     MOVELTPRC      TXRESV
CLJ  C                     MOVEL'*'       D#TXRV           未傳台北
     C                     WRITETXREC
     CSR                   ENDSR
     C*-----------------------------------------------------------*
9112 C** WRITE TO ARDLWT--磅單與運費調整對照檔                  *
 .   C*-----------------------------------------------------------*
 .   CSR         WR#DW     BEGSR
 .   C*利用磅單檔中的出貨編號至出貨通知檔找到交單編號，
 .   C*在利用此交單編號到交貨通知單檔中找到該筆資料，
 .   C*再判斷該筆資料的補運費碼是否＝Ｙ　
 .   C*
 .   C                     MOVELS4VREA    W6AREA           廠區別
 .   C                     MOVELS4PCOD    W6VNO1           進出別
 .   C                     Z-ADDS4VRNO    W6VNO2           磅單編號
 .   C* 找出單編號
 .   C           W#MASP    CHAINRWTMASP              40
 .   C           *IN40     IFEQ *OFF
 .   C                     MOVELW6OTNO    D#OTNO
 .   C                     MOVELD#VEAR    S3VREA           地區別
 .   C                     MOVELD#VRNO    S3VRNO           出貨通知編號
 .   C                     Z-ADD0         S3VRTM           出貨通知序號
 .   C*找交單編號
 .   C           W#SLPD    SETLLRSASLPD
 .   C                     READ RSASLPD             N    41
 .   C                     MOVELD#VRNO    W#VRNO  50
 .   C           D#VEAR    IFEQ S3VREA
 .   C           W#VRNO    ANDEQS3VRNO
 .   C                     MOVELS3DVN1    S2AREA           填單廠區
 .   C                     MOVELS3DVN2    S2DVNO           流水號
 .   C                     MOVELS3DVN3    S2ITEM           項次
 .   C  N41      W#DLVY    CHAINRSADLVY              42
 .   C*
 .   C  N42                MOVELS2MARK    D#MARK
 9203C  N42      S2AREA    IFEQ 'P'                        填單廠區
 9203C           D#MAR1    ANDEQ'Y'                        補運費碼
 .   C                     MOVELTXORNO    AWORNO           訂單號碼
 .   C                     MOVELTXNO      AWWTNO           磅單號碼
 .   C           W#DLWT    CHAINRARDLWT              43
 .   C                     MOVELTXCUNO    AWCUNO           客戶代號
 .   C                     MOVELTXCUNM    AWCUNM           客戶名稱
 .   C                     MOVELS3DVN1    D#DVN1
 .   C                     Z-ADDS3DVN2    D#DVN2
 .   C                     MOVELD#DVNO    AWDLVY           交單號碼
 .   C                     Z-ADDTXDATE    AWWTDT           出貨日期
 .   C           *IN43     IFEQ *ON
 .   C                     MOVEL'A'       AWFLAG
 .   C                     WRITERARDLWT
 .   C                     ELSE
 .   C                     MOVEL'C'       AWFLAG
 .   C                     MOVEL*BLANK    AWCOD3           作廢碼
 .   C                     UPDATRARDLWT
 .   C                     ENDIF
 .   C                     ENDIF
 .   C                     ENDIF
 .   C                     ENDIF
9112 CSR                   ENDSR
     C*-------------------------------------------------------*
     CSR         OU#01     BEGSR
     C           LCNT      IFGT TCNT
     C                     ADD  1         PAG
     C                     EXCPTEH#01
     C                     Z-ADD10        LCNT
     C                     END
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C*-------------------------------------------------------*
     OPRINT1  E  201           EH#01
     O                                   11 '報表編號:'
     O                                 +  1 'SAR04'
     O                                   56 '* 應收帳款過帳單 *'
     O                                   98 '印表日期'
     O                                   99 ':'
     O                         UDATE Y  108
     O        E  1             EH#01
     O                                   99 '頁　　次:'
     O                         PAG   Z +  1
     O        E  1             EH#01
     O                                   11 '截止日期:'
9710 O                         YMD     +  1 '    /  /  '
     O        E  1             EH#01
     O                         ALS      108
     O        E  1             EH#01
     O                                   10 '出貨日期'
     O                                   20 '磅單編號'
     O                                   30 '客戶代號'
     O                                   40 '客戶名稱'
     O                                   50 '訂單號碼'
     O                                   56 '品名'
     O                                   70 '重　量'
     O                                   72 'kg'
     O                                   80 '單價'
     O                                   94 '金　　額'
     O                                  108 '小　　計'
     O        E 1              EH#01
     O                         ALS      108
     O        E 1              ED#01
     O                         S4DATEY   10
     O                         S4VREA    12
     O                         S4VRNO    17
     O                                   18 '-'
     O                         S4VRTM    19
     O                         W1CUNO    28
     O                         S4CUST    40
     O                         WORNO1    41
     O                         WORNO2    46
     O                                   47 '-'
     O                         WORNO3    50
     O                         S4PDNM    54
     O                         WQTY  K   70
     O                         S1UPRCK   80
     O                         WAMTP K   94
     O        E 01             ET#01
     O                         WAMTT K  108
     O        E  2             ET#02
     O                                   10 '銷貨合計'
     O                                   11 ':'
     O                         WAMTTT2   25
     O                                   92 '應收合計'
     O                                   93 ':'
     O                         WAMTTRK  108
     OPRINT   E  103   33
     O                                    6 'RPG :'
     O                                 +  1 'SAR04R-2'
     O                                   55 '*** 單價錯誤清單 ***'           X
     O                                   90 '印表日期'
     O                                   91 ':'
     O                         UDATE Y  100
     O        E  1     33
     O                                    8 '日　期'
     O                                   21 '磅單號碼'
     O                                   41 '訂單號碼'
     O                                   60 '數　　量'
     O                                   80 '備　　註'
     O        E  1     33
     O                                    8 '--------'
     O                                   21 '----------'
     O                                   41 '----------'
     O                                   60 '----------'
     O                                   80 '----------'
     O        E  1     90
     O                         S4DATEY   10
     O                         S4VREA    15
     O                         S4VRNO    20
     O                         S4ORNO    40
     O                         S4NWGTK   60
     O                 35                80 '單價＝０'
     O                 36                80 '無此訂單'
     O                 37                80 '超過入帳日'
9710 O                 37      YMD1      90 '    /  /  '
