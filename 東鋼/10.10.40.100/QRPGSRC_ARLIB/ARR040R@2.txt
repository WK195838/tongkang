     H****************************************************************
     H*
     H*            UPDATE  DATE  99/09/01  2010AR517  S00WCJ (9909A)
     H*
     H****************************************************************
     H                                                  1                 BR310
     FINVMST  IP  E                    DISK
     FINVDTL  IF  E           K        DISK
     FQPRINT  O   F     132            PRINTER
     E                    TABA    5   5  1   TABB   10
     E                    TABC    2   2  1   TABD    8
     IINREC
     I                                              INNO  L1
     IAADS        DS
     I                                       11  180YMD
     I                                       11  140YY
     I                                       15  160MM
     I           UDS
     I                                        1   8 DATEA
     I                                        1   4 YY1
     I                                        5   6 MM1
     I                                      951 985 COMP
     C****************************************************************
9909AC           *DATE     SUB  19000000  U#SYSD  80
     C  N10                Z-ADD0         PAG     30
     C  N10                Z-ADD80        LCNT    30
     C  N10                Z-ADD55        TCNT    30
     C  N10                SETON                     10
     C                     Z-ADD0         AMT    110
     C                     SETON                     76
     C                     MOVELINNO      IVNO
     C           IVNO      SETLLINVDTL               97
     C           *IN97     DOWEQ'0'
     C           IVNO      READEINVDTL                   97
     C   97                LEAVE
     C**
     C           IVACNT    LOKUPTABA      TABB           44
     C   44                MOVE TABB      SPNAME 10
     C           INKIND    LOKUPTABC      TABD           55
     C   55                MOVE TABD      IVNAME  8
     C                     ADD  IVAMT     AMT
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD8         LCNT
     C   75                SETON                     76
     C                     ADD  1         LCNT
     C                     EXCPTED#01
     C                     SETOF                     76
     C                     ENDDO
     C                     EXCPTET#L1
     C                     ADD  2         LCNT
     OQPRINT  E  202           EH#01
     O                                   20 '報表編號:AR040'
     O                         COMP      79                                    X
     O                                  117 '印表日期'
     O                                  118 ':'
9909AO                         U#SYSD   129 '    /  /  '
     O        E  2             EH#01
     O                         YY1       50
     O                                 +  0 '年'
     O                         MM1     +  0
     O                                 +  0 '月'
     O                                   76 '作廢發票明細表'
     O                                  117 '頁　　數'
     O                                  118 ':'
     O                         PAG   Z  126
     O        E  1             EH#01
     O                                   13 '發票號碼'
     O                                   25 '日　期'
     O                                   38 '訂單號碼'
     O                                   51 '客戶名稱'
     O                                   62 '帳款別'
     O                                   72 '品名'
     O                                   82 '重　量'
     O                                   95 '單　價'
     O                                  110 '金　　額'
     O                                  122 '發票別'
     O        E  1             EH#01
     O                                   20 '    ----------------'
     O                                   40 '--------------------'
     O                                   60 '--------------------'
     O                                   80 '--------------------'
     O                                  100 '--------------------'
     O                                  120 '--------------------'
     O                                  127 '-------'
     O        E  1             ED#01
     O                 76      INNO   B  14
     O                         IVACDT    25 '    /  /  '
     O                         IVORNO B  37
     O                 76      INCUNM    51
     O                         SPNAME    63
     O                         IVPDCD    71
     O                         IVQTY KB  82
     O                         IVUPRCKB  95
     O                         IVAMT KB 110
     O                 76      IVNAME   122
     O        E  1             ET#L1
     O                                   94 '**合計**'
     O                         AMT   KB 110
     O        E  1             ET#L1
     O                                   20 '    ================'
     O                                   40 '===================='
     O                                   60 '===================='
     O                                   80 '===================='
     O                                  100 '===================='
     O                                  120 '===================='
     O                                  127 '======='
** TABA  TABB
1銷貨收入2銷貨退回3銷貨折讓4扣預收款5　　稅額
** TABC  TABD
2二聯式3三聯式
