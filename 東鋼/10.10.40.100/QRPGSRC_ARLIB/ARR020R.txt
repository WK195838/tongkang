     H*****************************************************************
     H*
     H*            1.PROGRAM-ID    ARR020R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S01LYH
     H*            4.FUNCTION      票據資料挑選　
     H*            5.DATE-WRITTEN  90/12/13
     H*
     H*****************************************************************
     H        1   Y                                     1
     FRCVDTLL1IP  E           K        DISK
     FARR020T O   E             69     PRINTER
     E                    TAB1    5   5  1   TAB2    8   *地區名稱
     IRVREC       01
     I                                              RVDUDTL1
     I            DS
     I                                        1  10 S#SRID
     I                                        1   8 D#SID1
     I                                        9  10 D#SID2
     I           UDS
     I                                     10011010 S#USER
     I                                     10111020 S#DEVN
     I                                     10211021 D#AREA
     I                                      401 401 S#AREA
     I                                      402 4090T#DA1
     I                                      410 4170T#DA2
     I                                      421 4280T#DAA
     I                                      431 4380T#DAB
     C*****************************************************************
     C*          Key       List
     C*****************************************************************
     C*FILE => VRNO
     C           K#DATE    KLIST
     C                     KFLD           RVDATE
     C                     KFLD           RVNTNO
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C*****************************************************************
     C*          Main      Program
     C*****************************************************************
     C           *IN99     IFEQ *OFF
     C                     EXSR INISR
     C                     MOVE *ON       *IN99
     C                     ENDIF
     C   L1                EXSR L1CLR
     C   01                EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C*INITIAL設定
     C           D#AREA    LOKUPTAB1      TAB2           50*EQ
     C  N50                MOVE *ALL'*'   T#AREA
     C   50                MOVE TAB2      T#AREA
     C                     MOVE *ALL'='   T#LIN1           * ====
     C                     MOVE *ALL'-'   T#LIN2           * ----
     C                     MOVE *ALL'='   T#LIN3           * ====
     C                     MOVE *ON       *IN69            *表頭
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C*清暫存值
     C                     Z-ADD0         T#CNT   50
     C                     Z-ADD0         T#AMT  110
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C*
     C                     ADD  1         T#CNT
     C                     ADD  1         T#CNTT
     C           RVDUDT    IFNE T#DUDT
     C                     Z-ADDRVDUDT    T#DUDT
     C                     MOVE *OFF      *IN60
     C                     ELSE
     C                     MOVE *ON       *IN60
     C                     ENDIF
     C                     MOVELRVNTNO    T#NTNO
     C                     MOVELRVPBID    T#PBID
     C                     MOVELRVPANO    T#PANO
     C                     MOVELRVCUNM    T#CUNM
     C                     Z-ADDRVDATE    T#DATE
     C                     MOVELRVNO      T#NO
      *
     C                     MOVEL*BLANK    T#PLAC
     C           RVPLAC    IFEQ 'D'
     C                     MOVEL'本埠'  T#PLAC
     C                     ENDIF
     C           RVPLAC    IFEQ 'E'
     C                     MOVEL'外埠'  T#PLAC
     C                     ENDIF
      *
     C                     MOVELRVVNO1    T#VNO1
     C                     Z-ADDRVRAMT    T#RAMT
     C                     ADD  RVRAMT    T#AMT
     C                     ADD  RVRAMT    T#AMTT
     C*列印
     C   69                WRITEAR020T1                    *表頭
     C                     MOVE *OFF      *IN69
     C                     WRITEAR020T2                    *表身
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C*合計列印
     C*
     C   69                WRITEAR020T1                    *表頭
     C                     MOVE *OFF      *IN69
     C                     WRITEAR020T3                    *小計
     C*
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C*列印
      *
     C   69                WRITEAR020T1                    *表頭
     C                     MOVE *OFF      *IN69
     C                     WRITEAR020T4
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C*NO DATA
     C                     EXSR INISR
     C                     WRITEAR020T1                    *OV
     C                     WRITEAR020TM                    *OV
     C*
     C                     ENDSR
     C*
**  TAB1 TAB2
P台北　T桃園　K高雄　H苗栗　 全公司
