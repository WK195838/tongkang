     H****************************************************************
     H*
     H*            UPDATE  DATE  99/09/01  2010AR517  S00WCJ (9909A)
     H*                         104/09/30  2015AR953  S00WCJ (0409A)
     H*                          產品代碼3碼擴5碼
     H*                         107/11/01  S00WCJ (0710A)
     H*                          總計金額欄位不足，金額無法完整顯示
     H*                         108/08/20  2019AR00057 S00WCJ (0808A)
     H*                          配合磅單過至項次修改
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTL  IP  E                    DISK
     FHIPROD  IF  E           K        DISK
0808AFQPRINT  O   F     158            PRINTER
      *************************************************************
     E**                  TAB1    1   5  1   TABR1  10
     I*************************************************************
     ITXREC
     I                                              TXACDTL2
     I                                              TXNO  L1
     I****
     IAADS        DS
     I              'も鋼筋　出貨日報表-C         CON3                       X
     I              '（代傳票）も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
CLJ  I            DS
     I                                        1   2 W#OEOF
     I                                        1   1 D#OE
     I                                        2   2 D#OF
     I            DS
     I                                        1  12 NAME1
---  I                                       12  12 D#NOF
0808AI            DS
0808AI                                        1  30 TXRESV
0808AI                                       13  14 D#VRTM
0808AI                                       19  21 D#PDS1
     I           UDS
     I                                        1   1 AREAF
     I                                        2   2 AREAT
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
9909AC           *DATE     SUB  19000000  U#SYSD  80
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     CLR                   EXSR TL#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
      *
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD55        TCNT    30
     C                     Z-ADD0         QTY3    80
0711AC                     Z-ADD0         AMT3   110
     C                     Z-ADD0         COUNT   30
     C                     Z-ADDTCNT      LCNT
0808AC                     MOVE *ALL'='   ALS   146
0808AC                     MOVE *ALL'-'   ALQ   146
     C                     SELEC
     C           TXAR      WHEQ 'P'
     C                     MOVEL'台北'  AREAC   6
     C           TXAR      WHEQ 'T'
     C                     MOVEL'桃園'  AREAC   6
     C           TXAR      WHEQ 'K'
     C                     MOVEL'高雄'  AREAC   6
     C           TXAR      WHEQ 'L'
     C                     MOVEL'林口'  AREAC   6
     C           TXAR      WHEQ 'M'
     C                     MOVEL'台中'  AREAC   6
     C           TXAR      WHEQ 'H'
     C                     MOVEL'苗栗'  AREAC   6
     C                     OTHER
     C                     MOVEL'　　'  AREAC
     C                     ENDSL
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD8         LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
0711AC                     Z-ADD0         QTY2    80
0711AC                     Z-ADD0         AMT2   110
     CSR                   ENDSR
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
0711AC                     Z-ADD0         QTY1    80
0711AC                     Z-ADD0         AMT1   110
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
0409AC                     MOVELTXPDNM    F4NAME
0409AC           F4NAME    CHAINHIPROD               97
CLJ  C           *IN97     IFEQ '0'
     C                     MOVE ''      W#OEOF
     C                     MOVELF4CHIN    NAME1
     C                     MOVE D#OF      D#NOF
     C                     ELSE
     C                     MOVE *BLANK    NAME1
---  C                     ENDIF
     C                     ADD  1         COUNT
9008 C                     MOVELTXORNO    ORNO    6
     C                     SELEC
     C           TXACNT    WHEQ '1'
     C                     MOVEL'銷貨'  ACNT    6
     C           TXACNT    WHEQ '2'
     C                     MOVEL'磅差'  ACNT    6
     C           TXACNT    WHEQ '3'
     C                     MOVEL'價差'  ACNT    6
     C                     OTHER
     C                     MOVEL'　　'  ACNT    6
     C                     ENDSL
     C                     Z-ADDTXAMT     RAMT   112
     C                     MOVELTXRESV    TPRCA   7
     C                     SETOF                     21
     C                     TESTN          TPRCA      21
     C   21                MOVE TPRCA     TPRCN   70
     C   21      TPRCN     DIV  100000    TPRC    75
     C  N21                Z-ADDTXUPRC    TPRC
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#01
     C                     ADD  TXQTY     QTY1
     C                     ADD  RAMT      AMT1
     C**
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C                     EXCPTET#L1
     C                     ADD  QTY1      QTY2
     C                     ADD  AMT1      AMT2
     C                     ADD  2         LCNT
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C                     EXCPTET#L2
     C                     ADD  QTY2      QTY3
     C                     ADD  AMT2      AMT3
     C                     ADD  1         LCNT
     CSR         TL#L2Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME LR PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     CSR         TL#LRZ    ENDSR
     OQPRINT  E   02           EH#01
     O                                   17 '報表編號: AR039'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
     O                         U#SYSDZ  132
9908AO        E 1              EH#01
     O                         CON3      79                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 2              EH#01
     O                                   11 '　地區別:'
     O                         TXAR    +  1
     O                         AREAC   +  1
     O        E 1              EH#01
0808AO                         ALS      146
     O        E 1              EH#01
     O                                   10 '入帳日期'
     O                                   20 '磅單號碼'
     O                                   30 '客戶編號'
     O                                   40 '客戶名稱'
     O                                   46 '訂單'
     O                                   54 '異動別'
     O                                   68 '品　名'
0808AO                                   78 '磅次'
0808AO                                   84 '徑度'
0808AO                                   94 '數　量'
0808AO                                  104 '單　價'
0808AO                                  119 '銷貨金額'
0808AO                                  131 '扣預收款'
0808AO                                  146 '應收金額'
     O        E 1              EH#01
0808AO                         ALS      146
     O        E 1              ED#01
9008 O                 75      TXACDT    10 '    /  /  '
LYW  O                 L2      TXACDT    10 '    /  /  '
     O                 75      TXNO      20
     O                 L1      TXNO      20
     O                         TXCUNO    27
     O                         TXCUNM    40
9008 O                         ORNO      47
LYW  O                         TXACNT    50
 .   O                         ACNT      56
0409AO                         TXPDNM  +  0
 .   O                         NAME1     72
0808AO                         D#VRTM    76
0808AO                         D#PDS1    82
0808AO                         TXQTY N   93
0808AO                         TPRC  2  103
0808AO                         RAMT  N  119
     O        E 11             ET#L1
0808AO                                   80 '磅單小計：'
0808AO                         QTY1  N   93
0808AO                         AMT1  N  116
0808AO                         AMT1  N  145
     O        E 1              ET#L2
     O                         TXACDT    59 '    /  /  '
0808AO                                   80 '合計：'
0808AO                         QTY2  N   93
0808AO                         AMT2  N  116
0808AO                         AMT2  N  145
     O        E 1              ET#LR
     O                                   20 '　筆數：'
     O                         COUNT N   26
0808AO                                   80 '　總合計：'
0808AO                         QTY3  N   93
0808AO                         AMT3  N  116
0808AO                         AMT3  N  145
     O        E   64           ET#LR
     O                                   30 '副總經理：'
     O                                   50 '　　經理：'
     O                                   70 '　　複核：'
     O                                   90 '　　經辦：'
     O                                  110 '　　入帳：'
