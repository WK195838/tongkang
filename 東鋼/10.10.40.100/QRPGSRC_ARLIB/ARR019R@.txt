     H        1   Y                                     1                 BR003
     FRCVDTL  IP  E                    DISK
     FQPRINT  O   F     132            PRINTER
      *************************************************************
     E                    TAB1    1   6  1   TABR1  10
     I*************************************************************
     IRVREC
     I                                              RVDUDTL1
     I****
     IAADS        DS
     I              '*到期日票據明細表*'C         CON3                       X
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     IBBDS        DS
     I                                        1  10 RVSBID
     I                                        1   3 BAID1
     I                                        4   7 BAID2
      *
     I                                       11  20 RVSANO
     I                                       11  11 ACTYPE
     I                                       12  12 ACCURR
     I                                       13  20 ACACNO
      *
     I                                       21  260YMD
     I                                       21  240YM
     I                                       21  220YY
     I                                       23  240MM
     I                                       25  260DD
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     CLR                   EXSR TL#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C           *ENTRY    PLIST
     C                     PARM           BDATE1  60
     C                     PARM           EDATE2  60
     C           BKKEY     KLIST
     C                     KFLD           BAID1
     C                     KFLD           BAID2
     C           ACKEY     KLIST
     C                     KFLD           ACTYPE
     C                     KFLD           ACCURR
     C                     KFLD           ACACNO
      *
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD55        TCNT    30
     C                     Z-ADD0         BAMT   110
     C                     Z-ADD0         BQTY    50
     C                     MOVE *ALL'='   ALS   132
     C                     MOVE *ALL'-'   ALQ    92
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD8         LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         AAMT   110
     C                     Z-ADD0         AQTY    50
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     MOVE *BLANK    CHAR10 10
     C           RVNTTP    LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     CHAR10
     C  N31                MOVEL'其他'  CHAR10
      *
     C           RVPLAC    COMP 'D'                      31
     C   31                MOVEL'本埠'  CHAR6X  6
     C  N31                MOVEL'外埠'  CHAR6X
      *
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#01
     C                     ADD  1         AQTY
     C                     ADD  RVRAMT    AAMT
     C                     ADD  1         BQTY
     C                     ADD  RVRAMT    BAMT
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C                     EXCPTET#L1
     C                     ADD  3         LCNT
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     CSR         TL#LRZ    ENDSR
      ***********************************************************
      ****   DEFINE OUPUT FORMAT                             ****
      ***********************************************************
     OQPRINT  E   02           EH#01
     O                                   17 '報表編號: AR019'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
     O                         UDATE Y  132
     O        E 1              EH#01
     O                         CON3      71                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 2              EH#01
     O                                   11 '到期日期:'
     O                         BDATE1  +  1 '  /  /  '
     O                                 +  1 '-'
     O                         EDATE2  +  1 '  /  /  '
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              EH#01
     O                                   10 '到期日期'
     O                                   20 '客戶編號'
     O                                   30 '客戶名稱'
     O                                   40 '繳款單號'
     O                                   50 '款項別'
     O                                   58 '票據別'
     O                                   74 '付款銀行'
     O                                   87 '付款帳號'
     O                                   99 '票據號碼'
     O                                  109 '付款地'
     O                                  128 '金　額'
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              ED#01
     O                 L1      RVDUDT     9 '  /  /  '
     O                 75      RVDUDT     9 '  /  /  '
     O                         RVCUNO    17
     O                         RVCUNM    30
     O                         RVNO      37
     O                                   38 '-'
     O                         RVITEM    40
     O                         RVUSTP    47
     O                         CHAR10    60
     O                         RVPBID    78
     O                         RVPANO    88
     O                         RVNTNO   100
     O                         CHAR6X   108
     O                         RVRAMTJ  128
     O        E 1              ET#L1
     O                         ALQ      132
     O        E 1              ET#L1
     O                                   82 '到期日小計：　共'
     O                         AQTY  J   92
     O                                   96 '張'
     O                                  113 '總金額:'
     O                         AAMT  J  128
     O        E 1              ET#L1
     O                         ALQ      132
     O        E 1              ET#LR
     O                                   82 '　　總　計：　共'
     O                         BQTY  J   92
     O                                   96 '張'
     O                                  113 '總金額:'
     O                         BAMT  J  128
     O        E 1              ET#LR
     O                         ALQ      132
** TAB1
A支票
B背書支票
C本票
D背書本票
E銀行本票
I信用狀　
