     H        1   Y                                     1                 BR003
     FARWF02  IP  E                    DISK
     FINVDTL  UF  E           K        DISK
     FACTRAN  O   E           K        DISK                      A
     FQPRINT  O   F     132            PRINTER
     ****
     E                    TAB1    1   3  1   TABR1  18
     IRARWF02
     I                                              W2ACDTL2
     I                                              W2RESVL1
     I                                              W2NO  L1
     ****
     I            DS
     I                                        1  18 KGRP
     I                                        1   4 KACNO
     I                                        5  14 KACNON
     I                                       15  18 KDPNO
     I            DS
     I                                        1   4 VRNOX
     I                                        2   40NO1
     I                                       11  17 VUNOG
     I                                       11  11 VUNOG1
     I                                       12  12 VUNOG2
     I                                       13  16 VUNOG3
     I                                       17  17 VUNOG4
     I            DS
     I                                        1   4 VRNOY
     I                                        2   40NO2
     I           UDS
9008 I                                        1   40YM
     I                                     10211021 TXAR
     C*****
     C           IVKEY     KLIST
     C                     KFLD           W2NO
     C                     KFLD           W2ACNT
     C                     KFLD           W2ITEM
     C  N10                EXSR IN#01
     C   L2                EXSR DL#02
     C   L1                EXSR DL#01
     C                     EXSR DP#01
     CL1                   EXSR TL#01
     CL2                   EXSR TL#02
     CLR                   EXSR TL#LR
     C**--------------------------------------------------**
     CSR         IN#01     BEGSR
     C                     MOVE 'G001'    VRNOX
     C                     MOVE 'G001'    VRNOY
     C                     Z-ADD0         AMTT   132
     C                     Z-ADD0         VAMT   132
     C                     Z-ADD0         AMTTC  132
     C                     Z-ADD0         AMTTD  132
     C                     Z-ADD0         ITEM    20
     C                     Z-ADD0         ITEM1   20
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD55        TCNT    20
     C                     Z-ADD0         PAG     30
     C                     MOVE *BLANK    VUNOG
     C                     MOVE *ALL'-'   ALDSH 120
     C           TXAR      IFEQ 'P'
     C                     SETON                     32
     C                     ELSE
     C                     SETOF                     32
     C                     ENDIF
     C                     MOVE *BLANK    KGRP
     C  N32      TXAR      LOKUPTAB1      TABR1          31
     C  N32 31             MOVELTABR1     KGRP
     C                     SETON                     10
     CSR                   ENDSR
     C***
     C**----------------------------------------------**
     C**    L2 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#02     BEGSR
     C   32                Z-ADD0         ITEM
     C  N32                Z-ADD1         ITEM
     C                     Z-ADD0         AMTT
     C           NO1       ADD  1         NO2
     C*
     C                     Z-ADD0         D#AMTT 132       每日彙總
     C                     Z-ADD0         C#AMTT 132
     CSR                   ENDSR
     C**----------------------------------------------**
     C**    L1 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#01     BEGSR
     C                     Z-ADD0         VAMT
     C*
     C                     Z-ADD0         D#AMT  132       借銷項稅額
     C                     Z-ADD0         C#AMT  132       貸銷項稅額
     CSR                   ENDSR
     C**----------------------------------------------**
     C**       DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     Z-ADDW2AMT     AMT    132
     C                     ADD  W2AMT     VAMT
     C                     ADD  W2AMT     AMTT
     C*
     C           W2FL03    IFEQ 'C'                        退貨
     C           W2FL03    OREQ 'D'                        折讓
     C           W2FL03    OREQ 'G'                        退預回
     C           W2FL03    OREQ 'H'                        扣預沖回
     C                     SUB  W2AMT     D#AMT            稅額折退
     C                     SUB  W2AMT     D#AMTT           稅額折退
     C*
     C                     ELSE                            出貨稅額
     C                     ADD  W2AMT     C#AMT
     C                     ADD  W2AMT     C#AMTT           小計
     C                     ENDIF
     C*
     C                     EXSR PR#01
     C           W2ACNT    IFEQ '5'
     C           IVKEY     CHAININVDTL               97
     C  N97                MOVEL'C'       IVFLAG
     C  N97                MOVELTXAR      IVTXAR
     C  N97                Z-ADDUDATE     IVTXDT
     C  N97                MOVELTXAR      VUNOG1
     C  N97                MOVELTXAR      VUNOG2
     C  N97 32             MOVELVRNOX     VUNOG3
     C  N97N32             MOVELVRNOY     VUNOG3
     C  N97                MOVELVUNOG     IVACNO
     C  N97                UPDATIVREC
     C                     ENDIF
     CSR                   ENDSR
     C**----------------------------------**
     C**   L1 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#01     BEGSR
     C           VAMT      COMP 0                    242825
     C   25                GOTO TL#01Z
     C*  28                Z-SUBVAMT      VAMT
     C*  28                ADD  VAMT      AMTTD
     C*  24                ADD  VAMT      AMTTC
     C*
     C                     ADD  D#AMT     AMTTD            總計
     C                     ADD  C#AMT     AMTTC
     C***
     C           D#AMT     IFLT 0
     C                     SUB  D#AMT     C#AMT
     C                     Z-ADD0         D#AMT
     C                     ENDIF
     C*
     C           C#AMT     IFLT 0
     C                     SUB  C#AMT     D#AMT
     C                     Z-ADD0         C#AMT
     C                     ENDIF
     C*
     C           C#AMT     IFNE 0
     C                     Z-ADDC#AMT     VAMT
     C                     SETON                     24
     C                     SETOF                     28
     C                     ADD  1         ITEM
     C                     EXSR WR#01
     C                     EXSR PR#02
     C                     ENDIF
     C*
     C           D#AMT     IFNE 0
     C                     Z-ADDD#AMT     VAMT
     C                     SETON                     28
     C                     SETOF                     24
     C                     ADD  1         ITEM
     C                     EXSR WR#01
     C                     EXSR PR#02
     C                     ENDIF
     C***
     C                     Z-ADD0         VAMT
     C**
     C           ITEM      IFGE 95
     C                     EXSR TL#02
     C                     EXSR DL#02
     C                     ENDIF
     CSR         TL#01Z    ENDSR
     C**----------------------------------**
     C**   L2 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#02     BEGSR
     C           AMTT      COMP 0                    262927
     C   27                EXCPTSP#01
     C   27                ADD  1         LCNT
     C   27                GOTO TL#02A
     C*
     C*  29                Z-SUBAMTT      VAMT
     C*  26                Z-ADDAMTT      VAMT
     C*
     C   32      ITEM      ADD  1         ITEM1
     C  N32                Z-ADD2         ITEM1
     C*
     C           D#AMTT    IFGT 0
     C                     Z-ADDD#AMTT    VAMT
     C                     SETOF                     26
     C                     SETON                     29
     C                     EXSR WR#02
     C                     EXSR PR#03
     C                     ADD  1         ITEM1
     C                     ENDIF
     C*
     C           C#AMTT    IFGT 0
     C                     Z-ADDC#AMTT    VAMT
     C                     SETON                     26
     C                     SETOF                     29
     C                     EXSR WR#02
     C                     EXSR PR#03
     C                     ENDIF
     C*
     C*         -------- 廠區寫入往來 -------
     C*
     C           *IN32     IFEQ '0'
     C           AMTT      COMP 0                    262927
     C   29                Z-SUBAMTT      VAMT
     C   26                Z-ADDAMTT      VAMT
     C                     EXSR WR#03
     C                     EXSR PR#04
     C                     ENDIF
     C***
     C***        ===============================
     C***
     C           TL#02A    TAG
     C   32                ADD  1         NO1
     C  N32                ADD  2         NO1
     CSR                   ENDSR
     C*****
     C**----------------------------------**
     C**   LR BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     C                     EXCPTET#LRR
     CSR                   ENDSR
     C*****
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#01     BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD10        LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#01     BEGSR
     C****  WRITE  TO TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     Z-ADDW2ACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM      ACITEM
     C   28                MOVEL'C'       ACDC
     C   24                MOVEL'D'       ACDC
     C                     MOVEL'1134'    ACNO
     C                     MOVEL'G'       ACTYCO
     C                     MOVELW2RESV    ACRLNO
     C                     MOVELW2CUNM    ACCSNM
     C                     Z-ADDVAMT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C                     WRITERACVOUR
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#02     BEGSR
     C                     EXCPTET#01
     C                     ADD  2         LCNT
     CSR                   ENDSR
     C**------------------------------------------**
     C**    L1     TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#02     BEGSR
     C****  WRITE  TO TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXAR      ACCARD
     C                     MOVEL'A'       ACKIND
     C                     Z-ADDW2ACDT    ACYMD
     C   32                MOVELVRNOX     ACVRNO
     C  N32                MOVELVRNOY     ACVRNO
     C                     Z-ADDITEM1     ACITEM
     C   26                MOVEL'C'       ACDC
     C   29                MOVEL'D'       ACDC
     C                     MOVEL'2127'    ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDVAMT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C                     WRITERACVOUR
     CSR                   ENDSR
     C**------------------------------------------**
     C**    L2     TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#03     BEGSR
     C****  WRITE  TO TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     Z-ADDW2ACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADD01        ACITEM
     C   26                MOVEL'C'       ACDC
     C   29                MOVEL'D'       ACDC
     C                     MOVELKACNO     ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDVAMT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C                     WRITERACVOUR
     C****  WRITE  TO NON-TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELW2AREA    ACCARD
     C                     MOVEL'A'       ACKIND
     C                     Z-ADDW2ACDT    ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADD01        ACITEM
     C   26                MOVEL'D'       ACDC
     C   29                MOVEL'C'       ACDC
     C                     MOVEL'3511'    ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDVAMT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C                     WRITERACVOUR
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#03     BEGSR
     C                     EXCPTET#02
     C                     ADD  3         LCNT
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#04     BEGSR
     C                     EXCPTET#03
     C                     ADD  3         LCNT
     CSR                   ENDSR
     OQPRINT  E  103           EH#01
     O                                   12 '報表編號：'
     O                                   18 'ARP015'
     O                                   71 '銷項稅額入帳清單'
     O                                  112 '列表日期:'
     O                         UDATE Y  120
     O        E  1             EH#01
     O                                   72 '********************'
     O                                  112 '頁　　數:'
     O                         PAG   Z  115
     O        E  1             EH#01
     O                                   63 '月份:'
     O                         YM    Y   68
     O        E  1             EH#01
     O                                   41 '借'
     O        E  1             EH#01
     O                                   10 '入帳日期'
     O                                   21 '發票號碼'
     O                                   28 '訂單'
     O                                   37 '客　戶'
     O                                   41 '貸'
     O                                   52 '會計科目'
     O                                   72 '借方金額'
     O                                   92 '貸方金額'
     O                                  106 '傳票編號'
     O*                                 114 '部門'
     O        E  1             EH#01
     O                         ALDSH    120
     O        E  1             ED#01
     O                         W2ACDTY   10
     O                         W2NO      21
     O                         W2RESV    27
     O                         W2CUNM    37
     O                         AMT   K   74
     O        E  1             ET#01
     O                    28             55 'C 1134應收帳款'
     O                    24             55 'D 1134應收帳款'
     O                    24   VAMT  K   74
     O                    28   VAMT  K   94
     O                         TXAR      97
     O                                   98 'P'
     O                         VRNOX    102
     O                                  103 '-'
     O                         ITEM     105
     O        E  1             ET#01
     O                                   55 '----------------'
     O                                   74 '--------------------'
     O                                   94 '--------------------'
     O        E  1             SP#01
     O        E  1             ET#02
     O                 26                40 'C'
     O                 29                40 'D'
     O                                   55 '2127銷項稅額'
     O                 29      VAMT  K   74
     O                 26      VAMT  K   94
     O                         TXAR      97
     O                         TXAR      98
     O                 32      VRNOX    102
     O                N32      VRNOY    102
     O                                  103 '-'
     O                         ITEM1    105
     O        E  1             ET#02
     O                                   55 '================'
     O                                   74 '===================='
     O                                   94 '===================='
     O        E  2             ET#03
     O                 29                40 'C'
     O                 26                40 'D'
     O                                   55 '3511公司往來'
     O                 26      VAMT  K   74
     O                 29      VAMT  K   94
     O                         TXAR      97
     O                         TXAR      98
     O                         VRNOY    102
     O                                  105 '-01'
     O        E  1             ET#03
     O                 26                40 'C'
     O                 29                40 'D'
     O                         KACNO     45
     O                         KACNON    55
     O                 29      VAMT  K   74
     O                 26      VAMT  K   94
     O                         TXAR      97
     O                                   98 'P'
     O                         VRNOX    102
     O                                  105 '-01'
     O        E  2             ET#LR
     O                                   52 '銷項稅額合計'
     O                         AMTTD K   74
     O                         AMTTC K   94
     O        E   60           ET#LRR
     O                                   50 '經理（廠長）：'
     O                                   73 '複核：'
     O                                   96 '製票：'
** TAB1
K3512高雄往來K720
H3514苗栗往來H800
M3515台中往來W040
