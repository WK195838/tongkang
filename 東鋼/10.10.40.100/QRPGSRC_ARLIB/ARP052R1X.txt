     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP052R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     應收系統檔案備份移出（銷貨明細）
     H*            5.DATE-WRITTEN  86/12/20
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTL  UF  E           K        DISK
     FTRNDTLBKO   E           K        DISK
     F            TXREC                             KRENAMETXRECW
     FARP052F IF  E           K        DISK
     F*****************************************************************
     I           UDS
     I                                     10211021 U#AREA
     I*
     IBKDTA      UDS                             36
9008 I                                        1   60W#BKDT
LYW  I                                        1   4 W#BKYM
     I*
     I            DS
     I                                        1  15 TXRESV
     I                                        1   4 D#ACDT
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C           *NAMVAR   DEFN ARBACKUPT BKDTA  36
     C           *LOCK     IN   BKDTA
     C                     UNLCKBKDTA
     C*
     C           *LOVAL    SETLLTXREC                69
     C           1         DOWEQ1
     C                     READ TXREC                    69
     C           *IN69     IFEQ '1'
     C                     LEAVE
     C                     ENDIF
     C*
     C           TXACDT    IFEQ 0                          未確認折讓
     C                     ITER
     C                     ENDIF
     C*
     C           TXCUNO    CHAINARP052F              69
     C           *IN69     IFEQ '0'                        未結案不搬
     C                     ITER
     C                     ENDIF
     C*
     C           TXACDT    IFEQ 999999                     折讓不代開
     C           D#ACDT    ANDGEW#BKYM
     C                     ITER
     C                     ENDIF
     C*
     C           TXACDT    IFNE 999999
     C           TXACDT    ANDGEW#BKDT
     C                     ITER
     C                     ENDIF
     C*
     C                     WRITETXRECW
     C                     DELETTXREC
     C                     ENDDO
     C*
     C                     SETON                     LR
     C                     RETRN
