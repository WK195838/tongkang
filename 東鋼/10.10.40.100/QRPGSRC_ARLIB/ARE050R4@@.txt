     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARE050R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02YSH
     H*            4.FUNCTION     進倉履約折讓自動處理作業-先前處理
     H*            5.DATE-WRITTEN  91/02/21
     H*            6.UPDATE  DATE  99/08/24  2010AR517  S00WCJ (9908A)
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARE050S4CF  E                    WORKSTN
     F                                        RRN2  KSFILE AR050F2
     F                                        RRN3  KSFILE AR050F3
     FDISMSTX IF  E           K        DISK
     FDISDTLX IF  E           K        DISK
     FDISMST  IF  E           K        DISK                      A
     F            RDISMST                           KRENAMERMST
     FDISDTL  IF  E           K        DISK                      A
     F            RDISDTL                           KRENAMERDTL
     E                    ERR     1   9 70               *錯誤訊息
     I            DS
     I                                        1  10 S#SRID
     I                                        1   8 D#SRI1
     I                                        9  10 D#SRI2
     I            DS
     I                                        1   60D#YM2
     I                                        1   40D#YY
     I                                        5   60D#MM
     I           UDS
     I                                      301 3060D#YYMM
     I                                      401 4060D#ACYM
     I                                     10011010 U#USER
     C*FILE => DISMST
     C           K#SMST    KLIST
     C                     KFLD           DMCUNO           客戶代號
     C                     KFLD           DMORNO           訂單編號
     C                     KFLD           DMYYMM           處理年月
     C                     KFLD           DMCONT           處理次數
     C                     KFLD           DMAREA           處理年月
     C*FILE => DISDTL
     C           K#SDTL    KLIST
     C                     KFLD           DSORNO           訂單編號
     C                     KFLD           DSYYMM           處理年月
     C                     KFLD           DSCONT           處理次數
     C                     KFLD           DSINNO           發票號碼
     C                     KFLD           DSPROD           產品類別
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'02'      SR2000           畫面二
     C           W#PRID    CASEQ'03'      SR3000            READC
     C           W#PRID    CASEQ'04'      SR4000           畫面四
     C           W#PRID    CASEQ'05'      SR5000           畫面四
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C*****************************************************************
     C           SR0000    BEGSR
     C*****************************************************************
     C                     MOVEL'ARE050S-'D#SRI1
     C                     MOVE '02'      W#PRID  2
     C                     MOVE *ALL'='   S#LIN1
     C                     MOVE *ALL'='   S#LIN2
     C                     MOVE *ALL'='   S#LIN3
     C*
     C                     MOVE *ON       *IN30
 9305C*                    MOVE '3'       S#PRO1
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR1400    BEGSR
     C*****************************************************************
     C* CLEAR SUBFILE
     C                     MOVE *ON       *IN74             SFLCLR
     C                     WRITEAR050F2C
     C                     MOVE *OFF      *IN74             SFLCLR
     C                     Z-ADD0         RRN2    40
     C*一次讀入
     C                     MOVE *OFF      *IN40
     C*
     C           *IN40     DOWEQ*OFF
     C                     READ RDISMST             N    40
     C   40                LEAVE
     C*
     C                     CLEARAR050F2
     C                     Z-ADD0         S#OPT             OP CODE
     C                     MOVE DMCUNO    S#CUNO           客戶代號
     C                     MOVE DMCUNM    S#CUNM           客戶名稱
     C                     MOVE DMORNO    S#ORNO           訂單號碼
     C                     Z-ADDDMYYMM    S#YYMM           處理年月
     C                     MOVE DMAREA    S#AREA           廠區別
     C                     Z-ADDDMQTY1    S#QTY1           發票出貨量
     C                     Z-ADDDMAMT1    S#AMT1           發票總金額
     C                     Z-ADDDMAMT2    S#AMT2           其他折讓
     C                     Z-ADDDMAMT3    S#AMT3           繳款金額
     C                     Z-ADDDMAMT4    S#AMT4           折讓單價
     C                     Z-ADDDMAMT5    S#AMT5           折讓總價
     C                     MOVE DMCOD1    S#COD1           折讓方式
     C                     MOVE DMCOD2    S#COD2           確認碼
     C                     MOVE DMCOD3    S#COD3           開立碼
     C                     MOVE DMCOD4    S#COD4           折扣碼
0205AC           DMACYM    IFNE 0
     C                     MOVE DMACYM    S#ACYM           入帳年月
0205AC                     ELSE
0205AC                     MOVELD#ACYM    S#ACYM
0205AC                     ENDIF
     C                     Z-ADDDMCONT    H#CONT
     C*
     C                     MOVE DMSALE    H#SALE           業務員
     C                     MOVE DMAREA    H#AREA           廠區別
     C*
     C           DMCOD3    IFNE *BLANK                     已開立
     C                     MOVE *ON       *IN31             PROTECT
     C                     ELSE
     C                     MOVE *OFF      *IN31             NOT PROTECT
     C                     ENDIF
     C*
     C                     ADD  1         RRN2
     C                     MOVE *ON       *IN70
     C*
     C                     WRITEAR050F2
     C*
     C           S#AREA    IFEQ 'H'
     C                     ADD  S#AMT5    S#HAMT
     C                     ELSE
     C                     ADD  S#AMT5    S#KAMT
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C           RRN2      IFNE 0                          至少一筆
     C                     Z-ADD1         S#NBR2
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2000    BEGSR
     C*****************************************************************
     C*
     C                     MOVE *LOVAL    DMCUNO           客戶代號
     C                     MOVE *LOVAL    DMORNO           訂單號碼
     C                     MOVE *LOVAL    DMYYMM           處理年月
     C                     MOVE *LOVAL    DMCONT           處理次數
     C           K#SMST    SETLLRDISMST
     C                     EXSR SR1400                     *讀入SFL
     C*
     C                     MOVE *BLANK    S#ERR2
     C           W#PRID    DOWEQ'02'
     C                     MOVEA'00'      *IN,71
     C           RRN2      IFEQ 0
     C                     MOVELERR,2     S#ERR2
     C                     MOVE *ON       *IN72            *SFLDSP OFF
     C                     ENDIF
     C*
     C                     WRITEAR050H
     C                     WRITEAR050F2M
     C                     EXFMTAR050F2C
     C                     MOVE *BLANK    S#ERR2
     C*F3=結束作業
     C           *IN03     IFEQ *ON
     C                     MOVE '00'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*F12=回前畫面
     C           *IN12     IFEQ *ON
     C                     MOVE '00'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C                     MOVE '03'      W#PRID
     C                     ENDDO
     C                     ENDSR
     C*****************************************************************
     C           SR3000    BEGSR
     C*****************************************************************
     C*READC
     C                     Z-ADD0         S#HAMT           H廠合計
     C                     Z-ADD0         S#KAMT           K廠合計
     C                     Z-ADD1         RRN2
     C           W#PRID    DOWEQ'03'
     C                     READCAR050F2                  50
     C   50                MOVE '02'      W#PRID
     C   50                LEAVE
     C*
     C           S#OPT     IFEQ 5
     C                     Z-ADDS#OPT     W#OPT   10
     C                     Z-ADD0         S#OPT
     C                     MOVE *ON       *IN70
     C                     UPDATAR050F2
     C                     MOVE '04'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4000    BEGSR
     C*****************************************************************
     C*W#PRID='03'--->ARE050S-03畫面
     C                     MOVE '03'      S#SRID
     C                     MOVE *BLANK    S#ERR3
     C* CLEAR SUBFILE
     C                     MOVE *ON       *IN79             SFLCLR
     C                     WRITEAR050F3C
     C                     MOVE *OFF      *IN79             SFLCLR
     C*
     C                     Z-ADD0         RRN3    40
     C                     EXSR SR4100                     *讀入SFL
     C*=======(顯示畫面)
     C           W#PRID    DOWEQ'04'
     C                     MOVEA'00'      *IN,76
     C           RRN3      IFEQ 0
     C                     MOVELERR,2     S#ERR3
     C                     MOVE *ON       *IN77            *SFLDSP OFF
     C                     ENDIF
     C*
     C                     WRITEAR050H
     C                     WRITEAR050F3M
     C                     EXFMTAR050F3C
     C                     MOVE *BLANK    S#ERR3
     C*F3=結束作業
     C           *IN03     IFEQ *ON
     C                     MOVE '00'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*F12=回前畫面
     C           *IN12     IFEQ *ON
     C                     MOVE '03'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*
     C*F10=存檔
     C           *IN10     IFEQ *ON
     C                     MOVE '05'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4100    BEGSR
     C*****************************************************************
     C*
     C*          S#PRO1    IFEQ '3'                        型鋼
     C*                    MOVE *ON       *IN80
     C*                    ELSE
     C*                    MOVE *OFF      *IN80
     C*                    ENDIF
     C*重SHOW檔頭
     C                     Z-ADDS#CONT    W#CONT  30       處理次數
     C*
     C                     CLEARAR050F3C
     C*
     C                     MOVE *ALL'='   S#LIN1
     C                     MOVE *ALL'='   S#LIN2
     C           H#AREA    IFEQ 'H'
     C                     MOVE '苗栗'  S#ARE3           廠區別
     C                     ELSE
     C                     MOVE '高雄'  S#ARE3
     C                     ENDIF
     C                     MOVELH#SALE    S#SAL3           業務員
     C                     MOVE S#CUNO    S#CUN3           客戶代號
     C                     MOVE S#CUNM    S#CUM3           客戶簡稱
     C                     MOVE S#YYMM    S#YM3            處理年月
     C                     MOVE S#ORNO    S#ORN3           訂單號碼
     C*                    MOVE W#PRO2    S#PRO3           產品類別
     C                     MOVE H#CONT    S#CONT           處理次數
     C*一次讀入
     C                     MOVE S#ORNO    DSORNO           訂單號碼
     C                     Z-ADDS#YYMM    DSYYMM           處理年月
     C                     Z-ADDS#CONT    DSCONT           處理次數
     C                     MOVE *BLANK    DSINNO           發票號碼
     C                     MOVE *BLANK    DSPROD           產品類別
     C           K#SDTL    SETLLRDISDTL
     C*
     C                     MOVE *OFF      *IN40
     C           *IN40     DOWEQ*OFF
     C                     READ RDISDTL             N    40
     C   40                LEAVE
     C*
     C           DSORNO    IFNE S#ORNO                     訂單號碼
     C           DSYYMM    ORNE S#YYMM                     處理年月
     C           DSAREA    ORNE S#AREA                     發票廠區
     C           DSCONT    ORNE S#CONT                     發票廠區
     C                     ITER
     C                     ENDIF
     C*
     C                     CLEARAR050F3
     C*                    MOVE DSCODE    S#CODE           開立碼
     C                     MOVE DSINNO    S#INNO           發票號碼
     C                     MOVE DSAREA    S#AREX           發票號碼
     C                     MOVE DSPROD    S#PROD           產品類別
     C                     Z-ADDDSQTY1    S#QTYX           產品數量
     C                     Z-ADDDSAMT1    S#AM1X           折讓金額
     C                     Z-ADDDSAMT2    S#AM2X           折讓稅額
     C                     MOVELDSTXNO    S#TXNO           折讓單號
     C                     Z-ADDDSTXIT    S#TXIT           折讓項次
     C*
     C                     ADD  1         RRN3
     C                     WRITEAR050F3
     C*
     C                     ENDDO
     C*
     C           RRN3      IFNE 0                          至少一筆
     C                     Z-ADD1         S#NBR3
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR5000    BEGSR
     C*****************************************************************
     C*READC
     C                     Z-ADD0         X#QTY1  90
     C                     Z-ADD1         RRN3
     C           W#PRID    DOWEQ'05'
     C                     READCAR050F3                  50
     C   50                MOVE '04'      W#PRID
     C   50                LEAVE
     C*
     C           S#CHIO    IFEQ 'Y'
     C                     EXSR SR5102
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C           X#QTY1    IFNE 0
     C                     EXSR SR5101
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR5100    BEGSR
     C*****************************************************************
     C                     EXSR SR5101
     C                     EXSR SR5102
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR5101    BEGSR
     C*****************************************************************
     C*紀錄DISMSTTEMP
     C                     MOVELS#CUNO    DMCUNO           客戶代號
     C                     MOVELS#ORNO    DMORNO           訂單號碼
     C                     Z-ADDS#YYMM    DMYYMM           處理年月
     C                     Z-ADDH#CONT    DMCONT           處理次數
     C                     MOVELS#AREA    DMAREA           訂單號碼
     C           K#SMST    CHAINRDISMST             N50
     C           K#SMST    CHAINRMST                 51
     C   51                Z-ADDX#QTY1    DMQTY1
     C   51                WRITERMST
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR5102    BEGSR
     C*****************************************************************
     C*紀錄DISDTLTEMP
     C                     MOVELS#ORNO    DSORNO           訂單號碼
     C                     Z-ADDS#YYMM    DSYYMM           處理年月
     C                     Z-ADDH#CONT    DSCONT           處理次數
     C                     MOVELS#INNO    DSINNO           訂單號碼
     C                     MOVELS#PROD    DSPROD
     C           K#SDTL    CHAINRDISDTL             N50
     C           K#SDTL    CHAINRDTL                 51
     C   51                WRITERDTL
     C   51                ADD  DSQTY1    X#QTY1
     C*
     C                     ENDSR
**  ERR
01-資料已存在，不得重新處理！
02-資料不存在，請重新處理！
03-本次處理年月不得為０！
04-請再按一次Ｆ６確認開立！
05-折讓單價與折讓總價不符，或相差兩元以上！
06-欄位不得為空白或０！
07-將針對已存檔確認之資料進行開立處理，請再按一次Ｆ６開始作業！
08-入帳年月不得為０！
09-入帳年月輸入錯誤！
