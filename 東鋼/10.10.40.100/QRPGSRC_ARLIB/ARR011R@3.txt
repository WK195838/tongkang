     H****************************************************************
     H*
     H*            UPDATE  DATE  99/08/31  2010AR517 S00WCJ (9908A)
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FRCVDTL  IP  E                    DISK
     FBANKID  IF  E           K        DISK
     FBANKAC  IF  E           K        DISK
     FQPRINT  O   F     158            PRINTER
      *************************************************************
     E                    TAB1    1   6  1   TABR1  10
     I*************************************************************
     IRVREC
     I                                              RVTBIDL3
     I                                              RVTANOL2
     I                                              RVPBIDL1
     I****
     IAADS        DS
     I              'も票據託收明細表も'C         CON3                       X
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     IBBDS        DS
     I                                        1  10 RVTBID
     I                                        1   3 BAID1
     I                                        4   7 BAID2
     I                                       11  20 RVTANO
     I                                       11  11 ACTYPE
     I                                       12  12 ACCURR
     I                                       13  20 ACACNO
     I           UDS
     I                                        1   80D#PRDT
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
9908AC           *DATE     SUB  19000000  U#SYSD  80
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C           BKKEY     KLIST
     C                     KFLD           BAID1
     C                     KFLD           BAID2
     C           ACKEY     KLIST
     C                     KFLD           ACTYPE
     C                     KFLD           ACCURR
     C                     KFLD           ACACNO
      *
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD55        TCNT    30
     C                     MOVE *ALL'='   ALS   141
     C                     MOVE *ALL'-'   ALQ    92
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD8         LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         AAMT   110
     C                     Z-ADD0         AQTY    50
     C                     Z-ADDTCNT      LCNT
     C           BKKEY     CHAINBANKID               31
     C   31                MOVE *BLANK    BABNAM
      *
     C           ACKEY     CHAINBANKAC               31
     C   31                MOVE *BLANK    ACBNAM
      *
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     MOVE *BLANK    CHAR10 10
     C           RVNTTP    LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     CHAR10
     C  N31                MOVEL'其他'  CHAR10
      *
     C           RVPLAC    COMP 'D'                      31
     C   31                MOVEL'本埠'  CHAR6X  6
     C  N31                MOVEL'外埠'  CHAR6X
      *
     C                     MOVEL*BLANK    R#NTNO 20
     C                     MOVELRVNTNO    R#NTNO
     C*
     C*                    ADD  2         LCNT
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  2         LCNT
     C                     EXCPTED#01
     C                     ADD  1         AQTY
     C                     ADD  RVRAMT    AAMT
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C                     EXCPTET#L2
     C                     Z-ADDTCNT      LCNT
     CSR         TL#L2Z    ENDSR
     O*****************************************************************
     OQPRINT  E   02           EH#01
     O                                   17 '報表編號: AR011'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
9908AO                         U#SYSDZ  132
     O        E 1              EH#01
     O                         CON3      71                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 2              EH#01
     O                                   11 '託收銀行:'
     O                         RVTBID  +  1
     O                         BABNAM  +  1
     O                                 +  3 '託收帳號:'
     O                         RVTANO  +  1
     O                         ACBNAM  +  1
     O                                 +  2 '託收日期:'
     O                         D#PRDT  +  1 '    /  /  '
     O        E 1              EH#01
     O                         ALS      141
     O        E 1              EH#01
     O                                   10 '客戶編號'
     O                                   21 '繳款單號'
     O                                   33 '票款別'
     O                                   45 '付款地'
     O                                   57 '付款銀行'
     O                                   73 '付款帳號'
     O                                   85 '票據號碼'
     O                                  116 '到期日期'
     O                                  130 '金　　額'
     O                                  141 '銀行簽章'
     O        E 1              EH#01
     O                         ALS      141
     O        E 1              ED#01
     O                         RVCUNO     7
     O                         RVNO      18
     O                         CHAR10    35
     O                         CHAR6X    43
     O                         RVPBID    61
     O                         RVPANO    74
     O                         R#NTNO    95
     O                         RVDUDT   115 '    /  /  '
     O                         RVRAMTJ  131
     O*       E 1              ED#01
     O        E 1              ET#L2
     O                         ALQ      141
     O        E 1              ET#L2
     O                                   66 '銀行小計：'
     O                                 +  1 '共'
     O                         AQTY  J
     O                                      '張'
     O                                 + 10 '總金額:'
     O                         AAMT  J  131
     O        E 1              ET#L2
     O                         ALQ      141
     O*****************************************************************
** ECK
A支票
B背書支票
C本票
D背書本票
E銀行本票
I信用狀　
