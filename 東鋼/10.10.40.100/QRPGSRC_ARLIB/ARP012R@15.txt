     H****************************************************************
     H*
     H*            UPDATE  DATE  99/05/25 2009AR406 S00WCJ (9905A)
     H*                          99/08/25 2010AR517 S00WCJ (9908A)
     H*                         100/07/05 2011AR607 S00WCJ (0007A)
     H*                          增加摘要三寫入庫別代號
     H*                         100/11/15  2011AR634 S00WCJ (0011A)
     H*                          配合IFRS修改
     H*                         101/09/14 2012AR681 S00WCJ (0109A)
     H*                          磅單X開頭，廠區為U
     H*                         101/09/20 2012AR682 S00WCJ (0109B)
     H*                         101/10/25 2012AR695 S00WCJ (0110A)
     H*                          磅單R開頭，廠區為U
     H*                         101/11/29 2012AR703 S00WCJ (0111A)
     H*                          磅單J開頭，廠區為U
     H*                         101/12/05 2012AR705 S00WCJ (0112A)
     H*                          磅單N開頭，廠區為U
     H*                         102/01/22 2013AR726 S00WCJ (0201A)
     H*                          磅單V開頭，廠區為U
     H*                         102/12/25 2013AR791 S00WCJ (0212A)
     H*                          修改八德往來與桃園往來
     H*                         103/04/11 2014AR820 S00WCJ (0304A)
     H*                          磅單Q開頭，廠區為U
     H*                         103/09/09 2014AR867 S00WCJ (0309A)
     H*                          不同品項分開代開
     H*                         104/09/23 2015AR953 S00WCJ (0409A)
     H*                          產品代碼3碼擴5碼
     H*                         106/03/16 2017AR1053 S00WCJ (0603A)
     H*                          品名由磅單數量最大值改為營業USER
     H*                          輸入之內容
     H*                         107/03/16  2018AR1138 S00WCJ (0703A)
     H*                          增加產品品名分項代開
     H*                         107/05/02  2018AR1159 S00WCJ (0705A)
     H*                          組織調整U380->U800
     H*                         108/12/25  S00WCJ (0812A)
     H*                          不同代開程式寫入總帳之ACCSNM發生內
     H*                          碼不一致之情形，與品妃姊討論後，寫入
     H*                          總帳前再重CHAIN CBCUST
     H*                         111/09/30 S00WCJ (1109A)
     H*                          TXORNOL3改為TXORNOL2
     H*                         112/05/15 2023AR00029 S00WCJ (1205A)
     H*                          磅單O開頭，廠區為U，取消N之設
     H*                          定
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
0007AFTRNDTLZ IP  E                    DISK
     FTRNDTLUPUF  E           K        DISK
     F            TXREC                             KRENAMETXW
     FACTRAN  UF  E           K        DISK                      A
     FHIPROD  IF  E           K        DISK
0812AFCBCUST  IF  E           K        DISK
0007AFQPRINT  O   F     176            PRINTER
     ******************************************************************
9905AE*                   TAB1    1   5  1   TABR1  19
9905AE                    TAB1    1   6  1   TABR1  19
0109BE                    TAB2    1   6  1   TABR2  21
     ******************************************************************
     ITXREC
     I                                              TXFL02L3
     I                                              TXIVNOL2
     I                                              TXCUNOL2
1109AI                                              TXORNOL2
0309AI                                              TXPDNML1
0703AI                                              TXRESVL1
     I                                              TXUPRCL1
0007AI                                              TXSTNOL1
     I*----------------------------------------------------------------
     ITXW
     I              TXFLAG                          TWFLAG
     I              TXCODE                          TWCODE
     I              TXNO                            TWNO
     I              TXITEM                          TWITEM
     I              TXACNT                          TWACNT
     I              TXDATE                          TWDATE
     I              TXACDT                          TWACDT
     I              TXCUNO                          TWCUNO
     I              TXCUNM                          TWCUNM
     I              TXORNO                          TWORNO
     I              TXIVNO                          TWIVNO
     I              TXPCNO                          TWPCNO
     I              TXVUNO                          TWVUNO
     I              TXRVID                          TWRVID
     I              TXSALE                          TWSALE
     I              TXSATP                          TWSATP
     I              TXIVTP                          TWIVTP
     I              TXPDNM                          TWPDNM
     I              TXQTY                           TWQTY
     I              TXUPRC                          TWUPRC
     I              TXAMT                           TWAMT
     I              TXTAX                           TWTAX
     I              TXFL01                          TWFL01
     I              TXFL02                          TWFL02
     I              TXTXAR                          TWTXAR
     I              TXTXDT                          TWTXDT
     I              TXRESV                          TWRESV
     I*----------------------------------------------------------------
     I            DS
     I                                        1  19 KGRP
     I                                        1   5 KACNO
     I                                        6  15 KACNON
     I                                       16  19 KDPNO
0109BI            DS
0109BI                                        1  21 K#GRP
0109BI                                        1   7 K#ACNO
0109BI                                        8  17 K#ACNN
0109BI                                       18  21 K#DPNO
     I            DS
     I                                        1   6 VRNOX
     I                                        2   60NO1
     I                                       11  16 VRNON
     I                                       12  160NO3
     I            DS
     I                                        1   6 VRNOY
     I                                        2   60NO2
     I            DS
     I                                        1   7 TWVUNO
     I                                        1   1 TTT1
     I                                        2   2 TTT2
     I                                        3   3 TTT3
     I                                        4   7 TTT4
CLJ  I*
     I            DS
     I                                        1   2 W#OEOF
     I                                        1   1 D#OE
     I                                        2   2 D#OF
     I            DS
     I                                        1  12 NAME1
     I                                       12  12 D#NOF
0603AI            DS
0603AI                                        1  30 TXRESV
0603AI                                        1   5 D#PDNM
---  I*
     I           UDS
9008 I                                        1   8 YMD
     I                                        1   60D#YM
     I                                        9   9 TXAR
0011AI                                      201 206 D#VNO1
0011AI                                      211 216 D#VNO2
     I                                     10011010 D#USER
0109BIIFRS       UDS                             50
0109BI                                        1   60D#IFYM
0109BI                                        1   80D#IFRS
     C*****************************************************************
     C*          KEY    LIST
     C*****************************************************************
     C           ACKEY1    KLIST
     C                     KFLD           TXAR
     C                     KFLD           D#YM
     C                     KFLD           ACVRNO
     C                     KFLD           ACITEM
     C*
     C           TXKEY     KLIST
     C                     KFLD           TXCODE
     C                     KFLD           TXNO
     C                     KFLD           TXITEM
     C*****************************************************************
     C*          MAIN   PROGRAM
     C*****************************************************************
9908AC           *DATE     SUB  19000000  U#SYSD  80
0109BC           *NAMVAR   DEFN ARIFRSCTL IFRS
0109BC           D#YM      IFLT D#IFYM
0109BC                     MOVEL*ON       *IN77
0109BC                     ELSE
0109BC                     MOVEL*OFF      *IN77
0109BC                     ENDIF
0109BC                     UNLCKIFRS
     C  N10                EXSR IN#01
     C   L3                EXSR DL#03
     C   L2                EXSR DL#02
     C   L1                EXSR DL#01
     C                     EXSR DP#01
     CL1                   EXSR TL#01
     CL2                   EXSR TL#02
     CL3                   EXSR TL#L3
     CLR                   EXSR TL#LR
     C**--------------------------------------------------**
     CSR         IN#01     BEGSR
     C*
9008 C                     MOVE YMD       DATEA   80
     C                     Z-ADD0         AMTT   132
     C                     Z-ADD0         AMTTT  132
     C                     Z-ADD1         ITEM1   30
     C                     Z-ADD1         ITEM2   30
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD58        TCNT    20
     C                     Z-ADD0         PAG     30
     C                     Z-ADD0         COUNT   40
     C                     MOVE *BLANK    KGRP
0109BC                     MOVEL*BLANKS   K#GRP
0109BC   77      TXAR      LOKUPTAB1      TABR1          31
0109BC   31 77             MOVELTABR1     KGRP
0109BC  N77      TXAR      LOKUPTAB2      TABR2          31
0109BC   31N77             MOVELTABR2     K#GRP
0011AC                     MOVEL*BLANKS   D#VNO1
0011AC                     MOVEL*BLANKS   D#VNO2
     C                     SETON                     10
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**  FIND LAST NO                                **
     C**----------------------------------------------**
     CSR         @FDNO     BEGSR
     C*
     C                     MOVEL'D00999'  ACVRNO
     C                     Z-ADD999       ACITEM
     C                     SETOF                     95
     C           ACKEY1    SETGTACTRAN
     C                     READPACTRAN                   95
     C  N95                MOVELACVRNO    TYPE    1
     C  N95      TYPE      COMP 'D'                  9595
     C  N95                MOVELACVRNO    VRNON
     C  N95                ADD  1         NO3
     C  N95                MOVELVRNON     VRNOX
     C   95                MOVEL'D00001'  VRNOX
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**    L3 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#03     BEGSR
      *
      * INITIAL NEXT VOUCHER NO
      *
     C                     EXSR @FDNO
     C                     MOVELVRNOX     VRNOY
0011AC           D#VNO1    IFEQ *BLANKS
0011AC                     MOVELVRNOY     D#VNO1
0011AC                     MOVELVRNOY     D#VNO2
0011AC                     ENDIF
     C           NO1       ADD  1         NO2
     C                     Z-ADD1         ITEM1   30
     C                     Z-ADD1         ITEM2   30
     C                     Z-ADD0         AMTT   132
0011AC           D#VNO2    IFLT VRNOY
0011AC                     MOVELVRNOY     D#VNO2
0011AC                     ENDIF
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**    L2 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#02     BEGSR
     C*
     C                     Z-ADD0         AMT2   132
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**    L1 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#01     BEGSR
     C*
0409AC                     MOVELTXPDNM    F4NAME
0409AC           F4NAME    CHAINHIPROD               31
CLJ  C   31                MOVE *BLANK    NAME1
     C           *IN31     IFEQ '0'
     C                     MOVE ''      W#OEOF
     C                     MOVELF4CHIN    NAME1
     C                     MOVE D#OF      D#NOF
---  C                     ENDIF
     C                     Z-ADD0         AMT1   132
     C                     Z-ADD0         QTY1    80
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**       DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C*
     C                     ADD  TXAMT     AMT1
     C                     ADD  TXAMT     AMT2
     C                     ADD  TXQTY     QTY1
     C***
     C                     ADD  TXAMT     AMTT
     C                     ADD  TXAMT     AMTTT
     C                     ADD  1         COUNT
      *
     C           TXKEY     CHAINTRNDTLUP             31
     C  N31                MOVELTXAR      TTT1
     C  N31                MOVELTWPCNO    TX@     1
     C  N31                MOVELTX@       TTT2
     C  N31                MOVELVRNOY     TTT3
     C  N31                MOVE VRNOY     TTT4
     C  N31                UPDATTXW
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   L1 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#01     BEGSR
     C*
     C           AMT1      IFNE 0
     C                     ADD  1         ITEM1
     C           AMT1      IFLT 0
     C                     Z-SUBAMT1      AMT1
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXSR WR#01
     C                     EXSR PR#01
     C                     ENDIF
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   L2 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#02     BEGSR
     C*
     C           AMT2      IFNE 0
     C                     ADD  1         ITEM2
     C           AMT2      IFLT 0
     C                     Z-SUBAMT2      AMT2
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXSR WR#02
     C                     EXSR PR#02
     C                     ENDIF
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   LR BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#L3     BEGSR
     C*
     C           AMTT      IFNE 0
     C           AMTT      IFLT 0
     C                     Z-SUBAMTT      AMTT
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXSR WR#03
     C                     EXSR PR#03
     C                     EXSR WR#04
     C                     EXSR PR#04
     C                     ADD  4         LCNT
     C                     ENDIF
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   LR BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#LR     BEGSR
     C*
     C           AMTTT     IFNE 0
     C                     EXCPTET#LR
     C                     EXCPTET#LRR
     C                     ENDIF
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    TOTAL  TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#01     BEGSR
     C*
     C****  WRITE  TO NON-TAIPEI "4111" 銷貨收入
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXPCNO    TX@     1
0007AC           TX@       IFEQ 'D'
0109AC           TX@       OREQ 'X'
0110AC           TX@       OREQ 'R'
0111AC           TX@       OREQ 'J'
0112AC*1205A     TX@       OREQ 'N'
0201AC           TX@       OREQ 'V'
0304AC           TX@       OREQ 'Q'
1205AC           TX@       OREQ 'O'
0007AC                     MOVEL'U'       TX@
0007AC                     ENDIF
     C                     MOVELTX@       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDD#YM      ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADDITEM1     ACITEM
     C   51                MOVEL'C'       ACDC
     C  N51                MOVEL'D'       ACDC
0109BC   77                MOVEL'4111'    ACNO
0109BC  N77                MOVEL'410101'  ACNO
     C                     MOVEL'L'       ACTYCO
     C                     MOVE *BLANK    KGRP
0109BC   77      TX@       LOKUPTAB1      TABR1          31
0109BC   31 77             MOVELTABR1     KGRP
0109BC  N77      TX@       LOKUPTAB2      TABR2          31
0109BC   31N77             MOVELTABR2     K#GRP
0109BC   77                MOVELKDPNO     ACDPNO
0109BC  N77                MOVELK#DPNO    ACDPNO
     C                     MOVELTXORNO    KORNO   6
     C                     MOVELKORNO     ACRLNO
0812AC           TXCUNO    CHAINCBCUST              N59
0812AC  N59                MOVELCBCUNM    ACCSNM
0812AC   59                MOVELTXCUNM    ACCSNM
     C                     Z-ADDAMT1      ACAMT
     C*
CLJ  C*                    MOVELTXPDNM    ACDSC1           移至下面
     C           TXACNT    IFNE '2'
     C                     Z-ADD0         QTY1
     C                     ENDIF
     C                     MOVELQTY1      ACDSC2
     C                     MOVEL'A'       ACSEPT
0603AC                     MOVELD#PDNM    ACDSC1           摘要一
0603AC*                    EXSR SR8000                     設ACDSC1
     C*
0007AC                     MOVELTXSTNO    ACDSC3           庫別代號
     C                     MOVELD#USER    ACENTM           建檔人員
9908AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9908AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#02     BEGSR
     C****  WRITE  TO TAIPEI  "1134"應收帳款
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDD#YM      ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM2     ACITEM
     C  N51                MOVEL'C'       ACDC
     C   51                MOVEL'D'       ACDC
0109BC   77                MOVEL'1134'    ACNO
0109BC  N77                MOVEL'111101'  ACNO
     C                     MOVEL'G'       ACTYCO
     C                     MOVELKORNO     ACRLNO
0812AC           TXCUNO    CHAINCBCUST              N59
0812AC  N59                MOVELCBCUNM    ACCSNM
0812AC   59                MOVELTXCUNM    ACCSNM
     C                     Z-ADDAMT2      ACAMT
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9908AC*                    MOVE UDATE     ACENTD           建檔日期
9908AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9908AC*                    MOVE UDATE     ACUPDD           異動日期
9908AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#03     BEGSR
     C*
     C****  WRITE  TO TAIPEI  "3511"公司往來
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXPCNO    TX@     1
0007AC           TX@       IFEQ 'D'
0109AC           TX@       OREQ 'X'
0110AC           TX@       OREQ 'R'
0111AC           TX@       OREQ 'J'
0112AC*1205A     TX@       OREQ 'N'
0201AC           TX@       OREQ 'V'
0304AC           TX@       OREQ 'Q'
1205AC           TX@       OREQ 'O'
0007AC                     MOVEL'U'       TX@
0007AC                     ENDIF
     C                     MOVELTX@       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDD#YM      ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADD1         ACITEM
     C  N51                MOVEL'C'       ACDC
     C   51                MOVEL'D'       ACDC
0109BC   77                MOVEL'3511'    ACNO
0109BC  N77                MOVEL'390101'  ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9908AC*                    MOVE UDATE     ACENTD           建檔日期
9908AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9908AC*                    MOVE UDATE     ACUPDD           異動日期
9908AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#04     BEGSR
     C*
     C****  WRITE  TO TAIPEI  "3512-6"廠區往來
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDD#YM      ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADD1         ACITEM
     C   51                MOVEL'C'       ACDC
     C  N51                MOVEL'D'       ACDC
     C                     MOVE *BLANK    KGRP
0109BC                     MOVEL*BLANKS   K#GRP
0109BC   77      TX@       LOKUPTAB1      TABR1          31
0109BC   31 77             MOVELTABR1     KGRP
0109BC  N77      TX@       LOKUPTAB2      TABR2          31
0109BC   31N77             MOVELTABR2     K#GRP
0109BC   77                MOVELKACNO     ACNO
0109BC  N77                MOVELK#ACNO    ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9908AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9908AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#01     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#02     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#02
     C                     ADD  1         LCNT
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#03     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#03
     C                     ADD  1         LCNT
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#04     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#04
     C                     ADD  2         LCNT
     C*
     CSR                   ENDSR
      *
CLJB C*****************************************************************
     C*  更改摘要一之品名為磅單內銷貨數量最多之品名
     C*****************************************************************
     CSR         SR8000    BEGSR
     C*
     C           K#TX      KLIST
     C                     KFLD           F#CODE  4
     C                     KFLD           F#NO    8
     C*
     C                     MOVEL'SA04'    F#CODE
     C                     MOVELTXPCNO    F#NO
0409AC                     MOVEL*BLANK    W#PDNM  5
     C                     Z-ADD0         W#QTY   70
     C           K#TX      CHAINTXW                  69
     C           *IN69     DOWEQ'0'
     C           TWQTY     IFGT W#QTY
     C                     Z-ADDTWQTY     W#QTY
     C                     MOVELTWPDNM    W#PDNM           量多之品名
     C                     ENDIF
     C           K#TX      READETXW                      69
     C                     ENDDO
     C*
     C                     MOVELW#PDNM    ACDSC1           摘要一
     C*
     CSR                   ENDSR
---  C*****************************************************************
     OQPRINT  E   03           EH#01
     O                                   12 '報表編號：'
     O                                   18 'ARP012'
     O                                   81 '調整單入帳清單'
     O                                  140 '列表日期:'
9908AO                         U#SYSD   150 '    /  /  '
     O        E 1              EH#01
     O                                   82 '******************'
     O                                  140 '頁　　數:'
     O                         PAG   Z  143
     O        E 1              EH#01
     O                                   71 '日期:'
     O                         DATEA     81 '    /  /  '
     O        E 1              EH#01
     O                                   41 '借'
     O        E 1              EH#01
     O                                   10 '入帳日期'
     O                                   20 '磅單編號'
     O                                   26 '訂單'
     O                                   35 '客　戶'
     O                                   41 '貸'
     O                                   51 '會計科目'
0109BO                                   67 '品　名'
0109BO                                   89 '數量'
0109BO                                   99 '單價'
0109BO                                  116 '借方金額'
0109BO                                  133 '貸方金額'
0109BO                                  140 '部門'
0109BO                                  154 '傳票編號'
0109BO                                  167 '庫別代號'
     O        E 1              EH#01
     O                                   20 '--------------------'
     O                                   40 '--------------------'
     O                                   60 '--------------------'
     O                                   80 '--------------------'
     O                                  100 '--------------------'
     O                                  120 '--------------------'
     O                                  132 '--------------------'
     O                                  150 '--------------------'
     O                                  155 '-----'
0007AO                                  165 '-----------'
     O        E 1              ED#01
     O                         ACYMD     10 '    /  /  '
     O                         TXPCNO    19
     O                         KORNO     26
     O                         ACCSNM    37
0109BO                 77 51             58 'C 4111  銷貨收入L'
0109BO                 77N51             58 'D 4111  銷貨收入L'
0109BO                N77 51             58 'C 410101銷貨收入L'
0109BO                N77N51             58 'D 410101銷貨收入L'
     O*                        TXPDNM    61
0603AO                         D#PDNM  +  1
     O                         NAME1   +  1
0109BO                         QTY1  K   89
0109BO                         TXUPRCK   99
0109BO                   N51   ACAMT K  116
0109BO                    51   ACAMT K  133
0109BO                         ACDPNO   143
0109BO                         TXAR     146
0109BO                         TX@      147
0109BO                         VRNOY    153
     O                                      '-'
0109BO                         ACITEM   157
0109BO                         ACDSC3   167
     O        E 1              ED#02
     O                         ACYMD     10 '    /  /  '
     O*                        TXPCNO    19
     O                         KORNO     26
     O                         ACCSNM    37
0109BO                 77N51             58 'C 1134  應收帳款G'
0109BO                 77 51             58 'D 1134  應收帳款G'
0109BO                N77N51             58 'C 111101應收帳款G'
0109BO                N77 51             58 'D 111101應收帳款G'
0109BO                    51   ACAMT K  116
0109BO                   N51   ACAMT K  133
0109BO                         TXAR     146
0109BO                                  147 'P'
0109BO                         VRNOX    153
     O                                      '-'
0109BO                         ACITEM   157
     O        E 1              ED#03
     O                         ACYMD     10 '    /  /  '
     O*                        TXPCNO    20
0109BO                 77N51             58 'C 3511  公司往來A'
0109BO                 77 51             58 'D 3511  公司往來A'
0109BO                N77N51             58 'C 390101公司往來A'
0109BO                N77 51             58 'D 390101公司往來A'
0109BO                    51   ACAMT K  116
0109BO                   N51   ACAMT K  133
0109BO                         TXAR     146
0109BO                         TX@      147
0109BO                         VRNOY    153
     O                                      '-'
0109BO                         ACITEM   157
     O        E 1              ED#04
     O                         ACYMD     10 '    /  /  '
     O*                        TXNO      20
     O                   N51             41 'D '
     O                    51             41 'C '
0109BO                 77      KACNO     45
0109BO                 77      KACNON    55
0109BO                N77      K#ACNO    47
0109BO                N77      K#ACNN    57
0109BO                                   58 'A'
0109BO                   N51   ACAMT K  116
0109BO                    51   ACAMT K  133
0109BO                         TXAR     146
0109BO                                  147 'P'
0109BO                         VRNOX    153
     O                                      '-'
0109BO                         ACITEM   157
     O        E 1              ED#04
     O        E 1              ET#LR
     O                                   60 '入帳筆數'
     O                         COUNT K +  1
     O                N51                96 '銷貨調整金額合計'
0109BO                N51      AMTTT K  116
     O                 51               113 '銷貨調整金額合計'
0109BO                 51      AMTTT K  133
     O        E   60           ET#LRR
     O                                   50 '經理（廠長）：'
     O                                   73 '複核：'
     O                                   96 '製票：'
** TAB1
K3512 高雄往來K210
T3513 桃園往來T380
H3514 苗栗往來H800
M3515 台中往來W040
L3516 林口往來L080
U3518 新桃往來U380
** TAB2
K390102 高雄往來K210
T390103 八德往來T380
H390104 苗栗往來H800
M390105 台中往來W040
L390106 林口往來L080
U390108 桃園往來U800
