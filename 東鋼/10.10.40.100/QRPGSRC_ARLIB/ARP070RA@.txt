     H            Y                                     1                 BR100
     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP070RA
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     客戶暫收統計檔上傳台北─各廠異動
     H*            5.DATE-WRITTEN  88/11/30
     H*
     H*****************************************************************
     FAR2159F UP  E                    DISK
     FAR2159FPUF  E           K        DISK                      A
     F            ARREC                             KRENAMEARRECP
     I*****************************************************************
     IARRECP      01
     I              ARFLAG                          AWFLAG
     I              ARCUNO                          AWCUNO
     I              ARAREA                          AWAREA
     I              ARAMT1                          AWAMT1
     I              ARAMT2                          AWAMT2
     I              ARAMT3                          AWAMT3
     I              ARAMT4                          AWAMT4
     I              ARAMT5                          AWAMT5
     I              ARAMT6                          AWAMT6
     I              ARRESV                          AWRESV
     I*
     I           UDS
     I                                     10211021 U#AREA
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C                     EXSR DTLSR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*
     C*********************************
     C*  初始程式
     C*********************************
     CSR         INISR     BEGSR
     C           K#AR      KLIST
     C                     KFLD           ARCUNO
     C                     KFLD           ARAREA
     CSR                   ENDSR
     C*
     C*********************************
     C*  DETAIL TIME 函式
     C*********************************
     CSR         DTLSR     BEGSR
     C           ARFLAG    COMP 'A'                      11
     C           ARFLAG    COMP 'C'                      12
     C           ARFLAG    COMP 'D'                      13
     C*
     C           ARAMT2    IFGE ARAMT1                     貨款較多
     C                     SUB  ARAMT1    ARAMT2
     C                     Z-ADD0         ARAMT1
     C                     ELSE                            現金較多
     C                     SUB  ARAMT2    ARAMT1
     C                     Z-ADD0         ARAMT2
     C                     ENDIF
     C*
     C           K#AR      CHAINARRECP               69
     C*
     C                     MOVELARFLAG    AWFLAG
     C                     MOVELARCUNO    AWCUNO
     C                     MOVELARAREA    AWAREA
     C                     Z-ADDARAMT1    AWAMT1
     C                     Z-ADDARAMT2    AWAMT2
     C                     Z-ADDARAMT3    AWAMT3
     C                     Z-ADDARAMT4    AWAMT4
     C                     Z-ADDARAMT5    AWAMT5
     C                     Z-ADDARAMT6    AWAMT6
     C                     MOVELARRESV    AWRESV
     C*
     C   69                WRITEARRECP
     C  N69                UPDATARRECP
     C*
     C                     MOVEL*BLANK    ARFLAG
     C   11
     COR 12                UPDATARREC
     C   13                DELETARREC
     C*
     CSR                   ENDSR
     C*********************************
     C*  LR 函式  印核覆
     C*********************************
     CSR         LRSR      BEGSR
     CSR                   ENDSR
     C*
     C*********************************
     C*  查無資料函式
     C*********************************
     CSR         MSGSR     BEGSR
     CSR                   ENDSR
     C*
     C*****************************************************************
