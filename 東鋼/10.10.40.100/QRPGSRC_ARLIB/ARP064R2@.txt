     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP064R2
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     訂單管制資料異動（先期異動）
     H*            5.DATE-WRITTEN  88/10/28
     H*
     H* 說明：依ARODCT內的資料來異動ARODCTP
     H*
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARODCT  UP  E           K        DISK
     F            RARODCT                           KRENAMERARW
     FARODCTP UF  E           K        DISK                      A
     F*****************************************************************
     IRARW        01
     I              A2FLAG                          AWFLAG
     I              A2ORNO                          AWORNO
     I              A2CTKD                          AWCTKD
     I              A2MTHD                          AWMTHD
     I              A2TYP1                          AWTYP1
     I              A2RAT1                          AWRAT1
     I              A2TYP2                          AWTYP2
     I              A2RAT2                          AWRAT2
     I              A2TYP3                          AWTYP3
     I              A2RAT3                          AWRAT3
     I*------------------------------------------------------
     I           UDS
     I                                        2   2 U#FLAG
     I                                     10011010 U#USID
     I                                     10011003 U#DPID
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C                     EXSR DTLSR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*
     C*********************************
     C*  初始程式
     C*********************************
     CSR         INISR     BEGSR
     C           K#A2      KLIST
     C                     KFLD           AWORNO
     C                     KFLD           AWCTKD
     CSR                   ENDSR
     C*
     C*********************************
     C*  DETAIL TIME 函式
     C*********************************
     CSR         DTLSR     BEGSR
     C           K#A2      CHAINRARODCT              69
     C                     EXSR SR1000                     搬移變數
     C  N69                UPDATRARODCT                    修改
     C   69                WRITERARODCT                    新增
     C*
     C           AWFLAG    IFEQ 'D'
     C                     DELETRARW                       刪除記錄
     C                     ELSE
     C                     MOVEL*BLANK    AWFLAG           清除旗標
     C                     UPDATRARW
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*--------------------------------
     C*  公用子函式（搬移變數）
     C*--------------------------------
     CSR         SR1000    BEGSR
     C                     MOVELAWFLAG    A2FLAG
     C                     MOVELAWORNO    A2ORNO
     C                     MOVELAWCTKD    A2CTKD
     C                     MOVELAWMTHD    A2MTHD
     C                     Z-ADDAWTYP1    A2TYP1
     C                     Z-ADDAWRAT1    A2RAT1
     C                     Z-ADDAWTYP2    A2TYP2
     C                     Z-ADDAWRAT2    A2RAT2
     C                     Z-ADDAWTYP3    A2TYP3
     C                     Z-ADDAWRAT3    A2RAT3
     CSR                   ENDSR
     C*
     C*********************************
     C*  LR 函式  印核覆
     C*********************************
     CSR         LRSR      BEGSR
     C                     MOVEL'X'       U#FLAG           有資料
     CSR                   ENDSR
     C*
     C*********************************
     C*  查無資料函式
     C*********************************
     CSR         MSGSR     BEGSR
     C           *LOVAL    SETLLRARODCT
     C                     READ RARODCT                  69
     C  N69                MOVEL'X'       U#FLAG           有資料
     C   69                MOVEL' '       U#FLAG           無資料
     CSR                   ENDSR
     C*
