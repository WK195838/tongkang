     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP065R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     發票開立挑選資料
     H*            5.DATE-WRITTEN  88/01/31
     H*
     H* 說明：將原銷貨明細檔內的資料挑出再寫入銷貨暫時檔內，
     H*       同時加上訂單管控的資訊。
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTL  IP  E           K        DISK
     FARCUCT  IF  E           K        DISK
     FARODCT  IF  E           K        DISK
     FWTMASPL2IF  E           K        DISK
     FTRNDTLN O   E           K        DISK
     F*****************************************************************
     I           UDS
     I                                     10011010 U#USID
     I                                     10011003 U#DPID
     I            DS
     I                                        1  22 D#DEST
     I                                        1   3 D#STAR
     I            DS
     I                                        1   4 D#MARK
     I                                        1   3 D#MAR1
     I            DS
     I                                        1   8 D#TXNO
     I                                        1   1 D#AREA
     I                                        2   3 D#VNO1
     I                                        4   80D#VNO2
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C                     EXSR DTLSR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*
     C*********************************
     C*  初始程式
     C*********************************
     CSR         INISR     BEGSR
     C           K#A1      KLIST
     C                     KFLD           A1CUNO
     C                     KFLD           A1CTKD
     C*
     C           K#A2      KLIST
     C                     KFLD           A2ORNO
     C                     KFLD           A2CTKD
     C*FILE==>WTMASPL2
     C           K#WTSP    KLIST
     C                     KFLD           W6AREA
     C                     KFLD           W6VNO1
     C                     KFLD           W6VNO2
     C*
     C                     MOVEL'＊'    D#MARK
     CSR                   ENDSR
     C*
     C*********************************
     C*  DETAIL TIME 函式
     C*********************************
     CSR         DTLSR     BEGSR
     C                     MOVEL'00'      TNMTHD           初始為隨車
     C                     Z-ADD0         TNTYP1           類別一
     C                     Z-ADD0         TNRAT1           比率金額一
     C                     Z-ADD0         TNTYP2           類別二
     C                     Z-ADD0         TNRAT2           比率金額二
     C                     Z-ADD0         TNTYP3           類別三
     C                     Z-ADD0         TNRAT3           比率金額三
     C*判斷是否存在客戶管制檔
     C                     MOVELTXCUNO    A1CUNO
     C                     MOVEL'04'      A1CTKD
     C           K#A1      CHAINRARCUCT              69
     C           *IN69     IFEQ '0'
     C                     MOVELA1MTHD    TNMTHD           預設客戶
     C                     Z-ADDA1TYP1    TNTYP1
     C                     Z-ADDA1RAT1    TNRAT1
     C                     Z-ADDA1TYP2    TNTYP2
     C                     Z-ADDA1RAT2    TNRAT2
     C           A1RFOR    IFEQ ' '                        採客戶
     C                     GOTO L#SAVE
     C                     ENDIF
     C                     ENDIF
     C*判斷是否存在訂單管制檔
     C                     MOVELTXORNO    A2ORNO           訂單編號
     C                     MOVEL'04'      A2CTKD           管制發票
     C           K#A2      CHAINRARODCT              69
     C           *IN69     IFEQ '0'                        採訂單
     C                     MOVELA2MTHD    TNMTHD
     C                     Z-ADDA2TYP1    TNTYP1
     C                     Z-ADDA2RAT1    TNRAT1
     C                     Z-ADDA2TYP2    TNTYP2
     C                     Z-ADDA2RAT2    TNRAT2
     C                     ENDIF
     C                     EXSR DISSR                      判斷進倉
     C*9105 針對區間開立作修正 START-----------------------
     C           *IN69     IFEQ '1'
     C                     MOVELTXORNO    W#ORN1  6
     C                     MOVELW#ORN1    A2ORNO
     C                     MOVE '001'     A2ORNO
     C                     MOVEL'04'      A2CTKD
     C           K#A2      SETLLRARODCT
     C                     READ RARODCT                  69
     C           *IN69     IFEQ '0'                        讀到
     C                     MOVELA2ORNO    W#ORN2  6
     C           W#ORN1    IFEQ W#ORN2
     C           A2MTHD    ANDEQ'03'                       區間開立
     C                     MOVELA2MTHD    TNMTHD
     C                     Z-ADDA2TYP1    TNTYP1
     C                     Z-ADDA2RAT1    TNRAT1
     C                     Z-ADDA2TYP2    TNTYP2
     C                     Z-ADDA2RAT2    TNRAT2
     C                     ENDIF                            ORNO
     C                     ENDIF                            IN69
     C                     ENDIF                            IN69
     C*9105 針對區間開立作修正 END  -----------------------
     C*
     C           L#SAVE    TAG                             存檔旗標
     C*
     C                     EXSR SR1000                     搬移資料
     C                     WRITETNREC
     CSR                   ENDSR
     C*
     C*-------------------------------
     C* 搬移資料
     C*-------------------------------
     CSR         SR1000    BEGSR
     C                     MOVELTXFLAG    TNFLAG           處理代碼
     C                     MOVELTXCODE    TNCODE           單據代碼
     C                     MOVELTXNO      TNNO             單據號碼
     C                     Z-ADDTXITEM    TNITEM           項次
     C                     MOVELTXACNT    TNACNT           原因碼
     C                     Z-ADDTXDATE    TNDATE           單據日期
     C                     Z-ADDTXACDT    TNACDT           入帳日期
     C                     MOVELTXCUNO    TNCUNO           客戶代號
     C                     MOVELTXCUNM    TNCUNM           客戶名稱
     C                     MOVELTXORNO    TNORNO           訂單編號
     C                     MOVELTXIVNO    TNIVNO           發票號碼
     C                     MOVELTXPCNO    TNPCNO           磅單編號
     C                     MOVELTXVUNO    TNVUNO           傳票號碼
     C                     MOVELTXRVID    TNRVID           收款業務員
     C                     MOVELTXSALE    TNSALE           出貨業務員
     C                     MOVELTXSATP    TNSATP           銷售別
     C                     MOVELTXIVTP    TNIVTP           發票別
     C                     MOVELTXPDNM    TNPDNM           品名代號
     C                     Z-ADDTXQTY     TNQTY            數量
     C                     Z-ADDTXUPRC    TNUPRC           單價
     C                     Z-ADDTXAMT     TNAMT            金額
     C                     Z-ADDTXTAX     TNTAX            稅額
     C                     MOVELTXFL01    TNFL01           折讓確認碼
     C                     MOVELTXFL02    TNFL02           過發票碼
     C                     MOVELTXTXAR    TNTXAR           異動廠區
     C                     Z-ADDTXTXDT    TNTXDT           異動日期
     C                     MOVELTXRESV    TNRESV           保留碼
     C*
     C                     MOVELTXORNO    TNORN5           五碼訂單
     CSR                   ENDSR
     C*
     C*-------------------------------
     C* 判斷是否進倉
     C*-------------------------------
     CSR         DISSR     BEGSR
     C                     MOVELTXNO      D#TXNO
     C                     MOVELD#AREA    W6AREA           廠區別
     C                     MOVELD#VNO1    W6VNO1           進出別
     C                     Z-ADDD#VNO2    W6VNO2           磅單序號
     C           K#WTSP    CHAINRWTMASP              40
     C  N40                MOVELW6DEST    D#DEST
     C  N40      D#STAR    IFEQ D#MAR1
     C                     MOVELTXORNO    W#ORNO  6
     C                     MOVELW#ORNO    A2ORNO           訂單編號
     C                     MOVE '000'     A2ORNO
     C                     MOVEL'04'      A2CTKD           管制類別
     C           K#A2      CHAINRARODCT              41
     C           *IN41     IFEQ *OFF
     C           A2MTHD    ANDEQ'99'
     C                     Z-ADDA2TYP3    TNTYP3
     C                     Z-ADDA2RAT3    TNRAT3
     C                     ENDIF
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*********************************
     C*  LR 函式  印核覆
     C*********************************
     CSR         LRSR      BEGSR
     CSR                   ENDSR
     C*
     C*********************************
     C*  查無資料函式
     C*********************************
     CSR         MSGSR     BEGSR
     CSR                   ENDSR
     C*
     C*****************************************************************
