     H            Y                                     1                 BR100
     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP028RA
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     發票轉出記錄異動傳至廠區
     H*            5.DATE-WRITTEN  88/07/30
     H*            6.UPDATE  DATE  107/12/23 2018AR00042 S00WCJ (0712A)
     H*                            新增折讓單號及備註欄位
     H*
     H*****************************************************************
     FINVTFRX IP  E           K        DISK
     F            ITREC                             KRENAMEITRECX
     FINVTFRP UF  E           K        DISK                      A
     F            ITREC                             KRENAMEITRECP
     FINVTFR  UF  E           K        DISK                      A
     I*****************************************************************
     IITRECX
     I              ITFLAG                          XTFLAG
     I              ITINNO                          XTINNO
     I              ITACIT                          XTACIT
     I              ITOTAM                          XTOTAM
     I              ITOTNO                          XTOTNO
     I              ITAREA                          XTAREA
     I              ITUSER                          XTUSER
     I              ITTXDT                          XTTXDT
     I              ITTXTM                          XTTXTM
0712AI              ITTRNO                          XTTRNO
0712AI              ITMAK1                          XTMAK1
     I*
     IITRECP
     I              ITFLAG                          PTFLAG
     I              ITINNO                          PTINNO
     I              ITACIT                          PTACIT
     I              ITOTAM                          PTOTAM
     I              ITOTNO                          PTOTNO
     I              ITAREA                          PTAREA
     I              ITUSER                          PTUSER
     I              ITTXDT                          PTTXDT
     I              ITTXTM                          PTTXTM
0712AI              ITTRNO                          PTTRNO
0712AI              ITMAK1                          PTMAK1
     I*
     I           UDS
     I                                       21  21 U#TXAR
     C*****************************************************************
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C                     EXSR DTLSR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*
     C*********************************
     C*  初始程式
     C*********************************
     CSR         INISR     BEGSR
     C           K#XT      KLIST
     C                     KFLD           XTINNO
     C                     KFLD           XTACIT
     CSR                   ENDSR
     C*
     C*********************************
     C*  DETAIL TIME 函式
     C*********************************
     CSR         DTLSR     BEGSR
     C           XTFLAG    COMP 'A'                      11
     C           XTFLAG    COMP 'C'                      12
     C           XTFLAG    COMP 'D'                      13
     C*
     C           K#XT      CHAINITRECP               68
     C           K#XT      CHAINITREC                69
     C*
     C                     MOVELXTFLAG    PTFLAG
     C                     MOVELXTINNO    PTINNO
     C                     Z-ADDXTACIT    PTACIT
     C                     Z-ADDXTOTAM    PTOTAM
     C                     MOVELXTOTNO    PTOTNO
     C                     MOVELXTAREA    PTAREA
     C                     MOVELXTUSER    PTUSER
     C                     Z-ADDXTTXDT    PTTXDT
     C                     Z-ADDXTTXTM    PTTXTM
0712AC                     MOVELXTTRNO    PTTRNO
0712AC                     MOVELXTMAK1    PTMAK1
     C*
     C   68                WRITEITRECP
     C  N68                UPDATITRECP
     C*
     C                     MOVEL*BLANK    ITFLAG
     C                     MOVELXTINNO    ITINNO
     C                     Z-ADDXTACIT    ITACIT
     C                     Z-ADDXTOTAM    ITOTAM
     C                     MOVELXTOTNO    ITOTNO
     C                     MOVELXTAREA    ITAREA
     C                     MOVELXTUSER    ITUSER
     C                     Z-ADDXTTXDT    ITTXDT
     C                     Z-ADDXTTXTM    ITTXTM
0712AC                     MOVELXTTRNO    ITTRNO
0712AC                     MOVELXTMAK1    ITMAK1
     C*
     C           *IN69     IFEQ '1'                        不存在
     C           *IN11     IFEQ '1'                        要存在
     C           *IN12     OREQ '1'
     C                     WRITEITREC
     C                     ENDIF
     C*
     C                     ELSE                            已存在
     C           *IN11     IFEQ '1'                        要存在
     C           *IN12     OREQ '1'
     C                     UPDATITREC
     C                     ENDIF
     C           *IN13     IFEQ '1'                        要刪除
     C                     DELETITREC
     C                     ENDIF
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*********************************
     C*  LR 函式  印核覆
     C*********************************
     CSR         LRSR      BEGSR
     CSR                   ENDSR
     C*
     C*********************************
     C*  查無資料函式
     C*********************************
     CSR         MSGSR     BEGSR
     CSR                   ENDSR
     C*
     C*****************************************************************
