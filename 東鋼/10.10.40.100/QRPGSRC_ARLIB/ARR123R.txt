     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR123R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     客戶銷售排行榜（財會用）－出表
     H*            5.DATE-WRITTEN  95/01/11
     H*            6.DATE-MODIFY
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR123F IP  E           K        DISK
     FARR123T O   E             69     PRINTER
     F*****************************************************************
     IRAR123F     01
     I           UDS
     I                                      300 3050U#YM1
     I                                      306 3110U#YM2
     I                                      312 3140U#RAK1
     I                                      315 3170U#RAK2
     I                                      318 3280U#AMT
     I                                     10011010 T#USID
     C*****************************************************************
     C*              MAIN  PROGRAM
     C*****************************************************************
     C  N99                EXSR INISR
     C                     EXSR DTLSR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     MOVE *ON       *IN99
     C                     MOVE *ON       *IN69
     C                     MOVE *ALL'='   T#LIN1
     C                     MOVE *ALL'='   T#LIN2
     C                     Z-ADD0         W#RANK  30
     C*初始表頭
     C                     MOVE U#YM1     T#YM1            銷貨年月起
     C                     MOVE U#YM2     T#YM2            銷貨年月止
     C                     MOVE U#RAK1    T#RAK1
     C                     MOVE U#RAK2    T#RAK2
     C                     Z-ADDU#AMT     T#AMTT
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     ADD  1         W#RANK
     C           W#RANK    IFGE U#RAK1
     C           W#RANK    ANDLEU#RAK2
     C                     MOVELW#RANK    T#RANK
     C                     MOVELA1CUNM    T#CUNM
     C           A1AMT5    DIV  1000      T#AMT     H
9506 C*          A1AMT5    DIV  U#AMT     W#PERT  74H
9506 C           T#AMT     DIV  U#AMT     W#PERT  74H
     C           W#PERT    MULT 100       T#PERT
     C*
     C   69                WRITEAR123T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR123T1D
     C*小計
     C                     ADD  T#AMT     T#AMTS
     C*
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C   69                WRITEAR123T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR123T1S
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C*
     C   69                WRITEAR123T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR123T1M
     C*
     C                     ENDSR
