     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP064R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     客戶管制資料異動（先期異動）
     H*            5.DATE-WRITTEN  88/10/28
     H*
     H* 說明：依ARCUCT內的資料來異動ARCUCTP
     H*
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARCUCT  UP  E           K        DISK
     F            RARCUCT                           KRENAMERARW
     FARCUCTP UF  E           K        DISK                      A
     F*****************************************************************
     IRARW        01
     I              A1FLAG                          AWFLAG
     I              A1CUNO                          AWCUNO
     I              A1CTKD                          AWCTKD
     I              A1RFOR                          AWRFOR
     I              A1MTHD                          AWMTHD
     I              A1TYP1                          AWTYP1
     I              A1RAT1                          AWRAT1
     I              A1TYP2                          AWTYP2
     I              A1RAT2                          AWRAT2
     I              A1TYP3                          AWTYP3
     I              A1RAT3                          AWRAT3
     I*------------------------------------------------------
     I           UDS
     I                                        1   1 U#FLAG
     I                                     10011010 U#USID
     I                                     10011003 U#DPID
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C                     EXSR DTLSR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*
     C*********************************
     C*  初始程式
     C*********************************
     CSR         INISR     BEGSR
     C           K#A1      KLIST
     C                     KFLD           AWCUNO
     C                     KFLD           AWCTKD
     CSR                   ENDSR
     C*
     C*********************************
     C*  DETAIL TIME 函式
     C*********************************
     CSR         DTLSR     BEGSR
     C           K#A1      CHAINRARCUCT              69
     C                     EXSR SR1000                     搬移變數
     C  N69                UPDATRARCUCT                    修改
     C   69                WRITERARCUCT                    新增
     C*
     C           AWFLAG    IFEQ 'D'
     C                     DELETRARW                       刪除記錄
     C                     ELSE
     C                     MOVEL*BLANK    AWFLAG           清除旗標
     C                     UPDATRARW
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*--------------------------------
     C*  公用子函式（搬移變數）
     C*--------------------------------
     CSR         SR1000    BEGSR
     C                     MOVELAWFLAG    A1FLAG
     C                     MOVELAWCUNO    A1CUNO
     C                     MOVE AWCTKD    A1CTKD
     C                     MOVELAWRFOR    A1RFOR
     C                     MOVELAWMTHD    A1MTHD
     C                     Z-ADDAWTYP1    A1TYP1
     C                     Z-ADDAWRAT1    A1RAT1
     C                     Z-ADDAWTYP2    A1TYP2
     C                     Z-ADDAWRAT2    A1RAT2
     C                     Z-ADDAWTYP3    A1TYP3
     C                     Z-ADDAWRAT3    A1RAT3
     CSR                   ENDSR
     C*
     C*********************************
     C*  LR 函式  印核覆
     C*********************************
     CSR         LRSR      BEGSR
     C                     MOVEL'X'       U#FLAG           有資料
     CSR                   ENDSR
     C*
     C*********************************
     C*  查無資料函式
     C*********************************
     CSR         MSGSR     BEGSR
     C           *LOVAL    SETLLRARCUCT
     C                     READ RARCUCT                  69
     C  N69                MOVEL'X'       U#FLAG           有資料
     C   69                MOVEL' '       U#FLAG           無資料
     CSR                   ENDSR
     C*
