     H            Y                                     1
     FTRNDTLW UP  E           K        DISK
     F            TXREC                             KRENAMETXRECW
     FTRNDTL  UF  E           K        DISK
     FINVMST  UF  E           K        DISK                      A
     FINVDTL  UF  E           K        DISK                      A
     FHIPROD  IF  E           K        DISK
     FGENSEQ  UF  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FSAMAST  IF  E           K        DISK
     FAFCBAL  UF  E           K        DISK
     FQPRINT  O   F     160     OV     PRINTER
     I****
     ITXRECW      01
     I              TXFLAG                          TWFLAG
     I              TXCODE                          TWCODEL4
     I              TXNO                            TWNO  L3
     I              TXITEM                          TWITEM
     I              TXACNT                          TWACNT
     I              TXDATE                          TWDATE
     I              TXACDT                          TWACDT
     I              TXCUNO                          TWCUNO
     I              TXCUNM                          TWCUNM
     I              TXORNO                          TWORNO
     I              TXIVNO                          TWIVNO
     I              TXPCNO                          TWPCNO
     I              TXVUNO                          TWVUNO
     I              TXRVID                          TWRVID
     I              TXSALE                          TWSALE
     I              TXSATP                          TWSATP
     I              TXIVTP                          TWIVTP
     I              TXPDNM                          TWPDNML1
     I              TXQTY                           TWQTY
     I              TXUPRC                          TWUPRCL1
     I              TXAMT                           TWAMT
     I              TXTAX                           TWTAX
     I              TXFL01                          TWFL01
     I              TXFL02                          TWFL02
     I              TXTXAR                          TWTXAR
     I              TXTXDT                          TWTXDT
     I              TXRESV                          TWRESVL2
     I              TXHOLD                          TWHOLD
     I****
     IAADS        DS
     I                                        1  12 GEGRP
     I                                        1   1 GE1
     I                                        2   5 GE2
     I                                        6  10 GE3
     I                                       11  20 INNOG
     I                                       11  12 INNO1
     I                                       13  200INNO2
     IBBDS        DS
     I                                        1   6 SPCUNO
     I                                        1   1 S1KIND
     I                                        2   2 S1CUN1
     I                                        3   5 S1CUN2
9008 I                                        6   6 S1CD01
LYW  I                                        7   7 S1CD02
 .   I                                        8   8 S1CD03
 .   I                                        9   9 S1CD04
 .   I                                       10  10 S1CD05
 .   I                                       11  19 TWORNO
 .   I                                       11  11 TORNO1
 .   I                                       12  160TORNO2
 .   I                                       17  19 TORNO3
CLJB I*
     I            DS
     I                                        1  15 TXRESV
     I                                        1   9 D#TXRV
     I            DS
     I                                        1   2 W#OEOF
     I                                        1   1 D#OE
     I                                        2   2 D#OF
     I            DS
     I                                        1  12 NAME1
     I                                       12  12 D#NOF
---  I*
     I           UDS
9008 I                                        1   80YMD
LYW  I                                        1   60YYMM
0528 I                                        7   7 AREA
0528 I                                     10211021 TXAR
     C*****************************************************************
     C           *ENTRY    PLIST
     C                     PARM           AREAX   1
     C*****************************************************************
8510 C           TXKEY     KLIST
8510 C                     KFLD           TWCODE
8510 C                     KFLD           TWNO
8510 C                     KFLD           TWITEM
0521 C           S1KEY     KLIST
0521 C                     KFLD           TORNO1
0521 C                     KFLD           TORNO2
0521 C                     KFLD           FORNO3
0521 C           CBKEY     KLIST
0521 C                     KFLD           SPCUNO
0521 C           GEKEY     KLIST
0521 C                     KFLD           GEKIND
0521 C                     KFLD           GEPRIN
0521 C           IVKEY     KLIST
0521 C                     KFLD           IVNO
0521 C                     KFLD           IVACNT
0521 C                     KFLD           IVITEM
     C*----------------------------------------------------------------
     C  N90                EXSR IN#01
     C  N90                SETON                     90
     C   L2 90             EXSR DL#L2
     C   L1 90             EXSR DL#L1
     C      90             EXSR DP#01
     CL1    90             EXSR TL#L1
     CL2    90             EXSR TL#L2
     CLR    90             EXSR TL#LR
     C*--------------------------------------------------*
     C*  DETAIL PROCESS L1
     C*--------------------------------------------------*
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         QTYT    70
     C                     SETOF                     28
     C                     Z-ADD0         AMTO   110
     CSR         DL#L1Z    ENDSR
     C*-----------------------------------------------*
     C*  DETAIL PROCESS L2                            *
     C*-----------------------------------------------*
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         AMTT   112
     C                     Z-ADD0         AAMT   110
     C                     Z-ADD0         BAMT   110
     C                     Z-ADD0         CAMT   110
     C                     Z-ADD0         TTAMT   90
     C                     Z-ADD0         TTAMK   90
     C                     Z-ADD0         TAMTX   90
     C                     Z-ADD0         TTAMTX  90
     C                     Z-ADD0         IT
     C                     Z-ADD0         IT1
     C                     Z-ADD0         IT2
     C                     TESTN          TORNO3     31
     C     N31             MOVE 001       FORNO3  30
9008 C      31             MOVELTORNO3    FORNO3
0521 C           S1KEY     CHAINSAMAST               50    *NR
0521 C           CBKEY     CHAINCBCUST               51    *NR
0521 C      50             MOVEL*ALL'#'   CBMUID
0521 C      50             MOVEL*ALL'#'   CBFNAM
0521 C      50             MOVEL*ALL'#'   CBADR1
0521 C      51             MOVEL*BLANK    CBCUNM
0521 C      51             MOVEL*ALL'*'   CBMUID
0521 C      51             MOVEL*ALL'*'   CBFNAM
0521 C      51             MOVEL*ALL'*'   CBADR1
     C***
     C     N51             MOVELCBIVCO    TWIVTP
     C           TWIVTP    IFEQ ' '
     C                     MOVEL'3'       TWIVTP
     C                     ENDIF
     C           TWIVTP    COMP '2'                      26
0324 C*          TWSATP    COMP 'M'                      25
     C  N26                ADD  1         CURNO3
     C  N26                Z-ADDCURNO3    INNO2
     C   26                ADD  1         CURNO2
     C   26                Z-ADDCURNO2    INNO2
CLJX C           CURNO2    IFGT W#ENO2
     C           CURNO3    ORGT W#ENO3
     C                     EXSR SR9999                     字軌用完
---  C                     ENDIF
     C  N26                MOVE PRE3      INNO1
     C   26                MOVE PRE2      INNO1
0521 C******
     C                     EXSR LN#CH
     C                     EXCPTED#L2
     C                     ADD  1         LCNT
     CSR         DL#L2Z    ENDSR
     C*---------------------------------------------*
     CSR         DP#01     BEGSR
     C                     MOVELTWRESV    RESV1   5
     C                     ADD  TWQTY     QTYT
     C                     ADD  TWAMT     AMTT
     C                     ADD  TWAMT     AMTO
     C  N28      TWACNT    COMP '3'                      28
     C                     MOVE 'C'       TWFLAG           *處理代碼
     C                     MOVELINNOG     TWIVNO           *發票號碼
     C                     MOVELAREA      TWTXAR           *異動廠區
     C                     Z-ADDUDATE     TWTXDT           *異動日期
     C                     MOVEL'Y'       TWFL02           *過發票碼
     C                     EXSR UPTRDL
     C                     UPDATTXRECW
     C                     EXSR LN#CH
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C*********
     C* UPTRDL*
     C*********
     CSR         UPTRDL    BEGSR
     C           TXKEY     CHAINTXREC                30
     C           *IN30     IFEQ '0'
     C                     EXSR MOVE1
     C                     UPDATTXREC
     C                     END
     CSR                   ENDSR
     C*********
     C* MOVE1 *
     C*********
     CSR         MOVE1     BEGSR
     C                     MOVE TWFLAG    TXFLAG
     C                     MOVE TWACNT    TXACNT
     C                     Z-ADDTWDATE    TXDATE
     C                     Z-ADDTWACDT    TXACDT
     C                     MOVE TWCUNO    TXCUNO
     C                     MOVE TWCUNM    TXCUNM
9008 C                     MOVELTWORNO    TXORNO
     C                     MOVE TWIVNO    TXIVNO
     C                     MOVE TWPCNO    TXPCNO
     C                     MOVE TWVUNO    TXVUNO
     C                     MOVE TWRVID    TXRVID
     C                     MOVE TWSALE    TXSALE
     C                     MOVE TWSATP    TXSATP
     C                     MOVE TWIVTP    TXIVTP
     C                     MOVE TWPDNM    TXPDNM
     C                     Z-ADDTWQTY     TXQTY
     C                     Z-ADDTWUPRC    TXUPRC
     C                     Z-ADDTWAMT     TXAMT
     C                     Z-ADDTWTAX     TXTAX
     C                     MOVE TWFL01    TXFL01
     C                     MOVE TWFL02    TXFL02
     C                     MOVE TWTXAR    TXTXAR
     C                     Z-ADDTWTXDT    TXTXDT
     C*                    MOVE TWRESV    TXRESV           勿清第十位
CLJ  C                     MOVELTWRESV    D#TXRV           以後
     CSR                   ENDSR
     C*********
     C* L1RTN *
     C*********
     CSR         TL#L1     BEGSR
     C           TWPDNM    CHAINHIPROD               20
CLJ  C* N20                MOVE F4CHIN    NAME1  12
     C           *IN20     IFEQ '0'
     C                     MOVE ''      W#OEOF
     C                     MOVELF4CHIN    NAME1
     C                     MOVE D#OF      D#NOF
---  C                     ENDIF
     C*
     C                     Z-ADDAMTO      TAMTO   90H
     C   26      TAMTO     MULT 1.05      TAMTO     H
     C                     Z-ADDTAMTO     PAMTO  112
     C           QTYT      IFNE 0
     C           TAMTO     DIV  QTYT      UPO     73H
CLJ  C*          -------------------------------
     C*   有鑑於以金額除以數量所得之單價可能於訂單單價不同
     C*   但金額及數量皆不可改，若採用訂單之單價仍能苻合
     C*   算式且與計算所得之單價不同時，改採訂單之單價
     C*          -------------------------------
     C           TWUPRC    IFNE UPO
     C           QTYT      MULT TWUPRC    W#TMPA  90H
     C           W#TMPA    IFEQ TAMTO
     C                     Z-ADDTWUPRC    UPO
     C                     MOVEL' '       MARK
     C                     ELSE                            金額不符
     C                     MOVEL'*'       MARK    1
     C                     ENDIF
     C                     ENDIF
---  C*          -------------------------------
     C                     END
     C*
CLJ  C           PAMTO     IFNE 0                          非零才寫
     C                     ADD  TAMTO     TTAMT   90
     C                     ADD  1         IT      20
     C                     MOVELINNOG     IVNO
     C                     MOVE '1'       IVACNT
     C                     Z-ADDIT        IVITEM
     C           IVKEY     CHAININVDTL               37
     C   37                EXSR WR#DL1
     C   37                EXCPTET#L1
     C   37                MOVEL' '       MARK
     C   37                ADD  1         LCNT
     C  N37                EXCPTBRERR
     C  N37                ADD  1         LCNT
     C                     ENDIF
     CSR                   ENDSR
     C*********
     C* L2RTN *
     C*********
     CSR         TL#L2     BEGSR
     C                     Z-ADD0         UPO
     C                     Z-ADD0         QTYT
     C                     Z-ADD0         WAMTP
     C           AMTT      COMP TTAMT                    81
     C                     TESTN          TORNO3     31
     C     N31             MOVE 001       FORNO3  30
9008 C      31             MOVELTORNO3    FORNO3
     C           S1KEY     CHAINSAMAST               36
     C           *IN36     IFEQ '0'
     C           TWCODE    IFNE 'AR05'
     C           S1PRAT    MULT 10        PRAT    32H
     C           TTAMT     MULT PRAT      WAMTP   90H
0528 C                     MOVE *BLANK    CHKMAK  8
     C                     Z-ADD0         PREBAL  90
     C                     SETOF                     21
     C                     MOVELTWORNO    AFORNO
0528 C           AFORNO    CHAINAFCBAL               53    *NR
0528 C           *IN53     IFEQ '0'
0528 C           AFNBAL    IFLE *ZEROS
0528 C                     Z-ADD*ZEROS    WAMTP
0528 C                     MOVE '*檢查*'CHKMAK
     C                     SETON                     21
     C                     Z-ADDAFNBAL    PREBAL
0528 C                     ELSE
0528 C           WAMTP     IFGT AFNBAL
0528 C                     Z-ADDAFNBAL    WAMTP
0528 C                     MOVE '*檢查*'CHKMAK
     C                     SETON                     21
     C                     Z-ADDAFNBAL    PREBAL
0528 C                     END
0528 C                     END
0528 C                     ELSE
0528 C                     Z-ADD*ZEROS    WAMTP
0528 C                     MOVE '*檢查*'CHKMAK
     C                     SETON                     21
0528 C                     END
0528 C           S1PRAT    COMP *ZEROS                   80
0528 C           WAMTP     IFNE *ZEROS
0528 C                     Z-SUBWAMTP     WAMTP
0528 C                     END
0528 C*                    Z-SUBWAMTP     WAMTP          80
     C*     26   WAMTP     MULT 1.05      WAMTP     H
     C     N80   IT        ADD  1         IT1     20
     C     N80             Z-ADDWAMTP     PAMTO
     C     N80             MOVE '4'       IVACNT
     C     N80             Z-ADDIT1       IVITEM
     C     N80   IVKEY     CHAININVDTL               37
     C     N80 37          EXSR WR#DL2
     C                     END
     C                     END
     C           TTAMT     ADD  WAMTP     TTAMK
     C           *IN26     IFEQ '0'
     C           TTAMK     MULT .05       TAMTX   90H
     C           TTAMK     ADD  TAMTX     TTAMTX
     C                     Z-ADDTAMTX     PAMTO
     C           IT1       ADD  1         IT2     20
     C                     MOVE '5'       IVACNT
     C                     Z-ADDIT2       IVITEM
     C           IVKEY     CHAININVDTL               37
     C   37                EXSR WR#DL3
     C                     ELSE
     C                     Z-ADDTTAMK     TTAMTX
     C                     END
     C                     MOVELINNOG     INNO
     C           INNO      CHAININVMST               37
     C   37                EXSR WR#MS
     C           TWCODE    IFEQ 'AR05'
     C                     SETON                     71
     C                     ELSE
     C                     SETOF                     71
     C                     ENDIF
     C                     EXSR LN#CH
     C                     EXCPTET#L2
     C                     ADD  3         LCNT
     CSR         TL#L2Z    ENDSR
     C*---------------------------------------------------*
     CSR         TL#LR     BEGSR
     C                     MOVE '01'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVELAREAX     GE1
     C                     MOVELYYMM      GE2
     C                     MOVEL*BLANK    GE3
     C                     MOVELGEGRP     GEPRIN
     C           GEKEY     CHAINGENSEQ               H1
     C                     Z-ADDCURNO2    GECUNO
     C                     UPDATGEREC
     C*****
     C                     MOVE '02'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVELAREAX     GE1
     C                     MOVELYYMM      GE2
     C                     MOVEL*BLANK    GE3
     C                     MOVELGEGRP     GEPRIN
     C           GEKEY     CHAINGENSEQ               H1
     C                     Z-ADDCURNO3    GECUNO
     C                     UPDATGEREC
     CSR                   ENDSR
     C*----------------------------------------------*
     CSR         IN#01     BEGSR
     C                     MOVE '01'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVELAREAX     GE1
     C                     MOVELYYMM      GE2
     C                     MOVEL*BLANK    GE3
     C                     MOVELGEGRP     GEPRIN
     C           GEKEY     CHAINGENSEQ               H1
     C                     MOVELGEPRE     PRE2    2
     C                     Z-ADDGECUNO    CURNO2  80
CLJX C                     Z-ADDGEENNO    W#ENO2  80
     C*****
     C                     MOVE '02'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVELAREAX     GE1
     C                     MOVELYYMM      GE2
     C                     MOVEL*BLANK    GE3
     C                     MOVELGEGRP     GEPRIN
     C           GEKEY     CHAINGENSEQ               H1
     C                     MOVELGEPRE     PRE3    2
     C                     Z-ADDGECUNO    CURNO3  80
CLJX C                     Z-ADDGEENNO    W#ENO3  80
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD55        TCNT    20
     C***
     C                     SELEC
     C           AREA      WHEQ 'P'
     C                     MOVEL'台北'  AREAC   6
     C           AREA      WHEQ 'K'
     C                     MOVEL'高雄'  AREAC
     C           AREA      WHEQ 'M'
     C                     MOVEL'台中'  AREAC
     C           AREA      WHEQ 'T'
     C                     MOVEL'桃園'  AREAC
     C           AREA      WHEQ 'L'
     C                     MOVEL'林口'  AREAC
     C           AREA      WHEQ 'H'
     C                     MOVEL'苗栗'  AREAC
     C                     OTHER
     C                     MOVEL'　　'  AREAC
     C                     ENDSL
     CSR         IN#01Z    ENDSR
     C*---------------------------------------------------*
     CSR         LN#CH     BEGSR
     C           LCNT      IFGT TCNT
     C                     ADD  1         PAG
     C                     EXCPTEH#01
     C                     Z-ADD6         LCNT
     C                     ENDIF
     CSR                   ENDSR
     C*---------------------------------------------------*
     CSR         WR#DL1    BEGSR
     C                     CLEARIVREC
     C                     MOVE 'A'       IVFLAG
     C                     MOVELINNOG     IVNO
     C                     MOVE '1'       IVACNT
     C                     Z-ADDIT        IVITEM
     C                     MOVELYMD       IVACDT
     C                     MOVELTWORNO    IVORNO
     C                     MOVELTWPDNM    IVPDCD
     C                     Z-ADDQTYT      IVQTY
     C                     Z-ADDUPO       IVUPRC
     C                     Z-ADDPAMTO     IVAMT
     C                     MOVELTWNO      IVAPNO
     C                     MOVE 'Y'       IVFL02
     C           TWCODE    IFEQ 'SA04'
     C                     MOVE 'A'       IVFL03
     C                     ELSE
     C                     MOVE 'B'       IVFL03
     C                     ENDIF
     C                     MOVELAREA      IVTXAR
     C                     Z-ADDUDATE     IVTXDT
     C                     WRITEIVREC
     C                     ADD  IVAMT     AAMT   110
     CSR                   ENDSR
     C*---------------------------------------------------*
     CSR         WR#DL2    BEGSR
     C                     CLEARIVREC
     C                     MOVE 'A'       IVFLAG
     C                     MOVELINNOG     IVNO
     C                     MOVE '4'       IVACNT
     C                     Z-ADDIT1       IVITEM
     C                     MOVELYMD       IVACDT
     C                     MOVELTWORNO    IVORNO
     C                     Z-ADDPAMTO     IVAMT
     C                     MOVELTWRESV    IVAPNO
     C                     MOVE 'Y'       IVFL02
     C                     MOVE 'E'       IVFL03
     C                     MOVELAREA      IVTXAR
     C                     Z-ADDUDATE     IVTXDT
     C                     WRITEIVREC
     C                     ADD  IVAMT     BAMT   110
     C  N53                SUB  IVAMT     AFEAMT
     C  N53                ADD  IVAMT     AFNBAL
     C  N53                MOVE UDATE     AFCHDT
     C  N53                UPDATAFREC
     CSR                   ENDSR
     C*---------------------------------------------------*
     CSR         WR#DL3    BEGSR
     C                     CLEARIVREC
     C                     MOVE 'A'       IVFLAG
     C                     MOVELINNOG     IVNO
     C                     MOVE '5'       IVACNT
     C                     Z-ADDIT2       IVITEM
     C                     MOVELYMD       IVACDT
     C                     MOVELTWORNO    IVORNO
     C                     Z-ADDPAMTO     IVAMT
     C                     MOVE 'Y'       IVFL02
     C           TWCODE    IFEQ 'SA04'
     C                     MOVE 'A'       IVFL03
     C                     ELSE
     C                     MOVE 'B'       IVFL03
     C                     ENDIF
     C                     MOVELAREA      IVTXAR
     C                     Z-ADDUDATE     IVTXDT
     C                     WRITEIVREC
     C                     ADD  IVAMT     CAMT   110
     CSR                   ENDSR
     C*---------------------------------------------------*
     CSR         WR#MS     BEGSR
     C                     CLEARINREC
     C                     MOVE 'A'       INFLAG
     C                     MOVELINNOG     INNO
     C                     MOVELSPCUNO    INCUNO
     C                     MOVELCBCUNM    INCUNM
     C                     MOVELTWRESV    INORNO
     C                     MOVELYMD       ININDT
     C           TWIVTP    IFEQ ' '
     C                     MOVEL'3'       TWIVTP
     C                     ENDIF
     C                     MOVELTWIVTP    INKIND
     C                     MOVELTWSALE    INSALE
     C                     MOVELTWSATP    INSATP
     C                     MOVELTWRVID    INRVID
     C           TWCODE    IFEQ 'SA04'
     C                     MOVEL'1'       INTYPE
     C                     ELSE
     C                     MOVE '3'       INTYPE
     C                     ENDIF
     C****
     C                     MOVELAREA      INAREA
     C                     MOVEL'1'       INTXTP
     C                     Z-ADDAAMT      INAAMT
     C                     Z-ADDCAMT      INATAX
     C                     Z-ADDBAMT      INBAMT
     C                     Z-ADDAAMT      INNBAL
     C                     ADD  CAMT      INNBAL
     C                     ADD  BAMT      INNBAL
     C                     MOVELAREA      INTXAR
     C                     Z-ADDUDATE     INTXDT
     C                     WRITEINREC
     CSR                   ENDSR
     C*
     C*---------------------------------------------------------
CLJX C* 字軌用完之異常處理
     C*---------------------------------------------------------
     CSR         SR9999    BEGSR
     C           CURNO2    IFGT W#ENO2
     C                     Z-ADDW#ENO2    CURNO2
     C                     ENDIF
     C           CURNO3    IFGT W#ENO3
     C                     Z-ADDW#ENO3    CURNO3
     C                     ENDIF
     C*
     C                     EXSR TL#LR                      字軌存檔
     C                     CALL 'ARP056P'
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C**************************************************************
     C*====================================================
     OQPRINT  E 2 01           EH#01
     O                                   11 '報表編號:'
     O                                   17 'AR003'
     O                                   77 '東和鋼鐵股份有限公司'           X
     O                                  119 '印表日期'
     O                                  120 ':'
     O                         UDATE Y  129
     O        E 1              EH#01
     O                                   10 '廠　　別'
     O                                   11 ':'
     O                         AREAC   +  1
     O                                   74 '過入發票核對表'
     O                                  119 '頁　　數'
     O                                  120 ':'
     O                         PAG   Z  125
     O        E 2              EH#01
     O                                    8 '訂　單'
     O                                   17 '日　期'
     O                                   26 '磅　單'
     O                                   36 '品名'
     O                                   42 '重量'
     O                                   44 'kg'
     O                                   52 '單價'
     O                                   67 '金　　額'
     O                                   82 '品　　名'
     O                                   92 '重　量'
     O                                  102 '單價'
     O                                  117 '金　　額'
     O                                  134 '發票號碼'
     O                                  142 '業務'
     O        E 1              EH#01
     O                                    8 '-------'
     O                                   17 '--------'
     O                                   28 '----------'
     O                                   34 '---'
     O                                   44 '--------'
     O                                   52 '------'
     O                                   67 '-------------'
     O                                   82 '------------'
     O                                   92 '---------'
     O                                  102 '------'
     O                                  117 '-----------'
     O*                                 126 '--------'
     O                                  134 '--------'
     O                                  142 '------'
     O        E 1      L2      ED#L2
     O                         CBCUNM    10
     O                         CBMUID    20
     O                         CBFNAM    56
     O                         CBADR1   118
     O                 26               132 '二聯式'
     O                N26               132 '三聯式'
     O        E 1              ED#01
     O                         RESV1      6
     O                         TWDATEY   17
     O                         TWITEM    26
     O                         TWNO      26
     O                         TWACNT    30
     O                         TWPDNM    34
     O                         TWQTY J   44
     O                         TWUPRCJ   52
     O                         TWAMT J   67
     O        E 1              ET#L1
     O                         NAME1     82
     O                         QTYT  J   92
     O                         UPO   J  102
     O                         AMTO  J  116
     O                         MARK     120
     O        E 1     N36N80N71ET#L2
     O                                   82 '扣預收'
     O                         TTAMT J   94
     O                                   95 'X'
     O                         S1PRATZ   99
     O                                  100 '%'
     O                         WAMTP J  116
0528 O                         CHKMAK   125
     O        E 00    N36N80 21ET#L2
     O                                  140 '目前餘額:'
     O                         PREBALN  155
     O        E 2        N26   ET#L2
     O                                   82 '稅　額'
     O                         TTAMK J   94
     O                                  100 'X   5%'
     O                         TAMTX J  116
     O        E 1              ET#L2
     O                                   82 '總　計'
     O                         INNOG    134
     O                         TTAMTXJ  116
     O                                  104 '=====================>'
     O                         TWSALE   140
     O        E  1     L1N37   BRERR
     O                                   67 '號碼重覆'
