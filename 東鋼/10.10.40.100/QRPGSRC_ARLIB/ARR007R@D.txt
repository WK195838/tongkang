     H****************************************************************
     H*
     H*            UPDATE  DATE  99/05/05 2010AR473 S00WCJ (9905A)
     H*                         100/01/03 2011ARXXX S00WCJ (0001A)
     H*                         100/05/13 2011CC49  S00WCJ (0005A)
     H*                          客戶DP30331全稱太長放不進檔案，以
     H*                          特殊模式處理
     H*                         100/10/25 2011AR629 S00WCJ (0010A)
     H*                          預收貨款列印名稱設定
     H*                         100/12/16 2011AR637 S00WCJ (0012A0
     H*                          客戶DP332全稱太長放不進檔案，以
     H*                          特殊模式處理
     H*                         101/02/01 S00WCJ (0102A)
     H*                          增加產品代號Z95
     H*                         104/04/20 2015AR926 S00WCJ (0404A)
     H*                          增加產品代號Z96.Z97.Z98
     H*                         104/07/27 2015AR945 S00WCJ (0407A)
     H*                          增加產品代碼Z92
     H*                         104/09/30 2015AR953 S00WCJ (0409A)
     H*                          產品代碼3碼擴5碼
     H*                         105/10/26 S00WCJ (0510A)
     H*                          客戶DP502全稱太長放不進檔案，以
     H*                          特殊模式處理
     H*                         107/01/25 2018AR1125 S00WCJ (0701A)
     H*                          發票YT44357156，為預收發票，因特殊
     H*                          需求品名列印為未交單價差
     H*                         108/02/01 2019ER00001 S00WCJ (0801A)
     H*                          新增產品代碼Z90
     H*                         109/02/26 2020AR00010 S00WCJ (0902A)
     H*                          新增產品代碼Z9A
     H*
     H****************************************************************
     H*電子計算機統一發票
     H        1   Y                                     1                 BR003
     FINVCUT  IP  E           K        DISK
     FINVMST  UF  E           K        DISK
     FINVDTL  IF  E           K        DISK
     FINVDTY  IF  E           K        DISK
     F            IVREC                             KRENAMEIVRECY
     FHIPROD  IF  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FQPRINT  O   F      80           LPRINTER
     LQPRINT   33FL 33OL
     F*---------------------------------------------------------------
0701AE                    HD      1   6 20
     E                    TABA   10  10  1 0 TABB    4
     E                    TABC    2   2  1   TABD   12
9010 E                    MSG     1   9 40
0510AE                    CU1     1   3 48
     E*---------------------------------------------------------------
     IRINVCUT
     I                                              CUNO  L1
     IIVRECY
     I              IVNO                            F#IVNO
     I              IVACNT                          F#ACNT
     I              IVITEM                          F#ITEM
     I              IVPDCD                          F#PDCD
     I              IVQTY                           F#QTY
     I              IVAMT                           F#AMT
     I              IVFL03                          F#FL03
     I            DS
9008 I                                        1   80ININDT
0001AI*                                       1   20Y1
0001AI*                                       3   40YY
0001AI                                        2   40YY
 .   I                                        5   60MM
 .   I                                        7   80DD
     I            DS
9008 I                                        1   6 INORNO
     I                                        1   1 ORNOA
     I            DS
     I                                        1  46 ADDR1
     I                                       46  46 OF
     I            DS
     I                                        1  18 ADDR2
     I                                        1   1 BOE
     I                                       18  18 BOF
     I            DS
     I                                        1  110INAAMT
     I                                        1   10N1
     I                                        2   20N2
     I                                        3   30N3
     I                                        4   40N4
     I                                        5   50N5
     I                                        6   60N6
     I                                        7   70N7
     I                                        8   80N8
     I                                        9   90N9
     I                                       10  100N10
     I                                       11  110N11
     I            DS
     I                                        1  132TT
     I                                        1   10T1
     I                                        2   20T2
     I                                        3   30T3
     I                                        4   40T4
     I                                        5   50T5
     I                                        6   60T6
     I                                        7   70T7
     I                                        8   80T8
     I                                        9   90T9
     I                                       10  100T10
     I                                       11  110T11
     I                                       12  120T12
     I                                       13  130T13
CLJ  I            DS
     I                                        1   2 W#OEOF
     I                                        1   1 D#OE
     I                                        2   2 D#OF
     I            DS
     I                                        1  20 PRNM
---  I                                       20  20 D#NOF
YSH  I            DS
     I                                        1  40 PRNMX
---  I                                       40  40 D#NOX
0010AI            DS
0010AI                                        1   6 INRESV
0010AI                                        6   6 D#RESV
0701AI            DS
0701AI                                        1  10 IVNO
0701AI                                        1   2 D#IVN1
0701AI                                        3  10 D#IVN2
     I           UDS
     I                                     10211021 U#LOC
     C**************************************************************
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C*
     C           CUNO      CHAININREC                44
     C           *IN44     IFEQ '0'
     C           INDECD    ANDNE'D'
     C           INPRTC    ANDEQ' '
0010AC                     MOVEL*OFF      *IN38             列印單價
     C                     EXSR DL#L1
     C                     EXSR DP#01
     C                     EXSR TL#L1
     C                     ENDIF
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C           IVKEY     KLIST
     C                     KFLD           IVNO
     C                     KFLD           IVACNT
     C                     KFLD           IVITEM
     C           CBKEY     KLIST
     C                     KFLD           INCUNO
     C           W#INVD    KLIST
     C                     KFLD           F#IVNO
     C                     KFLD           F#ACNT
     C                     KFLD           F#ITEM
      *
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD32        OVRFLW  30
LYW  C*                    MOVE '  '      Y1
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP OVRFLW               75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD16        LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADDOVRFLW    LCNT
      *
     C                     Z-ADD0         ITEM    20
     C                     Z-ADD0         IDN     40
     C                     Z-ADD0         TOT1   130
     C                     Z-ADD0         TOT2   130
     C                     Z-ADD0         W#AMT0  90
     C                     Z-ADD0         AMT1
     C                     Z-ADD0         AMT2
     C           ORNOA     IFEQ 'X'                        訂單第一碼
     C                     SETON                     16
     C                     ELSE
     C                     SETOF                     16
     C                     ENDIF
     C**
     C           CBKEY     CHAINCBCUST               31
     C   31                MOVE *BLANK    CBCUNM
     C   31                MOVE *BLANK    CBMUID
     C   31                MOVE *BLANK    CBADR1
     C   31                MOVE *BLANK    CBFNAM
     C*
0103AC                     MOVEL*BLANKS   W#FNAM
0005AC  N31                MOVELCBFNAM    W#FNAM 42
0005AC           INCUNO    IFEQ 'DP3031'
0005AC                     MOVELCU1,1     W#FNAM
0005AC                     ENDIF
0012AC           INCUNO    IFEQ 'DP332'
0012AC                     MOVELCU1,2     W#FNAM
0012AC                     ENDIF
0510AC           INCUNO    IFEQ 'DP502'
0510AC                     MOVELCU1,3     W#FNAM
0510AC                     ENDIF
     C*
     C           INKIND    LOKUPTABC      TABD           82
     C                     MOVE TABD      OP     12        發票聯式
     C*
     C                     MOVE CBADR1    ADDR   62
     C                     MOVELADDR      ADDR1
     C                     MOVE ADDR      ADDR2
     C                     MOVE ''      OF
     C                     MOVEL''      BOE
     C                     MOVE ''      BOF
     C*
CLJ  C*          INTXTP    IFNE '1'
     C*                    SETON                     15
     C*                    ELSE
     C*                    SETOF                     15
     C*                    END
     C*
     C                     SELEC
     C           INTXTP    WHEQ '1'                        應稅
     C                     SETOF                     15
     C           INTXTP    WHEQ '2'                        零稅率
     C                     SETON                     1577
     C           INTXTP    WHEQ '3'                        免稅
     C                     SETON                     15
     C                     SETOF                     77
     C                     ENDSL
---  C*
     C           N1        ADD  N2        B1      30
     C           N3        ADD  N4        B2      30
     C           N5        ADD  N6        B3      30
     C           N7        ADD  N8        B4      30
     C           N9        ADD  N10       B5      30
     C           B1        ADD  B2        BB1     30
     C           B3        ADD  B4        BB2     30
     C           B5        ADD  N11       BB3     20
     C           BB1       ADD  BB2       BF1     30
     C           BB3       ADD  BF1       CKN1    30
     C           CKN1      MULT MM        CKN2    40
     C           CKN2      ADD  DD        IDN              檢查號碼
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     MOVELINNO      IVNO
     C                     MOVE *BLANK    IVACNT
     C                     MOVE *BLANK    IVITEM
     C                     SETOF                     97
     C           IVKEY     SETLLINVDTL               97
     C           *IN97     DOWEQ'0'
     C           INNO      READEINVDTL                   97
     C   97                LEAVE
      *
     C           IVACNT    IFEQ '2'                        退貨
     C           IVACNT    OREQ '3'
     C           IVACNT    OREQ '6'                        繳款轉入
     C           IVACNT    OREQ '7'                        轉出
     C                     ITER
     C                     END
      *
     C           IVACNT    IFEQ '5'
     C           IVFL03    IFEQ 'A'
     C           IVFL03    OREQ 'B'
     C           IVFL03    OREQ 'F'
     C           IVFL03    OREQ 'Z'
     C                     ADD  IVAMT     AMT2    90
     C                     ENDIF
     C                     ITER
     C                     END
      *
     C           IVACNT    IFEQ '1'                        銷貨
     C           IVFL03    COMP 'A'                  3131  出貨
     C   31      IVFL03    COMP 'B'                  3131  調整
     C   31                ITER
     C                     END
      *
     C           IVACNT    IFEQ '4'                        扣預收
     C           IVFL03    COMP 'E'                  3131  扣預收
     C   31      IVFL03    COMP 'F'                  3131  預收
     C   31                ITER
     C                     END
      *
0409AC                     MOVELIVPDCD    F4NAME
0409AC           F4NAME    CHAINHIPROD               31
CLJ  C* N31                MOVE ACPRNM    PRNM   12
     C           *IN31     IFEQ '0'
     C                     MOVE ''      W#OEOF
YSH  C                     MOVELIVPDCD    PDCD    1        品名
YSH  C           PDCD      IFNE 'Z'
0407AC           IVPDCD    OREQ 'Z92'
9905AC           IVPDCD    OREQ 'Z93'
9905AC           IVPDCD    OREQ 'Z94'
0102AC           IVPDCD    OREQ 'Z95'
0404AC           IVPDCD    OREQ 'Z96'
0404AC           IVPDCD    OREQ 'Z97'
0404AC           IVPDCD    OREQ 'Z98'
0802AC           IVPDCD    OREQ 'Z90'
0902AC           IVPDCD    OREQ 'Z9A'
YSH  C                     MOVE *OFF      *IN30
     C                     MOVELF4CHIN    PRNM
     C                     MOVE D#OF      D#NOF
YSH  C                     ELSE
YSH  C                     MOVE *ON       *IN30
YSH  C*                    MOVELF4CHIN    PRNMX
YSH  C                     SELEC
YSH  C           IVPDCD    WHEQ 'Z01'
YSH  C                     MOVELMSG,1     PRNMX
YSH  C           IVPDCD    WHEQ 'Z02'
YSH  C                     MOVELMSG,2     PRNMX
YSH  C           IVPDCD    WHEQ 'Z03'
YSH  C                     MOVELMSG,3     PRNMX
YSH  C           IVPDCD    WHEQ 'Z04'
YSH  C                     MOVELMSG,4     PRNMX
YSH  C           IVPDCD    WHEQ 'Z05'
YSH  C                     MOVELMSG,5     PRNMX
YSH  C           IVPDCD    WHEQ 'Z06'
YSH  C                     MOVELMSG,6     PRNMX
YSH  C           IVPDCD    WHEQ 'Z07'
YSH  C                     MOVELMSG,7     PRNMX
YSH  C           IVPDCD    WHEQ 'Z08'
YSH  C                     MOVELMSG,8     PRNMX
YSH  C           IVPDCD    WHEQ 'Z99'
YSH  C                     MOVELMSG,9     PRNMX
YSH  C                     ENDSL
YSH  C                     MOVE D#OF      D#NOX
YSH  C                     ENDIF
---  C                     ENDIF
     C*
9508 C*排除規格檔中產品類別為空白，
 .   C*否則會相關折扣字樣無法印出。
 .   C*
 .   C           *IN31     IFEQ *OFF
 .   C           PDCD      ANDNE'Z'
 .   C           F4NAME    ANDEQ*BLANK
 .   C                     MOVE *ON       *IN31
9508 C                     ENDIF
     C*
     C*
     C   31      IVACNT    COMP '4'                      32
     C   31 32   IVAMT     COMP 0                    33  33
     C   31 32 33          MOVELHD,1      PRNM
     C   31 32N33          MOVELHD,2      PRNM
0010AC   31 32   D#RESV    IFEQ 'Y'
0010AC                     MOVELHD,5      PRNM
0010AC                     MOVEL*ON       *IN38            不印單價
0010AC                     ENDIF
0701AC   31 32   D#IVN1    IFEQ 'YT'
0701AC           D#IVN2    ANDEQ'44357156'
0701AC                     MOVELHD,6      PRNM
0701AC                     ENDIF
      *
     C                     Z-ADDIVAMT     AMT0    90
     C                     ADD  IVAMT     AMT1    90
      *
     C**         IVACNT    COMP '8'                      31
     C**N31                ADD  AMT1      TOT1
     C**         TOT1      ADD  AMT2      TOT2
      *
     C   16      IVACNT    IFEQ '1'
     C                     MOVELIVRESV    TPRCA   2
     C                     SETOF                     21
     C                     TESTN          TPRCA      21
     C   21                MOVE TPRCA     TPRCN   20
     C   21      IVUPRC    MULT 100000    TPRC    90
     C   21                ADD  TPRCN     TPRC
     C   21      TPRC      DIV  100000    UPRC    95
     C  N21                Z-ADDIVUPRC    UPRC
     C                     ENDIF
      *
     C                     ADD  1         ITEM
      *
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTD#P
     C*抓折扣
     C           IVACNT    IFEQ '1'
     C                     MOVELIVNO      F#IVNO
     C                     MOVEL'3'       F#ACNT
     C                     Z-ADDIVITEM    F#ITEM
     C           W#INVD    CHAINIVRECY               50
     C           *IN50     IFEQ *OFF
     C           F#FL03    ANDEQ'K'
     C           F#QTY     IFNE 0
     C                     MOVELHD,3      PRNM
     C                     MOVELF#PDCD    IVPDCD
     C                     Z-ADDF#QTY     IVQTY
     C                     Z-ADDF#AMT     AMT0
     C                     Z-ADDF#AMT     AMT0
     C                     ADD  F#AMT     AMT1
     C*
     C                     ADD  1         ITEM
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTD#P
     C*
     C                     ELSE
     C                     ADD  F#AMT     W#AMT0
     C                     ADD  F#AMT     AMT1
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C                     MOVEL'C'       INFLAG
     C                     MOVEL'X'       INPRTC
     C                     UPDATINREC
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C*
     C           W#AMT0    IFNE 0
     C                     MOVELHD,4      PRNM
     C                     Z-ADDW#AMT0    AMT0
     C                     ADD  1         ITEM
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTD#P
     C                     ENDIF
     C*
     C           AMT1      ADD  AMT2      TT
     C                     Z-ADDTT        TT3    100
     C                     EXSR CHG1
     C                     EXCPTT#L1
     CSR         TL#L1Z    ENDSR
     C**********
     C* CHG1   *
     C**********
     C           CHG1      BEGSR
     C           T1        LOKUPTABA      TABB           81
     C                     MOVE TABB      TN1     4
     C           T2        LOKUPTABA      TABB           81
     C                     MOVE TABB      TN2     4
     C           T3        LOKUPTABA      TABB           81
     C                     MOVE TABB      TN3     4
     C           T4        LOKUPTABA      TABB           81
     C                     MOVE TABB      TN4     4
     C           T5        LOKUPTABA      TABB           81
     C                     MOVE TABB      TN5     4
     C           T6        LOKUPTABA      TABB           81
     C                     MOVE TABB      TN6     4
     C           T7        LOKUPTABA      TABB           81
     C                     MOVE TABB      TN7     4
     C           T8        LOKUPTABA      TABB           81
     C                     MOVE TABB      TN8     4
     C           T9        LOKUPTABA      TABB           81
     C                     MOVE TABB      TN9     4
     C           T10       LOKUPTABA      TABB           81
     C                     MOVE TABB      TN10    4
     C           T11       LOKUPTABA      TABB           81
     C                     MOVE TABB      TN11    4
     C           T12       LOKUPTABA      TABB           81
     C                     MOVE TABB      TN12    4
     C           T13       LOKUPTABA      TABB           81
     C                     MOVE TABB      TN13    4
     C                     ENDSR
     C*---------------------------------------------------------------
     OQPRINT  E    3           EH#01
 CLJ O                                   13 '東和'
     O        E  1 4           EH#01
 CLJ O                                   14 'TUNG HO'
     O                         OP        55
     O        E  1 6           EH#01
     O                         YY        26
     O                         MM        30
     O                         DD        34
     O        E 11             EH#01
     O                         INNO      22
     O                         IDN       47
     O        E 11             EH#01
     O                         CBMUID    20
     O                         INORNO    48
     O        E 11             EH#01
0005AO*               N16      CBFNAM    46
0005AO                N16      W#FNAM    52
0005AO*                16      CBFNAM    48
0005AO                 16      W#FNAM    48
     O        E 11             EH#01
     O                N16      ADDR1     56
     O                 16      CBADR1    74
     O        E  2             EH#01
     O                N16      ADDR2     28
     O        E 1              D#P
     O                         ITEM  M    3
0409AO                         IVPDCD B  10
CLJ  O*                        PRNM   B  22
YSH  O                N30      PRNM   B  30
YSH  O                N30      IVQTY MB  39
0010AO                N16N30N38IVUPRC B  48 '    0.   -'
0010AO                 16N30N38UPRC   B  48 '   0.     '
YSH  O                 30      PRNMX  B  48
     O                         AMT0  JB  59
     O                         IVAPNO  +  3
     O        E   25           T#L1
     O                         AMT1  1B  58
     O        E 1              T#L1
     O                N15                21 'V'
CLJ  O                 15 77             34 'V'
---  O                 15N77             46 'V'
     O                         AMT2  2   58
     O        E 2              T#L1
     O                         TN4       14
     O                         TN5       19
     O                         TN6       23
     O                         TN7       27
     O                         TN8       32
     O                         TN9       36
     O                         TN10      41
     O                         TN11      45
     O                         TT3   1B  58
** HD
預收貨款
扣預收款
折扣
扣物調款
鋼筋
未交單價差
** TABA  TABB
0零1壹2貳3參4肆5伍6陸7柒8捌9玖
** TABC  TABD
2（二聯式）3（三聯式）
** MSG
東和鋼鐵體系企業間電子化計畫第一期款
東和鋼鐵體系企業間電子化計畫第二期款
東和鋼鐵體系企業間電子化計畫第三期款
東和鋼鐵體系企業間電子化計畫第四期款
東和鋼鐵體系企業間電子化計畫第一期款
東和鋼鐵體系企業間電子化計畫第二期款
東和鋼鐵體系企業間電子化計畫第三期款
東和鋼鐵體系企業間電子化計畫第四期款
傾銷費用補助款
** CU1
春原營造股份有限公司捷運頂埔段工程辦事處
日商華大成營造工程股份有限公司台灣分公司
日商清水營造工程股份有限公司台灣分公司
