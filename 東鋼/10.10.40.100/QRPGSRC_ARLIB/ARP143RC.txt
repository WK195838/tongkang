     A*****************************************************************
     A*
     A*            *****   PROGRAM INTRODUCTION   *****
     A*
     A*            1.PROGRAM-ID    ARP143RC
     A*            2.PROGRAM-TYPE  RPG
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     台北INENBUT-->桃園、苗栗、高雄
     A*            5.DATE-WRITTEN  107/12/11 2018AR00031
     A*            6.UPDATE  DATE  108/05/09 S00WCJ (0805A)
     A*                            增加寫入LOG檔
     A*
     A*****************************************************************
     H            Y                                     1                 BR100
     FINENBUT UP  E           K        DISK
     F            RINENBU                           KRENAMENBUECD
     FINENBU  UF  E           K        DISK                      A
0805AFINENBUG O   E           K        DISK
0805AF            RINENBU                           KRENAMENBUG
     INBUECD      01
     I              NUFLAG                          FLAG
     I              NUMSTE                          MSTE
     I              NUHEBN                          HEBN
     I              NUBRBN                          BRBN
     I              NUINTE                          INTE
     I              NUYEMH                          YEMH
     I              NUINTK                          INTK
     I              NUINBN                          INBN
     I              NUINEN                          INEN
     I              NUFACT                          FACT
     I              NUYYMM                          YYMM
     I              NUKIND                          KIND
     I              NUTMDT                          TMDT
     I              NUTMTM                          TMTM
     I              NUTMOP                          TMOP
     I              NUTAWS                          TAWS
     I              NUSTCL                          STCL
     I              NUXPDD                          XPDD
     I              NUXPDT                          XPDT
     I              NUXUSR                          XUSR
     I              NUMAR1                          MAR1
     I              NUPTYN                          PTYN
     I           UDS
     I                                        6   6 AREA
0805AI                                     10011010 U#USID
     C           KEY       KLIST
     C                     KFLD           FACT
     C                     KFLD           YYMM
     C                     KFLD           KIND
     C*
     C                     MOVEA'0000'    *IN,11
     C           FLAG      COMP 'A'                      11
     C           FLAG      COMP 'C'                      12
     C           FLAG      COMP 'D'                      13
     C*
     C           KEY       CHAINRINENBU              31
     C                     EXSR MOVE1
     C           *IN31     IFEQ '1'
     C           *IN11     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     WRITERINENBU
0805AC                     EXSR MOVE2
0805AC                     WRITENBUG
     C                     END
     C                     END
     C           *IN31     IFEQ '0'
     C           *IN11     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     UPDATRINENBU
0805AC                     EXSR MOVE2
0805AC                     WRITENBUG
     C                     END
     C                     END
     C*
     C                     DELETNBUECD
     C*
     C****************************************************************
     C           MOVE1     BEGSR
     C****************************************************************
     C                     Z-ADDTMDT      NUTMDT
     C                     Z-ADDTMTM      NUTMTM
     C                     MOVELTMOP      NUTMOP
     C                     MOVELTAWS      NUTAWS
     C                     MOVELSTCL      NUSTCL
     C                     ENDSR
     C**************************************************************
0805AC           MOVE2     BEGSR
     C**************************************************************
0805AC                     MOVELFLAG      NUFLAG
0805AC                     MOVELMSTE      NUMSTE
0805AC                     MOVELHEBN      NUHEBN
0805AC                     MOVELBRBN      NUBRBN
0805AC                     MOVELINTE      NUINTE
0805AC                     MOVELYEMH      NUYEMH
0805AC                     MOVELINTK      NUINTK
0805AC                     MOVELINBN      NUINBN
0805AC                     MOVELINEN      NUINEN
0805AC                     MOVELFACT      NUFACT
0805AC                     MOVELYYMM      NUYYMM
0805AC                     MOVELKIND      NUKIND
0805AC                     Z-ADDTMDT      NUTMDT
0805AC                     Z-ADDTMTM      NUTMTM
0805AC                     MOVELTMOP      NUTMOP
0805AC                     MOVELTAWS      NUTAWS
0805AC                     MOVELSTCL      NUSTCL
0805AC                     Z-ADDXPDD      NUXPDD
0805AC                     Z-ADDXPDT      NUXPDT
0805AC                     MOVELXUSR      NUXUSR
0805AC                     MOVELMAR1      NUMAR1
0805AC                     MOVELPTYN      NUPTYN
0805AC                     MOVEL'ARP143RC'NUAPNM
0805AC           *DATE     SUB  19000000  NUCHDD
0805AC                     TIME           NUCHDT
0805AC                     MOVELU#USID    NUCUSR
0805AC                     ENDSR
