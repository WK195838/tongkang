     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP127R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION     加工代開傳票還原
     H*            5.DATE-WRITTEN  107/03/22 2018AR1144
     H*            6.UPDATE  DATE
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTL  UF  E           K        DISK
     FTRNDTLW IF  E           K        DISK
     F            TXREC                             KRENAMETXRECW
     FACTRAN  UF  E           K        DISK
     FACTRANW IF  E           K        DISK
     F            RACVOUR                           KRENAMERACVOURW
     I*************************************************************
     I*
     I            DS
     I                                        1  30 TXRESV
     I                                       16  17 D#RESV
     I           UDS
     I                                        1   60U#YYMM
0011AI                                      201 206 D#VNO1
0011AI                                      211 216 D#VNO2
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     I            DS
     I                                        1   6 ACVRNO
     I                                        1   1 D#VRNO
     C**************************************************************
     C*   程式參數接收區
     C**************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P#CNTT  6
     C                     PARM           P#CNTA  6
     C**************************************************************
     C*   資料搜尋關鍵
     C**************************************************************
     C           K#TX      KLIST
     C                     KFLD           TXCODE
     C                     KFLD           TXNO
     C                     KFLD           TXITEM
     C*
     C           K#AC      KLIST
     C                     KFLD           U#AREA
     C                     KFLD           U#YYMM
     C                     KFLD           ACVRNO
     C                     KFLD           ACITEM
     C**************************************************************
     C*   主程式開始
     C**************************************************************
     C                     Z-ADD0         W#CNTT  60
     C                     Z-ADD0         W#CNTA  60
0011AC                     MOVEL*BLANKS   D#VNO1
0011AC                     MOVEL*BLANKS   D#VNO2
     C*
     C                     EXSR SR1000                     清除銷貨檔
     C                     EXSR SR2000                     刪除傳票檔
     C*
     C                     MOVE W#CNTT    P#CNTT
     C                     MOVE W#CNTA    P#CNTA
     C                     SETON                     LR
     C************************************
     C*  清除銷貨明細之傳票號碼
     C************************************
     CSR         SR1000    BEGSR
     C           *LOVAL    SETLLTXRECW               69
     C                     MOVEL*OFF      *IN69
     C           *IN69     DOWEQ'0'
     C                     READ TXRECW                   69
     C   69                LEAVE
     C           K#TX      CHAINTXREC                69
     C           D#RESV    IFNE '1J'
     C                     ITER
     C                     ENDIF
     C                     MOVEL*BLANK    TXVUNO
     C                     UPDATTXREC
     C                     ADD  1         W#CNTT
     C                     ENDDO
     CSR                   ENDSR
     C*
     C************************************
     C*  刪除傳票檔內的相關記錄
     C************************************
     CSR         SR2000    BEGSR
     C           *LOVAL    SETLLRACVOURW             69
     C  N69                READ RACVOURW                 69
     C           *IN69     DOWEQ'0'
     C           D#VNO1    IFEQ *BLANKS
     C                     MOVELACVRNO    D#VNO1
     C                     MOVELACVRNO    D#VNO2
     C                     ENDIF
     C           D#VNO2    IFLT ACVRNO
     C                     MOVELACVRNO    D#VNO2
     C                     ENDIF
     C           K#AC      CHAINRACVOUR              69
     C                     DELETRACVOUR
     C                     ADD  1         W#CNTA
     C                     READ RACVOURW                 69
     C                     ENDDO
     CSR                   ENDSR
     C*
     C**************************************************************
