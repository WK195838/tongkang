     H*****************************************************************
     H*
     H*          >>  PROGRAM INTRODUCTION  <<
     H*          1.PROGRAM-ID    ARR105R
     H*          2.PROGRAM-TYPE  RPG/400
     H*          3.AUTHOR        S02CSF
     H*          4.FUNCTION     應收帳款承購狀況彙總表　　
     H*          5.DATE-WRITTEN  92/01/24
     H*          6.UPDATE  DATE  99/09/03  2010AR517  S00WCJ (9909A)
     H*
     H*****************************************************************
     H        1   Y                                     1
     FARCUDT  IP  E           K        DISK
     FARR105T O   E             66     PRINTER
     E*****************************************************************
     IRARCUDT
     I                                              ACBKIDL1
     I           UDS
     I                                     10011010 U#USID
     I                                     10111020 S#DEVN
     I*****************************************************************
     C*         主程式開始
     C*****************************************************************
9909AC           *DATE     SUB  19000000  U#SYSD  80
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     MOVE *ON       *IN99
     C                     MOVE *ON       *IN66
     C*
     C                     MOVEL*ALL'='   T#LIN1
     C                     MOVEL*ALL'='   T#LIN2
     C                     MOVEL*ALL'-'   T#LIN3
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR105T1S
     C                     MOVE *ON       *IN66
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     CLEARAR105T1D
     C*
     C*承購止日等於０，或承購止日大於當日
     C*
     C           ACDAT2    IFEQ 0
9909AC           ACDAT2    ORGT U#SYSD
     C*
     C                     MOVELACCUNO    T#CUNO           客戶代號
     C                     MOVELACCUNM    T#CUNM           客戶名稱
     C                     MOVELACBKNM    T#BKNM           承購銀行
     C                     Z-ADDACAAMT    T#AAMT           承購額度（Ａ）
     C                     Z-ADDACBAMT    T#BAMT           融資額度
     C                     Z-ADDACFAMT    T#FAMT           當日承購額度
     C                     Z-ADDACEAMT    T#EAMT           非當日承購額度
     C                     Z-ADDACAAM1    T#AAM1           已承購金額（Ｂ）
     C                     Z-ADDACCAMT    T#CAMT           已融資金額
     C                     Z-ADDACHAMT    T#HAMT           逾期未兌現金額
     C                     Z-ADDACGAMT    T#GAMT           可動撥餘額
     C                     Z-ADDACIAMT    T#IAMT           帳上餘額
     C*
     C   66                WRITEAR105T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR105T1D
     C*
     C                     ADD  T#AAMT    T#SAMT           承購額度小計
     C                     ADD  T#BAMT    T#BAMS           融資額度小計
     C                     ADD  T#FAMT    T#FAMS           當日承購額度小計
     C                     ADD  T#EAMT    T#EAMS           非當日承購額度小計
     C                     ADD  T#AAM1    T#AAMS           已承購金額小計　
     C                     ADD  T#CAMT    T#CAMS           已融資金額小計
     C                     ADD  T#HAMT    T#HAMS           逾期未兌現金額小計
     C                     ADD  T#GAMT    T#GAMS           可動撥餘額小計
     C                     ADD  T#IAMT    T#IAMS           帳上餘額小計
     C*
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C   66                WRITEAR105T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR105T1L
     C                     WRITEAR105T1S
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C*
     C   66                WRITEAR105T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR105T1M
     C*
     C                     ENDSR
