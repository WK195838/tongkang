     A*                                                              **
     A*    PROGRAM   ID       :  ARE029R                             **
     A*    PROGRAM   FUNCTION : 訂單管制檔資料維護                 **
     A*    AUTHOR             :  ERIC. LEE.                          **
     A*    CREAT     DATE     :  85/10/14                            **
     A*    RELATION  PROGRAM  :                                      **
     A*    NOTE               :                                      **
     A*    COMMON   INDICATOR :  60                                  **
     A*****************************************************************
     FARE029S CF  E                    WORKSTN
     FARODCT  UF  E           K        DISK                      A
     FSAMAST  IF  E           K        DISK
     E                    ERR     1   9 50
     I            DS
9008 I                                        1   9 ORNO
LYW  I                                        1   7 BORNO
 .   I                                        1   1 ORNO1
 .   I*                                       2   6 ORNO2
 .   I                                        2   60ORNO02
 .   I*                                       7   9 ORNO3
 .   I                                        7   90ORNO03
     I            DS
9008 I                                        1   6 FORNO
LYW  I                                        1   1 S1OREA
 .   I                                        2   60S1ORNO
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10221022 AKIND
     C****************************************************************
     C           KMAST     KLIST
     C                     KFLD           KMAST1  1
9009 C                     KFLD           KMAST2  50
LYW  C                     KFLD           KMAST3  30
     C           KODCT     KLIST
     C                     KFLD           ORNO
     C                     KFLD           CTKD
     C                     KFLD           ODKD
     C                     KFLD           ITEM
     C****************************************************************
     C           *IN99     DOUEQ'1'
     C                     MOVEA'0000'    *IN,30
     C                     MOVE *ZEROS    SEL
     C                     MOVE *BLANK    MODE
     C*                    MOVE *BLANK    NAME
     C                     WRITEAR029F1
     C                     EXFMTAR029F2
     C           SEL       COMP 0                        99
     C           SEL       COMP 1                        98
     C           SEL       COMP 2                        97
     C           SEL       COMP 3                        96
     C           SEL       COMP 4                        95
     C   98                MOVE '新　增'MODE
     C   97                MOVE '修　改'MODE
     C   96                MOVE '刪　除'MODE
     C   95                MOVE '查　詢'MODE
     C   98                EXSR ADDRTN
     C   97                EXSR MDFRTN
     C   96                EXSR DELRTN
     C   95                EXSR INQRTN
     C                     END
     C   99                SETON                     LR
     C   99                RETRN
     C****************************************************************
     C***        A D D      S U B R O U T I N E                    ***
     C****************************************************************
     CSR         ADDRTN    BEGSR
     C                     WRITEAR029F1
     C                     EXSR INZSR1
     C           *IN99     DOUEQ'1'
     C           ADTAG1    TAG
     C                     EXFMTAR029F3
     C*                    SETOF                     303132
     C   90                EXSR CLRMSG
     C           *IN99     IFEQ '0'
     C                     EXSR CKRTN1
     C           *IN90     IFEQ '0'
     C                     EXSR INZSR
     C           *IN90     DOUEQ'0'
     C                     EXFMTAR029F4
     C   90                EXSR CLRMSG
     C           *IN99     IFEQ '0'
     C                     EXSR CKRTN2
     C           *IN90     IFEQ '0'
     C                     EXSR MVTOFL
     C                     WRITERARODCT
     C                     EXSR INZSR1
     C                     END
     C                     END
     C                     END
     C                     END
     C                     END
     C                     END
     C                     SETOF                     99
     CSR                   ENDSR
     C****************************************************************
     CSR         MDFRTN    BEGSR
     C                     WRITEAR029F1
     C           *IN99     DOUEQ'1'
     C                     EXFMTAR029F3
     C*                    SETOF                     33
     C   90                EXSR CLRMSG
     C           *IN99     IFEQ '0'
     C                     EXSR CKRTN1
     C           *IN90     IFEQ '0'
     C                     EXSR MVTOFM
     C           *IN90     DOUEQ'0'
     C                     EXFMTAR029F4
     C   90                EXSR CLRMSG
     C           *IN99     IFEQ '0'
     C                     EXSR CKRTN2
     C           *IN90     IFEQ '0'
     C                     EXSR MVTOFL
     C                     UPDATRARODCT
     C                     EXSR INZSR1
     C                     END
     C                     END
     C                     END
     C                     END
     C                     END
     C                     END
     C                     SETOF                     99
     CSR                   ENDSR
     C****************************************************************
     CSR         DELRTN    BEGSR
     C                     WRITEAR029F1
     C           *IN99     DOUEQ'1'
     C                     EXFMTAR029F3
     C*                    SETOF                     4041
     C   90                EXSR CLRMSG
     C           *IN99     IFEQ '0'
     C                     EXSR CKRTN1
     C           *IN90     IFEQ '0'
     C                     EXSR MVTOFM
     C           *IN99     IFEQ '0'
     C                     EXFMTAR029F4
     C   90                EXSR CLRMSG
     C  N99 11             MOVE ERR,5     ERRMSG
     C  N99 11             EXSR EMTMSG
     C   90                EXSR CLRMSG
     C           *IN99     IFEQ '0'
     C           *IN11     ANDEQ'1'
     C                     EXSR MVTOFL
     C                     DELETRARODCT
     C                     END
     C                     END
     C                     END
     C                     END
     C                     END
     C                     SETOF                     99
     CSR                   ENDSR
     C****************************************************************
     CSR         INQRTN    BEGSR
     C                     WRITEAR029F1
     C           *IN99     DOUEQ'1'
     C                     EXFMTAR029F3
     C   90                EXSR CLRMSG
     C           *IN99     DOUEQ'1'
     C                     SETOF                     40
     C  N02                READ RARODCT                  40
     C   02                READPRARODCT                  40
     C  N02 40             MOVE ERR,4     ERRMSG
     C  N02 40             EXSR WRTMSG
     C   02 40             MOVE ERR,3     ERRMSG
     C   02 40             EXSR WRTMSG
     C     N40             EXSR MVTOFM
     C     N40             EXSR CKRTN1
     C   40                SETOF                         90
     C                     EXFMTAR029F4
     C   40
     COR 90                EXSR CLRMSG
     C   02 40   KODCT     SETLLRARODCT
     C  N02 40   KODCT     SETGTRARODCT
     C                     END
     C                     END
     C                     SETOF                         99
     CSR                   ENDSR
     C****************************************************************
     CSR         INZSR     BEGSR
     C                     MOVE *BLANK    MTHD
     CSR                   ENDSR
     C****************************************************************
     CSR         INZSR1    BEGSR
     C                     MOVE *BLANK    ORNO1
9104 C                     MOVE '00000'   ORNO02
LYW  C                     MOVE '000'     ORNO03
     C                     MOVE *BLANK    CTKD
     C                     MOVE *BLANK    ODKD
     C                     MOVE *ZEROS    ITEM
     CSR                   ENDSR
     C****************************************************************
     CSR         CKRTN1    BEGSR
     C                     MOVEA'000'     *IN,30
     C                     SETOF                     90
9009 C                     MOVELORNO1     KMAST1
     C                     Z-ADDORNO02    KMAST2
     C                     Z-ADDORNO03    KMAST3
     C           KMAST     SETLLRSAMAST              60
     C           *IN60     IFEQ '0'
     C                     READ RSAMAST                  61
     C           *IN61     IFEQ '1'
     C           *IN61     OREQ '0'
     C           FORNO     ANDNEBORNO
     C                     SETON                     30
     C                     MOVE ERR,6     ERRMSG
     C                     EXSR WRTMSG
     C                     END
     C                     ELSE
     C                     SETON                     30
     C                     MOVE ERR,6     ERRMSG
     C                     EXSR WRTMSG
     C                     END
     C*
     C  N90      CTKD      IFNE '01'
     C           CTKD      ANDNE'02'
     C           CTKD      ANDNE'03'
     C           CTKD      ANDNE'04'
     C           CTKD      ANDNE'05'
     C           CTKD      ANDNE'06'
     C                     SETON                     31
     C                     MOVE ERR,7     ERRMSG
     C                     EXSR WRTMSG
     C                     END
     C*
     C  N90      ODKD      IFNE '01'
     C           ODKD      ANDNE'11'
     C           ODKD      ANDNE'12'
     C           ODKD      ANDNE'13'
     C           ODKD      ANDNE'21'
     C           ODKD      ANDNE'31'
     C                     SETON                     32
     C                     MOVE ERR,8     ERRMSG
     C                     EXSR WRTMSG
     C                     END
     C*
     C           *IN90     IFEQ '0'
     C           KODCT     CHAINRARODCT              60
     C           *IN60     IFEQ '0'
     C           *IN98     ANDEQ'1'
     C                     MOVEA'000'     *IN,30
     C                     MOVE ERR,1     ERRMSG
     C                     EXSR WRTMSG
     C                     END
     C           *IN60     IFEQ '1'
     C           *IN97     IFEQ '1'
     C           *IN96     OREQ '1'
     C           *IN95     OREQ '1'
     C                     MOVEA'000'     *IN,30
     C                     MOVE ERR,2     ERRMSG
     C                     EXSR WRTMSG
     C                     END
     C                     END
     C                     END
     C                     ENDSR
     C****************************************************************
     CSR         CKRTN2    BEGSR
     C                     SETOF                     9033
     C           MTHD      IFNE '01'
     C           MTHD      ANDNE'02'
     C           MTHD      ANDNE'03'
     C           MTHD      ANDNE'04'
     C           MTHD      ANDNE'05'
     C                     SETON                     33
     C                     MOVE ERR,9     ERRMSG
     C                     EXSR WRTMSG
     C                     END
     CSR                   ENDSR
     C****************************************************************
     CSR         MVTOFL    BEGSR
     C                     MOVE *BLANK    A2FLAG
9008 C   98                MOVELORNO      A2ORNO
     C   98                MOVE CTKD      A2CTKD
     C   98                MOVE ODKD      A2ODKD
     C   98                MOVE ITEM      A2ITEM
     C           *IN98     IFEQ '1'
     C           *IN97     OREQ '1'
     C                     MOVE MTHD      A2MTHD
     C                     END
     CSR                   ENDSR
     C****************************************************************
     CSR         MVTOFM    BEGSR
9008 C   95                MOVELA2ORNO    ORNO
     C   95                MOVE A2CTKD    CTKD
     C   95                MOVE A2ODKD    ODKD
     C   95                MOVE A2ITEM    ITEM
     C                     MOVE A2MTHD    MTHD
     CSR                   ENDSR
     C****************************************************************
     CSR         CLRMSG    BEGSR
     C                     SETOF                     90
     C                     MOVE *BLANK    ERRMSG
     C                     WRITEAR029F5
     CSR                   ENDSR
     C****************************************************************
     CSR         WRTMSG    BEGSR
     C                     SETON                     90
     C                     WRITEAR029F5
     CSR                   ENDSR
     C****************************************************************
     CSR         EMTMSG    BEGSR
     C                     SETON                     90
     C                     EXFMTAR029F5
     CSR                   ENDSR
     C****************************************************************
**
01資料已存在．
02資料不存在．
03資料第一筆．
04資料最後一筆．
05確定刪除按【ＰＦ１１】，其它則否．
06訂單編號不存在．
07管制類別錯誤．
08訂單類別錯誤．
09開立方式錯誤．
