     H            Y                                     1
     FTRNDTL  UP  E           K        DISK
     FINVMST  UF  E           K        DISK                      A
     FINVDTL  UF  E           K        DISK                      A
     FHIPROD  IF  E           K        DISK
     FGENSEQ  UF  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FSAMAST  IF  E           K        DISK
     FQPRINT  O   F     160     OV     PRINTER
     I****
     ITXREC
     I                                              TXCODEL4
     I                                              TXPCNOL2
     I                                              TXPDNML1
     I*                                             TXUPRCL1
     I****
     IAADS        DS
     I                                        1  12 GEGRP
     I                                        1   1 GE1
     I                                        2   5 GE2
     I                                        6  10 GE3
     I                                       11  20 INNOG
     I                                       11  12 INNO1
     I                                       13  200INNO2
     IBBDS        DS
     I                                        1   6 SPCUNO
     I                                        1   1 S1KIND
     I                                        2   2 S1CUN1
     I                                        3   5 S1CUN2
9008 I                                        6   6 S1CD01
LYW  I                                        7   7 S1CD02
 .   I                                        8   8 S1CD03
 .   I                                        9   9 S1CD04
 .   I                                       10  10 S1CD05
     I                                        1   5 BCNO1
     I                                        6   6 BCNO2
9008 I                                       11  19 TXORNO
LYW  I                                       11  11 TORNO1
 .   I                                       12  160TORNO2
 .   I                                       17  19 TORNO3
CLJ  I*
     I            DS
     I                                        1   2 W#OEOF
     I                                        1   1 D#OE
     I                                        2   2 D#OF
     I            DS
     I                                        1  12 NAME1
     I                                       12  12 D#NOF
---  I*
     I           UDS
9008 I                                        1   80YMD
LYW  I                                        1   60YYMM
0528 I                                        7   7 AREA
0528 I                                     10211021 TXAR
     C*****************************************************************
     C           *ENTRY    PLIST
     C                     PARM           AREAX   1
     C*****************************************************************
0521 C           S1KEY     KLIST
0521 C                     KFLD           TORNO1
0521 C                     KFLD           TORNO2
0521 C                     KFLD           FORNO3
0521 C           CBKEY     KLIST
0521 C                     KFLD           SPCUNO
0521 C           GEKEY     KLIST
0521 C                     KFLD           GEKIND
0521 C                     KFLD           GEPRIN
0521 C           IVKEY     KLIST
0521 C                     KFLD           IVNO
0521 C                     KFLD           IVACNT
0521 C                     KFLD           IVITEM
     C*----------------------------------------------------------------
     C  N90                EXSR IN#01
     C  N90                SETON                     90
     C   L2 90             EXSR DL#L2
     C   L1 90             EXSR DL#L1
     C      90             EXSR DP#01
     CL1    90             EXSR TL#L1
     CL2    90             EXSR TL#L2
     CLR    90             EXSR TL#LR
     C*--------------------------------------------------*
     C*  DETAIL PROCESS L1
     C*--------------------------------------------------*
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         QTYT    70
     C                     SETOF                     28
     C                     Z-ADD0         AMTO    90
     CSR         DL#L1Z    ENDSR
     C*-----------------------------------------------*
     C*  DETAIL PROCESS L2                            *
     C*-----------------------------------------------*
     CSR         DL#L2     BEGSR
     C                     MOVEL'2'       TXIVTP
     C                     ADD  1         CURNO2
CLJX C           CURNO2    IFGT W#ENO2
     C                     EXSR SR9999
---  C                     ENDIF
     C                     Z-ADDCURNO2    INNO2
     C                     MOVE PRE2      INNO1
     C                     Z-ADD0         AMTT   112
     C                     Z-ADD0         AAMT   110
     C                     Z-ADD0         TTAMT   90
     C                     Z-ADD0         TTAMK   90
     C                     Z-ADD0         TAMTX   90
     C                     Z-ADD0         TTAMTX  90
     C                     Z-ADD0         IT
     C                     TESTN          TORNO3     31
9009 C     N31             MOVE 001       FORNO3  30
     C      31             MOVE TORNO3    FORNO3
0521 C           S1KEY     CHAINSAMAST               50    *NR
0521 C           CBKEY     CHAINCBCUST               51    *NR
0521 C      50             MOVEL*ALL'#'   CBMUID
0521 C      50             MOVEL*ALL'#'   CBFNAM
0521 C      50             MOVEL*ALL'#'   CBADR1
0521 C      51             MOVEL*BLANK    CBCUNM
0521 C      51             MOVEL*ALL'*'   CBMUID
0521 C      51             MOVEL*ALL'*'   CBFNAM
0521 C      51             MOVEL*ALL'*'   CBADR1
     C                     EXSR LN#CH
     C                     EXCPTED#L2
     C                     ADD  1         LCNT
     CSR         DL#L2Z    ENDSR
     C*---------------------------------------------*
     CSR         DP#01     BEGSR
     C                     MOVELTXPCNO    RESV1   5
     C                     ADD  TXQTY     QTYT
     C                     ADD  TXAMT     AMTT
     C                     ADD  TXAMT     AMTO
     C  N28      TXACNT    COMP '3'                      28
     C  N28      TXACNT    COMP '2'                      28
     C                     MOVE 'C'       TXFLAG
     C                     MOVELINNOG     TXIVNO
     C                     MOVELAREA      TXTXAR
     C                     Z-ADDUDATE     TXTXDT
     C                     MOVEL'Y'       TXFL02
     C                     UPDATTXREC
     C                     MOVELTXRESV    TPRCA   7
     C                     SETOF                     21
     C                     TESTN          TPRCA      21
     C   21                MOVE TPRCA     TPRCN   70
     C   21      TPRCN     DIV  100000    TPRC    75
     C  N21                Z-ADDTXUPRC    TPRC
     C                     EXSR LN#CH
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C*********
     C* L1RTN *
     C*********
     CSR         TL#L1     BEGSR
     C           TXPDNM    CHAINHIPROD               20
CLJ  C* N20                MOVE ACPRNM    NAME1  12
     C           *IN20     IFEQ '0'
     C                     MOVE ''      W#OEOF
     C                     MOVELF4CHIN    NAME1
     C                     MOVE D#OF      D#NOF
---  C                     ENDIF
     C  N28      QTYT      MULT TPRC      TAMTO   90H
     C   28                Z-ADDAMTO      TAMTO     H
     C           QTYT      IFNE 0
     C           TAMTO     DIV  QTYT      UPO     75H
     C                     END
     C                     ADD  TAMTO     TTAMT   90
     C                     ADD  1         IT      20
     C                     MOVELINNOG     IVNO
     C                     MOVE '1'       IVACNT
     C                     Z-ADDIT        IVITEM
     C           IVKEY     CHAININVDTL               37
     C   37                EXSR WR#DL1
     C   37                EXCPTET#L1
     C   37                ADD  1         LCNT
     C  N37                EXCPTBRERR
     C  N37                ADD  1         LCNT
     CSR                   ENDSR
     C*********
     C* L2RTN *
     C*********
     CSR         TL#L2     BEGSR
     C                     Z-ADD0         UPO
     C                     Z-ADD0         QTYT
     C                     MOVELINNOG     INNO
     C           INNO      CHAININVMST               37
     C   37                EXSR WR#MS
     C                     EXSR LN#CH
     C                     EXCPTET#L2
     C                     ADD  3         LCNT
     CSR         TL#L2Z    ENDSR
     C*---------------------------------------------------*
     CSR         TL#LR     BEGSR
     C                     MOVE '01'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVELAREAX     GE1
9008 C                     MOVE YYMM      GE2
     C                     MOVEL*BLANK    GE3
     C                     MOVELGEGRP     GEPRIN
     C           GEKEY     CHAINGENSEQ               H1
CLJ  C                     EXSR SR9000                     票軌更改
     C                     Z-ADDCURNO2    GECUNO
     C                     UPDATGEREC
     C*****
     CSR                   ENDSR
     C*----------------------------------------------*
     CSR         IN#01     BEGSR
     C                     MOVE '01'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVELAREAX     GE1
9008 C                     MOVE YYMM      GE2
     C                     MOVEL*BLANK    GE3
     C                     MOVELGEGRP     GEPRIN
     C           GEKEY     CHAINGENSEQ               H1
CLJ  C                     EXSR SR9000                     票軌更改
     C                     MOVELGEPRE     PRE2    2
     C                     Z-ADDGECUNO    CURNO2  80
CLJX C                     Z-ADDGEENNO    W#ENO2  80
     C*****
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD60        TCNT    20
     C***
     C                     SELEC
     C           AREA      WHEQ 'P'
     C                     MOVEL'台北'  AREAC   6
     C           AREA      WHEQ 'K'
     C                     MOVEL'高雄'  AREAC
     C           AREA      WHEQ 'M'
     C                     MOVEL'台中'  AREAC
     C           AREA      WHEQ 'T'
     C                     MOVEL'桃園'  AREAC
     C           AREA      WHEQ 'L'
     C                     MOVEL'林口'  AREAC
     C           AREA      WHEQ 'H'
     C                     MOVEL'苗栗'  AREAC
     C                     OTHER
     C                     MOVEL'　　'  AREAC
     C                     ENDSL
     CSR         IN#01Z    ENDSR
     C*---------------------------------------------------*
     CSR         LN#CH     BEGSR
     C           LCNT      IFGT TCNT
     C                     ADD  1         PAG
     C                     EXCPTEH#01
     C                     Z-ADD6         LCNT
     C                     ENDIF
     CSR                   ENDSR
     C*---------------------------------------------------*
     CSR         WR#DL1    BEGSR
     C                     CLEARIVREC
     C                     MOVE 'A'       IVFLAG
     C                     MOVELINNOG     IVNO
     C                     MOVE '1'       IVACNT
     C                     Z-ADDIT        IVITEM
9008 C                     MOVE YMD       IVACDT
     C                     MOVELTXORNO    IVORNO
     C                     MOVELTXPDNM    IVPDCD
     C                     Z-ADDQTYT      IVQTY
     C           UPO       MULT 100000    UPOB    70
     C                     MOVE UPOB      SAV2    2
     C                     MOVELSAV2      IVRESV
     C                     Z-ADDUPO       IVUPRC
     C                     Z-ADDTAMTO     IVAMT
     C                     MOVELTXNO      IVAPNO
     C                     MOVE 'Y'       IVFL02
     C           TXCODE    IFEQ 'SA04'
     C                     MOVE 'A'       IVFL03
     C                     ELSE
     C                     MOVE 'B'       IVFL03
     C                     ENDIF
     C                     MOVELAREA      IVTXAR
     C                     Z-ADDUDATE     IVTXDT
     C                     WRITEIVREC
     C                     ADD  IVAMT     AAMT   110
     CSR                   ENDSR
     C*---------------------------------------------------*
     CSR         WR#MS     BEGSR
     C                     CLEARINREC
     C                     MOVE 'A'       INFLAG
     C                     MOVELINNOG     INNO
     C                     MOVELSPCUNO    INCUNO
     C                     MOVELCBCUNO    INCUNM
     C                     MOVELTXPCNO    INORNO
9008 C                     MOVE YMD       ININDT
     C                     MOVEL'2'       INKIND
     C                     MOVELTXSALE    INSALE
     C                     MOVELTXSATP    INSATP
     C                     MOVELTXRVID    INRVID
     C                     MOVEL'1'       INTYPE
     C****
     C                     MOVELAREA      INAREA
     C                     MOVEL'2'       INTXTP
     C                     Z-ADDAAMT      INAAMT
     C                     Z-ADDAAMT      INNBAL
     C                     MOVELAREA      INTXAR
     C                     Z-ADDUDATE     INTXDT
     C                     WRITEINREC
     CSR                   ENDSR
     C**************************************************************
CLJ  C*          ------------------------------二月一票軌指向奇數月
     CSR         SR9000    BEGSR
     C           *INH1     IFEQ '1'
     C                     MOVE GE2       W#YM    40
     C           W#YM      DIV  2         W#Q     40
     C                     MVR            W#R     10
     C           W#R       IFEQ 0                          偶數月份
     C                     SUB  1         W#YM
     C                     MOVE W#YM      GE2
     C                     MOVELGEGRP     GEPRIN
     C           GEKEY     CHAINGENSEQ               H1
     C                     ENDIF
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*---------------------------------------------------------
CLJX C* 字軌用完之異常處理
     C*---------------------------------------------------------
     CSR         SR9999    BEGSR
     C           CURNO2    IFGT W#ENO2
     C                     Z-ADDW#ENO2    CURNO2
     C                     ENDIF
     C*
     C                     EXSR TL#LR                      字軌存檔
     C                     CALL 'ARP056P'
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C**************************************************************
     C*====================================================
     OQPRINT  E 2 01           EH#01
     O                                   11 '報表編號:'
     O                                   17 'AR004'
     O                                   77 '東和鋼鐵股份有限公司'           X
     O                                  119 '印表日期'
     O                                  120 ':'
     O                         UDATE Y  129
     O        E 1              EH#01
     O                                   10 '廠　　別'
     O                                   11 ':'
     O                         AREAC   +  1
     O                                   78 '過入發票核對表（外銷）'
     O                                  119 '頁　　數'
     O                                  120 ':'
     O                         PAG   Z  125
     O        E 2              EH#01
     O                                    8 '訂　單'
     O                                   17 '日　期'
     O                                   26 '磅　單'
     O                                   36 '品名'
     O                                   42 '重量'
     O                                   44 'kg'
     O                                   54 '單價'
     O                                   69 '金　　額'
     O                                   84 '品　　名'
     O                                   94 '重　量'
     O                                  106 '單價'
     O                                  121 '金　　額'
     O                                  138 '發票號碼'
     O                                  146 '業務'
     O        E 1              EH#01
     O                                    8 '-------'
     O                                   17 '--------'
     O                                   28 '----------'
     O                                   34 '---'
     O                                   44 '--------'
     O                                   54 '------'
     O                                   69 '-------------'
     O                                   84 '------------'
     O                                   94 '---------'
     O                                  106 '------'
     O                                  121 '-----------'
     O*                                 130 '--------'
     O                                  138 '--------'
     O                                  146 '------'
     O        E 1      L2      ED#L2
     O                         CBCUNM    10
     O                         CBMUID    20
     O                         CBFNAM    56
     O                         CBADR1   118
     O                                  132 '二聯式'
     O        E 1              ED#01
     O                         RESV1      6
     O                         TXDATEY   17
     O                         TXITEM    26
     O                         TXNO      26
     O                         TXACNT    30
     O                         TXPDNM    34
     O                         TXQTY J   44
     O                         TPRC  1   54
     O                         TXAMT J   69
     O        E 1              ET#L1
     O                         NAME1     84
     O                         QTYT  J   94
     O                         AMTO  J  120
     O                         UPO   1  108
     O        E 1              ET#L2
     O                                   84 '總　計'
     O                         INNOG    138
     O                         AAMT  J  120
     O                                  108 '=====================>'
     O                         TXSALE   144
     O        E  1     L1N37   BRERR
     O                                   67 '號碼重覆'
