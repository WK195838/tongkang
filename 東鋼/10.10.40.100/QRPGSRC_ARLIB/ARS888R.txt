     H*****************************************************************
     H*
     H*            *****   PROGRAM INTRODUCTION   *****
     H*
     H*            1.PROGRAM-ID    MTS888R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S00TPC
     H*            4.FUNCTION     區間傳票代開同步(IFRS同步ROC)
     H*            5.DATE-WRITTEN  100/11/08 1001108MT S00TPC
     H*
     H*****************************************************************
     H            Y
     FACVOURN UF  E           K        DISK                      A
     F            RACVOUR                           KRENAMERACVOURN
     FACVOUR  IF  E           K        DISK
     FACNCMPL1IF  E           K        DISK
     I           UDS
     I                                      951 985 S#COMP
     I                                     10011010 D#USER
     I                                     10111020 S#DEVN
     I                                     10211021 U#LOC
     C*****************************************************************
     C*          Parameter List
     C*****************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P#AREA  1        *開立廠區
     C                     PARM           P#YM    6        *年月
     C                     PARM           P#VRNS  6        *起始傳票號碼
     C                     PARM           P#VRNE  6        *終止傳票號碼
     C*****************************************************************
     C*          Key       List
     C*****************************************************************
     C*FILE => ACVOUR
     C           KEY01     KLIST
     C                     KFLD           W#AREA
     C                     KFLD           W#YM
     C                     KFLD           W#VRNS
     C*FILE => ACVOUR
     C           KEY02     KLIST
     C                     KFLD           W#AREA
     C                     KFLD           W#YM
     C*****************************************************************
     C*          Main      Program
     C*****************************************************************
     C*
     C                     MOVELP#AREA    W#AREA  1
     C                     MOVELP#YM      W#YM    60
     C                     MOVELP#VRNS    W#VRNS  6
     C                     MOVELP#VRNE    W#VRNE  6
     C*
     C*DELETE IFRS傳票檔ACVOURN
     C           KEY01     SETLLRACVOURN
     C                     MOVE *OFF      *IN52
     C           *IN52     DOWEQ*OFF
     C           KEY02     READERACVOURN                 52*EOF
     C   52                LEAVE
     C*
     C           ACVRNO    IFGT W#VRNE
     C                     LEAVE
     C                     ENDIF
     C*
     C                     DELETRACVOURN
     C                     ENDDO
     C*
     C*READ ROC傳票檔ACVOUR WRITE TO IFRS傳票檔ACVOURN
     C           KEY01     SETLLRACVOUR
     C                     MOVE *OFF      *IN52
     C           *IN52     DOWEQ*OFF
     C           KEY02     READERACVOUR                  52*EOF
     C   52                LEAVE
     C*
     C           ACVRNO    IFGT W#VRNE
     C                     LEAVE
     C                     ENDIF
     C*
     C*CHAIN ACNCMP新舊會科對照表
     C                     MOVELACNO      W#ACNO  9        *ROC 會計科目
     C           W#ACNO    CHAINRACNCMP              60    *NR
     C  N60                MOVELAZNO      ACNO             *IFRS會計科目
     C*
     C                     WRITERACVOURN                   *IFRS傳票檔
     C                     ENDDO
     C*
     C                     MOVE *ON       *INLR
