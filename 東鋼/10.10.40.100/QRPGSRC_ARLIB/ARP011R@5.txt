     H*****************************************************************
     H*
     H*          >>  PROGRAM INTRODUCTION  <<
     H*          1.PROGRAM-ID    ARP011RS
     H*          2.PROGRAM-TYPE  RPG/400
     H*          3.AUTHOR
     H*          4.FUNCTION     銷貨收入代開傳票
     H*          5.DATE-WRITTEN
     H*          6.DATE-MODIFY
     H*
     H*         說明：配合新總帳上線擴檔欄位長度變更 97/07/14
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTL  IP  E                    DISK
     FTRNDTLUPUF  E           K        DISK
     F            TXREC                             KRENAMETXW
     FACTRAN  UF  E           K        DISK                      A
     FHIPROD  IF  E           K        DISK
     FQPRINT  O   F     176            PRINTER
     F*----------------------------------------------------------------
     E                    TAB1    1   5  1   TABR1  19
     E*
     ITXREC
     I                                              TXACDTL3
     I                                              TXIVNOL2
     I                                              TXCUNOL2
     I                                              TXUPRCL1
     I                                              TXPDNML1
     ****
     I            DS
     I                                        1  19 KGRP
     I                                        1   5 KACNO
     I                                        6  15 KACNON
     I                                       16  19 KDPNO
     I                                       20  20 TXAREA
     I                                       20  27 TXNO
     I            DS
     I                                        1   6 VRNOX
     I                                        2   60NO1
     I                                       11  16 VRNON
     I                                       12  160NO3
     I            DS
     I                                        1   6 VRNOY
     I                                        2   60NO2
     I            DS
     I                                        1   7 TXVUNO
     I                                        1   1 TTT1
     I                                        2   2 TTT2
     I                                        3   3 TTT3
     I                                        4   7 TTT4
     I            DS
     I                                        1   6 TXCUNO
     I                                        1   1 CUNO
CLJ  I*
     I            DS
     I                                        1   2 W#OEOF
     I                                        1   1 D#OE
     I                                        2   2 D#OF
     I            DS
     I                                        1  12 NAME1
     I                                       12  12 D#NOF
---  I*
     I           UDS
9008 I                                        1   8 YMD
     I                                        1   60D#YM
     I                                        9   9 TXAR
     I                                     10011010 D#USER
     C*****************************************************************
     C*          KEY  LIST
     C*****************************************************************
     C           ACKEY1    KLIST
     C                     KFLD           TXAR
     C                     KFLD           D#YM
     C                     KFLD           ACVRNO
     C                     KFLD           ACITEM
     C*
     C           TXKEY     KLIST
     C                     KFLD           TXCODE
     C                     KFLD           TXNO
     C                     KFLD           TXITEM
     C*
     C*****************************************************************
     C  N10                EXSR IN#01
     C   L3                EXSR DL#03
     C   L2                EXSR DL#02
     C   L1                EXSR DL#01
     C                     EXSR DP#01
     CL1                   EXSR TL#01
     CL2                   EXSR TL#02
     CL3                   EXSR TL#L3
9107 CLR                   EXSR LRSR
     CLR                   EXCPTET#LRR
     C**--------------------------------------------------**
     CSR         IN#01     BEGSR
     C*
     C                     MOVE YMD       DATEA   80
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD58        TCNT    20
     C                     Z-ADD0         PAG     30
     C                     MOVE *BLANK    KGRP
     C           TXAR      LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     KGRP
     C                     SETON                     10
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**  FIND LAST NO                                **
     C**----------------------------------------------**
     CSR         @FDNO     BEGSR
     C*
9205 C                     MOVEL'C00799'  ACVRNO
     C                     Z-ADD999       ACITEM
     C                     SETOF                     95
     C           ACKEY1    SETGTACTRAN
     C                     READPACTRAN                   95
     C  N95                MOVELACVRNO    TYPE    1
     C  N95      TYPE      COMP 'C'                  9595
     C  N95                MOVELACVRNO    VRNON
     C  N95                ADD  1         NO3
     C  N95                MOVELVRNON     VRNOX
     C   95                MOVEL'C00001'  VRNOX
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**    L3 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#03     BEGSR
     C                     Z-ADDTCNT      LCNT
     C*
     C                     EXSR @FDNO
     C                     MOVELVRNOX     VRNOY
     C           NO1       ADD  1         NO2
     C                     Z-ADD0         AMTT   132
     C                     Z-ADD0         AMTTT  110
     C                     Z-ADD1         ITEM1   30
     C                     Z-ADD1         ITEM2   30
     C                     Z-ADD0         COUNT   40
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**    L2 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#02     BEGSR
     C*
     C                     Z-ADD0         AMT2   132
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**    L1 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#01     BEGSR
     C*
     C           TXPDNM    CHAINHIPROD               31
CLJ  C   31                MOVE *BLANK    NAME1  12
     C           *IN31     IFEQ '0'
     C                     MOVE ''      W#OEOF
     C                     MOVELF4CHIN    NAME1
     C                     MOVE D#OF      D#NOF
---  C                     ENDIF
     C*
     C                     Z-ADD0         AMT1   132
     C                     Z-ADD0         QTY1    80
     C*
     CSR                   ENDSR
     C**----------------------------------------------**
     C**       DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C*
     C                     MOVELTXAREA    TXARA   1
TEST C           TXNO      IFEQ 'M1133574'
 .   C                     Z-ADD0         W#TEST  10
 .   C                     ENDIF
     C*
     C           TXARA     IFEQ 'A'
     C                     MOVELTXAR      TXARA
     C                     ENDIF
     C*
     C                     ADD  TXAMT     AMT1
     C                     ADD  TXAMT     AMT2
     C                     ADD  TXQTY     QTY1
     C***
     C                     ADD  TXAMT     AMTT
     C                     ADD  1         COUNT
9107 C                     ADD  TXAMT     AMTTX  132
9107 C                     ADD  1         COUNTX  40
     C*
     C           TXKEY     CHAINTRNDTLUP             31
     C  N31                MOVELTXAR      TTT1
     C  N31                MOVELTXARA     TTT2
     C  N31                MOVELVRNOY     TTT3
     C  N31                MOVE VRNOY     TTT4
     C  N31                UPDATTXW
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   L1 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#01     BEGSR
     C*
     C                     SETOF                     2223
     C           AMT1      IFNE 0
     C                     ADD  1         ITEM1
     C           AMT1      IFLT 0
     C                     Z-SUBAMT1      AMT1
     C                     Z-SUBQTY1      QTY1
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXSR WR#01
     C                     EXSR PR#01
     C                     ENDIF
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   L2 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#02     BEGSR
     C*
     C           AMT2      IFNE 0
     C                     ADD  1         ITEM2
     C           AMT2      IFLT 0
     C                     Z-SUBAMT2      AMT2
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXSR WR#02
     C                     EXSR PR#02
     C                     ENDIF
     C*
     CSR                   ENDSR
     C**----------------------------------**
     C**   L3 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#L3     BEGSR
     C*
     C           AMTT      IFNE 0
     C           AMTT      IFLT 0
     C                     Z-SUBAMTT      AMTT
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXSR WR#03
     C                     EXSR PR#03
     C                     EXSR WR#04
     C                     EXSR PR#04
     C                     EXCPTET#LR
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*****************************************************************
9107 CSR         LRSR      BEGSR
 .   C*
 .   C           AMTTX     IFLT 0
 .   C                     Z-SUBAMTTX     AMTTX
 .   C                     MOVEL'0'       *IN,51
 .   C                     ELSE
 .   C                     MOVEL'1'       *IN,51
 .   C                     ENDIF
 .   C                     EXCPTET#LRX
 .   C*
 .   CSR                   ENDSR
     C**------------------------------------------**
     C**    TOTAL  TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#01     BEGSR
     C*
     C****  WRITE  TO NON-TAIPEI "4111" 銷貨收入（內銷）
     C****  WRITE  TO NON-TAIPEI "4114" 銷貨收入（外銷）
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXARA     ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXARA     ACAREA           開立廠區
     C                     Z-ADDD#YM      ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADDITEM1     ACITEM
     C   51                MOVEL'C'       ACDC
     C  N51                MOVEL'D'       ACDC
     C*
     C           CUNO      IFEQ 'E'
     C                     MOVEL'4114'    ACNO
     C                     SETON                     22
     C                     ELSE
     C                     MOVEL'4111'    ACNO
     C                     SETON                     23
     C                     ENDIF
     C*
     C                     MOVEL'L'       ACTYCO
     C                     MOVE *BLANK    KGRP
     C           TXAREA    LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     KGRP
     C                     MOVELKDPNO     ACDPNO
     C                     MOVELTXORNO    KORNO   6
     C                     MOVELKORNO     ACRLNO
     C                     MOVELTXCUNM    ACCSNM
     C                     Z-ADDAMT1      ACAMT
     C                     MOVELTXPDNM    ACDSC1
     C*
     C           TXACNT    IFEQ '3'
     C                     Z-ADD0         QTY1
     C                     END
     C*
     C                     MOVELQTY1      ACDSC2
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
     C                     MOVE UDATE     ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
     C                     MOVE UDATE     ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#02     BEGSR
     C*
     C****  WRITE  TO TAIPEI  "1134"應收帳款
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXARA     ACAREA           開立廠區
     C                     Z-ADDD#YM      ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM2     ACITEM
     C  N51                MOVEL'C'       ACDC
     C   51                MOVEL'D'       ACDC
     C                     MOVEL'1134'    ACNO
     C                     MOVEL'G'       ACTYCO
     C                     MOVELKORNO     ACRLNO
     C                     MOVELTXCUNM    ACCSNM
     C                     Z-ADDAMT2      ACAMT
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
     C                     MOVE UDATE     ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
     C                     MOVE UDATE     ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#03     BEGSR
     C*
     C****  WRITE  TO TAIPEI  "3511"公司往來
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXARA     ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXARA     ACAREA           開立廠區
     C                     Z-ADDD#YM      ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADD1         ACITEM
     C  N51                MOVEL'C'       ACDC
     C   51                MOVEL'D'       ACDC
     C                     MOVEL'3511'    ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
     C                     MOVE UDATE     ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
     C                     MOVE UDATE     ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#04     BEGSR
     C*
     C****  WRITE  TO TAIPEI  "3512-6"廠區往來
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXARA     ACAREA           開立廠區
     C                     Z-ADDD#YM      ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADD1         ACITEM
     C   51                MOVEL'C'       ACDC
     C  N51                MOVEL'D'       ACDC
     C                     MOVE *BLANK    KGRP
     C           TXAREA    LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     KGRP
     C                     MOVELKACNO     ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
     C                     MOVE UDATE     ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
     C                     MOVE UDATE     ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#01     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#02     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#02
     C                     ADD  1         LCNT
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#03     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#03
     C                     ADD  1         LCNT
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#04     BEGSR
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#04
     C                     ADD  1         LCNT
     C*
     CSR                   ENDSR
     O*****************************************************************
     OQPRINT  E   03           EH#01
     O                                   12 '報表編號：'
     O                                   18 'ARP011'
     O                                   81 '銷貨收入入帳清單'
     O                                  140 '列表日期:'
     O                         UDATE Y  148
     O        E 1              EH#01
     O                                   82 '********************'
     O                                  140 '頁　　數:'
     O                         PAG   Z  143
     O        E 1              EH#01
     O                                   70 '日期:'
     O                         DATEA     80 '    /  /  '
     O        E 1              EH#01
     O                                   41 '借'
     O        E 1              EH#01
     O                                   10 '入帳日期'
     O                                   20 '磅單編號'
     O                                   26 '訂單'
     O                                   35 '客　戶'
     O                                   41 '貸'
     O                                   51 '會計科目'
     O                                   69 '品　名'
     O                                   91 '數量'
     O                                  101 '單價'
     O                                  118 '借方金額'
     O                                  135 '貸方金額'
     O                                  142 '部門'
     O                                  156 '傳票編號'
     O        E 1              EH#01
     O                                   20 '--------------------'
     O                                   40 '--------------------'
     O                                   60 '--------------------'
     O                                   80 '--------------------'
     O                                  100 '--------------------'
     O                                  120 '--------------------'
     O                                  132 '--------------------'
     O                                  150 '--------------------'
     O                                  159 '---------'
     O        E 1              ED#01
     O                         ACYMD     10 '    /  /  '
     O                         TXNO      18
     O                         KORNO     26
     O                         ACCSNM    37
     O                    51             40 'C'
     O                   N51             40 'D'
     O                         ACNO      51
     O                 22                60 '外銷銷貨收入L'
     O                 23                60 '內銷銷貨收入L'
     O                         TXPDNM    65
     O                         NAME1   +  1
     O                         QTY1  K   91
     O                         TXUPRCK  101
     O                   N51   ACAMT K  118
     O                    51   ACAMT K  135
     O                         ACDPNO   145
     O                         TXAR     148
     O                         TXARA    149
     O                         VRNOY    155
     O                                      '-'
     O                         ACITEM   159
     O        E 1              ED#02
     O                         ACYMD     10 '    /  /  '
     O*                        TXNO      18
     O                         KORNO     26
     O                         ACCSNM    37
     O                   N51             60 'C 1134應收帳款    G'
     O                    51             60 'D 1134應收帳款    G'
     O                    51   ACAMT K  118
     O                   N51   ACAMT K  135
     O                         TXAR     148
     O                                  149 'P'
     O                         VRNOX    155
     O                                      '-'
     O                         ACITEM   159
     O        E 1              ED#03
     O                         ACYMD     10 '    /  /  '
     O*                        TXNO      20
     O                   N51             60 'C 3511公司往來    A'
     O                    51             60 'D 3511公司往來    A'
     O                    51   ACAMT K  118
     O                   N51   ACAMT K  135
     O                         TXAR     148
     O                         TXARA    149
     O                         VRNOY    155
     O                                      '-'
     O                         ACITEM   159
     O        E 1              ED#04
     O                         ACYMD     10 '    /  /  '
     O*                        TXNO      20
     O                   N51             41 'D '
     O                    51             41 'C '
     O                         KACNO
     O                         KACNON
     O                                   60 'A'
     O                   N51   ACAMT K  118
     O                    51   ACAMT K  135
     O                         TXAR     148
     O                                  149 'P'
     O                         VRNOX    155
     O                                      '-'
     O                         ACITEM   159
     O        E 2              ET#LR
     O                                   64 '入帳筆數'
     O                         COUNT K +  1
     O                N51               100 '銷貨金額合計'
     O                N51      AMTT  K  118
     O                 51               117 '銷貨金額合計'
     O                 51      AMTT  K  135
9107 O        E 2              ET#LRX
 .   O                                   64 '總入帳筆數'
 .   O                         COUNTXK +  1
 .   O                N51               100 '銷貨金額合計'
 .   O                N51      AMTTX K  118
 .   O                 51               117 '銷貨金額合計'
 .   O                 51      AMTTX K  135
     O        E   60           ET#LRR
     O                                   50 '經理（廠長）：'
     O                                   73 '複核：'
     O                                   96 '製票：'
** TAB1
K3512 高雄往來K210
T3513 桃園往來T380
H3514 苗栗往來H800
M3515 台中往來W040
L3516 林口往來L080
