     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    HSP551R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02YSH
     H*            4.FUNCTION     信用狀記錄 NOTES(HSLCME)
     H*            5.DATE-WRITTEN  88/07/07
     H*
     H*****************************************************************
     H        1   Y                                     1
     FLLCMST  IP  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FARLCBL  O   E           K        DISK
     I           UDS
     I                                     10011010 D#USER
     C*****************************************************************
     C*          MAIN  PROGRAM
     C*****************************************************************
     C                     EXSR SR1000
     CLR                   RETRN
     C*
     C*****************************************************************
     CSR         SR1000    BEGSR
     C*
     C                     MOVEL*BLANK    P#DATE
     C                     MOVE UDATE     P#DATE           前推三十天
     C                     CALL 'UTS104R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MOD1  1
     C                     PARM '1'       P#MOD2  1
     C                     PARM '0007'    P#DAYS  4
     C                     PARM *BLANK    P#RTND  8
     C                     PARM *BLANK    P#ERRC  1
     C*
     C                     MOVE P#RTND    W#DATE  80
     C           LLENDT    IFLT W#DATE                     過期七天
     C                     GOTO ES1000                     不考慮
     C                     ENDIF
     C*
     C                     MOVE LLNO      ALLCNO           信用狀號
     C           LLCUNO    CHAINCBREC                40
     C  N40                MOVELCBMUID    ALVANO           客戶統編
     C                     MOVE LLCUNM    ALCUNM           客戶名稱
     C                     MOVE LLBANK    ALBANK           開狀銀行
     C           19110000  ADD  LLEFDT    ALEFDT           開狀日期
     C           19110000  ADD  LLDUDT    ALDUDT           管制日期
     C                     Z-ADDLLCRLM    ALCRLM           信用額度
     C                     Z-ADDLLBAMT    ALXAMT           餘額
     C                     MOVE LLCLOS    ALCLOS           結案碼
     C                     MOVE D#USER    ALADDM           異動人員
     C                     MOVE UDATE     W#ADDD  60       異動日期
     C           19110000  ADD  W#ADDD    ALADDD           異動日期
     C                     TIME           ALADDT           異動時間
     C*
     C           ALVANO    IFNE *BLANK
     C                     WRITERARLCBL
     C                     ENDIF
     CSR         ES1000    ENDSR
