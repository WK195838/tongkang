     H        1   Y                                     1                 BR003
     FINVMST  IP  E                    DISK
     FTRNDTLL1IF  E           K        DISK
     FQPRINT  O   F     132            PRINTER
      *************************************************************
     E**                  TAB1    1   5  1   TABR1  10
     I*************************************************************
     IINREC
     I                                              INCUNOL3
     I                                              INRVIDL2
     I                                              INORNOL1
     I****
     IAADS        DS
     I              'も應收帳款明細表も'C         CON3                       X
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
      *
     IBBDS        DS
     I                                       21  260YMD
     I                                       21  240YM
     I                                       21  220YY
     I                                       23  240MM
     I                                       25  260DD
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           TXKEY     KLIST
     C                     KFLD           TXCUNO
     C                     KFLD           TXORN5
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L3                EXSR DL#L3
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     CL3                   EXSR TL#L3
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C           *ENTRY    PLIST
     C                     PARM           CUST1   6
     C                     PARM           CUST2   6
      *
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD55        OVRFLW  30
     C                     MOVE *ALL'='   ALS   132
     C                     MOVE *ALL'-'   ALQ    70
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP OVRFLW               75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD10        LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L3                             **
     C**----------------------------------------------**
     CSR         DL#L3     BEGSR
     C                     Z-ADD0         GAMT4   80
     C                     Z-ADD0         GAMT5   80
     C                     Z-ADD0         GAMT6   80
     C*                    Z-ADDOVRFLW    LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         FAMT4   80
     C                     Z-ADD0         FAMT5   80
     C                     Z-ADD0         FAMT6   80
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     Z-ADDINAAMT    TAMTP   80
     C                     Z-ADDINBAMT    TAMT0   70
     C                     Z-ADDINATAX    TAX     80
     C           INAAMT    ADD  INBAMT    TAMT1   80
     C                     ADD  INATAX    TAMT1
     C           INCAMT    ADD  INDAMT    TAMT2   80
     C           INCTAX    ADD  INDTAX    TAMT3   80
     C                     ADD  TAMT3     TAMT2
     C           TAMT1     ADD  TAMT2     TAMT4   80
     C           INEAMT    ADD  INFAMT    TAMT5   80
     C                     Z-ADDINNBAL    TAMT6   80
      *
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#01
      *
     C                     ADD  TAMT4     GAMT4
     C                     ADD  TAMT5     GAMT5
     C                     ADD  TAMT6     GAMT6
     C                     ADD  TAMT4     FAMT4
     C                     ADD  TAMT5     FAMT5
     C                     ADD  TAMT6     FAMT6
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C                     SETOF                     2122
     C                     MOVELINCUNO    TXCUNO
     C                     MOVELINORNO    TXORN5
     C           TXKEY     SETLLTRNDTLL1             97
     C           *IN97     DOWEQ'0'
     C                     READ TRNDTLL1                 97
     C   97                LEAVE
     C  N21                MOVELTXNO      PNO     8
     C  N21                SETON                     21
     C                     SELEC
     C           TXCUNO    WHNE INCUNO
     C           TXORN5    ORNE INORNO
     C   22                EXSR @PR#L1
     C                     SETON                     97
     C                     ITER
     C           TXNO      WHNE PNO
     C   22                EXSR @PR#L1
     C                     ENDSL
     C                     ADD  TXAMT     AMT1    80
     C                     ADD  TXAMT     FAMT4
     C                     ADD  TXAMT     FAMT6
     C                     ADD  TXAMT     GAMT4
     C                     ADD  TXAMT     GAMT6
     C  N22                SETON                     22
     C                     ENDDO
     C                     EXCPTET#L1
     C                     ADD  2         LCNT
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PRINT                    **
     C**-----------------------------------------**
     CSR         @PR#L1    BEGSR
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#02
     C                     SETOF                     22
     C                     Z-ADD0         AMT1
     C                     MOVELTXNO      PNO
     CSR                   ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     CSR         TL#L2Z    ENDSR
     C**  TOTAL TIME L3 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L3     BEGSR
     C                     EXCPTET#L2
     C                     ADD  2         LCNT
     CSR         TL#L3Z    ENDSR
      ***********************************************************
      ****   DEFINE OUPUT FORMAT                             ****
      ***********************************************************
     OQPRINT  E   03           EH#01
     O                                   17 '報表編號: AR008'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
     O                         UDATE Y  132
     O        E 1              EH#01
     O                         CON3      71                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 2              EH#01
     O                                   11 '客戶編號:'
     O                         CUST1   +  1
     O                                 +  2 '至'
     O                         CUST2   +  1
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              EH#01
     O                                   98 '銷退／　'
     O        E 1              EH#01
     O                                   10 '客戶編號'
     O                                   20 '客戶名稱'
     O                                   28 '業務員'
     O                                   38 '訂單號碼'
     O                                   48 '發票號碼'
     O                                   58 '貨　款'
     O                                   68 '扣預收款'
     O                                   78 '銷項稅額'
     O                                   88 '發票金額'
     O                                   98 '折讓金額'
     O                                  110 '應收貨款'
     O                                  120 '已收金額'
     O                                  132 '應收帳款'
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              ED#01
     O                 L3      INCUNO     8
     O                 75      INCUNO     8
     O                 L3      INCUNM    20
     O                 75      INCUNM    20
     O                 L2      INRVID    25
     O                 75      INRVID    25
     O                 L1      INORNO    36
     O                 75      INORNO    36
     O                         INNO      48
     O                         TAMTP 1   58
     O                         TAMT0 J   68
     O                         TAX   J   78
     O                         TAMT1 J   88
     O                         TAMT2 J   98
     O                         TAMT4 J  110
     O                         TAMT5 J  120
     O                         TAMT6 J  132
     O        E 1              ED#02
     O                         PNO       46
     O                         AMT1  J   58
     O                         AMT1  J  110
     O                         AMT1  J  132
     O        E 1              ET#L1
     O                         ALQ      132
     O        E 1              ET#L1
     O                                   82 '訂單小計：'
     O                         FAMT4 J  110
     O                         FAMT5 J  120
     O                         FAMT6 J  132
     O        E 1              ET#L2
     O                                   82 '客戶小計：'
     O                         GAMT4 J  110
     O                         GAMT5 J  120
     O                         GAMT6 J  132
     O        E 1              ET#L2
     O                         ALQ      132
