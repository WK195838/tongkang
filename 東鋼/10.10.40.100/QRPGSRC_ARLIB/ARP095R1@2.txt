     H*****************************************************************
     H*
     H*          >>  PROGRAM INTRODUCTION  <<
     H*          1.PROGRAM-ID    ARP095R1
     H*          2.PROGRAM-TYPE  RPG/400
     H*          3.AUTHOR        S02CSF
     H*          4.FUNCTION     應收帳款承購發票自動結案 FOR匯票
     H*          5.DATE-WRITTEN  92/03/03
     H*          6.DATE-MODIFY
     H*
     H*****************************************************************
     H        1   Y                                     1
     FARCVMS  IP  E           K        DISK
     FARCVDT  IF  E           K        DISK
     FARCVCK  UF  E           K        DISK
     FARINVM  UF  E           K        DISK
     IRARCVMS     01
     C*****************************************************************
     C*          KEY   LIST
     C*****************************************************************
     C*FILE==>ARCVDT
     C           W#CVDT    KLIST
     C                     KFLD           ATRCNO           沖銷單號
     C                     KFLD           ATITEM           沖銷項次
     C*FILE==>ARCVCK
     C           W#CVCK    KLIST
     C                     KFLD           AKRCNO           沖銷單號
     C                     KFLD           AKRCTM           沖銷項次
     C*****************************************************************
     C*         主程式開始
     C*****************************************************************
     C   01                EXSR DTLSR
     CLR                   RETRN
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C           ASRCNO    IFEQ 'XP1067'
     C                     Z-ADD0         W#TMP
     C                     ENDIF
     C*
     C                     MOVELASRCNO    AKRCNO            沖銷單號
     C                     Z-ADD0         AKRCTM            沖銷項次
     C           W#CVCK    SETLLRARCVCK
     C                     MOVE *OFF      *IN40
     C           *IN40     DOWEQ*OFF
     C                     READ RARCVCK                  40
     C   40                LEAVE
     C*
     C           AKRCNO    IFNE ASRCNO
     C                     LEAVE
     C                     ENDIF
     C*
     C           AKACTP    IFEQ 'C'
     C                     ITER
     C                     ENDIF
     C*沖銷
     C           AKNTTP    IFEQ 'H'                        庫存現金
     C           AKNTTP    OREQ 'J'                        銀行存款
     C           AKACNO    OREQ '2159'
     C           AKNTCD    IFEQ *BLANK                     未兌現
     C           AKDUDT    ANDLEUDATE                      已到期
     C                     MOVEL'Y'       AKNTCD
     C                     UPDATRARCVCK
     C                     EXSR SR1000
     C                     ENDIF
     C                     ENDIF
     C*
     C           AKACNO    IFEQ '7111'                     運費
     C           AKACNO    OREQ '7112'                     檢驗費
     C           AKACNO    OREQ '7142'                     郵電費
     C                     EXSR SR2000
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR1000    BEGSR
     C*****************************************************************
     C                     Z-ADD0         W#DAMT 100
     C*
     C                     MOVELAKRCNO    ATRCNO           沖銷單號
     C                     Z-ADD0         ATITEM           沖銷項次
     C           W#CVDT    SETLLRARCVDT
     C                     MOVE *OFF      *IN41
     C           *IN41     DOWEQ*OFF
     C                     READ RARCVDT                  41
     C   41                LEAVE
     C*
     C           ATRCNO    IFNE AKRCNO
     C                     LEAVE
     C                     ENDIF
 TESTC*
     C                     MOVE ATINNO    W#INNO  8
     C           W#INNO    IFEQ '38878698'
     C                     ADD  1         W#TMP   10
     C                     ENDIF
     C*
     C           ATINNO    CHAINRARINVM              42
     C  N42      AMCLOC    IFEQ *BLANK
     C*
     C*總繳款金額＋折退金額＋折退稅額＝發票金額
     C           ATAMT3    SUB  AMBAMT    W#DAMT           折讓金額
     C                     SUB  AMBTAX    W#DAMT           折讓稅額
     C           W#DAMT    SUB  AMDAMT    W#DAMT           發票金額
     C           W#DAMT    IFGE 0
     C                     Z-ADDATAMT3    AMFAMT           沖銷金額
     C                     MOVEL'Y'       AMCLOC           結案碼
     C                     MOVEL'SYSTEM'  AMCLOM           結案人員
     C                     MOVE UDATE     AMCLOD           結案日期
     C                     ELSE
     C                     ADD  ATAMT3    AMFAMT           沖銷金額
     C                     ENDIF
     C*
     C                     UPDATRARINVM
     C*
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2000    BEGSR
     C*****************************************************************
     C                     MOVELAKRCNO    ATRCNO           沖銷單號
     C                     Z-ADD0         ATITEM           沖銷項次
     C           W#CVDT    SETLLRARCVDT
     C                     MOVE *OFF      *IN43
     C           *IN43     DOWEQ*OFF
     C                     READ RARCVDT                  43
     C   43                LEAVE
     C*
     C           ATRCNO    IFNE AKRCNO
     C                     LEAVE
     C                     ENDIF
     C*
     C           ATAMT2    IFEQ 0                          發票餘額
     C           ATINNO    CHAINRARINVM              44
     C  N44      AMCLOC    IFEQ *BLANK
     C                     MOVEL'Y'       AMCLOC           結案碼
     C                     MOVEL'SYSTEM'  AMCLOM           結案人員
     C                     MOVE UDATE     AMCLOD           結案日期
     C                     UPDATRARINVM
     C*
     C                     ENDIF
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C                     ENDSR
