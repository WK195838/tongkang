     H****************************************************************
     H*
     H*            UPDATE  DATE  99/05/25 2009AR406 S00WCJ (9905A)
     H*                          99/08/26 2010AR517 S00WCJ (9908A)
     H*                         100/11/28  2011AR634 S00WCJ (0011A)
     H*                          配合IFRS修改
     H*                         101/09/20 2012AR682 S00WCJ (0109A)
     H*                         101/12/13 2012AR704 S00WCJ (0112A)
     H*                          配合IFRS正式轉換修改
     H*                         102/12/25 2013AR791 S00WCJ (0212A)
     H*                           修改八德往來與桃園往來
     H*                         107/05/02 2018AR1159 S00WCJ (0705A)
     H*                          組織調整U380->U800
     H*                         108/12/10 S00WCJ (0812A)
     H*                          修正列印超過3頁會多印一頁空白之BUG
     H*                         108/12/25  S00WCJ (0812A)
     H*                          不同代開程式寫入總帳之ACCSNM發生內
     H*                          碼不一致之情形，與品妃姊討論後，寫入
     H*                          總帳前再重CHAIN CBCUST
     H*                         109/07/03  S00WCJ (0907A)
     H*                          品妃姊反應10906月份傳票號碼H00051.
     H*                           H00052若只看H00052僅一筆C 390102
     H*                          之資料是錯的，由於金額為0故未造成
     H*                          帳務錯誤，告知若往來帳金額為0，則
     H*                          不須再開立一張傳票(上述傳票為K廠
     H*                          傳票，其他廠區一併修正)
     H*                         110/01/25  S00WCJ (1001A)
     H*                          修正暫收款做未往來傳票，造成同一天未
     H*                          做還原再重覆代開，誤刪前一天最後一張
     H*                          傳票之BUG
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FRCVWF   IP  E           K        DISK
     FRCVDTL  UF  E           K        DISK
     F            RVREC                             KRENAMERVREC1
     FINVDTLL1UF  E           K        DISK
     FINVMST  IF  E           K        DISK
     FBCNAME  IF  E           K        DISK
     FBANKAC  IF  E           K        DISK
     FSARVIN  IF  E           K        DISK
     FSARVOR  IF  E           K        DISK
     FACTRAN  UF  E           K        DISK                      A
0812AFCBCUST  IF  E           K        DISK
     FQPRINT  O   F     176            PRINTER
     ****
9905AE*                   TAB1    1   3  1   TABR1  19
9905AE                    TAB1    1   4  1   TABR1  19
0109AE                    TAB2    1   6  1   TABR2  21
     IRVREC
     I                                              RVNO  L1
     I*
     IACREC
     I              ACTYPE                          F#TYPE
     I              ACCURR                          F#CURR
     I              ACACNO                          F#ACNO
     I              ACBNAM                          F#BNAM
     I              ACNAME                          F#NAME
     I              ACAREA                          F#AREA
     I              ACBAID                          F#BAID
     I              ACOLID                          F#OLID
     I              ACRESV                          F#RESV
     ****
     I            DS
     I                                        1  19 KGRP
     I                                        1   5 KACNO
     I                                        6  15 KACNON
     I                                       16  19 KDPNO
0109AI            DS
0109AI                                        1  21 K#GRP
0109AI                                        1   7 K#ACNO
0109AI                                        8  17 K#ACNN
0109AI                                       18  21 K#DPNO
     I            DS
     I                                        1   6 VRNOX
     I                                        1   1 AR1
     I                                        2   60NO1
     I                                       11  16 VRNON
     I                                       12  160NO3
     I            DS
     I                                        1   6 VRNOY
     I                                        1   1 AR2
     I                                        2   60NO2
     I                                       11  17 VRNOG
     I                                       11  11 VRNOG1
     I                                       12  12 VRNOG2
     I                                       13  17 VRNOG3
     I                                       14  170VRNOG4
     I*                                      17  17 VRNOG5
     I                                       21  30 RVSANO
     I                                       21  21 ANTYPE
     I                                       22  22 ANCURR
     I                                       23  30 ANACNO
     I            DS
     I                                        1  40 DSC0
     I                                        1  20 DSC1
     I                                       21  40 DSC2
     I            DS
     I                                        1  10 ACNOM
     I                                        1   9 ACNOM1
     I                                       10  10 ACNOM2
     I                                       41  47 VUNOG
     I                                       41  41 VUNOG1
     I                                       42  42 VUNOG2
     I                                       43  47 VUNOG3
     I*                                      47  47 VUNOG4
9602 I            DS
 .   I                                        1  10 SOORNO
 .   I                                        1   6 D#ORN6
 .   I            DS
 .   I                                        1   6 RVCUNO
 .   I                                        1   1 D#MKTP
 .   I            DS
 .   I                                        1  10 SIINNO
9602 I                                        7  10 D#BLK
     I           UDS
9008 I                                        1   80YMD
     I                                        1   60YM
0011AI                                      201 206 D#VNO1
0011AI                                      211 216 D#VNO2
     I                                     10011010 D#USER
     I                                     10211021 TXAR
0109AIIFRS       UDS                             50
0109AI                                        1   60D#IFYM
0109AI                                        1   80D#IFRS
     C*****************************************************************
     C*          KEY  LIST
     C*****************************************************************
     C           IVKEY     KLIST
     C                     KFLD           IVACNT
     C                     KFLD           IVAPNO
     C           RVKEY     KLIST
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C           ACKEY1    KLIST
     C                     KFLD           TXAR
     C                     KFLD           YM
     C                     KFLD           ACVRNO
     C                     KFLD           ACITEM
     C           ACKEY     KLIST
     C                     KFLD           TXAR
     C                     KFLD           YM
     C                     KFLD           ACVRNO
     C           ANKEY     KLIST
     C                     KFLD           ANTYPE
     C                     KFLD           ANCURR
     C                     KFLD           ANACNO
     C*****************************************************************
     C*          MAIN  PROGRAM
     C*****************************************************************
9908AC           *DATE     SUB  19000000  U#SYSD  80
0109AC           *NAMVAR   DEFN ARIFRSCTL IFRS
0109AC           YMD       IFLT D#IFRS
0109AC                     MOVEL*ON       *IN77
0109AC                     ELSE
0109AC                     MOVEL*OFF      *IN77
0109AC                     ENDIF
0109AC                     UNLCKIFRS
     C  N10                EXSR IN#01
     C   L1 10             EXSR DL#01
     C      10             EXSR DP#01
     CL1    10             EXSR TL#01
     CLR    10             EXSR TL#LR
     C**--------------------------------------------------**
     CSR         IN#01     BEGSR
     C                     Z-ADD0         AMTI   132
     C                     Z-ADD0         AMTF   132
     C                     Z-ADD0         AMTT   132
     C                     Z-ADD0         VAMT   132
     C                     Z-ADD0         AMTTT  132
9505 C                     Z-ADD0         AMTTC  132
     C                     Z-ADD0         ITEM    30
     C                     Z-ADD0         ITEM1   30
     C                     Z-ADD80        LCNT    20
0812AC                     Z-ADD58        TCNT    20
     C                     Z-ADD0         PAG     30
     C                     MOVEL*ALL'-'   ADASH 158
     C                     MOVEL*BLANK    VUNOG
     C           TXAR      IFEQ 'P'
     C                     SETON                     32
     C                     MOVEL'P'       TXAR1
     C                     ELSE
     C                     SETOF                     32
     C                     ENDIF
     C                     MOVE *BLANK    KGRP
0109AC                     MOVEL*BLANKS   K#GRP
0109AC  N32 77   TXAR      LOKUPTAB1      TABR1          31
0109AC  N32 31 77          MOVELTABR1     KGRP
0109AC  N32N77   TXAR      LOKUPTAB2      TABR2          31
0109AC  N32 31N77          MOVELTABR2     K#GRP
     C                     SETON                     10
0011AC                     MOVEL*BLANKS   D#VNO1
0011AC                     MOVEL*BLANKS   D#VNO2
     CSR                   ENDSR
     C***
     C**----------------------------------------------**
     C**    L1 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#01     BEGSR
     C   32                Z-ADD0         ITEM
0907AC* N32                Z-ADD1         ITEM
0907AC  N32                Z-ADD0         ITEM
     C  N32                Z-ADD1         ITEM1
     C                     Z-ADD0         AMTT
     C                     Z-ADD0         AMTI
     C                     Z-ADD0         AMTF
9008 C                     Z-ADD0         DYMD    80
     ****
     C           RVVNO1    IFNE *BLANK
     C                     EXSR @DLNO
     C                     ELSE
     C                     EXSR @FDNO
     C                     ENDIF
     C                     MOVELVRNOX     VRNOY
     C           NO1       ADD  1         NO2
     CSR                   ENDSR
     C**----------------------------------------------**
     C**  DELETE ACTRAN  (IF ALREADY HAVE NO)         **
     C**----------------------------------------------**
     CSR         @DLNO     BEGSR
     C                     MOVELRVVNO1    VRNOG
     C                     MOVELVRNOG3    AR1              給VRNOX
     C                     Z-ADDVRNOG4    NO1
     C*
1001AC*          *IN32     IFEQ '0'
1001AC*          NO1       DIV  2         A       30
1001AC*                    MVR            B       30
1001AC*          B         IFEQ 0
1001AC*                    SUB  1         NO1
1001AC*                    ENDIF
1001AC*                    ENDIF
     C*
     C                     MOVELVRNOX     VRNOG3
     C                     MOVE VRNOX     VRNOG4
     C                     MOVE *BLANK    ACVRNO
     C                     MOVELVRNOX     ACVRNO
0011AC           D#VNO1    IFEQ *BLANKS
0011AC                     MOVELVRNOX     D#VNO1
0011AC                     ENDIF
0011AC                     MOVELVRNOX     D#VNO2
     C           ACKEY     SETLLACTRAN               95
     C           *IN95     DOWEQ'0'
     C  N95      ACKEY     READEACTRAN                   95
     C   95                LEAVE
     C*                    MOVELACDSC2    DSC2
     C*          DSC21     IFNE RVNO
     C*                    LEAVE
     C*                    ENDIF
     C                     DELETRACVOUR
     C                     ENDDO
     C***
     C           *IN32     IFEQ '0'
     C                     ADD  1         VRNOG4
     C                     MOVE *BLANK    ACVRNO
     C                     MOVELVRNOG3    ACVRNO
     C                     MOVE VRNOG4    ACVRNO
     C           ACKEY     SETLLACTRAN               95
     C           *IN95     DOWEQ'0'
     C  N95      ACKEY     READEACTRAN                   95
     C   95                LEAVE
     C                     DELETRACVOUR
     C                     ENDDO
     C                     ENDIF
0011AC           D#VNO2    IFNE ACVRNO
0011AC           ACVRNO    ANDGTD#VNO2
0011AC                     MOVELACVRNO    D#VNO2
0011AC                     ENDIF
0112AC           YMD       IFLT D#IFRS
0011AC                     CALL 'ARS888P'
0011AC                     PARM           TXAR
0011AC                     PARM           YM
0011AC                     PARM           D#VNO1
0011AC                     PARM           D#VNO2
0112AC                     ENDIF
     CSR                   ENDSR
     C**----------------------------------------------**
     C**  FIND LAST NO                                **
     C**----------------------------------------------**
     CSR         @FDNO     BEGSR
     C                     MOVEL'H99999'  ACVRNO
     C                     Z-ADD999       ACITEM
     C                     SETOF                     95
     C           ACKEY1    SETLLACTRAN
     C                     READPACTRAN                   95
     C  N95                MOVELACVRNO    TYPE    1
     C  N95      TYPE      COMP 'H'                  9595
     C  N95                MOVELACVRNO    VRNON
     C  N95                ADD  1         NO3
     C  N95                MOVELVRNON     VRNOX
     C   95                MOVEL'H00001'  VRNOX
0011AC           D#VNO1    IFEQ *BLANKS
0011AC                     MOVELVRNOX     D#VNO1
0011AC                     ENDIF
0011AC                     MOVELVRNOX     D#VNO2
     CSR                   ENDSR
     C**----------------------------------------------**
     C**       DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C**
     C                     SELEC
     C           RVNTTP    WHGE 'A'
     C           RVNTTP    ANDLE'E'
0109AC   77                MOVEL'1131'    ACNN    6
0109AC  N77                MOVEL'111001'  ACNN
     C                     MOVEL'H'       ACTP    1
     C           RVNTTP    WHEQ 'I'
0109AC   77                MOVEL'1131'    ACNN    6
0109AC  N77                MOVEL'111001'  ACNN
     C                     MOVEL'H'       ACTP    1
     C           RVDUDT    IFGT DYMD
     C                     Z-ADDRVDUDT    DYMD
     C                     ENDIF
     C           RVNTTP    WHEQ 'F'
     C           RVNTTP    OREQ 'G'
     C           RVNTTP    OREQ 'J'
     C                     MOVELRVACNO    ACNN
     C                     MOVEL'C'       ACTP
     C                     EXSR CK#SNO
     C           RVNTTP    WHEQ 'H'
0109AC   77                MOVEL'1111'    ACNN
0109AC  N77                MOVEL'110101'  ACNN
     C                     MOVEL'D'       ACTP
     C                     MOVEL'NT'      RVRLNO
     C*          RVACNO    WHEQ '2221'
     C*                    MOVEL'2221'    ACNN
     C*                    MOVEL'G'       ACTP
     C*          RVACNO    WHEQ '1512'
     C*                    MOVEL'1512'    ACNN
     C*                    MOVEL'E'       ACTP
     C*          RVACNO    WHEQ '1138'
     C*                    MOVEL'1138'    ACNN
     C*                    MOVEL'E'       ACTP
     C*          RVACNO    WHEQ '2131'
     C*                    MOVEL'2131'    ACNN
     C*                    MOVEL'G'       ACTP
     C*          RVACNO    WHEQ '2121'
     C*                    MOVEL'2121'    ACNN
     C*                    MOVEL'K'       ACTP
     C*          RVACNO    WHEQ '2129'
     C*                    MOVEL'2129'    ACNN
     C*                    MOVEL'E'       ACTP
     C*          RVACNO    WHEQ '2122'
     C*                    MOVEL'2122'    ACNN
     C*                    MOVEL'G'       ACTP
     C*          RVACNO    WHEQ '1137'
     C*                    MOVEL'1137'    ACNN
     C*                    MOVEL'A'       ACTP
     C*          RVACNO    WHEQ '2159'
     C*                    MOVEL'2159'    ACNN
     C*                    MOVEL'D'       ACTP
     C*          RVACNO    WHEQ '1169'
     C*                    MOVEL'1169'    ACNN
     C*                    MOVEL'D'       ACTP
     C*          RVACNO    WHEQ '1114'
     C*                    MOVEL'1114'    ACNN
     C*                    MOVEL'C'       ACTP
     C*          RVACNO    WHEQ '1111'
     C*                    MOVEL'1111'    ACNN
     C*                    MOVEL'D'       ACTP
     C*                    MOVEL'NT'      RVRLNO
     C*          RVACNO    WHEQ '8111'
     C*                    MOVEL'8111'    ACNN
     C*                    MOVEL'G'       ACTP
     C*          RVACNO    WHEQ '8149'
     C*          RVACNO    OREQ '8249'
     C*          RVACNO    OREQ '7112'
     C*          RVACNO    OREQ '7114'
     C*          RVACNO    OREQ '7111'
     C*                    MOVELRVACNO    ACNN
     C*                    MOVEL'M'       ACTP
     C*          RVACNO    WHEQ '8211'
     C*                    MOVELRVACNO    ACNN
     C*                    MOVEL'D'       ACTP
     C                     OTHER
     C                     MOVELRVACNO    ACNN
     C*                    MOVEL'B'       ACTP
     C                     ENDSL
     C***
0109AC                     MOVELACNN      ACNN3   4
     C           ACNN3     IFEQ '711'
0109AC           ACNN3     OREQ '7101'
     C                     MOVEL'M'       ACTP
     C                     ENDIF
     C***
     C                     MOVELACNN      ACNN2   2
     C           ACNN2     IFEQ '35'
0109AC           ACNN2     OREQ '39'
     C                     MOVEL'A'       ACTP
     C                     ENDIF
     C***
     C           RVACTP    IFEQ ' '
     C                     MOVEL'D'       RVACTP
     C                     ENDIF
     C***
     C                     SETOF                     70
     C                     MOVELACNN      ACNN1   1
     C  N32      ACNN2     IFEQ '71'
     C           ACNN1     OREQ '8'
     C           ACNN      OREQ '2221'
0109AC           ACNN      OREQ '220901'
     C                     SETON                     70
     C                     MOVEL'P'       TXAR1   1
     C                     ELSE
     C                     SETOF                     70
     C                     MOVELTXAR      TXAR1
     C                     ENDIF
     C****
     C           RVACTP    IFEQ 'D'
     C                     ADD  RVRAMT    AMTTT
     C  N70                ADD  RVRAMT    AMTT
     C   70                ADD  RVRAMT    AMTF
     C                     ELSE
     C                     SUB  RVRAMT    AMTTT
     C  N70                SUB  RVRAMT    AMTT
     C   70                SUB  RVRAMT    AMTF
     C                     ENDIF
     ***
     C                     MOVELACNN      W#ACNO  9
     C           W#ACNO    CHAINBCNAME               97
     C   97                MOVEL*BLANK    ACNOM
     C   97                MOVEL*BLANK    ACTP
     C  N97                MOVELACCHNM    ACNOM1
     C  N97                MOVE ''      ACNOM2
     C  N97                MOVELACDM06    ACTP
     C***
     C                     Z-ADDRVRAMT    VAMT   132
     C  N70                EXSR WR#01
     C   70                EXSR WR#011
     C                     EXSR PR#01
     C           RVKEY     CHAINRCVDTL               95
     C      95             GOTO D#PRZ
     C                     MOVELTXAR      VUNOG1
     C                     MOVELTXAR1     VUNOG2
     C  N70                MOVE VRNOX     VUNOG3
     C  N70                MOVELAR1       VUNOG3
     C   70                MOVE VRNOY     VUNOG3
     C   70                MOVELAR2       VUNOG3
     C                     MOVELVUNOG     RVVNO1
     C                     MOVEL'C'       RVFLAG
     C                     MOVELTXAR      RVTXAR
9908AC*                    Z-ADDUDATE     RVTXDT
9908AC                     Z-ADDU#SYSD    RVTXDT
     C                     UPDATRVREC1
     CSR         D#PRZ     ENDSR
     C**----------------------------------**
     C**   L1 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#01     BEGSR
     C   32                Z-ADDITEM      ITEM1   30
     C***
     C           DYMD      IFEQ 0
     C                     Z-ADDYMD       DYMD
     C                     ENDIF
     C***
     C                     MOVEL'6'       IVACNT
     C                     MOVELRVNO      IVAPNO
     C           IVKEY     SETLLINVDTLL1             95
     C           *IN95     DOWEQ'0'
     C  N95      IVKEY     READEINVDTLL1                 95
     C   95                LEAVE
     C           IVFLAG    IFEQ 'D'
9104 C           IVDECD    OREQ 'D'
     C                     ITER
     C                     ENDIF
     C           IVNO      CHAININVMST               96
     C           IVAMT     IFGE 0
     C                     MOVEL'D'       DC      1
     C                     Z-ADDIVAMT     VAMT
     C                     ELSE
     C                     MOVEL'C'       DC
     C                     Z-SUBIVAMT     VAMT
9505 C                     ADD  IVAMT     AMTTC
     C                     ENDIF
     C                     ADD  IVAMT     AMTI
     C                     ADD  1         ITEM1
     C                     EXSR WR#02
     C                     EXSR PR#02
     C                     MOVELTXAR      VUNOG1
     C                     MOVEL'P'       VUNOG2
     C   32                MOVE VRNOX     VUNOG3
     C   32                MOVELAR1       VUNOG3
     C  N32                MOVE VRNOY     VUNOG3
     C  N32                MOVELAR2       VUNOG3
     C                     MOVELVUNOG     IVACNO
     C                     MOVEL'C'       IVFLAG
     C                     MOVELTXAR      IVTXAR
9908AC*                    Z-ADDUDATE     IVTXDT
9908AC                     Z-ADDU#SYSD    IVTXDT
     C                     UPDATIVREC
     C                     ENDDO
     C****
     C                     MOVE *BLANK    W#FLAG  1
     C                     MOVE *OFF      *IN39
     C*
     C           D#MKTP    IFEQ 'E'
     C           RVNO      CHAINSIREC                95
     C           *IN95     IFEQ *OFF
     C           D#BLK     ANDEQ*BLANK
     C*
     C                     MOVEL'X'       W#FLAG
     C                     MOVE *ON       *IN39
     C                     MOVEL'C'       DC
     C                     Z-ADDSIINXA    VAMT
     C           SIINXA    MULT -1        W#AMTC 132
     C                     ADD  W#AMTC    AMTTC
     C                     ADD  SIINXA    AMTI
     C                     ADD  1         ITEM1
     C                     EXSR WR#02
     C                     EXSR PR#02
     C*                    MOVELTXAR      VUNOG1
     C*                    MOVEL'P'       VUNOG2
     C*  32                MOVELVRNOX     VUNOG3
     C* N32                MOVELVRNOY     VUNOG3
     C*                    MOVELVUNOG     IVACNO
     C*                    MOVEL'C'       IVFLAG
     C*                    MOVELTXAR      IVTXAR
     C*                    Z-ADDUDATE     IVTXDT
     C*                    UPDATIVREC
     C                     ENDIF
     C                     ENDIF
     C****
0907AC           AMTT      IFEQ 0
0907AC           AMTF      ANDEQ0
0907AC                     MOVEL*ON       *IN33
0907AC                     ELSE
0907AC                     MOVEL*OFF      *IN33
0907AC                     ENDIF
0907AC*
     C   32                EXCPTSP#01
     C   32                ADD  1         LCNT
0907AC* N32                EXSR WR#03
0907AC* N32                EXSR PR#03
0907AC  N32N33             EXSR WR#03
0907AC  N32N33             EXSR PR#03
0011AC           D#VNO2    IFLT VRNOY
0011AC                     MOVELVRNOY     D#VNO2
0011AC                     ENDIF
     CSR         TL#01Z    ENDSR
     C**----------------------------------**
     C**   LR BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     CSR                   ENDSR
     C*****
     C**------------------------------------------**
     C**    CHECK   存入帳號 820101             **
     C**------------------------------------------**
     CSR         CK#SNO    BEGSR
     C           YMD       IFLT 820101
     C           ANKEY     CHAINBANKAC               97
     C  N97                MOVELF#OLID    RVSANO
     C                     ENDIF
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#01     BEGSR
     C****  WRITE  TO LOCAL
     C                     ADD  1         ITEM
     C                     MOVE *BLANK    DSC0
     C                     CLEARRACVOUR
     C                     MOVEL'O'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXAR      ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDYM        ACYM             年月
     C                     Z-ADDRVDATE    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM      ACITEM
     C                     MOVELRVACTP    ACDC
     C                     MOVELACNN      ACNO
     C                     MOVELACTP      ACTYCO
     C                     SELEC
     C           ACTP      WHEQ 'B'
     C                     MOVELRVDPNO    ACDPNO
     C*                    MOVELRVRLNO    ACRLNO
     C           ACTP      WHEQ 'M'
     C           ACNN      IFEQ '8149'
     C           ACNN      OREQ '8249'
0109AC           ACNN      OREQ '811210'
0109AC           ACNN      OREQ '821210'
     C                     MOVELRVDPNO    ACDPNO
     C                     MOVELRVDPNO    ACRLNO
     C                     ELSE
     C                     MOVELRVDPNO    ACDPNO
     C                     MOVELRVRLNO    ACRLNO
     C                     ENDIF
     C           ACTP      WHEQ 'C'
     C                     MOVELRVSANO    ACBKNO
     C           ACTP      WHEQ 'K'
     C                     MOVELRVSANO    ACBKNO
     C                     MOVELRVNTNO    ACRLNO
     C                     MOVE RVDUDT    ACDUE
0812AC           RVCUNO    CHAINCBCUST              N59
0812AC  N59                MOVELCBCUNM    ACCSNM
0812AC   59                MOVELRVCUNM    ACCSNM
     C           ACTP      WHEQ 'H'
     C                     MOVE RVDUDT    ACDUE
0812AC           RVCUNO    CHAINCBCUST              N59
0812AC  N59                MOVELCBCUNM    ACCSNM
0812AC   59                MOVELRVCUNM    ACCSNM
     C                     MOVELRVPANO    DSC1
     C                     MOVELRVNTNO    ACRLNO
     C           ACTP      WHEQ 'D'
     C                     MOVELRVRLNO    ACRLNO
     C           ACTP      WHEQ 'E'
     C           ACNN      IFEQ '2125'
0109AC           ACNN      OREQ '210902'
     C                     MOVELRVRESV    ACCSNM
     C                     ELSE
0812AC           RVCUNO    CHAINCBCUST              N59
0812AC  N59                MOVELCBCUNM    ACCSNM
0812AC   59                MOVELRVCUNM    ACCSNM
     C                     ENDIF
     C           ACTP      WHEQ 'G'
     C                     MOVELRVRLNO    ACRLNO
0812AC           RVCUNO    CHAINCBCUST              N59
0812AC  N59                MOVELCBCUNM    ACCSNM
0812AC   59                MOVELRVCUNM    ACCSNM
     C                     ENDSL
     C                     Z-ADDVAMT      ACAMT
     C                     MOVELRVNO      DSC2
     C                     MOVEL'A'       ACSEPT
     C                     MOVELDSC1      ACDSC1
     C                     MOVELDSC2      ACDSC2
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9908AC*                    MOVE UDATE     ACENTD           建檔日期
9908AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9908AC*                    MOVE UDATE     ACUPDD           異動日期
9908AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#011    BEGSR
     C*
     C****  WRITE  TO LOCAL
     C                     ADD  1         ITEM1
     C                     MOVE *BLANK    DSC0
     C                     CLEARRACVOUR
     C                     MOVEL'O'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXAR1     ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDYM        ACYM             年月
     C                     Z-ADDRVDATE    ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADDITEM1     ACITEM
     C                     MOVELRVACTP    ACDC
     C                     MOVELACNN      ACNO
     C                     MOVELACTP      ACTYCO
     C                     SELEC
     C           ACTP      WHEQ 'B'
     C                     MOVELRVDPNO    ACDPNO
     C*                    MOVELRVRLNO    ACRLNO
     C           ACTP      WHEQ 'M'
     C                     MOVELRVDPNO    ACDPNO
     C                     MOVELRVRLNO    ACRLNO
     C           ACTP      WHEQ 'C'
     C                     MOVELRVSANO    ACBKNO
     C           ACTP      WHEQ 'K'
     C                     MOVELRVSANO    ACBKNO
     C                     MOVELRVNTNO    ACRLNO
     C                     MOVE RVDUDT    ACDUE
0812AC           RVCUNO    CHAINCBCUST              N59
0812AC  N59                MOVELCBCUNM    ACCSNM
0812AC   59                MOVELRVCUNM    ACCSNM
     C           ACTP      WHEQ 'H'
     C                     MOVE RVDUDT    ACDUE
0812AC           RVCUNO    CHAINCBCUST              N59
0812AC  N59                MOVELCBCUNM    ACCSNM
0812AC   59                MOVELRVCUNM    ACCSNM
     C                     MOVELRVPANO    DSC1
     C                     MOVELRVNTNO    ACRLNO
     C           ACTP      WHEQ 'D'
     C                     MOVELRVRLNO    ACRLNO
     C           ACTP      WHEQ 'E'
     C           ACNN      IFEQ '2125'
0109AC           ACNN      OREQ '210902'
     C                     MOVELRVRESV    ACCSNM
     C                     ELSE
0812AC           RVCUNO    CHAINCBCUST              N59
0812AC  N59                MOVELCBCUNM    ACCSNM
0812AC   59                MOVELRVCUNM    ACCSNM
     C                     ENDIF
     C           ACTP      WHEQ 'G'
     C                     MOVELRVRLNO    ACRLNO
0812AC           RVCUNO    CHAINCBCUST              N59
0812AC  N59                MOVELCBCUNM    ACCSNM
0812AC   59                MOVELRVCUNM    ACCSNM
     C                     ENDSL
     C                     Z-ADDVAMT      ACAMT
     C                     MOVELRVNO      DSC2
     C                     MOVEL'A'       ACSEPT
     C                     MOVELDSC1      ACDSC1
     C                     MOVELDSC2      ACDSC2
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9908AC*                    MOVE UDATE     ACENTD           建檔日期
9908AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9908AC*                    MOVE UDATE     ACUPDD           異動日期
9908AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#01     BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD10        LCNT
     C   75                EXCPTEH#01
     C           RVACTP    IFEQ 'D'
     C                     SETON                     41
     C                     ELSE
     C                     SETOF                     41
     C                     ENDIF
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C**------------------------------------------**
     C**    L1     TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#02     BEGSR
     C*
     C****  WRITE  TO TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'O'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDYM        ACYM             年月
     C                     Z-ADDIVACDT    ACYMD
     C   32                MOVELVRNOX     ACVRNO
     C  N32                MOVELVRNOY     ACVRNO
     C                     Z-ADDITEM1     ACITEM
     C                     MOVELDC        ACDC
0109AC      77             MOVEL'1134'    ACNO
0109AC  N77                MOVEL'111101'  ACNO
     C                     MOVEL'G'       ACTYCO
     C                     Z-ADDVAMT      ACAMT
     C  N96                MOVELINORNO    ACRLNO
0812AC  N96      INCUNO    CHAINCBCUST              N59
0812AC  N96N59             MOVELCBCUNM    ACCSNM
0812AC  N96 59             MOVELINCUNM    ACCSNM
     C*                    MOVELDYMD      ACDSC1
     C                     MOVEL'A'       ACSEPT
     C*
     C           W#FLAG    IFEQ 'X'
     C                     Z-ADDRVDATE    ACYMD
0812AC           RVCUNO    CHAINCBCUST              N59
0812AC  N59                MOVELCBCUNM    ACCSNM
0812AC   59                MOVELRVCUNM    ACCSNM
     C           RVNO      CHAINSOREC                40
     C  N40                MOVELD#ORN6    ACRLNO
     C                     ENDIF
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9908AC*                    MOVE UDATE     ACENTD           建檔日期
9908AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9908AC*                    MOVE UDATE     ACUPDD           異動日期
9908AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#02     BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD10        LCNT
     C   75                EXCPTEH#01
     C           DC        IFEQ 'D'
     C                     SETON                     42
     C                     ELSE
     C                     SETOF                     42
     C                     ENDIF
     C                     EXCPTET#01
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C**------------------------------------------**
     C**    L2     TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#03     BEGSR
     C****  WRITE  TO TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'O'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDYM        ACYM             年月
     C                     Z-ADDYMD       ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADD01        ACITEM
     C                     ADD  AMTF      AMTI
     C           AMTI      IFGE 0
     C                     MOVEL'C'       ACDC
     C                     MOVEL'C'       DC1     1
     C                     ELSE
     C                     MOVEL'D'       ACDC
     C                     MOVEL'D'       DC1
     C                     Z-SUBAMTI      AMTI
     C                     ENDIF
     C                     Z-ADDAMTI      ACAMT
0109AC   77                MOVELKACNO     ACNO
0109AC  N77                MOVELK#ACNO    ACNO
     C                     MOVEL'A'       ACTYCO
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9908AC*                    MOVE UDATE     ACENTD           建檔日期
9908AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9908AC*                    MOVE UDATE     ACUPDD           異動日期
9908AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C                     WRITERACVOUR
     C*
     C****  WRITE  TO NON-TAIPEI
     C                     CLEARRACVOUR
     C                     MOVEL'O'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXAR      ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     Z-ADDYM        ACYM             年月
     C                     Z-ADDYMD       ACYMD
     C                     MOVELVRNOX     ACVRNO
0907AC*                    Z-ADD01        ACITEM
0907AC                     ADD  1         ITEM
0907AC                     Z-ADDITEM      ACITEM
     C           AMTT      IFGT 0
     C                     MOVEL'C'       ACDC
     C                     MOVEL'C'       DC2     1
     C                     ELSE
     C                     MOVEL'D'       ACDC
     C                     MOVEL'D'       DC2
     C                     Z-SUBAMTT      AMTT
     C                     ENDIF
     C                     Z-ADDAMTT      ACAMT
0109AC   77                MOVEL'3511'    ACNO
0109AC  N77                MOVEL'390101'  ACNO
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9908AC*                    MOVE UDATE     ACENTD           建檔日期
9908AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9908AC*                    MOVE UDATE     ACUPDD           異動日期
9908AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#03     BEGSR
     C           DC1       IFEQ 'D'
     C                     SETON                     43
     C                     ELSE
     C                     SETOF                     43
     C                     ENDIF
     C           DC2       IFEQ 'D'
     C                     SETON                     44
     C                     ELSE
     C                     SETOF                     44
     C                     ENDIF
     C                     EXCPTET#03
     C                     ADD  3         LCNT
     CSR                   ENDSR
     OQPRINT  E  103           EH#01
     O                                   12 '報表編號：'
     O                                   18 'ARP018'
     O                                   81 '繳款收入入帳清單'
     O                                  140 '列表日期:'
9908AO*                        UDATE Y  148
9908AO                         U#SYSD   150 '    /  /  '
     O        E  1             EH#01
     O                                   82 '********************'
     O                                  140 '頁　　數:'
     O                         PAG   Z  143
     O        E  1             EH#01
     O                                   73 '入帳日期:'
     O                         YMD       83 '    /  /  '
     O        E  1             EH#01
     O                                   24 '借'
     O        E  1             EH#01
     O*                                  10 '入帳日期'
     O                                   10 '繳款編號'
     O                                   20 '客　戶'
     O                                   24 '貸'
     O                                   40 '會　計　科　目'
0109AO                                   62 '借方金額'
0109AO                                   82 '貸方金額'
0109AO                                   92 '到期日期'
0109AO                                  102 '票據號碼'
0109AO                                  122 '付款銀行'
0109AO                                  140 '票據帳號'
0109AO                                  152 '存入帳號'
0109AO                                  164 '傳票編號'
0109AO                                  174 '部門'
     O        E  1             EH#01
0109AO                         ADASH    160
     O        E  1             ED#01
9009 O*                        RVDATEY   10
     O                         RVNO       7
     O                                    8 '-'
     O                         RVITEM    10
     O                         RVCUNM    20
     O                         RVACTP    22
0109AO                         ACNN      29
0109AO                         ACNOM     39
0109AO                         ACTP      40
0109AO                 41      VAMT  K   62
0109AO                N41      VAMT  K   82
0109AO                         RVDUDT    91 '    /  /  '
0109AO                         ACRLNO   112
0109AO                         RVPBID   126
0109AO                         RVPANO   140
0109AO                         RVSANO   152
0109AO                         TXAR     156
0109AO                         TXAR1    157
0109AO                N70      VRNOX    163
0109AO                 70      VRNOY    163
0109AO                                  164 '-'
0109AO                N70      ITEM     167
0109AO                 70      ITEM1    167
0109AO                         RVDPNO   173
     O        E  1             ET#01
     O                N39      INCUNM    20
     O                 39      RVCUNM    20
0109AO                         DYMD      91 '    /  /  '
     O                         DC        22
0109AO                 77                40 '1134  應收帳款G'
0109AO                N77                40 '111101應收帳款G'
0109AO                 42      VAMT  K   62
0109AO                N42      VAMT  K   82
0109AO                N39      IVNO     130
0109AO                N39      INORNO    99
0109AO                 39      D#ORN6    99
0109AO                         TXAR     156
0109AO                                  157 'P'
0109AO                    32   VRNOX    163
0109AO                   N32   VRNOY    163
0109AO                                  164 '-'
0109AO                         ITEM1    167
     O        E  1             SP#01
     O        E  1             ET#03
     O                         DC2       22
0109AO                 77                39 '3511  公司往來'
0109AO                N77                39 '390101公司往來'
0109AO                 44      AMTT  K   62
0109AO                N44      AMTT  K   82
0109AO                         TXAR     156
0109AO                         TXAR     157
0109AO                         VRNOX    163
0109AO                                  167 '-001'
     O        E  2             ET#03
     O                         DC1       22
0109AO                 77      KACNO     28
0109AO                 77      KACNON    38
0109AO                N77      K#ACNO    30
0109AO                N77      K#ACNN    40
0109AO                 43      AMTI  K   62
0109AO                N43      AMTI  K   82
0109AO                         TXAR     156
0109AO                                  157 'P'
0109AO                         VRNOY    163
0109AO                                  167 '-001'
     O        E  1             ET#LR
     O                                   62 '應收帳款　沖銷總額:'
0109AO                         AMTTT K   82
9505 O                                   92 '貸方總額:'
0109AO                         AMTTC 2  112
** TAB1
K3512 高雄往來K210
H3514 苗栗往來H800
T3513 桃園往來T380
U3518 新桃往來U380
** TAB2
K390102 高雄往來K210
T390103 八德往來T380
H390104 苗栗往來H800
M390105 台中往來W040
L390106 林口往來L080
U390108 桃園往來U800
