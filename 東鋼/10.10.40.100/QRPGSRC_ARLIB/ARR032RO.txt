     H************************************************************
     H*                                                          *
     H*         PROGRAM   ID        :   ARR032R                  *
     H*         PROGRAM   FUNCTION  :  銷項統一發票明細表列印  *
     H*         AUTHER              :   ANNCY                    *
     H*         CREATE    DATE      :   81/09/23                 *
     H*         MAINTAIN  DATE      :                            *
     H*         RELATION  PROGRAM   :   ARR032P,ARR032R          *
     H*         NOTE                :                            *
     H*                                                          *
     H        1   Y                                     1                 BR003
     FAR1WF1  IP  E           K        DISK
     FQPRINT  O   F     132     OF     PRINTER
     F*---------------------------------------------------------------
     E                    HD      1   4 32
     E*------------------------------------------------------------
     IRAR1WF1
     I                                              A1INVOL1
     I                                              A1SEQ L1
     I           UDS
     I                                        1   2 YA
     I                                        3   4 MA
     I                                        5   5 KIND
     I*
     I                                       11  20 NA
     I                                       17  200NA1
     I                                       21  30 NB
     I                                       27  300NB1
     I                                       31  40 NC
     I                                       37  400NC1
     I                                       41  50 ND
     I                                       47  500ND1
     I*
     I                                       51  60 NM
     I                                       57  600NM1
     I                                       61  70 NN
     I                                       67  700NN1
     I                                       71  80 NOX
     I                                       77  800NO1
     I                                       81  90 NP
     I                                       87  900NP1
     C*----------------------------------------------------------------
     C   L1                EXSR L1RTN
     C   01                EXSR RTN01
     CL1                   EXSR RTNL1
     CLR                   EXSR RTNLR
     C**********
     C* L1RTN  *
     C**********
     C           L1RTN     BEGSR
     C                     SETOF                     11
     C                     Z-ADD0         CNT     30
     C                     Z-ADD0         BLANK   40
     C                     Z-ADD0         VOID    40
     C                     Z-ADD0         TXN     40
     C                     Z-ADD0         TOT1   130
     C                     Z-ADD0         TOT2   130
     C                     ENDSR
     C**********
     C* RTN01  *
     C**********
     C           RTN01     BEGSR
     C                     SETOF                     17
     C           KIND      IFEQ '2'
     C                     SETON                     11
     C                     ELSE
     C                     SETOF                     11
     C                     END
     C                     MOVE NO        NO@     20
     C                     ADD  1         CNT
     C           A1TXNO    IFEQ '1'
     C                     MOVE 'V'       TXNO    1
     C                     ADD  1         TXN
     C                     ADD  1         TXNT    40
     C                     END
     C           MARK      IFEQ 'S'
     C                     MOVE '作廢'  NAME    8
     C                     MOVE ' '       TXNO
     C                     MOVE *BLANK    A1IDNO
     C                     Z-ADD0         A1AMT
     C                     Z-ADD0         A1TAX
     C                     ADD  1         VOID
     C                     ADD  1         TVOD    40
     C                     ELSE
     C                     MOVE *BLANK    NAME
     C                     END
     C                     ADD  A1AMT     TOT1
     C                     ADD  A1TAX     TOT2
     C                     ADD  A1AMT     T1     130
     C                     ADD  A1TAX     T2     130
     C   11      T1        DIV  1.05      T3     130H
     C   11      T1        SUB  T3        T4     130
     C                     ENDSR
     C**********
     C* RTNL1  *
     C**********
     C           RTNL1     BEGSR
     C           50        SUB  CNT       BLANK
     C                     ADD  BLANK     BLK1    40
     C           BLANK     IFGT 0
     C                     SETON                     17
     C                     ELSE
     C                     MOVE *BLANK    HD,3
     C                     SETOF                     17
     C                     END
     C                     ENDSR
     C**********
     C* RTNLR  *
     C**********
     C           RTNLR     BEGSR
     C                     Z-ADD0         NX1     40
     C                     Z-ADD0         NX2     40
     C                     Z-ADD0         BLKA    40
     C                     Z-ADD0         BLKB    40
     C                     Z-ADD0         BLK     40
     C                     Z-ADD0         BLK2    40
     C  N11      NB1       SUB  NA1       NX1
     C  N11      NX1       ADD  1         BLKA
     C  N11      ND1       SUB  NC1       NX2
     C  N11      NX2       ADD  1         BLKB
     C  N11      BLKA      ADD  BLKB      BLK
     C  N11      BLK       ADD  BLK1      BLK2
     C**
     C   11      NN1       SUB  NM1       NX1
     C   11      NX1       ADD  1         BLKA
     C   11      NP1       SUB  NO1       NX2
     C   11      NX2       ADD  1         BLKB
     C   11      BLKA      ADD  BLKB      BLK
     C   11      BLK       ADD  BLK1      BLK2
     C                     ENDSR
     C*---------------------------------------------------------------
     OQPRINT  H  103   L1
     O                                   13 'PROG:ARR032R '
     O                N11      HD,1      56
     O                 11      HD,2      56
     O                                   70 '列表日期：'
     O                         UDATE Y   79
     O        H  1     L1
     O                                   37 '中華民國'
     O                                   44 '年'
     O                         YA        40
     O                                   50 '月'
     O                         MA        46
     O                                   70 '頁數：'
     O                         PAGE      79
     O        H  1     L1
     O                                   14 '統一編號：'
     O                                   32 '７５２８０２０９'
     O        H  1     L1
     O                                   14 '營業人名稱：'
     O                         HD,4      46
     O                                   75 '發票號碼'
     O        H  1     L1
     O                                   14 '稅籍編號：'
     O                                   34 '２１０１００１４６'
     O                         IVNO      74
     O        H  1     L1
     O                                   11 '發票號碼'
     O                N11                21 '買受人'
     O                                   43 '應'
     O                                   48 '零'
     O                                   53 '免'
     O        H  1     L1
     O                                   11 '末二位'
     O                N11                23 '統一編號'
     O                N11                37 '銷　售　額'
     O                 11                37 '發票總計金額'
     O                                   53 '稅　稅率　稅'
     O                                   65 '稅　額'
     O                                   75 '備　註'
     O        D 1      01
     O                         NO        10
     O                N11      A1IDNO    24
     O                         A1AMT L   37
     O                         TXNO      42
     O                N11      A1TAX L   65
     O                         NAME      75
     O        T 1      17
     O                         HD,3      46
     O        T 1      L1
     O                                   10 '應稅：'
     O                         TXN   L   16
     O                                   20 '張'
     O                N11                48 '稅額：'
     O                N11      TOT2  L   64
     O        T 1      L1
     O                                   10 '作廢：'
     O                         VOID  L   16
     O                                   20 '張'
     O                N11                48 '銷售額：'
     O                N11      TOT1  M   64
     O                 11                48 '發票總金額：'
     O                 11      TOT1  M   64
     O        T 1      L1
     O                                   10 '空白：'
     O                         BLANK L   16
     O                                   20 '張'
     O        T 2      LRN11
     O                                   16 '三聯式：'
     O                         NA        28
     O                                   34 '至'
     O                         NB        46
     O                                   64 '均　為　空　白'
     O        T 1      LRN11
     O                                   16 '三聯式：'
     O                         NC        28
     O                                   34 '至'
     O                         ND        46
     O                                   64 '均　為　空　白'
     O        T 2      LR 11
     O                                   16 '二聯式：'
     O                         NM        28
     O                                   34 '至'
     O                         NN        46
     O                                   64 '均　為　空　白'
     O        T 1      LR 11
     O                         NOX       28
     O                                   34 '至'
     O                         NP        46
     O                                   64 '均　為　空　白'
     O        T 2      LR
     O                                   12 '本月合計：'
     O        T 1      LR
     O                                   10 '應稅：'
     O                         TXNT  L   16
     O                                   20 '張'
     O                                   48 '稅額：'
     O                N11      T2    M   64
     O                 11      T4    M   64
     O        T 1      LR
     O                                   10 '作廢：'
     O                         TVOD  L   16
     O                                   20 '張'
     O                                   48 '銷售額：'
     O                N11      T1    M   64
     O                 11      T3    M   64
     O        T 1      LR
     O                                   10 '空白：'
     O                         BLK2  L   16
     O                                   20 '張'
     O                 11                48 '發票總金額：'
     O                 11      T1    M   64
** HD
營業人使用三聯式統一發票明細表
營業人使用二聯式統一發票明細表
以　　下　　空　　白
東和鋼鐵企業股份有限公司
