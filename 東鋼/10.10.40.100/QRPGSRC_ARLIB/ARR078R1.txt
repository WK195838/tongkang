     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR078R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     銷貨帳款每日應收明細表－前期處理
     H*            5.DATE-WRITTEN  88/06/11
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARSTTX  IP  E           K        DISK
     F            AXREC                             KRENAMEAXRECP
     FARSTTX@ O   E           K        DISK
     I*****************************************************************
     I            DS
     I                                        1  10 AXRESV
     I                                        1   1 D#RESV
     I           UDS
     I                                      201 204 U#DAY1
     I                                      205 208 U#DAY2
     I                                      209 212 U#DAY3
     I*
     I                                      216 216 U#FLG1
     I                                      217 217 U#FLG2
     I                                      218 218 U#FLG3
     I                                      219 219 U#FLG4
     I*
     I                                      221 221 U#STC1
     I                                      222 222 U#STC2
     I                                      223 223 U#STC3
     I                                      224 224 U#STC4
     I*
     I                                      231 238 U#ETXD
     I*
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C                     EXSR DTLSR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*
     C*********************************
     C*  初始程式
     C*********************************
     CSR         INISR     BEGSR
     CSR                   ENDSR
     C*
     C*********************************
     C*  DETAIL TIME 函式
     C*********************************
     CSR         DTLSR     BEGSR                           判斷狀態碼
     C                     SELEC
     C           AXSTCD    WHEQ '1'
     C           U#STC1    IFEQ ' '
     C                     GOTO ESDTL
     C                     ENDIF
     C           AXSTCD    WHEQ '2'
     C           U#STC2    IFEQ ' '
     C                     GOTO ESDTL
     C                     ENDIF
     C           AXSTCD    WHEQ '3'
     C           U#STC3    IFEQ ' '
     C                     GOTO ESDTL
     C                     ENDIF
     C           AXSTCD    WHEQ '4'
     C           AXSTCD    OREQ '*'
     C           U#STC4    IFEQ ' '
     C                     GOTO ESDTL
     C                     ENDIF
     C                     ENDSL
     C*
     C                     MOVE U#DAY1    W#DAY1  40
     C                     MOVE U#DAY2    W#DAY2  40
     C                     MOVE U#DAY3    W#DAY3  40
     C*
     C                     MOVEL*BLANK    AXRESV
     C*
     C                     SELEC                           設定區間碼
     C           AXINDU    WHLT W#DAY1
     C                     MOVEL'1'       D#RESV
     C           AXINDU    WHLT W#DAY2
     C                     MOVEL'2'       D#RESV
     C           AXINDU    WHLT W#DAY3
     C                     MOVEL'3'       D#RESV
     C                     OTHER
     C                     MOVEL'4'       D#RESV
     C                     ENDSL
     C*
     C                     SELEC
     C           D#RESV    WHEQ '1'
     C           U#FLG1    IFEQ ' '
     C                     GOTO ESDTL
     C                     ENDIF
     C           D#RESV    WHEQ '2'
     C           U#FLG2    IFEQ ' '
     C                     GOTO ESDTL
     C                     ENDIF
     C           D#RESV    WHEQ '3'
     C           U#FLG3    IFEQ ' '
     C                     GOTO ESDTL
     C                     ENDIF
     C           D#RESV    WHEQ '4'
     C           U#FLG4    IFEQ ' '
     C                     GOTO ESDTL
     C                     ENDIF
     C                     ENDSL
     C*
     C                     WRITEAXREC                      寫入檔案
     CSR         ESDTL     ENDSR
     C*
     C*********************************
     C*  LR 函式 （印出合計）
     C*********************************
     CSR         LRSR      BEGSR
     CSR                   ENDSR
     C*
     C*********************************
     C*  查無資料函式
     C*********************************
     CSR         MSGSR     BEGSR
     CSR                   ENDSR
     C*
     C*****************************************************************
     C*  子函式集
     C*****************************************************************
