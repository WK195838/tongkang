     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR078RS
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     銷貨帳款每日應收明細表－畫面
     H*            5.DATE-WRITTEN  88/06/10
     H*            6.UPDATE  DATE  99/09/03  2010AR517  S00WCJ (9909A)
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR078S CF  E                    WORKSTN
     F                                        RRN1  KSFILE AR078F1
     FARSAID  IF  E           K        DISK
     E*************************************************************
     E                    ARY1       50 16
     E                    T#MSG   1   5 70
     I*************************************************************
     IAXDTA      UDS                             28
     I                                        1   4 X#DAY1
     I                                        5   8 X#DAY2
     I                                        9  12 X#DAY3
     I                                       21  28 X#ETXD
     I            DS
     I                                        1  16 A#DATA
     I                                        1   4 D#DEPT
     I                                        5   6 D#RVID
     I                                        7  16 D#NAME
     I           UDS
     I                                        1 200 U#MBRS
     I                                      201 204 U#DAY1
     I                                      205 208 U#DAY2
     I                                      209 212 U#DAY3
     I                                      216 216 S#FLG1
     I                                      217 217 S#FLG2
     I                                      218 218 S#FLG3
     I                                      219 219 S#FLG4
     I                                      221 221 S#STC1
     I                                      222 222 S#STC2
     I                                      223 223 S#STC3
     I                                      224 224 S#STC4
     I                                      231 238 U#ETXD
     I*
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     C**************************************************************
     C*   程式參數接收區
     C**************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P#IN03  1        結束旗標
     C**************************************************************
     C*   主程式開始
     C**************************************************************
9909AC           *DATE     SUB  19000000  U#SYSD  80
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C*   副程式開始
     C**************************************************************
     C*----------------------------------------
     C*  宣告及初始變數
     C*----------------------------------------
     CSR         SR0000    BEGSR
     C                     Z-ADD0         RRN1    40
     C*
     C                     EXSR SR1100                     初始畫面一
     C                     MOVEL'01'      W#PRID  2
     CSR                   ENDSR
     C*
     C*
     C************************************
     C*  畫面一:選擇要列印的營業員
     C************************************
     CSR         SR1000    BEGSR
     C                     WRITEAR078F1M
     C           S#SFN1    IFEQ 0
     C                     SETOF                     7273
     C                     SETON                     7174
     C                     ELSE                            有資料
     C                     SETOF                     73
     C                     SETON                     717274
     C                     ENDIF
     C                     EXFMTAR078F1C                   螢幕輸入
     C*
     C                     SETOF                     515253
     C                     MOVEL*BLANK    S#MSG1
     C           S#CRN1    IFNE 0
     C                     Z-ADDS#CRN1    S#NBR1           記錄位置
     C                     ENDIF
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C           *IN12     OREQ '1'
     C                     MOVEL'00'      W#PRID
     C                     MOVEL'1'       P#IN03
     C           *IN05     WHEQ '1'
     C                     EXSR SR1200                     清除
     C           *IN09     WHEQ '1'
     C                     EXSR SR1300                     全選
     C           *IN10     WHEQ '1'
     C                     EXSR SR1400                     存檔執行
     C*
     C                     OTHER
     C                     EXSR SR1500                     檢核
     C                     ENDSL
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  初始畫面一
     C*----------------------------------------
     CSR         SR1100    BEGSR
     C                     Z-ADD0         I       20
     C                     MOVEL*ALL'9'   ARY1
     C                     Z-ADD0         S#SFN1
     C                     Z-ADD0         RRN1
     C                     Z-ADD1         S#NBR1
     C*
     C                     SETOF                     7172
     C                     SETON                     73    清除 SF
     C                     WRITEAR078F1C
     C*
     C           *LOVAL    SETLLRARSAID              69
     C           *IN69     DOWEQ'0'
     C                     READ RARSAID                  69
     C   69                LEAVE
     C                     ADD  1         I
     C                     MOVELSADEPT    D#DEPT
     C                     MOVELSARVID    D#RVID
     C                     MOVELSANAME    D#NAME
     C                     MOVELA#DATA    ARY1,I
     C                     ENDDO
     C*
     C                     SORTAARY1
     C*
     C                     Z-ADDI         S#SFN1
     C           1         DO   S#SFN1    I
     C                     MOVELARY1,I    A#DATA
     C                     Z-ADDI         RRN1
     C                     CLEARAR078F1
     C                     MOVELD#DEPT    S#DEPT
     C                     MOVELD#RVID    S#RVID
     C                     MOVELD#NAME    S#NAME
     C                     WRITEAR078F1
     C                     ENDDO
     C*
     C           *NAMVAR   DEFN ARXCTLDTA AXDTA  28
     C                     MOVE X#DAY1    S#DAY1
     C                     MOVE X#DAY2    S#DAY2
     C                     MOVE X#DAY3    S#DAY3
     C                     MOVE X#ETXD    S#ETXD
     C*
     C                     MOVEL'Y'       S#FLG3
     C                     MOVEL'Y'       S#FLG4
     C*
     C                     MOVEL'Y'       S#STC1
     C                     MOVEL'Y'       S#STC2
     C                     MOVEL'Y'       S#STC3
     C                     MOVEL'Y'       S#STC4
     CSR                   ENDSR
     C*
     C*-------------------------------
     C*  畫面一：清除
     C*-------------------------------
     CSR         SR1200    BEGSR
     C           1         DO   S#SFN1    RRN1
     C           RRN1      CHAINAR078F1              69
     C                     MOVEL' '       S#OPT1
     C                     UPDATAR078F1
     C                     ENDDO
     CSR                   ENDSR
     C*
     C*-------------------------------
     C*  畫面一：全選
     C*-------------------------------
     CSR         SR1300    BEGSR
     C           1         DO   S#SFN1    RRN1
     C           RRN1      CHAINAR078F1              69
     C                     MOVEL'1'       S#OPT1
     C                     UPDATAR078F1
     C                     ENDDO
     CSR                   ENDSR
     C*
     C*-------------------------------
     C*  畫面一：存檔執行
     C*-------------------------------
     CSR         SR1400    BEGSR
     C                     EXSR SR1500
     C           W#RTNV    IFEQ 'F'
     C                     GOTO ES1400
     C                     ENDIF
     C*
     C                     MOVEL*BLANK    U#MBRS
     C           1         DO   S#SFN1    RRN1
     C           RRN1      CHAINAR078F1              69
     C           S#OPT1    IFNE ' '
     C           U#MBRS    CAT  '"':1     U#MBRS
     C           U#MBRS    CAT  S#RVID:0  U#MBRS
     C           U#MBRS    CAT  '"':0     U#MBRS
     C                     ENDIF
     C                     ENDDO
     C*
     C                     MOVE S#DAY1    U#DAY1
     C                     MOVE S#DAY2    U#DAY2
     C                     MOVE S#DAY3    U#DAY3
     C           S#ETXD    IFEQ 0
     C                     MOVEL*ALL'0'   U#ETXD
9909AC                     MOVE U#SYSD    U#ETXD
     C                     ELSE
     C                     MOVE S#ETXD    U#ETXD
     C                     ENDIF
     C                     MOVEL'0'       P#IN03
     C                     MOVEL'00'      W#PRID           存檔結束
     CSR         ES1400    ENDSR
     C*
     C*-------------------------------
     C*  畫面一：執行檢核
     C*-------------------------------
     CSR         SR1500    BEGSR
     C                     MOVEL'F'       W#RTNV  1
     C*
     C           S#DAY1    IFGT S#DAY2
     C                     MOVELT#MSG,1   S#MSG1
     C                     SETON                     51
     C                     GOTO ES1500
     C                     ENDIF
     C*
     C           S#DAY2    IFGT S#DAY3
     C                     MOVELT#MSG,2   S#MSG1
     C                     SETON                     51
     C                     GOTO ES1500
     C                     ENDIF
     C*
     C           S#FLG1    IFEQ ' '
     C           S#FLG2    ANDEQ' '
     C           S#FLG3    ANDEQ' '
     C           S#FLG4    ANDEQ' '
     C                     MOVELT#MSG,3   S#MSG1
     C                     SETON                     52
     C                     GOTO ES1500
     C                     ENDIF
     C*
     C           S#STC1    IFEQ ' '
     C           S#STC2    ANDEQ' '
     C           S#STC3    ANDEQ' '
     C           S#STC4    ANDEQ' '
     C                     MOVELT#MSG,4   S#MSG1
     C                     SETON                     53
     C                     GOTO ES1500
     C                     ENDIF
     C*
     C           1         DO   S#SFN1    RRN1
     C           RRN1      CHAINAR078F1              69
     C           S#OPT1    IFNE ' '
     C                     MOVEL'T'       W#RTNV
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C*
     C           W#RTNV    IFEQ 'F'
     C                     MOVELT#MSG,5   S#MSG1
     C                     ENDIF
     CSR         ES1500    ENDSR
     C*
     C*****************************************************************
** T#MSG
０１－逾期日數不得小於領款日數。
０２－逾期日數不得大於催收日數。
０３－請至少指定一個列印區間來列印。
０４－請至少指定一個應收狀態來列印。
０５－您尚未選擇任何一位營業員來列印。
