     H****************************************************************
     H*
     H*            UPDATE  DATE  99/08/25  2010AR517 S00WCJ (9908A)
     H*                         104/09/23  2015AR953 S00WCJ (0409A)
     H*                          產品代碼3碼擴5碼
     H*
     H****************************************************************
     H            Y                                     1
     FTRNDTLX IP  E           K        DISK
     F            TXREC                             KRENAMETXRECX
     FTRNDTL  UF  E           K        DISK
     FINVMST  UF  E           K        DISK                      A
     FINVDTL  UF  E           K        DISK                      A
     FHIPROD  IF  E           K        DISK
     FGENSEQ  UF  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FHSCINVL2IF  E           K        DISK
     FSAMAST  IF  E           K        DISK
     FQPRINT  O   F     160     OV     PRINTER
     I****
     ITXRECX
     I              TXCODE                          F#CODE
     I              TXNO                            F#NO
     I              TXITEM                          F#ITEM
     I              TXPCNO                          F#PCNO
     I              TXPDNM                          F#PDNM
     I              TXUPRC                          F#UPRC
     I              TXQTY                           F#QTY
     I              TXAMT                           F#AMT
     I              TXACNT                          F#ACNT
     I              TXORNO                          F#ORNO
     I              TXRESV                          F#RESV
     I              TXVUNO                          F#VUNO
     C*
     I                                              F#CODEL4
     I                                              F#PCNOL2
     I                                              F#PDNML1
     I                                              F#UPRCL1
     I****
     IAADS        DS
     I                                        1  12 GEGRP
     I                                        1   1 GE1
     I                                        2   5 GE2
     I                                        6  10 GE3
     I                                       11  20 INNOG
     I                                       11  12 INNO1
     I                                       13  200INNO2
     IBBDS        DS
     I                                        1   6 SPCUNO
     I                                        1   1 S1KIND
     I                                        2   2 S1CUN1
     I                                        3   5 S1CUN2
9008 I                                        6   6 S1CD01
LYW  I                                        7   7 S1CD02
 .   I                                        8   8 S1CD03
 .   I                                        9   9 S1CD04
 .   I                                       10  10 S1CD05
     I                                        1   5 BCNO1
     I                                        6   6 BCNO2
9008 I                                       11  19 F#ORNO
LYW  I                                       11  11 TORNO1
 .   I                                       12  160TORNO2
 .   I                                       17  19 TORNO3
0209AICCDS        DS
0209AI              '東和鋼鐵企業股份有-C         CON4                       X
0209AI              '限公司'
     I*
9105 I            DS
9105 I                                        1   9 D#ORNO
9105 I                                        1   6 D#ORN1
9105 I                                        7   90D#ORN2
CLJ  I*
     I            DS
     I                                        1   2 W#OEOF
     I                                        1   1 D#OE
     I                                        2   2 D#OF
     I            DS
     I                                        1  12 NAME1
     I                                       12  12 D#NOF
---  I*
     IARPDTA     UDS                              5
     I                                        1   50D#CONT
     I           UDS
9008 I                                      301 3060YMD
LYW  I                                      301 3040YYMM
0528 I                                      307 307 AREA
0528 I                                      308 313 ORNOX
0528 I                                     10211021 TXAR
     C*****************************************************************
     C           *ENTRY    PLIST
     C                     PARM           AREAX   1
     C*****************************************************************
0521 C           S1KEY     KLIST
0521 C                     KFLD           TORNO1
0521 C                     KFLD           TORNO2
0521 C                     KFLD           FORNO3
0521 C           CBKEY     KLIST
0521 C                     KFLD           SPCUNO
0521 C           GEKEY     KLIST
0521 C                     KFLD           GEKIND
0521 C                     KFLD           GEPRIN
0521 C           IVKEY     KLIST
0521 C                     KFLD           IVNO
0521 C                     KFLD           IVACNT
0521 C                     KFLD           IVITEM
9104 C           K#NDTL    KLIST
9104 C                     KFLD           F#CODE
9104 C                     KFLD           F#NO
9104 C                     KFLD           F#ITEM
9105 C           K#CINV    KLIST
9105 C                     KFLD           C1ORNO
9105 C                     KFLD           C1OITM
     C*----------------------------------------------------------------
9908AC           *DATE     SUB  19000000  U#SYSD  80
     C  N90                EXSR IN#01
     C  N90                SETON                     90
     C   L2 90             EXSR DL#L2
     C   L1 90             EXSR DL#L1
     C      90             EXSR DP#01
     CL1    90             EXSR TL#L1
     CL2    90             EXSR TL#L2
     CLR    90             EXSR TL#LR
     C*--------------------------------------------------*
     C*  DETAIL PROCESS L1
     C*--------------------------------------------------*
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         QTYT    70
     C                     SETOF                     28
     C                     Z-ADD0         AMTO    90
     CSR         DL#L1Z    ENDSR
     C*-----------------------------------------------*
     C*  DETAIL PROCESS L2                            *
     C*-----------------------------------------------*
     CSR         DL#L2     BEGSR
     C                     MOVEL'2'       TXIVTP
     C                     ADD  1         CURNO2
CLJX C           CURNO2    IFGT W#ENO2
     C                     EXSR SR9999
---  C                     ENDIF
     C                     Z-ADDCURNO2    INNO2
     C                     MOVE PRE2      INNO1
     C                     Z-ADD0         AMTT   112
     C                     Z-ADD0         AAMT   110
     C                     Z-ADD0         TTAMT   90
     C                     Z-ADD0         TTAMK   90
     C                     Z-ADD0         TAMTX   90
     C                     Z-ADD0         TTAMTX  90
     C                     Z-ADD0         IT
     C                     TESTN          TORNO3     31
     C     N31             MOVE 001       FORNO3  30
     C      31             MOVE TORNO3    FORNO3
0521 C           S1KEY     CHAINSAMAST               50    *NR
0521 C           CBKEY     CHAINCBCUST               51    *NR
0521 C      50             MOVEL*ALL'#'   CBMUID
0521 C      50             MOVEL*ALL'#'   CBFNAM
0521 C      50             MOVEL*ALL'#'   CBADR1
0521 C      51             MOVEL*BLANK    CBCUNM
0521 C      51             MOVEL*ALL'*'   CBMUID
0521 C      51             MOVEL*ALL'*'   CBFNAM
0521 C      51             MOVEL*ALL'*'   CBADR1
     C                     EXSR LN#CH
     C                     EXCPTED#L2
     C                     ADD  1         LCNT
     CSR         DL#L2Z    ENDSR
     C*---------------------------------------------*
     CSR         DP#01     BEGSR
     C                     MOVELF#PCNO    RESV1   6
     C                     ADD  F#QTY     QTYT
     C                     ADD  F#AMT     AMTT
     C                     ADD  F#AMT     AMTO
     C  N28      F#ACNT    COMP '3'                      28
     C  N28      F#ACNT    COMP '2'                      28
     C           K#NDTL    CHAINTXREC                40
     C                     MOVE 'C'       TXFLAG
     C                     MOVELINNOG     TXIVNO
     C                     MOVELAREA      TXTXAR
9908AC*                    Z-ADDUDATE     TXTXDT
9908AC                     Z-ADDU#SYSD    TXTXDT
     C                     MOVEL'Y'       TXFL02
     C                     UPDATTXREC
     C                     MOVELF#RESV    TPRCA   7
     C                     SETOF                     21
     C                     TESTN          TPRCA      21
     C   21                MOVE TPRCA     TPRCN   70
     C   21      TPRCN     DIV  100000    TPRC    75
     C  N21                Z-ADDF#UPRC    TPRC      H
     C                     EXSR LN#CH
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     C                     ADD  1         W#CONT  50
     CSR                   ENDSR
     C*********
     C* L1RTN *
     C*********
     CSR         TL#L1     BEGSR
0409AC                     MOVELTXPDNM    F4NAME
0409AC           F4NAME    CHAINHIPROD               20
CLJ  C* N20                MOVE ACPRNM    NAME1  12
     C           *IN20     IFEQ '0'
     C                     MOVE ''      W#OEOF
     C                     MOVELF4CHIN    NAME1
     C                     MOVE D#OF      D#NOF
---  C                     ENDIF
     C*9105 匯率換算(外銷發票開立時,美金單價*報關匯率=
     C* 台幣金額,故每筆銷貨之單價與金額皆以CI為準)
9105 C                     MOVE F#ORNO    D#ORNO
9105 C                     MOVE D#ORN1    C1ORNO
9105 C                     Z-ADDD#ORN2    C1OITM
9105 C           K#CINV    CHAINRHSCINV              98
     C*重新計算金額
9105 C  N98      C1UPRC    MULT C1TRAT    F#UPRC    H
9105 C  N98      QTYT      MULT F#UPRC    TAMTO     H
9105 C*判斷是否為最後一筆
9105 C                     MOVE F#RESV    W#FINL  3
9105 C  N98      W#CONT    IFEQ D#CONT
9105 C                     Z-ADDW#FLAM    TAMTO
9105 C                     ELSE
9105 C                     SUB  TAMTO     W#FLAM
9105 C                     ENDIF
     C*
9105 C  N28 98   QTYT      MULT TPRC      TAMTO   90H
9105 C  N28 98             Z-ADDTAMTO     AMTO
9105 C   28 98             Z-ADDAMTO      TAMTO     H
     C           QTYT      IFNE 0
     C           TAMTO     DIV  QTYT      UPO     75H
     C                     END
     C                     ADD  TAMTO     TTAMT   90
     C                     ADD  1         IT      20
     C                     MOVELINNOG     IVNO
     C                     MOVE '1'       IVACNT
     C                     Z-ADDIT        IVITEM
     C           IVKEY     CHAININVDTL               37
     C   37                EXSR WR#DL1
     C   37                EXCPTET#L1
     C   37                ADD  1         LCNT
     C  N37                EXCPTBRERR
     C  N37                ADD  1         LCNT
     C*9205-------------一張發票最多有七項
     C   37      IT        IFEQ 7
     C                     SETON                     L2
     C                     ENDIF
     C*9205-------------
     CSR                   ENDSR
     C*********
     C* L2RTN *
     C*********
     CSR         TL#L2     BEGSR
     C                     Z-ADD0         UPO
     C                     Z-ADD0         QTYT
     C                     MOVELINNOG     INNO
     C           INNO      CHAININVMST               37
     C   37                EXSR WR#MS
     C                     EXSR LN#CH
     C                     EXCPTET#L2
     C                     ADD  3         LCNT
     CSR         TL#L2Z    ENDSR
     C*---------------------------------------------------*
     CSR         TL#LR     BEGSR
     C                     MOVE '01'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVELAREAX     GE1
9008 C                     MOVE YYMM      GE2
     C                     MOVEL*BLANK    GE3
     C                     MOVELGEGRP     GEPRIN
     C           GEKEY     CHAINGENSEQ               H1
     C                     Z-ADDCURNO2    GECUNO
     C                     UPDATGEREC
     C*****
     CSR                   ENDSR
     C*----------------------------------------------*
     CSR         IN#01     BEGSR
     C                     MOVE '01'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVELAREAX     GE1
9008 C                     MOVE YYMM      GE2
     C                     MOVEL*BLANK    GE3
     C                     MOVELGEGRP     GEPRIN
     C           GEKEY     CHAINGENSEQ               H1
     C                     MOVELGEPRE     PRE2    2
     C                     Z-ADDGECUNO    CURNO2  80
CLJX C                     Z-ADDGEENNO    W#ENO2  80
     C*****
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD60        TCNT    20
     C***
     C                     SELEC
     C           AREA      WHEQ 'P'
     C                     MOVEL'台北'  AREAC   6
     C           AREA      WHEQ 'K'
     C                     MOVEL'高雄'  AREAC
     C           AREA      WHEQ 'M'
     C                     MOVEL'台中'  AREAC
     C           AREA      WHEQ 'T'
     C                     MOVEL'桃園'  AREAC
     C           AREA      WHEQ 'L'
     C                     MOVEL'林口'  AREAC
     C           AREA      WHEQ 'H'
     C                     MOVEL'苗栗'  AREAC
     C                     OTHER
     C                     MOVEL'　　'  AREAC
     C                     ENDSL
     C*
9105 C                     MOVE ORNOX     C1ORNO
9105 C                     Z-ADD0         C1OITM
9105 C           K#CINV    SETLLRHSCINV              98    一定找到
9105 C                     READ RHSCINV                  98一定找到
     C*重新計算金額
     C  N98                SELEC
     C           TXAR      WHEQ 'M'
     C                     Z-ADDC1AMTM    W#FLAM 100       報關金額
     C           TXAR      WHEQ 'T'
     C                     Z-ADDC1AMTT    W#FLAM
     C           TXAR      WHEQ 'H'
     C                     Z-ADDC1AMTH    W#FLAM
     C           TXAR      WHEQ 'K'
     C                     Z-ADDC1AMTK    W#FLAM
     C                     ENDSL
     C*
     C           *NAMVAR   DEFN ARP04DTA  ARPDTA  5
     C           *LOCK     IN   ARPDTA
     C                     UNLCKARPDTA
     CSR         IN#01Z    ENDSR
     C*---------------------------------------------------*
     CSR         LN#CH     BEGSR
     C           LCNT      IFGT TCNT
     C                     ADD  1         PAG
     C                     EXCPTEH#01
     C                     Z-ADD6         LCNT
     C                     ENDIF
     CSR                   ENDSR
     C*---------------------------------------------------*
     CSR         WR#DL1    BEGSR
     C                     CLEARIVREC
     C                     MOVE 'A'       IVFLAG
     C                     MOVELINNOG     IVNO
     C                     MOVE '1'       IVACNT
     C                     Z-ADDIT        IVITEM
9008 C                     MOVE YMD       IVACDT
     C                     MOVELTXORNO    IVORNO
     C                     MOVELTXPDNM    IVPDCD
     C                     Z-ADDQTYT      IVQTY
     C           UPO       MULT 100000    UPOB    70
     C                     MOVE UPOB      SAV2    2
     C                     MOVELSAV2      IVRESV
     C                     Z-ADDUPO       IVUPRC
     C                     Z-ADDTAMTO     IVAMT
     C                     MOVELTXNO      IVAPNO
     C                     MOVE 'Y'       IVFL02
     C           TXCODE    IFEQ 'SA04'
     C                     MOVE 'A'       IVFL03
     C                     ELSE
     C                     MOVE 'B'       IVFL03
     C                     ENDIF
     C                     MOVELAREA      IVTXAR
9908AC*                    Z-ADDUDATE     IVTXDT
9908AC                     Z-ADDU#SYSD    IVTXDT
     C                     WRITEIVREC
     C                     ADD  IVAMT     AAMT   110
     CSR                   ENDSR
     C*---------------------------------------------------*
     CSR         WR#MS     BEGSR
     C                     CLEARINREC
     C                     MOVE 'A'       INFLAG
     C                     MOVELINNOG     INNO
     C                     MOVELSPCUNO    INCUNO
     C                     MOVELCBCUNM    INCUNM
     C                     MOVELF#PCNO    INORNO
9008 C                     MOVE YMD       ININDT
     C                     MOVEL'2'       INKIND
     C                     MOVELTXSALE    INSALE
     C                     MOVELTXSATP    INSATP
     C                     MOVELTXRVID    INRVID
     C                     MOVEL'1'       INTYPE
     C****
     C                     MOVELAREA      INAREA
     C                     MOVEL'2'       INTXTP
     C                     Z-ADDAAMT      INAAMT
     C                     Z-ADDAAMT      INNBAL
     C                     MOVELAREA      INTXAR
9908AC*                    Z-ADDUDATE     INTXDT
9908AC                     Z-ADDU#SYSD    INTXDT
9104 C                     MOVE F#PCNO    W#SEQ1  20
9104 C                     Z-ADDW#SEQ1    W#SEQ2  30
9104 C                     MOVELW#SEQ2    INRESV
     C                     WRITEINREC
     CSR                   ENDSR
     C*
     C*---------------------------------------------------------
CLJX C* 字軌用完之異常處理
     C*---------------------------------------------------------
     CSR         SR9999    BEGSR
     C           CURNO2    IFGT W#ENO2
     C                     Z-ADDW#ENO2    CURNO2
     C                     ENDIF
     C*
     C                     EXSR TL#LR                      字軌存檔
     C                     CALL 'ARP056P'
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C*************************************************************
     C*====================================================
     OQPRINT  E 2 01           EH#01
     O                                   11 '報表編號:'
     O                                   17 'AR004'
0209AO                         CON4      75                                    X
     O                                  119 '印表日期'
     O                                  120 ':'
9908AO*                        UDATE Y  129
9908AO                         U#SYSDZ  129
     O        E 1              EH#01
     O                                   10 '廠　　別'
     O                                   11 ':'
     O                         AREAC   +  1
     O                                   78 '過入發票核對表（外銷）'
     O                                  119 '頁　　數'
     O                                  120 ':'
     O                         PAG   Z  125
     O        E 2              EH#01
     O                                    8 '訂　單'
     O                                   17 '日　期'
     O                                   26 '磅　單'
     O                                   36 '品名'
     O                                   42 '重量'
     O                                   44 'kg'
     O                                   54 '單價'
     O                                   69 '金　　額'
     O                                   84 '品　　名'
     O                                   94 '重　量'
     O                                  106 '單價'
     O                                  121 '金　　額'
     O                                  138 '發票號碼'
     O                                  146 '業務'
     O        E 1              EH#01
     O                                    8 '-------'
     O                                   17 '--------'
     O                                   28 '----------'
     O                                   34 '---'
     O                                   44 '--------'
     O                                   54 '------'
     O                                   69 '-------------'
     O                                   84 '------------'
     O                                   94 '---------'
     O                                  106 '------'
     O                                  121 '-----------'
     O*                                 130 '--------'
     O                                  138 '--------'
     O                                  146 '------'
     O        E 1      L2      ED#L2
     O                         CBCUNM    10
     O                         CBMUID    20
     O                         CBFNAM    56
     O                         CBADR1   118
     O                                  132 '二聯式'
     O        E 1              ED#01
     O                         RESV1      6
     O                         TXDATEY   17
     O                         TXITEM    26
     O                         TXNO      26
     O                         TXACNT    30
0409AO                         TXPDNM  +  0
     O                         TXQTY J   44
     O                         TPRC  1   54
     O                         TXAMT J   69
     O        E 1              ET#L1
     O                         NAME1     84
     O                         QTYT  J   94
     O                         AMTO  J  120
     O                         UPO   1  108
     O        E 1              ET#L2
     O                                   84 '總　計'
     O                         INNOG    138
     O                         AAMT  J  120
     O                                  108 '=====================>'
     O                         TXSALE   144
     O        E  1     L1N37   BRERR
     O                                   67 '號碼重覆'
