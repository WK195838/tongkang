     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR098R1
     H*            2.PROGRAM-TYPE  RPG
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     自製出貨分析表　　
     H*            5.DATE-WRITTEN  91/09/24
     H*            6.DATE-MODIFY
     H*
     H*            *將外購成品出貨明細表寫入ARR098F2,辨別碼=2
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTL  IP  E           K        DISK
     FWTMASPL4IF  E           K        DISK
     FARR098F1O   E           K        DISK
     ITXREC
     I                                              TXPDNML1
     I            DS
     I                                        1   8 TXNO
     I                                        1   1 W6AREA
     I                                        2   3 W6VNO1
     I                                        4   80W6VNO2
     I           UDS
     I                                      410 410 D#AREA
     I                                      411 4180T#DATS
     I                                      419 4260T#DATE
     I                                      427 429 T#PDNS
     I                                      430 432 T#PDNE
     C*****************************************************************
     C*          Main      Program
     C*****************************************************************
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     SETON                       99
     C*
     C           K#MASP    KLIST
     C                     KFLD           W6ENTD
     C                     KFLD           W6AREA
     C                     KFLD           W6VNO1
     C                     KFLD           W6VNO2
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     Z-ADD0         W#QTY            *出貨數量
     C                     Z-ADD0         W#AMT            *出貨金額
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     Z-ADDTXDATE    W6ENTD
     C           K#MASP    CHAINRWTMASP             N40
     C  N40      W6TRCD    IFEQ '1E'
     C*          W6WTNO    ANDNE'G'
     C*          W6WTNO    ANDNE'F'
 9111C                     ADD  TXQTY     W#QTY   90       數量
     C                     ADD  TXAMT     W#AMT  110       金額
     C*
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C                     MOVELD#AREA    F1AREA           廠區
     C                     MOVELTXPDNM    F1PDNM           品名
     C                     MOVELTXPDNM    W#PDN1  2
     C           W#PDN1    IFEQ '37'
     C           W#PDN1    OREQ '3A'
     C                     MOVEL'B'       F1PDN1
     C                     ELSE
     C                     MOVEL'A'       F1PDN1
     C                     ENDIF
     C                     MOVEL'2'       F1CODE           辨別碼
     C                     Z-ADDW#QTY     F1QTY4           數量
     C                     Z-ADDW#AMT     F1AMT4           收入
     C                     Z-ADDW#QTY     F1QTY            銷貨淨數量
     C                     Z-ADDW#AMT     F1AMT            銷貨淨收入
     C                     WRITERAR098F1
     C*
     C                     ENDSR
