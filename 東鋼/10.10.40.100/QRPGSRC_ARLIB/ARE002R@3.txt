     H        1   Y                                     1                 BR003
     FARE002S CF  E                    WORKSTN
     F                                        RRN   KSFILE SF1
     FTRNDTL  UF  E           K        DISK                      A
     FSAMAST  IF  E           K        DISK
     FHIPROD  IF  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FGENSEQ  UF  E           K        DISK                      A
9112 FARDLWT  UF  E           K        DISK
8902 FAMCTRL  IF  E           K        DISK
  .  FACCTRL  IF  E           K        DISK
     E*************************************************************
     E                    ERR     1  19 70
     I*************************************************************
8902 I            DS
9008 I                                        1   80D#DATE
LYW  I                                        1   60D#YYMM
     IAADS        DS
     I                                        1   6 W1CUNO
     I                                        1   1 S1KIND
     I                                        2   2 S1CUN1
     I                                        3   5 S1CUN2
9008 I                                        6   6 S1CD01
     I                                       11  20 GRP
     I                                       11  11 GE1
     I                                       12  20 GE2
     I                                       21  26 NOG
     I                                       21  21 NOA
     I                                       22  260NOB
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           GEKEY     KLIST
     C                     KFLD           GEKIND
     C                     KFLD           GEPRIN
     C           TXKEY     KLIST
     C                     KFLD           TXCODE
     C                     KFLD           TXNO
     C                     KFLD           TXITEM
     C           TX1KEY    KLIST
     C                     KFLD           TXCODE
     C                     KFLD           TXNO
     C           S1KEY     KLIST
     C                     KFLD           S1OREA
     C                     KFLD           S1ORNO
     C                     KFLD           S1ORTM
     C           CBKEY     KLIST
     C                     KFLD           CBCUNO
8901 C           K#CTRL    KLIST
 .   C                     KFLD           A5AREA
 .   C                     KFLD           A5YYMM
8901 C           K#CTRC    KLIST
 .   C                     KFLD           AAAREA
 .   C                     KFLD           AAYYMM
 .   C                     KFLD           AAFSYS
9112 C*FILE==>ARDLWT
 .   C           W#DLWT    KLIST
 .   C                     KFLD           AWORNO
9112 C                     KFLD           AWWTNO
     C**************************************************************
     C  N90                MOVE '1'       SCRN    1
     C  N90                MOVE '1'       *IN,90
9112 C*用50燈號來判斷是要寫入ARDLWT調整單號或清空ARDLWT調整單號
9112 C                     MOVE *OFF      *IN50
     C**************************************************************
     C           *IN03     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01            畫面一
     C           SCRN      CASEQ'2'       SR#02            畫面二
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     CSR         SR#01     BEGSR
     C                     EXFMTTITLE
     C   KC                EXSR KC#01                      離開
     C                     EXSR CK#01                      CHECK畫面一
     C  N99                EXSR PR#02
     C  N99                MOVE '2'       SCRN
     CSR         SR#01Z    ENDSR
     C******
     CSR         SR#02     BEGSR
     C                     WRITEUND01
     C                     EXFMTSFCTL1
     C   KC                EXSR KC#01                      離開
     C   KL                MOVE '1'       SCRN             回上頁
     C   KL                MOVE *BLANK    ERRMSG
     C   KL                GOTO SR#02Z
     C   KJ                EXSR KJ#02                      存檔
     C   KJ                GOTO SR#02Z
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C                     ELSE
     C           CODE      IFEQ '4'
     C                     MOVE '1'       SCRN
     C                     ENDIF
     C                     ENDIF
     CSR         SR#02Z    ENDSR
     C*****
     CSR         KC#01     BEGSR                           離開
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C****
     CSR         KJ#02     BEGSR
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C                     ENDIF
     C  N99                EXSR FL#02
     C  N99                MOVE '1'       SCRN
     CSR         KJ#02Z    ENDSR
     C****
     C*CHECK畫面一
     CSR         CK#01     BEGSR
     C                     SETOF                     414299
     C                     SETOF                     43
     C                     MOVE *BLANK    ERRMSG
     C                     SELEC
     C           CODE      WHEQ ' '
     C                     SETON                     4199
     C                     MOVE ERR,1     ERRMSG
     C           CODE      WHEQ '1'
     C                     MOVE '新增'  MOD
     C                     MOVE '0'       *IN71
     C           CODE      WHEQ '2'
     C                     MOVE '更正'  MOD
     C                     MOVE '0'       *IN71
     C           CODE      WHEQ '3'
     C                     MOVE '作廢'  MOD
     C                     MOVE '1'       *IN71
     C           CODE      WHEQ '4'
     C                     MOVE '查詢'  MOD
     C                     MOVE '1'       *IN71
     C                     OTHER
     C                     SETON                     4199
     C                     MOVE ERR,2     ERRMSG
     C                     ENDSL
     C******
     C*非新增時，需輸入調整單號
     C           CODE      IFNE '1'
     C           NO        ANDEQ*BLANK
     C                     SETON                     4299
     C                     MOVE ERR,3     ERRMSG
     C                     ENDIF
     C**
     C*新增時，不可輸入調整單號
     C           CODE      IFEQ '1'
     C           NO        ANDNE*BLANK
     C                     SETON                     4299
     C                     MOVE ERR,15    ERRMSG
     C                     ENDIF
     C**
　 C*若此張調整單非屬本廠區，不可更改或刪除
     C           CODE      IFEQ '2'
     C           CODE      OREQ '3'
     C                     MOVELNO        NOAREA  1        調整單號第一碼
     C           NOAREA    IFNE TXAR
     C                     SETON                     4299
     C                     MOVE ERR,16    ERRMSG
     C                     ENDIF
     C                     ENDIF
     C**
     C           *IN99     IFEQ '0'
     C                     MOVEL'AR05'    TXCODE
     C                     MOVEL*BLANK    TXNO
     C                     MOVELNO        TXNO
     C                     Z-ADD1         TXITEM
     C           TXKEY     CHAINTRNDTL              N97
     C                     SELEC
　 C*此張調整單已存在
     C           CODE      WHEQ '1'
     C           *IN97     ANDEQ'0'
     C                     SETON                     4299
     C                     MOVE ERR,4     ERRMSG
　 C*此張調整單已開發票，不可再修改
     C           CODE      WHEQ '2'
     C           *IN97     ANDEQ'0'
     C           TXFL02    ANDNE' '
     C                     SETON                     4299
     C                     MOVE ERR,5     ERRMSG
　 C*此張調整單已開發票，不可再刪除
     C           CODE      WHEQ '3'
     C           *IN97     ANDEQ'0'
     C           TXFL02    ANDNE' '
     C                     SETON                     4299
     C                     MOVE ERR,6     ERRMSG
　 C*此張調整單不存在
     C*          CODE      WHNE '1'
     C*          *IN97     ANDEQ'1'
     C*                    SETON                     4299
     C*                    MOVE ERR,7     ERRMSG
     C                     ENDSL
8902 C*檢核是否關帳 (台北用ACCTRL 廠區用AMCTRL)
  .  C           *IN97     IFEQ *OFF
  .  C           CODE      ANDNE'4'
  .  C           TXAR      IFNE 'P'
  .  C                     MOVELTXNO      A5AREA
  .  C                     Z-ADDTXDATE    D#DATE
  .  C                     Z-ADDD#YYMM    A5YYMM
  .  C           K#CTRL    CHAINRAMCTRL             N60
  .  C           *IN60     IFEQ *OFF
  .  C           A5FG12    ANDEQ'Y'
  .  C                     SETON                     4299
  .  C                     MOVE ERR,19    ERRMSG
  .  C                     ENDIF
  .  C                     ELSE
  .  C                     MOVELTXNO      AAAREA
  .  C                     Z-ADDTXDATE    D#DATE
  .  C                     Z-ADDD#YYMM    AAYYMM
  .  C                     MOVE 'SA'      AAFSYS
  .  C           K#CTRC    CHAINRACCTRL             N60
  .  C           *IN60     IFEQ *OFF
  .  C           AAESFG    ANDEQ'Y'
  .  C                     SETON                     4299
  .  C                     MOVE ERR,19    ERRMSG
  .  C                     ENDIF
  .  C*
  .  C                     ENDIF                           * TXAR
  .  C*
  .  C                     ENDIF                           * IN97
  .  C*
     C                     ENDIF                           * IN99
     CSR         CK#01Z    ENDSR
     C******
     CSR         PR#02     BEGSR
     C*
     C                     SETON                     80    SFL CLEAR
     C                     WRITESFCTL1
     C                     SETOF                     80
     C*
     C                     Z-ADD0         RRN     20
     C                     MOVE *BLANK    ORNO             訂單編號
     C                     MOVE *BLANK    CUNO             客戶代號
     C                     MOVE *BLANK    CUNM             客戶名稱
     C                     Z-ADDUDATE     DATE
     C                     Z-ADD0         TTLAMT           合計
     C**
     C           CODE      CASEQ'1'       PR#ADD
     C                     CAS            PR#OTH
     C                     ENDCS
     CSR         PR#02Z    ENDSR
     C******
     C******
     CSR         PR#ADD    BEGSR
     C                     EXSR @GETNO
     C                     CLEARSF1
     C           RRN       DOWLT24
     C                     ADD  1         RRN
     C                     Z-ADDRRN       ITEM
     C                     WRITESF1
     C                     ENDDO
     C                     SETON                     10
     CSR         PR#ADZ    ENDSR
     C******
     C******
     C*取得調整單號
     CSR         @GETNO    BEGSR
     C                     MOVEL'05'      GEKIND           編碼種類
     C                     MOVE *BLANK    GEPRIN           編碼原則
     C                     MOVE *BLANK    GRP
     C                     MOVELTXAR      GE1
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ              N97
     C   97                Z-ADD0         GECUNO           目前號碼
     C                     MOVELTXAR      NOA
     C           GECUNO    ADD  1         NOB
     C                     MOVELNOG       NO
     CSR                   ENDSR
     C***
     CSR         PR#OTH    BEGSR
     C                     MOVELTXORNO    ORNO
     C                     MOVELTXCUNO    CUNO
     C                     MOVELTXCUNM    CUNM
     C                     Z-ADDTXDATE    DATE
     C                     MOVEL'AR05'    TXCODE
     C                     MOVEL*BLANK    TXNO
     C                     MOVELNO        TXNO
     C                     Z-ADD0         TXITEM
     C           TXKEY     SETLLTRNDTL               97
     C  N97      TX1KEY    READETRNDTL                   97
     C           *IN97     DOWEQ'0'
     C                     CLEARSF1
     C   71                SETON                     70
     C                     ADD  1         RRN
     C           RRN       IFNE TXITEM
     C           RRN       DOWLTTXITEM
     C                     Z-ADDRRN       ITEM
     C                     WRITESF1
     C                     ADD  1         RRN
     C                     ENDDO
     C                     ENDIF
     C                     Z-ADDTXITEM    ITEM
     C                     MOVELTXACNT    JUST
     C                     MOVELTXPDNM    PDNM
     C                     Z-ADDTXQTY     QTY
     C                     Z-ADDTXUPRC    PRC
     C                     Z-ADDTXAMT     AMT
     C                     MOVELTXPCNO    PNO
     C                     MOVELTXFLAG    FLAG
     C           TXFLAG    IFNE 'D'
     C                     ADD  TXAMT     TTLAMT
     C                     ENDIF
     C                     WRITESF1
     C           TX1KEY    READETRNDTL                   97
     C                     ENDDO
     C****
     C           CODE      IFEQ '2'
     C                     CLEARSF1
     C           RRN       DOWLE23
     C                     ADD  1         RRN
     C                     Z-ADDRRN       ITEM
     C                     WRITESF1
     C                     ENDDO
     C                     ENDIF
     C                     SETON                     10
     CSR         PR#OTZ    ENDSR
     C******
     CSR         CK#02     BEGSR
     C                     SETOF                     99
     C                     MOVEA'00'      *IN,43
     C                     MOVE *BLANK    ERRMSG
     C                     Z-ADD0         TTLAMT
     C***
　 C*訂單編號必須輸入
     C                     MOVE *BLANK    CUNO
     C                     MOVE *BLANK    CUNM
     C           ORNO      IFEQ *BLANK
     C                     SETON                     4399
     C                     MOVELERR,12    ERRMSG
     C                     ELSE
     C                     MOVELORNO      S1OREA
     C                     MOVE ORNO      S1ORNO
     C                     Z-ADD1         S1ORTM
     C           S1KEY     CHAINSAMAST               97
     C*訂單編號不存在
     C   97                SETON                     4399
     C   97                MOVELERR,8     ERRMSG
9008 C  N97                MOVELS1CD01    BK05    1        分公司碼
     C  N97                MOVELW1CUNO    CUNO
     C  N97                MOVE BK05      CUNO
     C  N97                MOVELS1RECV    RVID    1        收款業務員
     C  N97                MOVELS1SND     SALE    1        交款業務員
     C***
     C  N97                MOVELCUNO      CBCUNO
     C  N97      CBKEY     CHAINCBCUST               97
     C  N97                MOVELCBCUNM    CUNM             訂單編號
     C  N97                MOVELCBSANO    TYPE    1        銷售別
     C  N97                MOVELCBIVCO    CHK     1        發票別
     C                     ENDIF
     C***
     C*                    CALL 'C01'
     C*                    PARM           DATE
     C*                    PARM           FLAG1   1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DATE      P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           FLAG1   1
　 C*單據日期錯誤
     C           FLAG1     IFNE '0'
     C                     SETON                     9944
     C                     MOVELERR,13    ERRMSG
     C                     ENDIF
     C***
     C                     Z-ADD0         RRN
     C                     MOVE '0'       *IN95
     C           *IN95     DOWEQ'0'
     C                     MOVEA'000000'  *IN,51
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  95
     C   95                LEAVE
     C           JUST      IFEQ *BLANK                     調整別
     C           PDNM      ANDEQ*BLANK                     品名
     C                     ITER
     C                     ENDIF
     C***
     C                     SELEC
     C*CHECK調整別
     C           JUST      WHEQ ' '
     C                     SETON                     9951
     C                     MOVE ERR,9     ERRMSG
     C           JUST      WHNE '1'
     C           JUST      ANDNE'2'
     C           JUST      ANDNE'3'
     C           JUST      ANDNE'4'
     C                     SETON                     9951
     C                     MOVE ERR,10    ERRMSG
     C                     ENDSL
     C***
     C*CHECK品名代號
     C           PDNM      IFNE *BLANK
     C           PDNM      CHAINHIPROD               97
     C   97                SETON                     9952
     C   97                MOVE ERR,11    ERRMSG
     C                     ENDIF
     C***
     C           AMT       IFEQ 0
     C           QTY       MULT PRC       AMT       H
     C                     ENDIF
     C***
     C*CHECK金額不可空白
     C           AMT       IFEQ 0
     C                     SETON                     9955
     C                     MOVELERR,14    ERRMSG
     C                     ENDIF
     C****
     C           FLAG      IFNE 'D'                        標記
     C                     ADD  AMT       TTLAMT
     C                     ENDIF
     C***
     C*CHECK磅單編號不可空白
     C           PNO       IFEQ *BLANK                     磅單編號
     C                     SETON                     9956
     C                     MOVELERR,17    ERRMSG
     C                     ELSE
     C                     MOVEL'SA04'    TXCODE
     C                     MOVEL*BLANK    TXNO
     C                     MOVELPNO       TXNO             磅單編號
     C                     Z-ADD1         TXITEM
     C           TXKEY     CHAINTRNDTL              N97
     C*CHECK磅單編號必須存在
     C   97                SETON                     9956
     C   97                MOVELERR,18    ERRMSG
     C                     ENDIF
     C***
     C                     UPDATSF1
     C***
     C                     ENDDO
     C******
     CSR         CK#02Z    ENDSR
     C******
     CSR         FL#02     BEGSR
     C           CODE      CASEQ'3'       DL#02            刪除
     C           CODE      CASEQ'1'       WR#02            新增
     C           CODE      CASEQ'2'       UP#02            更新
     C                     ENDCS
     CSR         FL#02Z    ENDSR
     C****
     CSR         DL#02     BEGSR
     C***
     C                     MOVE 'AR05'    TXCODE
     C                     MOVEL*BLANK    TXNO
     C                     MOVELNO        TXNO
     C                     Z-ADD0         TXITEM
     C           TXKEY     SETLLTRNDTL               97
     C  N97      TX1KEY    READETRNDTL                   97
     C           *IN97     DOWEQ'0'
     C                     MOVE 'D'       TXFLAG
     C                     MOVE TXAR      TXTXAR           異動廠區
     C                     Z-ADDUDATE     TXTXDT           異動日期
     C                     UPDATTXREC
 9112C                     MOVE *ON       *IN50
 9112C                     EXSR UP#DW                      清空ARDLWT調整單
     C           TX1KEY    READETRNDTL                   97
     C                     ENDDO
     CSR         DL#02Z    ENDSR
     *****
     CSR         WR#02     BEGSR
     C                     CLEARGEREC
     C                     MOVEL'05'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVE *BLANK    GRP
     C                     MOVELTXAR      GE1
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ               97
     C   97                Z-ADD1         GECUNO
     C   97                WRITEGEREC
     C  N97                ADD  1         GECUNO
     C  N97                UPDATGEREC
     C                     MOVELTXAR      NOA
     C                     Z-ADDGECUNO    NOB
     C                     MOVELNOG       NO
     C***
     C                     SETOF                     95
     C                     Z-ADD0         RRN
     C           *IN95     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  95
     C   95                LEAVE
     C***
     C           JUST      IFEQ *BLANK                     調整別
     C           PDNM      ANDEQ*BLANK                     品名
     C           FLAG      OREQ 'D'                        標記
     C                     ITER
     C                     ENDIF
     C****
     C                     EXSR WR#TX                      寫入TRNDTL
 9112C                     MOVE *OFF      *IN50
 9112C                     EXSR UP#DW                      寫入ARDLWT
     C***
     C                     ENDDO
     C***
     CSR         WR#02Z    ENDSR
     *****
     CSR         UP#02     BEGSR
     C***
     C                     Z-ADD0         RRN
     C                     SETOF                     95
     C           *IN95     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  95
     C   95                LEAVE
     C                     MOVE 'AR05'    TXCODE
     C                     MOVEL*BLANK    TXNO
     C                     MOVELNO        TXNO
     C                     Z-ADDRRN       TXITEM
     C           TXKEY     CHAINTRNDTL               96
     C           JUST      IFEQ *BLANK                     調整別
     C           PDNM      ANDEQ*BLANK                     品名
     C           FLAG      OREQ 'D'                        標記
     C   96                ITER
     C                     MOVE 'D'       TXFLAG
     C                     MOVE TXAR      TXTXAR
     C                     Z-ADDUDATE     TXTXDT
     C                     UPDATTXREC
 9112C                     MOVE *ON       *IN50
 9112C                     EXSR UP#DW                      清空ARDLWT調整單
     C                     ELSE
     C  N96                EXSR UP#TX
     C   96                EXSR WR#TX
 9112C                     MOVE *OFF      *IN50
 9112C                     EXSR UP#DW                      寫入ARDLWT
     C                     ENDIF
     C***
     C                     ENDDO
     C***
     CSR         UP#02Z    ENDSR
     C***************************
     C***  WRITE  TO  TRNDTL  ***
     C***************************
     CSR         WR#TX     BEGSR
     C                     CLEARTXREC
     C                     MOVE 'AR05'    TXCODE
     C                     MOVELNO        TXNO
     C                     MOVEL'A'       TXFLAG
     C                     Z-ADDRRN       TXITEM
     C                     MOVELJUST      TXACNT
     C                     Z-ADDDATE      TXDATE
     C                     Z-ADDDATE      TXACDT
     C                     MOVELCUNO      TXCUNO
     C                     MOVELCUNM      TXCUNM
     C                     MOVELORNO      TXORNO
     C                     MOVELTYPE      TXSATP
     C                     MOVELRVID      TXRVID
     C                     MOVELSALE      TXSALE
     C                     MOVELCHK       TXIVTP
     C                     MOVELPDNM      TXPDNM
     C                     Z-ADDQTY       TXQTY
     C                     Z-ADDPRC       TXUPRC
     C                     Z-ADDAMT       TXAMT
     C                     MOVELPNO       TXPCNO           磅單號碼
     C                     MOVELTXAR      TXTXAR
     C                     Z-ADDUDATE     TXTXDT
     C                     WRITETXREC
     CSR         WR#TXZ    ENDSR
     C***************************
     C***  UPDATE TO  TRNDTL  ***
     C***************************
     CSR         UP#TX     BEGSR
     C                     MOVE 'C'       TXFLAG
     C                     MOVELJUST      TXACNT
     C                     Z-ADDDATE      TXDATE
     C                     Z-ADDDATE      TXACDT
     C                     MOVELCUNO      TXCUNO
     C                     MOVELCUNM      TXCUNM
     C                     MOVELORNO      TXORNO
     C                     MOVELTYPE      TXSATP
     C                     MOVELRVID      TXRVID
     C                     MOVELSALE      TXSALE
     C                     MOVELCHK       TXIVTP
     C                     MOVELPDNM      TXPDNM
     C                     Z-ADDQTY       TXQTY
     C                     Z-ADDPRC       TXUPRC
     C                     Z-ADDAMT       TXAMT
     C                     MOVELPNO       TXPCNO
     C                     MOVELTXAR      TXTXAR
     C                     Z-ADDUDATE     TXTXDT
     C                     UPDATTXREC
     CSR         UP#TXZ    ENDSR
 9112C**************************
  .  C***  UPDATE TO ARDLWT  ***
  .  C**************************
  .  CSR         UP#DW     BEGSR
  .  C*寫入ARDLWT調整單號
  .  C                     MOVELTXORNO    AWORNO           訂單編號
  .  C                     MOVELTXPCNO    AWWTNO           磅單編號
  .  C           W#DLWT    CHAINRARDLWT              40
  .  C           *IN50     IFEQ *OFF
  .  C                     MOVELTXNO      AWTXNO           調整單號
  .  C                     Z-ADDTXDATE    AWTXDT           調整單開立日
  .  C                     MOVEL'Y'       AWCOD1           調整開立碼
  .  C                     ELSE
  .  C*清空ARDLWT調整單號
  .  C                     MOVEL*BLANK    AWTXNO           調整單號
  .  C                     Z-ADD0         AWTXDT           調整單開立日
  .  C                     MOVEL*BLANK    AWCOD1           調整開立碼
  .  C                     ENDIF
  .  C*
  .  C  N40                UPDATRARDLWT
  .  CSR                   ENDSR
 9112C*
** ERR
功能代碼必須輸入．
功能代碼必須為１，２，３，４，
非新增時，調整單號必須輸入
此張調整單已存在
此張調整單已開發票，不可再修改
此張調整單已開發票，不可再刪除
此張調整單不存在
訂單編號不存在
調整別必須輸入
調整別必須為１，２，３，４
品名代號不存在
訂單編號必須輸入
單據日期錯誤
金額必須輸入
新增時，調整單號不可輸入
此張調整單非屬本廠區，不可更改或刪除
磅單編號必須輸入
磅單編號不存在
財會單位已關帳,不允許修改或作廢,請通知財會相關人員
