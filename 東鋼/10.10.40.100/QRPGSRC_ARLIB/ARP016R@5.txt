     H****************************************************************
     H*
     H*            UPDATE  DATE  99/08/26  2010AR517  S00WCJ (9908A)
     H*                         100/11/28  2011AR634 S00WCJ (0011A)
     H*                          配合IFRS修改
     H*                         101/09/20  2012AR682 S00WCJ (0109A)
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FRCVDTLL1IP  E           K        DISK
     FRCVDTL  UF  E           K        DISK
     F            RVREC                             KRENAMERVREC1
     FBANKAC  IF  E           K        DISK
     FACTRAN  UF  E           K        DISK                      A
     FQPRINT  O   F     176            PRINTER
     ****
     IRVREC
     I                                              RVSANOL1
     I*
     IACREC
     I              ACTYPE                          F#TYPE
     I              ACCURR                          F#CURR
     I              ACACNO                          F#ACNO
     I              ACBNAM                          F#BNAM
     I              ACNAME                          F#NAME
     I              ACAREA                          F#AREA
     I              ACBAID                          F#BAID
     I              ACOLID                          F#OLID
     I              ACRESV                          F#RESV
     ****
     I            DS
     I                                        1   6 VRNOX
     I                                        2   60NO1
     I                                       11  16 VRNON
     I                                       12  160NO3
     I            DS
     I                                       11  17 VRNOG
     I                                       11  11 VRNOG1
     I                                       12  12 VRNOG2
     I                                       13  16 VRNOG3
     I                                       14  160VRNOG4
     I                                       17  17 VRNOG5
     I                                       21  30 RVSANO
     I                                       21  21 ANTYPE
     I                                       22  22 ANCURR
     I                                       23  30 ANACNO
     I            DS
     I                                       21  36 DSC0
     I                                       21  28 DSC1
     I                                       21  30 DSC11
     I                                       29  36 DSC2
     I                                       31  36 DSC21
     I*                                      41  47 VUNOG
     I*                                      41  41 VUNOG1
     I*                                      42  42 VUNOG2
     I*                                      43  46 VUNOG3
     I*                                      47  47 VUNOG4
     I                                       41  47 VUNOG
     I                                       41  41 VUNOG1
     I                                       42  47 VUNOG2
     I*                                      46  47 VUNOG3
     I           UDS
9008 I                                        1   80YMD
     I                                        1   60YM
0011AI                                      201 206 D#VNO1
0011AI                                      211 216 D#VNO2
     I                                     10011010 D#USER
     I                                     10211021 TXAR
0109AIIFRS       UDS                             50
0109AI                                        1   60D#IFYM
0109AI                                        1   80D#IFRS
     C*****************************************************************
     C*          KEY  LIST
     C*****************************************************************
     C           ACKEY1    KLIST
     C                     KFLD           TXAR
     C                     KFLD           YM
     C                     KFLD           ACVRNO
     C                     KFLD           ACITEM
     C           ACKEY     KLIST
     C                     KFLD           TXAR
     C                     KFLD           YM
     C                     KFLD           ACVRNO
     C           RVKEY     KLIST
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C           ANKEY     KLIST
     C                     KFLD           ANTYPE
     C                     KFLD           ANCURR
     C                     KFLD           ANACNO
     C*****************************************************************
     C*          MAIN   PROGRAM
     C*****************************************************************
9908AC           *DATE     SUB  19000000  U#SYSD  80
0109AC           *NAMVAR   DEFN ARIFRSCTL IFRS
0109AC           YMD       IFLT D#IFRS
0109AC                     MOVEL*ON       *IN77
0109AC                     ELSE
0109AC                     MOVEL*OFF      *IN77
0109AC                     ENDIF
0109AC                     UNLCKIFRS
     C  N10                EXSR IN#01
     C   L1 10             EXSR DL#01
     C      10             EXSR DP#01
     CL1    10             EXSR TL#01
     CLR    10             EXSR TL#LR
     C**--------------------------------------------------**
     CSR         IN#01     BEGSR
     C                     Z-ADD0         AMTI   132
     C                     Z-ADD0         AMTT   132
     C                     Z-ADD0         VAMT   132
     C                     Z-ADD0         AMTTT  132
     C                     Z-ADD0         ITEM    30
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD60        TCNT    20
     C                     Z-ADD0         PAG     30
     C                     MOVEL*ALL'-'   ADASH 150
     C                     MOVEL*BLANK    VUNOG
0011AC                     MOVEL*BLANKS   D#VNO1
0011AC                     MOVEL*BLANKS   D#VNO2
     C                     SETON                     10
     CSR                   ENDSR
     C***
     C**----------------------------------------------**
     C**    L1 DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DL#01     BEGSR
     C                     Z-ADD0         ITEM
     C                     Z-ADD0         AMTT
     ****
     C                     EXSR @FDNO
     CSR                   ENDSR
     C**----------------------------------------------**
     C**  FIND LAST NO                                **
     C**----------------------------------------------**
     CSR         @FDNO     BEGSR
     C                     MOVEL'A99999'  ACVRNO
     C                     Z-ADD999       ACITEM
     C                     SETOF                     95
     C           ACKEY1    SETLLACTRAN
     C                     READPACTRAN                   95
     C  N95                MOVELACVRNO    TYPE    1
     C  N95      TYPE      COMP 'A'                  9595
     C  N95                MOVELACVRNO    VRNON
     C  N95                ADD  1         NO3
     C  N95                MOVELVRNON     VRNOX
     C   95                MOVEL'A00001'  VRNOX
0011AC           D#VNO1    IFEQ *BLANKS
0011AC                     MOVELVRNOX     D#VNO1
0011AC                     MOVELVRNOX     D#VNO2
0011AC                     ENDIF
0101AC           VRNOX     IFGT D#VNO2
0101AC                     MOVELVRNOX     D#VNO2
0101AC                     ENDIF
     CSR                   ENDSR
     C**----------------------------------------------**
     C**       DETAIL TIME                            **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C**
     C                     ADD  RVRAMT    AMTTT
     C                     ADD  RVRAMT    AMTT
     C***
     C                     Z-ADDRVRAMT    VAMT   132
     C                     EXSR WR#01
     C                     EXSR PR#01
     C           RVKEY     CHAINRCVDTL               95
     C*    N95             MOVELTXAR      VUNOG1
     C*    N95             MOVELTXAR      VUNOG2
     C*    N95             MOVELVRNOX     VUNOG3
     C*    N95             MOVELVUNOG     RVVNO2
     C     N95             MOVELTXAR      VUNOG1
     C     N95             MOVELVRNOX     VUNOG2
     C*因欄位長度不夠放
     C*    N95             MOVELITEM      VUNOG3
     C     N95             MOVELVUNOG     RVVNO2
     C     N95             MOVEL'C'       RVFLAG
     C     N95             MOVELTXAR      RVTXAR
9908AC*    N95             Z-ADDUDATE     RVTXDT
9908AC     N95             Z-ADDU#SYSD    RVTXDT
     C     N95             UPDATRVREC1
     CSR                   ENDSR
     C**----------------------------------**
     C**   L1 BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#01     BEGSR
     C                     EXCPTSP#01
     C                     ADD  1         LCNT
     CSR         TL#01Z    ENDSR
     C**----------------------------------**
     C**   LR BREAK PROCESS TOTAL TIME    **
     C**----------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     CSR                   ENDSR
     C*****
     C**------------------------------------------**
     C**    CHECK   存入帳號 820101             **
     C**------------------------------------------**
     CSR         CK#SNO    BEGSR
     C           ANKEY     CHAINBANKAC               97
     C   97                MOVE *BLANK    F#NAME
     C           YMD       IFLT 820101
     C  N97                MOVELF#OLID    RVSANO
     C                     ENDIF
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#01     BEGSR
     C**** WRITE TO 1112
     C                     ADD  1         ITEM
     C                     CLEARRACVOUR
     C                     MOVEL'P'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXAR      ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     MOVELYMD       ACYM             年月
     C                     Z-ADDRVNTDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM      ACITEM
     C                     MOVEL'D'       ACDC
      *
     C                     MOVE *OFF      *IN88
     C                     MOVELRVSANO    W#SANO  2
     C           W#SANO    IFEQ 'BU'
     C                     MOVE *ON       *IN88
0109AC   77                MOVEL'1114'    ACNO
0109AC  N77                MOVEL'110104'  ACNO
     C                     ELSE
0109AC   77                MOVEL'1112'    ACNO
0109AC  N77                MOVEL'110102'  ACNO
     C                     ENDIF
      *
     C                     MOVEL'C'       ACTYCO
     C                     Z-ADDVAMT      ACAMT
     C                     EXSR CK#SNO
     C                     MOVELRVSANO    ACBKNO
     C                     MOVEL'A'       ACSEPT
     C                     MOVELRVNO      ACDSC1
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9908AC*                    MOVE UDATE     ACENTD           建檔日期
9908AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9908AC*                    MOVE UDATE     ACUPDD           異動日期
9908AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C                     WRITERACVOUR
     C                     Z-ADDITEM      SIT1    30
     C*
     C**** WRITE TO 1131
     C           ITEM      ADD  1         ITEM1   30
     C                     Z-ADDITEM1     ITEM
     C                     CLEARRACVOUR
     C                     MOVEL'P'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXAR      ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXAR      ACAREA           開立廠區
     C                     MOVELYMD       ACYM             年月
     C                     Z-ADDRVNTDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM      ACITEM
     C                     MOVEL'C'       ACDC
0109AC   77                MOVEL'1131'    ACNO
0109AC  N77                MOVEL'111001'  ACNO
     C                     MOVEL'H'       ACTYCO
     C                     MOVELRVNTNO    ACRLNO
     C                     MOVE RVDUDT    ACDUE
     C                     MOVELRVCUNM    ACCSNM
     C                     Z-ADDVAMT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C                     MOVELRVNO      ACDSC1
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9908AC*                    MOVE UDATE     ACENTD           建檔日期
9908AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9908AC*                    MOVE UDATE     ACUPDD           異動日期
9908AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#01     BEGSR
     C*
     C                     MOVEL*BLANK    R#NTNO 20
     C                     MOVELRVNTNO    R#NTNO
     C*
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD10        LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#01
     C                     ADD  2         LCNT
     CSR                   ENDSR
     OQPRINT  E  103           EH#01
     O                                   12 '報表編號：'
     O                                   18 'ARP016'
     O                                   83 '應收票據兌現代開傳票'
     O                                  140 '列表日期:'
9908AO*                        UDATE Y  148
9908AO                         U#SYSD   150 '    /  /  '
     O        E  1             EH#01
     O                                   82 '********************'
     O                                  140 '頁　　數:'
     O                         PAG   Z  143
     O        E  1             EH#01
     O                                   73 '入帳日期:'
     O                         YMD       83 '    /  /  '
     O        E  1             EH#01
     O                                   14 '借'
     O        E  1             EH#01
     O*                                  10 '繳款編號'
     O                                   10 '客戶名稱'
     O                                   14 '貸'
     O                                   30 '會　計　科　目'
0109AO                                   49 '借方金額'
0109AO                                   69 '貸方金額'
0109AO                                   82 '到期日期'
0109AO                                   93 '票據號碼'
0109AO                                  106 '銀行名稱'
0109AO                                  130 '票據帳號'
0109AO                                  142 '存入帳號'
0109AO                                  155 '傳票編號'
0109AO                                  163 '部門'
     O        E  1             EH#01
     O                         ADASH    150
     O        E  1             ED#01
     O*                        RVNO       7
     O*                                   8 '-'
     O*                        RVITEM    10
     O                         RVCUNM    10
     O                                   12 'D'
0109AO                N88 77             19 '1112  '
0109AO                 88 77             19 '1114  '
0109AO                N88N77             19 '110102'
0109AO                 88N77             19 '110104'
0109AO                N88                29 '銀行存款'
0109AO                 88                29 '外幣存款'
0109AO                                   30 'C'
0109AO                         VAMT  K   52
0109AO                         F#NAME   118
0109AO                         RVSANO   143
0109AO                         TXAR     147
0109AO                         TXAR     148
0109AO                         VRNOX    154
0109AO                                  155 '-'
0109AO                         SIT1     158
0109AO                         RVDPNO   164
     O        E  1             ED#01
     O                                   12 'C'
0109AO                 77                19 '1131  '
0109AO                N77                19 '111001'
0109AO                                   29 '應收票據'
0109AO                                   30 'H'
0109AO                         VAMT  K   72
0109AO                         RVDUDT    81 '    /  /  '
0109AO                         R#NTNO   103
0109AO                         RVPBID   119
0109AO                         RVPANO   131
0109AO                         TXAR     147
0109AO                         TXAR     148
0109AO                         VRNOX    154
0109AO                                  155 '-'
0109AO                         ITEM1    158
     O        E  1             SP#01
     O        E  1             ET#LR
     O                                   52 '應收票據　兌現總額:'
0109AO                         AMTTT K   72
     O        T   63   LR
     O                                   39 '協理:'
     O                                   80 '課長:'
     O                                  120 '製表:'
