     A*****************************************************************
     A*
     A*            *****   PROGRAM INTRODUCTION   *****
     A*
     A*            1.PROGRAM-ID    ARP126R
     A*            2.PROGRAM-TYPE  RPG
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     加工收入代開傳票
     A*            5.DATE-WRITTEN  107/03/21 2018AR1144 S00WCJ (0703A)
     A*            6.UPDATE  DATE
     A*
     A*****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTL  IP  E                    DISK
     FTRNDTLUPUF  E           K        DISK
     F            TXREC                             KRENAMETXW
     FACTRAN  UF  E           K        DISK                      A
     FHIPROD  IF  E           K        DISK
0611AFCBCUST  IF  E           K        DISK
     FQPRINT  O   F     176            PRINTER
9905AE                    TAB1    1   6  1   TABR1  19
0107AE                    TAB2    1   6  1   TABR2  21
     I*****************************************************************
     ITXREC
     I                                              TXACDTL3
     I                                              TXIVNOL2
     I                                              TXCUNOL2
     I                                              TXUPRCL1
     I                                              TXPDNML1
     I            DS
     I                                        1  19 KGRP
     I                                        1   5 KACNO
     I                                        6  15 KACNON
     I                                       16  19 KDPNO
     I                                       20  27 TXNO
     I                                       20  20 TXAREA
0107AI            DS
0107AI                                        1  21 K#GRP
0107AI                                        1   7 K#ACNO
0107AI                                        8  17 K#ACNN
0107AI                                       18  21 K#DPNO
     I            DS
     I                                        1   6 VRNOX
     I                                        2   60NO1
     I                                       11  16 VRNON
     I                                       12  160NO3
     I            DS
     I                                        1   6 VRNOY
     I                                        2   60NO2
     I            DS
     I                                        1   7 TXVUNO
     I                                        1   1 TTT1
     I                                        2   2 TTT2
     I                                        3   3 TTT3
     I                                        4   7 TTT4
     I            DS
     I                                        1   6 TXCUNO
     I                                        1   1 CUNO
CLJ  I*
     I            DS
     I                                        1   2 W#OEOF
     I                                        1   1 D#OE
     I                                        2   2 D#OF
     I            DS
     I                                        1  12 NAME1
     I                                       12  12 D#NOF
     I*
     I           UDS
     I                                        1   8 YMD
     I                                        1   60D#YM
     I                                        9   9 TXAR
0011AI                                      201 206 D#VNO1
0011AI                                      211 216 D#VNO2
     I                                     10011010 D#USER
0107AIIFRS       UDS                             50
0107AI                                        1   60D#IFYM
0107AI                                        1   80D#IFRS
     C*****************************************************************
     C*              KEY   LIST
     C*****************************************************************
     C*FILE==>ACTRAN(傳票檔)
     C           ACKEY1    KLIST
     C                     KFLD           TXAR
     C                     KFLD           D#YM
     C                     KFLD           ACVRNO
     C                     KFLD           ACITEM
     C*FILE==>TRNDTL(銷貨明細檔)
     C           TXKEY     KLIST
     C                     KFLD           TXCODE
     C                     KFLD           TXNO
     C                     KFLD           TXITEM
     C*****************************************************************
9909AC           *DATE     SUB  19000000  U#SYSD  80
0107AC           *NAMVAR   DEFN ARIFRSCTL IFRS
0107AC           D#YM      IFLT D#IFYM
0107AC                     MOVEL*ON       *IN77
0107AC                     ELSE
0107AC                     MOVEL*OFF      *IN77
0107AC                     ENDIF
0109AC                     UNLCKIFRS
     C  N99                EXSR INISR
     C   L3                EXSR L3CLR
     C   L2                EXSR L2CLR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CL2 99                EXSR L2SR
     CL3 99                EXSR L3SR
     CLR 99                EXSR LRSR
     CLR                   EXCPTET#LRR
     C*****************************************************************
     CSR         INISR     BEGSR
     C*****************************************************************
     C                     SETON                     99
     C*
0001AC                     MOVE YMD       DATEA   80
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD58        TCNT    20
     C                     Z-ADD0         PAG     30
     C                     MOVE *BLANK    KGRP
0107AC                     MOVEL*BLANKS   K#GRP
0107AC   77      TXAR      LOKUPTAB1      TABR1          31
     C   31 77             MOVELTABR1     KGRP
0107AC  N77      TXAR      LOKUPTAB2      TABR2          31
     C   31N77             MOVELTABR2     K#GRP
0011AC                     MOVEL*BLANKS   D#VNO1
0011AC                     MOVEL*BLANKS   D#VNO2
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         NUMSR     BEGSR
     C*****************************************************************
     C*找目前已使用到的傳票號碼
     C                     MOVEL'C03050'  ACVRNO
     C                     Z-ADD999       ACITEM
     C                     SETOF                     95
     C           ACKEY1    SETGTACTRAN
     C                     READPACTRAN                   95
     C  N95                MOVELACVRNO    TYPE    1
     C  N95                MOVE ACVRNO    W1VRNO  50
     C           TYPE      IFEQ 'C'
     C           W1VRNO    ANDLT3001
     C                     MOVEL*ON       *IN95
     C                     ELSE
     C                     MOVEL*OFF      *IN95
     C                     ENDIF
     C  N95      TYPE      COMP 'C'                  9595
     C  N95                MOVELACVRNO    VRNON
     C  N95                ADD  1         NO3
     C  N95                MOVELVRNON     VRNOX
     C   95                MOVEL'C03001'  VRNOX
     C           D#VNO1    IFEQ *BLANKS
     C                     MOVELVRNOX     D#VNO1
     C                     MOVELVRNOX     D#VNO2
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         L3CLR     BEGSR
     C*****************************************************************
     C                     Z-ADDTCNT      LCNT
     C*
     C                     EXSR NUMSR
     C                     MOVELVRNOX     VRNOY
     C*
     C           NO1       ADD  1         NO2
     C                     Z-ADD0         AMTT   132
     C                     Z-ADD0         AMTTT  110
     C                     Z-ADD1         ITEM1   30
     C                     Z-ADD1         ITEM2   30
     C                     Z-ADD0         COUNT   40
     C*
0011AC           D#VNO2    IFLT VRNOY
0011AC                     MOVELVRNOY     D#VNO2
0011AC                     ENDIF
0011AC*
     CSR                   ENDSR
     C*****************************************************************
     CSR         L2CLR     BEGSR
     C*****************************************************************
     C                     Z-ADD0         AMT2   132
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         L1CLR     BEGSR
     C*****************************************************************
0404AC                     MOVELTXPDNM    F4NAME
0404AC           F4NAME    CHAINHIPROD               31
     C   31                MOVE *BLANK    NAME1  12
     C           *IN31     IFEQ '0'
     C                     MOVE ''      W#OEOF
     C                     MOVELF4CHIN    NAME1
     C                     MOVE D#OF      D#NOF
     C                     ENDIF
     C                     Z-ADD0         AMT1   132       銷貨金額
     C                     Z-ADD0         QTY1    80       銷貨數量
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         DTLSR     BEGSR
     C*****************************************************************
TEST C           TXNO      IFEQ 'M1133574'
 .   C                     Z-ADD0         W#TEST  10
 .   C                     ENDIF
     C*
     C                     MOVELTXAREA    TXARA   1
     C           TXARA     IFEQ 'A'
     C                     MOVELTXAR      TXARA            代開廠區
     C                     ENDIF
     C                     ADD  TXAMT     AMT1             銷貨金額
     C                     ADD  TXAMT     AMT2
     C                     ADD  TXQTY     QTY1             銷貨數量
     C*
     C                     ADD  TXAMT     AMTT
     C                     ADD  1         COUNT
     C                     ADD  TXAMT     AMTTX  132
     C                     ADD  1         COUNTX  40
     C*
     C           TXKEY     CHAINTRNDTLUP             31
     C  N31                MOVELTXAR      TTT1
     C  N31                MOVELTXARA     TTT2
     C  N31                MOVELVRNOY     TTT3
     C  N31                MOVE VRNOY     TTT4
     C  N31                UPDATTXW
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         L1SR      BEGSR
     C*****************************************************************
     C                     SETOF                     2223
     C           AMT1      IFNE 0
     C                     ADD  1         ITEM1
     C           AMT1      IFLT 0
     C                     Z-SUBAMT1      AMT1
     C                     Z-SUBQTY1      QTY1             銷貨數量
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXSR WR#01                      銷貨收入
     C                     EXSR PR#01                      報表
     C*
     C           ITEM1     IFEQ 998
     C                     ADD  1         ITEM2
     C                     EXSR WR#02
     C                     EXSR PR#02
     C                     EXSR WR#03
     C                     EXSR PR#03
     C                     EXSR WR#04
     C                     EXSR PR#04
     C                     ADD  2         NO3
     C                     MOVELVRNON     VRNOX
     C                     ADD  2         NO2
     C                     Z-ADD1         ITEM1
     C                     Z-ADD1         ITEM2
     C                     Z-ADD0         AMT1
     C                     Z-ADD0         AMT2
     C                     Z-ADD0         AMTT
     C*                    EXSR L3CLR
     C*                    EXSR L2CLR
     C*                    EXSR L1CLR
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         L2SR      BEGSR
     C*****************************************************************
     C           AMT2      IFNE 0
     C                     ADD  1         ITEM2
     C           AMT2      IFLT 0
     C                     Z-SUBAMT2      AMT2
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXSR WR#02
     C                     EXSR PR#02
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         L3SR      BEGSR
     C*****************************************************************
     C           AMTT      IFNE 0
     C           AMTT      IFLT 0
     C                     Z-SUBAMTT      AMTT
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXSR WR#03
     C                     EXSR PR#03
     C                     EXSR WR#04
     C                     EXSR PR#04
     C                     EXCPTET#LR
     C                     ENDIF
     CSR                   ENDSR
     C*****************************************************************
     CSR         LRSR      BEGSR
     C           AMTTX     IFLT 0
     C                     Z-SUBAMTTX     AMTTX
     C                     MOVEL'0'       *IN,51
     C                     ELSE
     C                     MOVEL'1'       *IN,51
     C                     ENDIF
     C                     EXCPTET#LRX
     C*
 .   CSR                   ENDSR
     C*****************************************************************
     CSR         WR#01     BEGSR
     C*****************************************************************
     C**    TOTAL  TIME  WRITE DATA TO ACTRAN     **
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXARA     ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXARA     ACAREA           開立廠區
     C                     Z-ADDD#YM      ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADDITEM1     ACITEM
     C   51                MOVEL'C'       ACDC
     C  N51                MOVEL'D'       ACDC
     C  N77                MOVEL'410301'  ACNO             加工收入
     C                     MOVELACNO      W#ACN4  6
     C                     MOVEL'L'       ACTYCO
     C                     MOVE *BLANK    KGRP
0107AC                     MOVEL*BLANKS   K#GRP
0107AC   77      TXAREA    LOKUPTAB1      TABR1          31
0107AC   31 77             MOVELTABR1     KGRP
0107AC  N77      TXAREA    LOKUPTAB2      TABR2          31
0107AC   31N77             MOVELTABR2     K#GRP
0107AC      77             MOVELKDPNO     ACDPNO
0107AC     N77             MOVELK#DPNO    ACDPNO
     C                     MOVELTXORNO    KORNO   6
     C                     MOVELKORNO     ACRLNO
0611AC           TXCUNO    CHAINCBCUST               59
0611AC  N59                MOVELCBCUNM    ACCSNM
0611AC   59                MOVELTXCUNM    ACCSNM
     C                     Z-ADDAMT1      ACAMT            銷貨金額
     C                     MOVELTXPDNM    ACDSC1
     C           TXACNT    IFEQ '3'
     C                     Z-ADD0         QTY1             銷貨數量
     C                     END
     C                     MOVELQTY1      ACDSC2           摘要二
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9909AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9909AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     C*
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#02     BEGSR
     C****  WRITE  TO TAIPEI  "1134"應收帳款
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXARA     ACAREA           開立廠區
     C                     Z-ADDD#YM      ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADDITEM2     ACITEM
     C  N51                MOVEL'C'       ACDC
     C   51                MOVEL'D'       ACDC
0107AC   77                MOVEL'1134'    ACNO
0107AC  N77                MOVEL'111101'  ACNO
9707 C                     MOVELACNO      W#ACN4
     C                     MOVEL'G'       ACTYCO
     C                     MOVELKORNO     ACRLNO
0611AC           TXCUNO    CHAINCBCUST               59
0611AC  N59                MOVELCBCUNM    ACCSNM
0611AC   59                MOVELTXCUNM    ACCSNM
     C                     Z-ADDAMT2      ACAMT            應收帳款
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9909AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9909AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#03     BEGSR
     C****  WRITE  TO TAIPEI  "3511"公司往來
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVELTXARA     ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXARA     ACAREA           開立廠區
     C                     Z-ADDD#YM      ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOY     ACVRNO
     C                     Z-ADD1         ACITEM
     C  N51                MOVEL'C'       ACDC
     C   51                MOVEL'D'       ACDC
0107AC   77                MOVEL'3511'    ACNO
0107AC  N77                MOVEL'390101'  ACNO
9707 C                     MOVELACNO      W#ACN4
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9909AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9909AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     CSR                   ENDSR
     C**------------------------------------------**
     C**    DETAIL TIME  WRITE DATA TO ACTRAN     **
     C**------------------------------------------**
     CSR         WR#04     BEGSR
     C****  WRITE  TO TAIPEI  "3512-6"廠區往來
     C                     CLEARRACVOUR
     C                     MOVEL'N'       ACVRTY
     C                     MOVE TXAR      ACDM03
     C                     MOVEL'P'       ACCARD
     C                     MOVEL'A'       ACKIND
     C                     MOVELTXARA     ACAREA           開立廠區
     C                     Z-ADDD#YM      ACYM             年月
     C                     Z-ADDTXACDT    ACYMD
     C                     MOVELVRNOX     ACVRNO
     C                     Z-ADD1         ACITEM
     C   51                MOVEL'C'       ACDC
     C  N51                MOVEL'D'       ACDC
     C                     MOVE *BLANK    KGRP
0107AC                     MOVEL*BLANK    K#GRP
0107AC   77      TXAREA    LOKUPTAB1      TABR1          31
0107AC   31 77             MOVELTABR1     KGRP
0107AC   77                MOVELKACNO     ACNO
0107AC  N77      TXAREA    LOKUPTAB2      TABR2          31
0107AC   31N77             MOVELTABR2     K#GRP
0107AC  N77                MOVELK#ACNO    ACNO
9707 C                     MOVELACNO      W#ACN4
     C                     MOVEL'A'       ACTYCO
     C                     Z-ADDAMTT      ACAMT
     C                     MOVEL'A'       ACSEPT
     C*
     C                     MOVELD#USER    ACENTM           建檔人員
9909AC                     MOVE U#SYSD    ACENTD           建檔日期
     C                     TIME           ACENTT           建檔時間
     C                     MOVELD#USER    ACUPDM           異動人員
9909AC                     MOVE U#SYSD    ACUPDD           異動日期
     C                     TIME           ACUPDT           異動時間
     C*
     C                     WRITERACVOUR
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#01     BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#02     BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#02
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#03     BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#03
     C                     ADD  1         LCNT
     CSR                   ENDSR
     C**--------------------------------------**
     C**  PRINT OUT DETAIL RECORD             **
     C**--------------------------------------**
     CSR         PR#04     BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                Z-ADD8         LCNT
     C   75                EXCPTEH#01
     C                     EXCPTED#04
     C                     ADD  1         LCNT
     CSR                   ENDSR
      *
     OQPRINT  E   03           EH#01
     O                                   12 '報表編號：'
     O                                   18 'ARP126'
     O                                   81 '加工收入入帳清單'
     O                                  140 '列表日期:'
9909AO                         U#SYSD   150 '    /  /  '
     O        E 1              EH#01
     O                                   82 '********************'
     O                                  140 '頁　　數:'
     O                         PAG   Z  143
     O        E 1              EH#01
     O                                   70 '日期:'
     O                         DATEA     80 '    /  /  '
     O        E 1              EH#01
     O                                   41 '借'
     O        E 1              EH#01
     O                                   10 '入帳日期'
     O                                   20 '磅單編號'
     O                                   26 '訂單'
     O                                   35 '客　戶'
     O                                   41 '貸'
     O                                   51 '會計科目'
0107AO                                   71 '品　名'
0107AO                                   93 '數量'
0107AO                                  103 '單價'
0107AO                                  120 '借方金額'
0107AO                                  137 '貸方金額'
0107AO                                  144 '部門'
0107AO                                  158 '傳票編號'
     O        E 1              EH#01
     O                                   20 '--------------------'
     O                                   40 '--------------------'
     O                                   60 '--------------------'
     O                                   80 '--------------------'
     O                                  100 '--------------------'
     O                                  120 '--------------------'
     O                                  132 '--------------------'
     O                                  150 '--------------------'
     O                                  159 '---------'
     O        E 1              ED#01
     O                         ACYMD     10 '    /  /  '
     O                         TXNO      19
     O                         KORNO     26
     O                         ACCSNM    37
     O                    51             40 'C'
     O                   N51             40 'D'
     O                         W#ACN4    47
     O                                   62 '加工收入  L'
     O                         TXPDNM  +  0
     O                         NAME1   +  1
0107AO                         QTY1  K   93
0107AO                         TXUPRCK  103
0107AO                   N51   ACAMT K  120
0107AO                    51   ACAMT K  137
0107AO                         ACDPNO   147
0107AO                         TXAR     150
0107AO                         TXARA    151
     O                         VRNOY    157
     O                                      '-'
     O                         ACITEM   161
     O        E 1              ED#02
     O                         ACYMD     10 '    /  /  '
     O*                        TXNO      18
     O                         KORNO     26
     O                         ACCSNM    37
0107AO                 77N51             62 'C 1134  應收帳款    G'
0107AO                 77 51             62 'D 1134  應收帳款    G'
0107AO                N77N51             62 'C 111101應收帳款    G'
0107AO                N77 51             62 'D 111101應收帳款    G'
0107AO                    51   ACAMT K  120
0107AO                   N51   ACAMT K  137
0107AO                         TXAR     150
0107AO                                  151 'P'
0107AO                         VRNOX    157
     O                                      '-'
0107AO                         ACITEM   161
     O        E 1              ED#03
     O                         ACYMD     10 '    /  /  '
     O*                        TXNO      20
0107AO                 77N51             62 'C 3511  公司往來    A'
0107AO                 77 51             62 'D 3511  公司往來    A'
0107AO                N77N51             62 'C 390101公司往來    A'
0107AO                N77 51             62 'D 390101公司往來    A'
0107AO                    51   ACAMT K  120
0107AO                   N51   ACAMT K  137
0107AO                         TXAR     150
0107AO                         TXARA    151
0107AO                         VRNOY    157
     O                                      '-'
0107AO                         ACITEM   161
     O        E 1              ED#04
     O                         ACYMD     10 '    /  /  '
     O*                        TXNO      20
0107AO                   N51             41 'D '
0107AO                    51             41 'C '
0107AO                 77      KACNO     47
0107AO                 77      KACNON    57
0107AO                N77      K#ACNO    47
0107AO                N77      K#ACNN    57
0107AO                                   62 'A'
0107AO                   N51   ACAMT K  120
0107AO                    51   ACAMT K  137
0107AO                         TXAR     150
0107AO                                  151 'P'
0107AO                         VRNOX    157
     O                                      '-'
0107AO                         ACITEM   161
     O        E 2              ET#LR
     O                                   64 '入帳筆數'
     O                         COUNT K +  1
     O                N51               102 '加工金額合計'
     O                N51      AMTT  K  120
     O                 51               119 '加工金額合計'
     O                 51      AMTT  K  137
     O        E 2              ET#LRX
 .   O                                   64 '總入帳筆數'
 .   O                         COUNTXK +  1
     O                N51               102 '加工金額合計'
     O                N51      AMTTX K  120
     O                 51               119 '加工金額合計'
     O                 51      AMTTX K  137
     O        E   60           ET#LRR
     O                                   50 '經理（廠長）：'
     O                                   73 '複核：'
     O                                   96 '製票：'
** TAB1
K3512 高雄往來K210
T3513 桃園往來T380
H3514 苗栗往來H800
M3515 台中往來W040
L3516 林口往來L080
U3518 新桃往來U380
** TAB2
K390102 高雄往來K800
T390103 八德往來T380
H390104 苗栗往來H800
M390105 台中往來W040
L390106 林口往來L080
U390108 桃園往來U380
