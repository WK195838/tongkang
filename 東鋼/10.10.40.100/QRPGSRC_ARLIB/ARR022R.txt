     H****************************************************************
     H*
     H*            UPDATE  DATE  99/08/31  2010AR517 S00WCJ (9908A)
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FBANKAC  IF  E           K        DISK
     FBANKID  IF  E           K        DISK
     FQPRINT  O   F     120            PRINTER
     E*************************************************************
     E                    TAB1    1   4  1   TABR1  12
     E                    TAB2    1  10  1   TABR2  10
     I*************************************************************
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C*** SET INITIAL
9908AC           *DATE     SUB  19000000  U#SYSD  80
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD60        TCNT    20
     C                     Z-ADD50        FCNT    20
     C                     MOVE *ALL'='   ALS   120
     C           BAKEY     KLIST
     C                     KFLD           BAID1
     C                     KFLD           BAID2
     C****
     C                     EXSR DP#01
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C******
     CSR         DP#01     BEGSR
     C                     READ BANKAC                   97
     C           *IN97     DOWEQ'0'
     C           ACTYPE    LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     TYPEC  12
     C  N31                MOVEL*BLANK    TYPEC
     C           ACCURR    LOKUPTAB2      TABR2          31
     C   31                MOVELTABR2     CURRC  10
     C  N31                MOVEL*BLANK    CURRC
     C                     MOVELACBAID    BAID1
     C                     MOVE ACBAID    BAID2
     C           BAKEY     CHAINBANKID               95
     C   95                MOVEL*BLANK    BANAME
     C                     EXSR PR#01
     C                     READ BANKAC                   97
     C                     ENDDO
     C****
     C                     EXCPTET#01
     C****
     CSR         DP#01Z    ENDSR
     C***:
     CSR         PR#01     BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD10        LCNT
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     CSR         PR#01Z    ENDSR
     C******
     OQPRINT  E   02           EH#01
     O                                   16 '報表編號: AR22'
     O                         CON4      72                                    X
     O                                  111 '製表日期'
     O                                  112 ':'
9908AO                         U#SYSDZ  120
     O        E 1              EH#01
     O                                   69 '銀行帳號名稱一覽表'             X
     O                                  111 '頁　　次'
     O                                  112 ':'
     O                         PAG   Z  115
     O        E 2              EH#01
     O                         ALS      120
     O        E 1              EH#01
     O                                    8 '存款別'
     O                                   20 '幣　別'
     O                                   32 '銀行帳號'
     O                                   46 '帳號簡稱'
     O                                   66 '帳　號　名　稱'
     O                                   84 '舊帳號代號'
     O                                   94 '所屬銀行'
     O                                  112 '銀　行　名　稱'
     O        E 1              EH#01
     O                         ALS      120
     O        E 1              ED#01
     O                         TYPEC     12
     O                         CURRC     22
     O                         ACTYPE    23
     O                         ACCURR    24
     O                         ACACNO    32
     O                         ACBNAM    46
     O                         ACNAME    72
     O                         ACOLID    83
     O                         BAID1     88
     O                                   89 '-'
     O                         BAID2     93
     O                         BANAME   118
     O        E 1              ET#01
     O                         ALS      120
** TAB1
A甲存
B乙存
C借款虛帳號
T定存
** TAB2
A澳大利幣
D德國馬克
F法國法朗
H港幣
J日幣
L英鎊
N新台幣
S新加坡幣
U美金
E歐元
