     H        1   Y                                     1                 BR003
     FINVMST  IP  E           K        DISK
     FCURCPR  O   E           K        DISK
     FCBCUST  IF  E           K        DISK
     FSAMAST  IF  E           K        DISK
     I*************************************************************
     IINREC
     I                                              INCUNOL2
     I                                              INORNOL1
     IAADS        DS
     I                                       11  160YMD
     I                                       11  140YM
     I                                       15  160DD
     I                                       21  25 INORNO
     I                                       21  21 ORNO1
     I                                       22  25 ORNO21
     I                                       26  270ORNO3
     I****
     I           UDS
     I                                        1   1 START
     I                                        2   2 END
     I                                        4   4 AREA
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C  N90                EXSR IN#01
     C  N90                SETON                         90
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C  N97                EXSR DP#01
     CL1N97                EXSR TL#L1
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     MOVE *BLANK    S1RECV
     C                     Z-ADD0         AMTT   110
     C                     SETOF                     21
     C                     TESTN          ORNO21     21
     C   21                MOVE ORNO21    ORNO2
     C  N21                Z-ADD0         ORNO2
     C           SAKEY     CHAINSAMAST               97
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     MOVE *BLANK    PFLAG   1
     C                     MOVE *BLANK    RFLAG   1
     C           CBKEY     CHAINCBCUST               97
     C   97                GOTO DL#L2Z
     C                     SELEC
     C           CBPDT1    WHNE 0
     C           CBPDT1    SUB  3         EDT     20
     C           EDT       IFLE DD
     C           CBPDT1    ANDGEDD
     C                     MOVEL'Y'       RFLAG
     C                     ENDIF
     C           CBPDT2    WHNE 0
     C           CBPDT2    SUB  3         EDT
     C           EDT       IFLE DD
     C           CBPDT2    ANDGEDD
     C                     MOVEL'Y'       RFLAG
     C                     ENDIF
     C           CBPDT3    WHNE 0
     C           CBPDT3    SUB  3         EDT
     C           EDT       IFLE DD
     C           CBPDT3    ANDGEDD
     C                     MOVEL'Y'       RFLAG
     C                     ENDIF
     C                     ENDSL
     C****
     C                     SELEC
     C           CBRDT1    WHNE 0
     C           CBRDT1    SUB  3         EDT     20
     C           EDT       IFLE DD
     C           CBRDT1    ANDGEDD
     C                     MOVEL'Y'       PFLAG
     C                     ENDIF
     C           CBRDT2    WHNE 0
     C           CBRDT2    SUB  3         EDT     20
     C           EDT       IFLE DD
     C           CBRDT2    ANDGEDD
     C                     MOVEL'Y'       PFLAG
     C                     ENDIF
     C           CBRDT3    WHNE 0
     C           CBRDT3    SUB  3         EDT     20
     C           EDT       IFLE DD
     C           CBRDT3    ANDGEDD
     C                     MOVEL'Y'       PFLAG
     C                     ENDIF
     C                     ENDSL
     C****
     C*          RFLAG     IFEQ *BLANK
     C*          PFLAG     ANDEQ*BLANK
     C*                    SETON                     97
     C*                    ENDIF
     CSR         DL#L2Z    ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     SETOF                     11
     C                     SELEC
     C           AREA      WHEQ 'P '
     C           S1RECV    IFNE 'G '
     C           S1RECV    ANDNE'N '
     C           S1RECV    ANDNE'Q '
     C           S1RECV    ANDNE'P '
     C           S1RECV    ANDNE'T '
     C           S1RECV    ANDNE'R '
     C                     SETON                     11
     C                     ENDIF
     C           AREA      WHEQ 'T'
     C           S1RECV    IFNE 'I '
     C           S1RECV    ANDNE'J '
     C                     SETON                     11
     C                     ENDIF
     C           AREA      WHEQ 'K'
     C           S1RECV    IFNE 'A '
     C           S1RECV    ANDNE'B '
     C           S1RECV    ANDNE'C '
     C           S1RECV    ANDNE'D '
     C                     SETON                     11
     C                     ENDIF
     C           AREA      WHEQ 'M'
     C           S1RECV    IFNE 'U '
     C           S1RECV    ANDNE'V '
     C           S1RECV    ANDNE'X '
     C                     SETON                     11
     C                     ENDIF
     C           AREA      WHEQ 'H'
     C           S1RECV    IFNE 'O '
     C           S1RECV    ANDNE'F '
     C           S1RECV    ANDNE'H '
     C                     SETON                     11
     C                     ENDIF
     C                     ENDSL
     C  N11                ADD  INNBAL    AMTT
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------------------**
     C**         SET   INITIAL   VALUE                       **
     C**-----------------------------------------------------**
     CSR         IN#01     BEGSR
     C           CBKEY     KLIST
     C                     KFLD           INCUNO
     C                     Z-ADD1         ORNO3
     C           SAKEY     KLIST
     C                     KFLD           ORNO1
     C                     KFLD           ORNO2   40
     C                     KFLD           ORNO3
     C                     Z-ADDUDATE     YMD
     CSR                   ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C           AMTT      IFEQ 0
     C                     GOTO TL#L1Z
     C                     ENDIF
     C**
     C                     CLEARCUREC
     C                     Z-ADDUDATE     CUDATE
     C                     MOVELINCUNO    CUCUNO
     C                     MOVELINCUNM    CUCUNM
     C                     MOVELINORNO    CUORNO
     C                     Z-ADDAMTT      CURAMT
     C                     MOVELS1RECV    CURVID
     C                     MOVELRFLAG     CUFL01
     C                     MOVELPFLAG     CUFL02
     C                     SELEC
     C           CURVID    WHEQ 'A '
     C           CURVID    OREQ 'B '
     C           CURVID    OREQ 'C '
     C           CURVID    OREQ 'D '
     C                     MOVEL'K'       CUAREA
     C           CURVID    WHEQ 'I '
     C           CURVID    OREQ 'J '
     C                     MOVEL'T'       CUAREA
     C           CURVID    WHEQ 'G '
     C           CURVID    OREQ 'P '
     C           CURVID    OREQ 'N '
     C           CURVID    OREQ 'Q '
     C           CURVID    OREQ 'R '
     C           CURVID    OREQ 'T '
     C                     MOVEL'P'       CUAREA
     C           CURVID    WHEQ 'U '
     C           CURVID    OREQ 'V '
     C           CURVID    OREQ 'X '
     C                     MOVEL'M'       CUAREA
     C                     ENDSL
     C                     WRITECUREC
     CSR         TL#L1Z    ENDSR
