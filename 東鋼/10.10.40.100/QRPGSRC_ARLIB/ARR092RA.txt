     H*****************************************************************
     H*
     H*            1.PROGRAM-ID    ARR091R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02YSH
     H*            4.FUNCTION      應收款項檢核表　　　　
     H*            5.DATE-WRITTEN  90/11/13
     H*
     H*****************************************************************
     H        1   Y                                     1
     FARCADT  IP  E           K        DISK
     FARR092F O   E           K        DISK
     IRARCADT     01
     I                                              ARCUNOL2
     I                                              ARWTNOL1
     C*****************************************************************
     C*          Main      Program
     C*****************************************************************
     C  N99                MOVE *ON       *IN99
     C   L2                EXSR L2CLR
     C   L1                EXSR L1CLR
     C   01                EXSR DTLSR
     CL1 99                EXSR L1SR
     CL2 99                EXSR L2SR
     CLR                   RETRN
     C*****************************************************************
     C           L2CLR     BEGSR
     C*****************************************************************
     C                     CLEARRARR092
     C                     Z-ADD0         W#AMT
     C                     Z-ADD0         W#AMTY
     C                     Z-ADD0         W#AMTZ
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     ADD  ARAMT     W#AMT  200
     C           ARAMT     MULT ARDAT3    W#AMTX 120
     C                     ADD  W#AMTX    W#AMTY 200
     C*計算發票帳齡
     C                     MOVE ARIVDT    P#PDAT
     C                     MOVE ARCKDT    P#EDAT
     C                     CALL 'UTS103R'
     C                     PARM           P#PDAT  8
     C                     PARM           P#EDAT  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#DIF   50
     C                     PARM *BLANK    P#ERR   1
     C*
     C           ARAMT     MULT P#DIF     W#AMTX
     C                     ADD  W#AMTX    W#AMTZ 200
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C                     ADD  1         A9CONT
     C                     ENDSR
     C*****************************************************************
     C           L2SR      BEGSR
     C*****************************************************************
     C                     MOVE ARCUNO    A9CUNO
     C           W#AMTY    DIV  W#AMT     A9DATS    H
     C           W#AMTZ    DIV  W#AMT     A9DAT2    H
     C                     WRITERARR092
     C                     ENDSR
