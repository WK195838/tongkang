     H        1   Y                                     1                 BR003
     FRCVDTL  IP  E                    DISK
     FBANKID  IF  E           K        DISK
     FQPRINT  O   F     176            PRINTER
      *************************************************************
     E                    TAB1    1   6  1   TABR1  10
     I*************************************************************
     IRVREC
     I                                              RVCUNOL2
     I                                              RVNO  L1
     I****
     IAADS        DS
     I              'も客戶別應收票據明-C         CON3                       X
     I              '細表も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     IBBDS        DS
     I                                        1  10 XXXX
     I                                        1   3 BAID1
     I                                        4   7 BAID2
      *
     I                                       21  260YMD
     I                                       21  240YM
     I                                       21  220YY
     I                                       23  240MM
     I                                       25  260DD
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     CLR                   EXSR TL#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C           *ENTRY    PLIST
     C                     PARM           BCUNO   6
     C                     PARM           ECUNO   6
     C           BKKEY     KLIST
     C                     KFLD           BAID1
     C                     KFLD           BAID2
      *
     C                     Z-ADD0         BAMT   110
     C                     Z-ADD0         BQTY    50
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD55        TCNT    30
     C                     MOVE *ALL'='   ALS   163
     C                     MOVE *ALL'-'   ALQ   106
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD8         LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         AAMT   110
     C                     Z-ADD0         AQTY    50
     C*                    Z-ADDTCNT      LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     MOVE *BLANK    CHAR10 10
     C           RVNTTP    LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     CHAR10
     C  N31                MOVEL'其他'  CHAR10
      *
     C           RVPLAC    COMP 'D'                      31
     C   31                MOVEL'本埠'  CHAR6   6
     C  N31                MOVEL'外埠'  CHAR6
      *
     C                     MOVE *BLANK    CHAR    1
     C           RVDUDT    COMP UDATE                  3131
     C   31                MOVEL'*'       CHAR
      *
     C                     MOVELRVTBID    XXXX
     C           BKKEY     CHAINBANKID               31
     C  N31                MOVELBABNAM    CHARAX 10
     C   31                MOVEL*BLANK    CHARAX 10
      *
     C                     Z-ADDRVRAMT    DEC9    90
      *
     C                     MOVEL*BLANK    R#NTNO 20
     C                     MOVELRVNTNO    R#NTNO
      *
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#01
     C                     ADD  1         AQTY
     C                     ADD  RVRAMT    AAMT
     C                     ADD  1         BQTY
     C                     ADD  RVRAMT    BAMT
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C                     EXCPTET#L2
     C                     ADD  3         LCNT
     CSR         TL#L2Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME LR PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     CSR         TL#LRZ    ENDSR
      ***********************************************************
      ****   DEFINE OUPUT FORMAT                             ****
      ***********************************************************
     OQPRINT  E   02           EH#01
     O                                   17 '報表編號: AR014'
     O                         CON4      75                                    X
     O                                  133 '製表日期'
     O                                  134 ':'
     O                         UDATE Y  142
     O        E 1              EH#01
     O                         CON3      75                                    X
     O                                  133 '頁　　次'
     O                                  134 ':'
     O                         PAG   Z  137
     O        E 2              EH#01
     O                                   11 '客戶編號:'
     O                         BCUNO   +  1
     O                                 +  1 '-'
     O                         ECUNO   +  1
     O        E 1              EH#01
     O                         ALS      163
     O        E 1              EH#01
     O                                   10 '客戶編號'
     O                                   20 '客戶名稱'
     O                                   32 '付款銀行'
     O                                   46 '付款帳號'
     O                                   57 '票據號碼'
     O                                   79 '到期日期'
     O                                   85 '款項'
     O                                   93 '票據別'
     O                                  103 '付款地'
     O                                  113 '繳款單號'
     O                                  124 '繳款日期'
     O                                  136 '金　額'
     O                                  146 '託收銀行'
     O                                  156 '託收帳號'
     O                                  163 '註記'
     O        E 1              EH#01
     O                         ALS      163
     O        E 1              ED#01
     O                 L2      RVCUNO     7
     O                 L2      RVCUNM    20
     O                         RVPBID    36
     O                         RVPANO    47
     O                         R#NTNO    68
     O                         RVDUDT    78 '    /  /  '
     O                         RVUSTP    83
     O                         CHAR10    95
     O                         CHAR6    102
     O                         RVNO     111
     O                         RVDATE   123 '    /  /  '
     O                         DEC9  J  136
     O                         CHARAX   146
     O                         RVTANO   157
     O                         CHAR     160
     O        E 1              ET#L2
     O                         ALQ      163
     O        E 1              ET#L2
     O                                   66 '客戶小計：　共'
     O                         AQTY  J   76
     O                                   80 '張'
     O                                   97 '總金額:'
     O                         AAMT  J  136
     O        E 1              ET#L2
     O                         ALQ      163
     O        E 1              ET#LR
     O                                   66 '　總合計：　共'
     O                         BQTY  J   76
     O                                   80 '張'
     O                                   97 '總金額:'
     O                         BAMT  J  136
     O*****************************************************************
** TAB1
A支票
B背書支票
C本票
D背書本票
E銀行本票
I信用狀　
