     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR116RS
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     請款別出貨及發票明細表（畫面）
     H*            5.DATE-WRITTEN  93/06/10
     H*            6.DATE-UPDATE
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR116S CF  E                    WORKSTN
     FINVMSTL5IF  E           K        DISK
     E*************************************************************
     E                    ERR     1   2 70
     I*************************************************************
     I           UDS
     I                                     10011010 U#USID
     C**************************************************************
     C*   程式參數傳遞區
     C**************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P#IN03  1
     C                     PARM           P#APNO  7
     C**************************************************************
     C*          KEY  LIST
     C**************************************************************
     C*FILE==>ACCTRL(傳票程序控制檔)
     C*          K#AC      KLIST
     C*                    KFLD           AAAREA            廠區別
     C*                    KFLD           AAYYMM            年月
     C*                    KFLD           AAFSYS            作業來源
     C**************************************************************
     C*   主程式開始
     C**************************************************************
     C                     EXSR SR0000                     初始程式
     C*
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C                     ENDCS
     C                     ENDDO
     C*
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C           SR0000    BEGSR
     C**************************************************************
     C                     MOVEL'01'      W#PRID  2        畫面一
     C*
     C                     ENDSR
     C**************************************************************
     C           SR1000    BEGSR
     C**************************************************************
     C*W#PRID='01'-->畫面一
     C           W#PRID    DOWEQ'01'
     C                     EXFMTAR116F1                    螢幕輸入
     C                     MOVEL*BLANK    S#ERR
     C                     MOVE *OFF      *IN99
     C*
     C           *IN03     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     MOVEL'1'       P#IN03           回前畫面
     C                     MOVEL'00'      W#PRID           結束
     C                     LEAVE
     C                     ENDIF
     C*檢核
     C                     EXSR SR1100                     檢核
     C                     ENDDO
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         SR1100    BEGSR
     C**************************************************************
     C           S#APNO    IFEQ *BLANK
     C                     MOVELERR,1     S#ERR
     C                     SETON                     6099
     C                     ENDIF
     C*
     C  N99      S#APNO    CHAININREC                40
     C  N99      *IN40     IFEQ *ON
     C                     MOVELERR,2     S#ERR
     C                     SETON                     6099
     C                     ENDIF
     C*
     C  N99                MOVEL'00'      W#PRID
     C  N99                MOVELS#APNO    P#APNO           成功
     C*
     C                     ENDSR
     C*****************************************************************
** ERR
０１－請款單號不可空白！
０２－此張請款單不存在！
