     H        1   Y                                     1                 BR003
     FINVDTL  IP  E           K        DISK
     FARWF02  O   E           K        DISK                      A
     FINVMST  IF  E           K        DISK
9104 FHSCINV  IF  E           K        DISK
     FACTRAN  UF  E           K        DISK
     F*---------------------------------------------------------------
     IIVREC
     I                                              IVNO  L1
     I           UDS
9008 I                                        1   40YM
     I                                        7   7 TXAR
     C**-------------------------------------------**
     C**  MAIN LOGIC                               **
     C**-------------------------------------------**
     C           ACKEY     KLIST
     C                     KFLD           ACVRNO
     C                     KFLD           ACITEM
9105 C           K#CINV    KLIST
9105 C                     KFLD           C1ORNO
9105 C                     KFLD           C1OITM
9105 C*
     C  N90                EXSR IN#01
     C   L1                EXSR DL#01
     C  N11                EXSR DP#01
     C**-------------------------------------------**
     C**   INITIAL ROUTINE                         **
     C**-------------------------------------------**
     CSR         IN#01     BEGSR
     C                     MOVEL'E000'    ACVRNO
     C                     Z-ADD0         ACITEM
     C           ACKEY     SETLLACTRAN               95
     C           *IN95     DOWEQ'0'
     C                     READ ACTRAN                   95
     C  N95      ACVRNO    IFGT 'E999'
     C                     SETON                     95
     C                     ENDIF
     C   95                LEAVE
     C                     DELETRACVOUR
     C                     ENDDO
     C                     SETON                     90
     C***
     CSR                   ENDSR
     C**-------------------------------------------**
     C**   L1 PROCESS                              **
     C**-------------------------------------------**
     CSR         DL#01     BEGSR
     C           IVNO      CHAININVMST               11
     C  N11                SELEC
     C           TXAR      WHEQ 'P'
     C           INAREA    IFEQ 'K'
     C           INAREA    OREQ 'M'
     C           INAREA    OREQ 'H'
     C                     SETON                     11
     C                     ELSE
     C           INAREA    IFEQ 'P'
     C*                    MOVEL'L'       INAREA
     C*       此類折讓台北的發票情形很少，目前依情況修改程式
CLJ  C                     MOVEL'T'       INAREA
     C                     ENDIF
     C                     ENDIF
     C           TXAR      WHNE 'P'
     C           INAREA    IFNE TXAR
     C                     SETON                     11
     C                     ENDIF
     C                     ENDSL
     CSR                   ENDSR
     C**--------------------------------------**
     C**   DEATIL TIME PROCESS                **
     C**--------------------------------------**
     CSR         DP#01     BEGSR
     C***
     C                     MOVELINORNO    IVORNO
     C                     CLEARRARWF02
     C                     MOVELIVFLAG    W2FLAG
     C                     MOVELIVNO      W2NO
     C                     MOVELIVACNT    W2ACNT
     C                     MOVELIVITEM    W2ITEM
     C                     MOVELIVACDT    W2ACDT
     C                     MOVELIVORNO    W2ORNO
     C                     MOVELIVPDCD    W2PDCD
     C                     MOVELIVQTY     W2QTY
     C                     MOVELIVUPRC    W2UPRC
     C*9105修正外銷START....
     C                     MOVELINORNO    W#ORNO  6        訂單編號
     C                     MOVELINORNO    C1ORNO           訂單編號
     C                     Z-ADD0         C1OITM           訂單項次
     C                     Z-ADD0         C1OSEQ           訂單流水號
     C                     Z-ADD0         C1CITM            CU項次
     C           K#CINV    SETLLRHSCINV
     C                     READ RHSCINV                  90
     C           C1ORNO    IFEQ W#ORNO
     C           *IN90     ANDEQ*OFF
     C           IVAMT     MULT C1TRAT    W2AMT            外銷
     C                     ELSE
     C                     Z-ADDIVAMT     W2AMT            內銷
     C                     ENDIF
     C*                    MOVELIVAMT     W2AMT
     C*9105修正外銷END  ....
     C                     MOVELIVDECD    W2DECD
     C                     MOVELIVDEDT    W2DEDT
     C                     MOVELIVAPNO    W2APNO
     C                     MOVELIVACNO    W2ACNO
     C                     MOVELIVFL01    W2FL01
     C                     MOVELIVFL02    W2FL02
     C                     MOVELIVFL03    W2FL03
     C                     MOVELIVTXAR    W2TXAR
     C                     MOVELIVTXDT    W2TXDT
     C                     MOVELIVRESV    W2RESV
     C  N11                MOVELINKIND    W2KIND
     C  N11                MOVELINAREA    W2AREA
     C  N11                MOVELINCUNO    W2CUNO
     C  N11                MOVELINCUNM    W2CUNM
9007 C           IVPDCD    IFLT '800'
9007 C           IVPDCD    ORGT '899'
     C                     WRITERARWF02
     C                     ENDIF
     CSR         DP#01Z    ENDSR
