     H****************************************************************
     H*
     H*            UPDATE  DATE  99/08/31  2010AR517  S00WCJ (9908A)
     H*                         104/09/30  2015AR953  S00WCJ (0409A)
     H*                          產品代碼3碼擴5碼
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTL  IP  E                    DISK
     FHIPROD  IF  E           K        DISK
     FQPRINT  O   F     132            PRINTER
      *************************************************************
     E                    TAB1    1   4  1   TABR1  12
     I*************************************************************
     ITXREC
     I                                              TXDATEL2
     I                                              TXNO  L1
     I****
     IAADS        DS
     I              'も銷貨調整通知單明-C         CON3                       X
     I              '細表も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     IBBDS        DS
     I                                        1  10 RVTBID
     I                                        1   3 BAID1
     I                                        4   7 BAID2
     I                                       11  20 RVTANO
     I                                       11  11 ACTYPE
     I                                       12  12 ACCURR
     I                                       13  20 ACACNO
CLJ  I*
     I            DS
     I                                        1   2 W#OEOF
     I                                        1   1 D#OE
     I                                        2   2 D#OF
     I            DS
     I                                        1  12 PDNM
     I                                       12  12 D#NOF
---  I*
     I           UDS
     I                                        1   80DATE1
     I                                        9  160DATE2
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
9908AC           *DATE     SUB  19000000  U#SYSD  80
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     CLR                   EXSR TL#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C*          BKKEY     KLIST
     C*                    KFLD           BAID1
     C*                    KFLD           BAID2
      *
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD57        TCNT    30
     C                     Z-ADD0         AAMT   110
     C                     Z-ADD0         GAMT   110
     C                     MOVE *ALL'='   ALS   132
     C                     MOVE *ALL'-'   ALQ    77
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD9         LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         AAMT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     MOVE *BLANK    CHAR12 12
     C           TXACNT    LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     CHAR12
      *
0409AC                     MOVELTXPDNM    F4NAME
0409AC           F4NAME    CHAINHIPROD               31
CLJ  C* N31                MOVELACPRNM    PDNM   12
     C           *IN31     IFEQ '0'
     C                     MOVE ''      W#OEOF
     C                     MOVELF4CHIN    PDNM
     C                     MOVE D#OF      D#NOF
---  C                     ENDIF
     C   31                MOVELTXPDNM    PDNM
      *
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#01
     C                     ADD  TXAMT     AAMT
     C                     ADD  TXAMT     GAMT
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C                     EXCPTET#L1
     C                     ADD  3         LCNT
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     CSR         TL#L2Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     CSR         TL#LRZ    ENDSR
     C**-----------------------------------------**
     OQPRINT  E   03           EH#01
     O                                   17 '報表編號: AR034'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
9908AO                         U#SYSDZ  132
     O        E 1              EH#01
     O                         CON3      75                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 2              EH#01
     O                                   58 '單據日期:'
     O                         DATE1   +  1 '    /  /  '
     O                                 +  1 '-'
     O                         DATE2   +  1 '    /  /  '
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              EH#01
     O                                   10 '單據日期'
     O                                   20 '調整單號'
     O                                   32 '客戶編號'
     O                                   43 '客戶名稱'
     O                                   54 '訂單編號'
     O                                   63 '調整別'
     O                                   77 '品　名'
     O                                   93 '數量'
     O                                  104 '單價'
     O                                  119 '金　額'
     O                                  130 '磅單編號'
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              ED#01
9008 O                 L2      TXDATE    10 '    /  /  '
     O                 L1      TXNO      19
     O                 L1      TXCUNO    29
     O                 L1      TXCUNM    43
     O                 L1      TXORNO    52
     O                         CHAR12    67
     O                         PDNM      81
     O                         TXQTY J   93
     O                         TXUPRCJ  104
     O                         TXAMT J  119
     O                         TXPCNO   129
     O        E 1              ET#L1
     O                         ALQ      132
     O        E 1              ET#L1
     O                                  104 '調整單小計：'
     O                         AAMT  J  119
     O        E 1              ET#L1
     O        E 1              ET#LR
     O                                  104 '總　　　計：'
     O                         GAMT  J  119
** ECK
1加工費
2補磅差金額
3補運費金額
4其他
