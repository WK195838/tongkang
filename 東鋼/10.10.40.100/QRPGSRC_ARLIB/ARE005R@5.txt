     H        1   Y                                     1                 BR003
     FARE005S CF  E                    WORKSTN
     F                                        RR1   KSFILE SF1
     F                                        RR3   KSFILE SF3
     FINVMST  UF  E           K        DISK
     FINVDTL  UF  E           K        DISK                      A
     FINVDTLL1UF  E           K        DISK
     F            IVREC                             KRENAMEIVREC1
     FRCVDTL  UF  E           K        DISK                      A
     FCBCUST  IF  E           K        DISK
     FLLCMST  IF  E           K        DISK
     FBANKAC  IF  E           K        DISK
     FBCNAME  IF  E           K        DISK
9611 FSARVOR  IF  E           K        DISK
9611 FHSCINV  IF  E           K        DISK
     E*************************************************************
     E                    ARRO      100 10
     E                    ARRX    1  10  1
     E                    ARRY    1  20  2
     E                    ERR     1  42 70
     I*************************************************************
     IACREC
     I              ACTYPE                          ACTYP1
     I              ACNAME                          ACNAM1
     I            DS
     I                                        1  10 W#SANO
     I                                        2   2 W#SN
     IAADS        DS
     I                                        1   5 W1CUNO
     I                                        1   1 S1KIND
     I                                        2   2 S1CUN1
     I                                        3   5 S1CUN2
     I                                       21  30 SBID
     I                                       21  23 SBID1
     I                                       24  27 SBID2
     I                                       28  30 SBID3
     I                                       31  40 SANO
     I                                       31  31 SANO1
     I                                       32  32 SANO2
     I                                       33  40 SANO3
     I                                       41  50 INVNO
     I                                       41  42 INVNO1
     I                                       43  46 INVNO2
     I                                       47  50 INVNO3
     IBBDS        DS
     I                                        1  10 GRP
     I                                        1   1 GE1
     I                                        2  10 GE2
     I                                       11  16 NOG
     I                                       11  11 NOA
     I                                       12  160NOB
9611 I            DS
 .   I                                        1   6 CUNO
9611 I                                        1   2 D#CUN1
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           INKEY     KLIST
     C                     KFLD           INNO
     C           IVKEY     KLIST
     C                     KFLD           IVNO
     C                     KFLD           IVACNT
     C                     KFLD           IVITEM
     C           CBKEY     KLIST
     C                     KFLD           CBCUNO
     C           RVKEY     KLIST
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C           ACKEY     KLIST
     C                     KFLD           SANO1
     C                     KFLD           SANO2
     C                     KFLD           SANO3
     C           IV1KEY    KLIST
     C                     KFLD           IVACNT
     C                     KFLD           IVAPNO
     C                     KFLD           IVNO
     C                     KFLD           IVITEM
     C           IV1TKY    KLIST
     C                     KFLD           IVACNT
     C                     KFLD           IVAPNO
     C**************************************************************
     C  N90                MOVEL'1'       SCRN    1
     C  N90                MOVEL'1'       *IN,90
     C                     MOVEA'0000000' *IN,21
     C**************************************************************
     C           *INKC     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
     C           SCRN      CASEQ'3'       SR#03
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     CSR         SR#01     BEGSR
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C                     EXSR CK#01
     C  N99                EXSR PR#02
     C  N99                MOVE '2'       SCRN
     CSR         SR#01Z    ENDSR
     C******
     CSR         SR#02     BEGSR
     C                     WRITEUND01
     C                     EXFMTSFCTL1
     C   KC                EXSR KC#01
     C   KL                MOVE '1'       SCRN
     C   KL                Z-ADD0         RR1
     C   KL                Z-ADD0         RR3
     C   KL                MOVE *BLANK    ERRMSG
     C   KL                GOTO SR#02Z
     C   KJ                EXSR KJ#02
     C   KJ                GOTO SR#02Z
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C                     ELSE
     C                     MOVE '3'       SCRN
     C                     EXSR PR#03
     C                     ENDIF
     CSR         SR#02Z    ENDSR
     C*****
     CSR         SR#03     BEGSR
     C                     WRITEUND03
     C                     EXFMTSFCTL3
     C   KL                MOVE '2'       SCRN
     C   KL                MOVE *BLANK    ERRMSG
     C   KL                GOTO SR#03Z
     C   KJ                EXSR KJ#03
     C   KJ                GOTO SR#03Z
     C   KC                EXSR KC#01
     C   KC                GOTO SR#03Z
     C           CODE      IFEQ '4'
     C                     MOVE '1'       SCRN
     C                     Z-ADD0         RR1
     C                     Z-ADD0         RR3
     C                     ENDIF
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#03
     C                     ENDIF
     CSR         SR#03Z    ENDSR
     C*****
     CSR         KC#01     BEGSR
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C****
     CSR         KJ#02     BEGSR
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C                     ENDIF
     C  N99                MOVE '3'       SCRN
     C  N99                EXSR PR#03
     CSR         KJ#02Z    ENDSR
     C****
     CSR         KJ#03     BEGSR
     C           CODE      IFNE '3'
     C           CODE      ANDNE'4'
     C                     EXSR CK#03
     C                     ENDIF
     C  N99      CODE      IFNE '4'
     C                     EXSR FL#04
     C                     ENDIF
     C  N99                MOVE '1'       SCRN
     C  N99                Z-ADD0         RR1
     C  N99                Z-ADD0         RR3
     CSR         KJ#03Z    ENDSR
     C****
     CSR         CK#01     BEGSR
     C                     SETOF                     414299
     C                     SETOF                     7170
     C                     MOVE *BLANK    ERRMSG
     C                     SELEC
     C           CODE      WHEQ ' '
     C                     SETON                     4199
     C                     MOVELERR,1     ERRMSG
     C           CODE      WHEQ '1'
     C                     MOVE '新增'  MOD
     C                     MOVE '0'       *IN71
     C           CODE      WHEQ '2'
     C                     MOVE '修改'  MOD
     C                     MOVE '0'       *IN71
     C           CODE      WHEQ '3'
     C                     MOVE '刪除'  MOD
     C                     MOVE '1'       *IN71
     C           CODE      WHEQ '4'
     C                     MOVE '查詢'  MOD
     C                     MOVE '1'       *IN71
     C                     OTHER
     C                     SETON                     4199
     C                     MOVE ERR,2     ERRMSG
     C                     ENDSL
     C******
     C*          CODE      IFEQ '2'
     C*          CODE      OREQ '3'
     C*                    MOVELNO        NOAREA  1
     C*          NOAREA    IFNE TXAR
     C*                    SETON                     4299
     C*                    MOVELERR,34    ERRMSG
     C*                    ENDIF
     C*                    ENDIF
     C**
     C*          CODE      IFEQ '1'
     C*          NO        ANDNE*BLANK
     C*                    SETON                     4299
     C*                    MOVELERR,31    ERRMSG
     C*                    ENDIF
     C**
     C***        CODE      IFNE '1'
     C           NO        IFEQ *BLANK
     C                     SETON                     4299
     C                     MOVELERR,3     ERRMSG
     C                     ENDIF
     C****
     C**
     C           *IN99     IFEQ '0'
     C                     MOVELNO        RVNO
     C                     Z-ADD1         RVITEM
     C           RVKEY     CHAINRCVDTL              N97
     C                     SELEC
     C           *IN97     WHEQ '0'
     C           RVTYPE    ANDNE'2'
     C           RVTYPE    ANDNE'1'
     C                     SETON                     4299
     C                     MOVELERR,36    ERRMSG
     C           CODE      WHEQ '1'
     C           *IN97     ANDEQ'0'
     C                     SETON                     4299
     C                     MOVELERR,4     ERRMSG
     C           CODE      WHNE '1'
     C           *IN97     ANDEQ'1'
     C                     SETON                     4299
     C                     MOVELERR,5     ERRMSG
     C*          CODE      WHEQ '2'
     C*          *IN97     ANDEQ'0'
     C*          RVDECD    ANDNE' '
     C*                    SETON                     4299
     C*                    MOVELERR,6     ERRMSG
     C           CODE      WHEQ '3'
     C           *IN97     ANDEQ'0'
     C           RVDECD    ANDNE' '
     C                     SETON                     4299
     C                     MOVELERR,7     ERRMSG
     C                     ENDSL
     C                     ENDIF
     CSR         CK#01Z    ENDSR
     C******
     CSR         PR#02     BEGSR
     C                     MOVE *BLANK    ERRMSG
     C                     SETON                     80
     C                     WRITESFCTL1
     C                     SETOF                     80
     C                     Z-ADD0         RR1     20
     C                     MOVEA'000000'  *IN,43
     C                     MOVEA'00000000'*IN,51
     C                     MOVEA'0000000' *IN,59
     C                     MOVE *BLANK    CUNO
     C                     MOVE *BLANK    CUNM
     C                     MOVE *BLANK    DPNO
     C                     MOVE *BLANK    RVID
     C                     Z-ADDUDATE     DATE    80
     C                     Z-ADD0         TTLAMT
     C**
     C           CODE      CASEQ'1'       P2#ADD
     C                     CAS            P2#OTH
     C                     ENDCS
     CSR         PR#02Z    ENDSR
     C******
     CSR         P2#ADD    BEGSR
     C*                    EXSR @GETNO
     C*                    MOVEL'B020'    DPNO
     C                     MOVEL*BLANK    SVNO1
     C                     CLEARSF1
     C           RR1       DOUGE90
     C                     ADD  1         RR1
     C                     Z-ADDRR1       ITEM
     C                     WRITESF1
     C                     ENDDO
     C                     SETON                     10
     CSR         P2#ADZ    ENDSR
     C******
     CSR         @GETNO    BEGSR
     C*                    MOVEL'04'      GEKIND
     C*                    MOVE *BLANK    GEPRIN
     C*                    MOVE *BLANK    GRP
     C*                    MOVELTXAR      GE1
     C*                    MOVELGRP       GEPRIN
     C*          GEKEY     CHAINGENSEQ              N97
     C*  97                Z-ADD10000     GECUNO
     C*                    MOVELTXAR      NOA
     C*          GECUNO    ADD  1         NOB
     C*                    MOVELNOG       NO
     CSR                   ENDSR
     C***
     CSR         P2#OTH    BEGSR
     C                     MOVELRVCUNO    CUNO
     C                     MOVELRVCUNM    CUNM
     C                     MOVELRVDPNO    DPNO
     C                     MOVELRVRVID    RVID
     C                     Z-ADDRVDATE    DATE
     C                     MOVELRVVNO1    SVNO1   7
     C**
     C                     MOVELNO        RVNO
     C                     Z-ADD0         RVITEM
     C           RVKEY     SETLLRCVDTL               97
     C  N97      NO        READERCVDTL              N    97
     C           RR1       DOWLE89
     C           *IN97     ANDEQ'0'
     C                     CLEARSF1
     C   71                SETON                     70
     C                     ADD  1         RR1
     C           RR1       IFNE RVITEM
     C           RR1       DOWLTRVITEM
     C                     WRITESF1
     C                     ADD  1         RR1
     C                     ENDDO
     C                     ENDIF
     C           RVFLAG    IFNE 'D'
     C           RVACTP    IFEQ 'C'
     C                     SUB  RVRAMT    TTLAMT
     C                     ELSE
     C                     ADD  RVRAMT    TTLAMT
     C                     ENDIF
     C                     ENDIF
     ****
     C                     MOVELRVFLAG    FLAG
     C                     MOVELRVITEM    ITEM
     C                     MOVELRVNTTP    NTTP
     C                     MOVELRVUSTP    USTP
     C                     MOVELRVACTP    ACTP
     C                     MOVELRVACNO    ACNOW
     C                     MOVELRVPBID    PBID
     C                     MOVELRVPANO    PANO
     C                     MOVELRVPLAC    PLAC
     C                     MOVELRVNTNO    NTNO
     C                     Z-ADDRVRAMT    RAMT
     C                     MOVELRVSBID    SBID
     C                     MOVELRVSANO    SANO
     C                     Z-ADDRVDUDT    DUDT
     C*                    MOVELRVAMCD    AMCD
     C                     MOVELRVRLNO    RLNO
     C                     WRITESF1
     C           NO        READERCVDTL              N    97
     C                     ENDDO
     C****
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C           RR1       OREQ 0
     C                     CLEARSF1
     C   71                SETON                     70
     C           RR1       DOWLE89
     C                     ADD  1         RR1
     C                     Z-ADDRR1       ITEM
     C                     WRITESF1
     C                     ENDDO
     C                     ENDIF
     C   71                SETON                     70
     C****
     C                     SETON                     10
     CSR         P2#OTZ    ENDSR
     C******
     CSR         CK#02     BEGSR
     C                     SETOF                     9996
     C                     MOVEA'000000'  *IN,43
     C                     MOVEL*BLANK    ERRMSG
     C                     MOVE *BLANK    CUNM
     C                     MOVE *BLANK    PBIDT  14
     C                     MOVE *BLANK    PANOT  10
     C                     MOVE *BLANK    PLACT   1
     C                     MOVE *BLANK    USTPT   2
     C                     MOVE *BLANK    NTTPT   1
     C                     Z-ADD0         TTLAMT
     C***
     C                     MOVELCUNO      CBCUNO
     C           CBKEY     CHAINCBCUST               97
     C  N97                MOVELCBCUNM    CUNM
     C   97                MOVELERR,8     ERRMSG
     C   97                SETON                     9944
     C***
     C           RVID      IFEQ *BLANK
     C                     SETON                     9946
     C                     MOVELERR,37    ERRMSG
     C                     ENDIF
     C***
     C                     SELEC
     C           DATE      WHEQ 0
     C                     SETON                     9945
     C                     MOVELERR,18    ERRMSG
     C                     OTHER
     C*                    CALL 'C01'
     C*                    PARM           DATE    60
     C*                    PARM ' '       FLAG1   1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DATE      P#DATE
     C                     MOVE *ALL'0'   P#MTL
     C                     MOVE *ALL' '   P#LY
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM ' '       FLAG1   1
     C           FLAG1     IFNE '0'
     C                     SETON                     9945
     C                     MOVELERR,18    ERRMSG
     C                     ENDIF
     C                     ENDSL
      **
     C                     Z-ADD0         RR1
     C           RR1       DOWLT90
     C                     MOVEA'00000000'*IN,51
     C                     MOVEA'0000000' *IN,59
     C                     ADD  1         RR1
     C           RR1       CHAINSF1                  97
     C   97                LEAVE
     C   21                MOVELPBIDT     PBID
     C                     MOVELPBID      PBIDT
     C   22                MOVELPANOT     PANO
     C                     MOVELPANO      PANOT
     C   23                MOVELPLACT     PLAC
     C                     MOVELPLAC      PLACT
     C   24                MOVELNTTPT     NTTP
     C                     MOVELNTTP      NTTPT
     C   25                MOVELUSTPT     USTP
     C                     MOVELUSTP      USTPT
     C           NTTP      IFEQ *BLANK
     C           USTP      ANDEQ*BLANK
     C           ACTP      ANDEQ*BLANK
     C           ACNOW     ANDEQ*BLANK
     C           FLAG      OREQ 'D'
     C                     ITER
     C                     UPDATSF1
     C                     ENDIF
     C***
     C                     SETON                     96
     C                     SELEC
     C*          NTTP      WHEQ ' '
     C*                    SETON                     9951
     C*                    MOVELERR,9     ERRMSG
     C           NTTP      WHNE ' '
     C           NTTP      LOKUPARRX                     97
     C  N97                SETON                     9951
     C  N97                MOVELERR,10    ERRMSG
     C                     ENDSL
     C***
     C                     SELEC
     C*          USTP      WHEQ '  '
     C*                    SETON                     9952
     C*                    MOVELERR,11    ERRMSG
     C           USTP      WHNE '  '
     C           USTP      LOKUPARRY                     97
     C  N97                SETON                     9952
     C  N97                MOVELERR,12    ERRMSG
     C                     ENDSL
     C***
     C           ACTP      IFEQ ' '
     C                     MOVEL'D'       ACTP
     C                     ENDIF
     C           ACTP      IFNE 'C'
     C           ACTP      ANDNE'D'
     C                     SETON                     9953
     C                     MOVELERR,19    ERRMSG
     C                     ENDIF
     *** 判斷帳號非”ｎ”為外幣存款
     C                     MOVELSANO      W#SANO
     ***
     C                     SELEC
     C           NTTP      WHGE 'A'
     C           NTTP      ANDLE'E'
     C                     MOVEL'1131'    ACNOW
     C           NTTP      WHEQ 'I'
     C                     MOVEL'1131'    ACNOW
     C           NTTP      WHEQ 'F'
     C           NTTP      OREQ 'G'
     C           NTTP      OREQ 'J'
     C           W#SN      IFEQ 'N'
     C                     MOVEL'1112'    ACNOW
     C                     ELSE
9704 C           ACNO      IFNE '1138'
9707 C           ACNOW     ANDNE'1138'
     C                     MOVEL'1114'    ACNOW
9704 C                     ENDIF
     C                     ENDIF
     C           NTTP      WHEQ 'H'
     C                     MOVEL'1111'    ACNOW
     C                     ENDSL
     C****
     C                     SELEC
     C           ACNOW     WHEQ *BLANK
     C                     SETON                     9954
     C                     MOVELERR,20    ERRMSG
     C           ACNOW     WHNE *BLANK
9707 C                     MOVELACNOW     W#ACNO  9
9707 C           W#ACNO    CHAINBCNAME               95
     C   95                SETON                     9954
     C   95                MOVELERR,20    ERRMSG
     C                     ENDSL
     C***
     C                     SELEC
     C           NTTP      WHEQ 'I'
     C           NTNO      CHAINLLCMST               95
     C   95                SETON                     9958
     C   95                MOVELERR,40    ERRMSG
     C                     ENDSL
     C***
CLJ  C*          NTTP      IFEQ 'I'
     C*          LLBAMT    ANDLTRAMT
     C*                    SETON                     9958
     C*                    MOVELERR,42    ERRMSG
     C*                    ENDIF
     C***
     C  N99      ACNOW     IFEQ '2221'
     C           RLNO      ANDEQ*BLANK
     C                     SETON                     9960
     C                     MOVELERR,39    ERRMSG
     C                     ENDIF
     C****
     C* N99      ACNOW     IFEQ '2159'
     C*          RLNO      ANDEQ*BLANK
     C*                    SETON                     9960
     C*                    MOVELERR,41    ERRMSG
     C*                    ENDIF
     C****
     C           NTTP      IFGE 'A'
     C           NTTP      ANDLE'E'
     C           NTTP      OREQ 'I'
     C                     SELEC
     C           PLAC      WHNE 'D'
     C           PLAC      ANDNE'E'
     C                     SETON                     9957
     C                     MOVELERR,21    ERRMSG
     C           NTNO      WHEQ *BLANK
     C                     SETON                     9958
     C                     MOVELERR,22    ERRMSG
     C           DUDT      WHEQ 0
     C                     SETON                     9963
     C                     MOVELERR,23    ERRMSG
     C           DUDT      WHNE 0
     C*                    CALL 'C01'
     C*                    PARM           DUDT
     C*                    PARM ' '       FLAG1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DUDT      P#DATE
     C                     MOVE *ALL'0'   P#MTL
     C                     MOVE *ALL' '   P#LY
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE
     C                     PARM '1'       P#MODE
     C                     PARM           P#MTL
     C                     PARM           P#LY
     C                     PARM '0'       FLAG1
     C           FLAG1     IFNE '0'
     C                     SETON                     9963
     C                     MOVELERR,24    ERRMSG
     C                     ENDIF
     C                     ENDSL
     C                     ENDIF
     ***
     C           RAMT      IFEQ 0
     C                     SETON                     9959
     C                     MOVELERR,25    ERRMSG
     C                     ENDIF
     ***
     C*          SBID      IFNE *BLANK
     C*          BAKEY     CHAINBANKID               97
     C*  97                SETON                     9961
     C*  97                MOVELERR,27    ERRMSG
     C*                    ENDIF
     ****
     C                     MOVEL*BLANK    SBID
     C           SANO      IFNE *BLANK
     C           ACKEY     CHAINBANKAC               97
     C   97                SETON                     9962
     C   97                MOVELERR,28    ERRMSG
     C  N97                MOVELACBAID    SBID
     C                     ENDIF
     ****:
     C*          AMCD      IFEQ *BLANK
     C*                    SETON                     9965
     C*                    MOVELERR,29    ERRMSG
     C*                    ELSE
     C*          AMCD      CHAINCAPTAL               97
     C*  97                SETON                     9965
     C*  97                MOVELERR,30    ERRMSG
     C*                    ENDIF
     C           ACTP      IFEQ 'C'
     C                     SUB  RAMT      TTLAMT
     C                     ELSE
     C                     ADD  RAMT      TTLAMT
     C                     ENDIF
     C                     UPDATSF1
     C***
     C                     ENDDO
     C           *IN96     IFEQ '0'
     C                     SETON                     99
     C                     MOVELERR,35    ERRMSG
     C                     ENDIF
     C******
     CSR         CK#02Z    ENDSR
     C******
     CSR         PR#03     BEGSR
     C                     MOVEL*BLANK    ERRMSG
     C                     Z-ADD0         TAAMT
     C                     SETOF                     99
     C           RR3       IFNE 0
     C                     GOTO PR#03Z
     C                     ENDIF
     C**
     C                     SETON                     80
     C                     WRITESFCTL3
     C                     SETOF                     80
     C                     Z-ADD0         RR3     20
     C                     CLEARSF3
     C***
     C           CODE      CASEQ'1'       P3#ADD
     C                     CAS            P3#OTH
     C                     ENDCS
     ***
     CSR         PR#03Z    ENDSR
     C**
     C******
     CSR         P3#ADD    BEGSR
     C                     Z-ADD0         RR3     20
     C**
     C           RR3       DOWLE95
     C                     ADD  1         RR3
     C                     CLEARSF3
     C                     Z-ADDRR3       ITEM
     C                     WRITESF3
     C                     ENDDO
     C                     SETON                     10
     C**
     CSR         P3#ADZ    ENDSR
     C******
     CSR         P3#OTH    BEGSR
     C                     Z-ADD0         RR3     20
     C                     Z-ADD0         TAAMT
     C**
     C                     MOVEL'6'       IVACNT
     C                     MOVEL*BLANK    IVAPNO
     C                     MOVELNO        IVAPNO
     C                     MOVEL*BLANK    IVNO
     C                     Z-ADD0         IVITEM
     C*
9611 C           D#CUN1    IFNE 'EH'
     C           D#CUN1    ANDNE'EP'
     C           IV1KEY    SETLLINVDTLL1             97
     C  N97      IV1TKY    READEINVDTLL1            N    97
     C           *IN97     DOWEQ'0'
     C                     CLEARSF3
     C   71                SETON                     70
     C*                    SETON                     72
     C                     ADD  1         RR3
     C                     Z-ADDRR3       ITEM
     C                     MOVELIVNO      INVNO
     C                     MOVELIVORNO    ORNO
     C                     Z-SUBIVAMT     AAMT
     C                     SUB  IVAMT     TAAMT
     C                     MOVELIVNO      INVNOH
     C                     Z-SUBIVAMT     AAMTH
     C                     MOVELIVFLAG    FLAG
     C                     WRITESF3
     C           IV1TKY    READEINVDTLL1            N    97
     C                     ENDDO
     C*
9611 C                     ELSE
 .   C                     Z-ADD0         W#AAMT  90
 .   C                     MOVELIVAPNO    W#RVNO  6
 .   C           W#RVNO    SETLLSARVOR               97
 .   C           *IN97     DOWEQ'0'
 .   C  N97      W#RVNO    READESARVOR              N    97
 .   C   97                LEAVE
 .   C                     ADD  SONOXA    W#AAMT
 .   C                     ENDDO
 .   C*
 .   C   71                SETON                     70
 .   C                     ADD  1         RR3
 .   C                     Z-ADDRR3       ITEM
 .   C                     MOVELSOORNO    INVNO
 .   C                     MOVELSOORNO    ORNO
 .   C                     Z-ADDW#AAMT    AAMT
 .   C                     ADD  W#AAMT    TAAMT
 .   C                     MOVELSOORNO    INVNOH
 .   C                     Z-ADDW#AAMT    AAMTH
 .   C                     MOVEL'A'       FLAG
 .   C                     WRITESF3
9611 C                     ENDIF
     C***
     C           CODE      IFEQ '2'
     C           RR3       OREQ 0
     C           RR3       DOWLE95
     C                     ADD  1         RR3
     C                     CLEARSF3
     C   71                SETON                     70
     C                     Z-ADDRR3       ITEM
     C                     WRITESF3
     C                     ENDDO
     C                     ENDIF
     C                     SETON                     10
     C   71                SETON                     70
     C*****
     CSR         P3#OTZ    ENDSR
     C******
     CSR         CK#03     BEGSR
     C                     CLEARARRO
     C                     SETOF                     996667
     C                     Z-ADD0         TAAMT
     C                     MOVEL*BLANK    ERRMSG
     C                     MOVEL*BLANK    INVNOT  2
     C                     MOVEL*BLANK    INVNOX  4
     C***
     C                     Z-ADD0         RR3
     C           RR3       DOWLE99
     C                     MOVEA'00'      *IN,66
     C                     ADD  1         RR3
     C           RR3       CHAINSF3                  97
     C   97                LEAVE
     C   26                MOVELINVNOT    INVNO1
     C                     MOVELINVNO1    INVNOT
     C   27                MOVELINVNOX    INVNO2
     C                     MOVELINVNO2    INVNOX
     C           INVNO     IFEQ *BLANK
     C           FLAG      OREQ 'D'
     C                     ITER
     C                     UPDATSF3
     C                     ENDIF
     C*
9611 C           D#CUN1    IFNE 'EH'
     C           D#CUN1    ANDNE'EP'
     C                     MOVELINVNO     INNO
     C           INKEY     CHAININVMST              N97
     C   97                SETON                     9966
     C   97                MOVELERR,13    ERRMSG
9611 C  N97                MOVELINORNO    ORNO
 .   C                     ELSE
 .   C                     MOVELINVNO1    W#ORNO  6
 .   C                     MOVE INVNO2    W#ORNO
 .   C           W#ORNO    CHAINHSCINV              N97
 .   C   97                SETON                     9966
 .   C   97                MOVELERR,13    ERRMSG
 .   C  N97                MOVELC1ORNO    ORNO
9611 C                     ENDIF
     C*
     C  N97      INNBAL    ADD  AAMTH     INNBAL
     C  N97      INNBAL    IFEQ 0
     C                     SETON                     9966
     C                     MOVELERR,15    W#MSG1
     C                     MOVE INNO      W#MSG1
     C                     MOVELW#MSG1    ERRMSG
     C                     ENDIF
     C*
     C  N97      AAMT      IFEQ 0
     C                     Z-ADDINNBAL    AAMT
     C                     ENDIF
     C*
     C  N97      INNBAL    IFLT AAMT
     C                     SETON                     996667
     C                     MOVELERR,32    W#MSG1 70
     C                     MOVE INNO      W#MSG1
     C                     MOVELW#MSG1    ERRMSG
     C                     ENDIF
     C*
     C  N97      INCUNO    IFNE CUNO
9611 C           C1OCUS    ANDNECUNO
     C                     SETON                     9966
     C                     MOVELERR,33    W#MSG1
     C                     MOVE INNO      W#MSG1
     C                     MOVELW#MSG1    ERRMSG
     C                     ENDIF
     C*
     C  N97      INDECD    IFEQ 'D'
9611 C           C1FLAG    ANDEQ'D'
     C                     SETON                     9966
     C                     MOVELERR,38    W#MSG1
     C                     MOVE INNO      W#MSG1
     C                     MOVELW#MSG1    ERRMSG
     C                     ENDIF
     C*
     C                     ADD  AAMT      TAAMT
     C           INVNO     LOKUPARRO                     97
     C      97             SETON                     9966
     C      97             MOVELERR,16    W#MSG1
     C      97             MOVE INNO      W#MSG1
     C      97             MOVELW#MSG1    ERRMSG
     C     N97             Z-ADDRR3       Y       20
     C     N97             MOVELINVNO     ARRO,Y
     C                     UPDATSF3
     C                     ENDDO
     C*
     C  N99      TAAMT     IFNE TTLAMT
     C                     SETON                     99
     C                     MOVELERR,14    ERRMSG
     C                     ENDIF
     C*
     CSR         CK#03Z    ENDSR
     C******
     C******
     CSR         FL#04     BEGSR
     C           CODE      CASEQ'3'       DL#04
     C           CODE      CASEQ'2'       UP#04
     C           CODE      CASEQ'1'       WR#04
     C                     ENDCS
     CSR         FL#04Z    ENDSR
     C****
     CSR         WR#04     BEGSR
     C***
     C*                    CLEARGEREC
     C*                    MOVEL'04'      GEKIND
     C*                    MOVE *BLANK    GEPRIN
     C*                    MOVE *BLANK    GRP
     C*                    MOVELTXAR      GE1
     C*                    MOVELGRP       GEPRIN
     C*          GEKEY     CHAINGENSEQ               97
     C*  97                Z-ADD10001     GECUNO
     C*  97                WRITEGEREC
     C* N97                ADD  1         GECUNO
     C* N97                UPDATGEREC
     C*                    MOVELTXAR      NOA
     C*                    Z-ADDGECUNO    NOB
     C*                    MOVELNOG       NO
     C***
     C                     Z-ADD0         RR1
     C                     SETOF                     95
     C           *IN95     DOWEQ'0'
     C                     ADD  1         RR1
     C           RR1       CHAINSF1                  95
     C   95                LEAVE
     C           NTTP      IFEQ *BLANK
     C           USTP      ANDEQ*BLANK
     C           ACTP      ANDEQ*BLANK
     C           ACNOW     ANDEQ*BLANK
     C           FLAG      OREQ 'D'
     C                     ITER
     C                     ENDIF
     C***
     C                     EXSR WR#RV
     C******
     C                     ENDDO
     C******
     C                     Z-ADD0         RR3
     C                     SETOF                     95
     C           *IN95     DOWEQ'0'
     C                     ADD  1         RR3
     C           RR3       CHAINSF3                  95
     C   95                LEAVE
     C           INVNO     IFEQ *BLANK
     C                     ITER
     C                     ENDIF
     C***
     C                     CLEARIVREC
     C                     MOVELINVNO     IVNO
     C                     MOVE '6'       IVACNT
     C                     Z-ADD0         X
     C                     SETOF                     75
     C           *IN75     DOWEQ'0'
     C                     ADD  1         X       20
     C                     Z-ADDX         IVITEM
     C           IVKEY     CHAININVDTL               75
     C  N75      IVFLAG    IFNE 'D'
     C                     ITER
     C                     ENDIF
     C  N75                MOVELIVAPNO    SAPNO   6
     C  N75      SAPNO     IFEQ NO
     C                     EXSR UP#IV
     C                     ELSE
     C                     ITER
     C                     END
     C   75                EXSR WR#IV
     C                     SETON                     75
     C                     ENDDO
     C***
     C                     EXSR UP#IN
     C                     ENDDO
     C                     MOVELNO        NOG
     C                     ADD  1         NOB
     C                     MOVELNOG       NO
     CSR         WR#04Z    ENDSR
     C******
     CSR         DL#04     BEGSR
     C                     EXSR DL#RV
     C                     EXSR DL#IV
     C****
     CSR         DL#04Z    ENDSR
     *****
     CSR         UP#04     BEGSR
     C***
     C                     Z-ADD0         RR1
     C                     SETOF                     95
     C           *IN95     DOWEQ'0'
     C                     ADD  1         RR1
     C           RR1       CHAINSF1                  95
     C   95                LEAVE
     C                     MOVELNO        RVNO
     C                     Z-ADDRR1       RVITEM
     C           RVKEY     CHAINRCVDTL               96
     C           NTTP      IFEQ *BLANK
     C           USTP      ANDEQ*BLANK
     C           ACTP      ANDEQ*BLANK
     C           ACNOW     ANDEQ*BLANK
     C           FLAG      OREQ 'D'
     C   96                ITER
     C                     MOVE 'D'       RVFLAG
     C                     MOVE 'D'       RVDECD
     C                     Z-ADDUDATE     RVDEDT
     C                     MOVE TXAR      RVTXAR
     C                     Z-ADDUDATE     RVTXDT
     C                     UPDATRVREC
     C                     ELSE
     C  N96                EXSR UP#RV
     C   96                EXSR WR#RV
     C                     ENDIF
     C***
     C                     ENDDO
     C******
     C                     EXSR DL#IV
     C*****
     C                     Z-ADD0         RR3
     C                     SETOF                     95
     C           *IN95     DOWEQ'0'
     C                     ADD  1         RR3
     C           RR3       CHAINSF3                  95
     C   95                LEAVE
     C***
     C           INVNO     IFEQ *BLANK
     C           FLAG      OREQ 'D'
9612 C           INVNO3    OREQ *BLANK
     C                     ITER
     C                     ENDIF
     C**
     C                     CLEARIVREC
     C                     MOVELINVNO     IVNO
     C                     MOVE '6'       IVACNT
     C                     Z-ADD0         X
     C                     SETOF                     75
     C           *IN75     DOWEQ'0'
     C                     ADD  1         X       20
     C                     Z-ADDX         IVITEM
     C           IVKEY     CHAININVDTL               75
     C  N75      IVFLAG    IFNE 'D'
     C                     ITER
     C                     ENDIF
     C  N75                MOVELIVAPNO    SAPNO   6
     C  N75      SAPNO     IFEQ NO
     C                     EXSR UP#IV
     C                     ELSE
     C                     ITER
     C                     END
     C   75                EXSR WR#IV
     C                     SETON                     75
     C                     ENDDO
     C                     EXSR UP#IN
     C                     ENDDO
     C***
     CSR         UP#04Z    ENDSR
     C***
     CSR         WR#RV     BEGSR
     C                     CLEARRVREC
     C                     MOVEL'A'       RVFLAG
     C                     MOVELNO        RVNO
     C                     Z-ADDRR1       RVITEM
     C                     Z-ADDDATE      RVDATE
     C                     MOVELCUNO      RVCUNO
     C                     MOVELCUNM      RVCUNM
     C                     MOVEL'2'       RVTYPE
     C                     MOVELSVNO1     RVVNO1
     C                     MOVELDPNO      RVDPNO
     C                     MOVELUSTP      RVUSTP
     C                     MOVELNTTP      RVNTTP
     C                     MOVELPBID      RVPBID
     C                     MOVELPANO      RVPANO
     C                     MOVELACTP      RVACTP
     C                     MOVELACNOW     RVACNO
     C                     MOVELPLAC      RVPLAC
     C                     MOVELNTNO      RVNTNO
     C                     Z-ADDDUDT      RVDUDT
     C                     Z-ADDRAMT      RVRAMT
     C                     MOVELSBID      RVSBID
     C                     MOVELSANO      RVSANO
     C*                    MOVELAMCD      RVAMCD
     C                     MOVELRLNO      RVRLNO
     C                     MOVELTXAR      RVTXAR
     C                     MOVELTXAR      RVAREA
     C                     Z-ADDUDATE     RVTXDT
     C                     MOVELRVID      RVRVID
     C                     WRITERVREC
     CSR         WR#RVZ    ENDSR
     C******
     CSR         WR#IV     BEGSR
     C******
     C                     CLEARIVREC
     C                     MOVEL'A'       IVFLAG
     C                     MOVELINVNO     IVNO
     C                     MOVEL'6'       IVACNT
     C                     Z-ADDX         IVITEM
     C                     Z-ADDDATE      IVACDT
     C                     Z-SUBAAMT      IVAMT
     C                     MOVELORNO      IVORNO
     C                     MOVEL*BLANK    IVAPNO
     C                     MOVELNO        IVAPNO
     C                     MOVELTXAR      IVTXAR
     C                     MOVEL'I'       IVFL03
     C                     MOVEL'Y'       IVFL02
     C                     Z-ADDUDATE     IVTXDT
     C                     WRITEIVREC
     C***
     C***
     CSR         WR#IVZ    ENDSR
     C***
     CSR         UP#RV     BEGSR
     C                     MOVEL'C'       RVFLAG
     C                     Z-ADDDATE      RVDATE
     C                     MOVELCUNO      RVCUNO
     C                     MOVELCUNM      RVCUNM
     C                     MOVEL*BLANK    RVDECD
     C                     Z-ADD0         RVDEDT
     C                     MOVEL'2'       RVTYPE
     C                     MOVELDPNO      RVDPNO
     C                     MOVELUSTP      RVUSTP
     C                     MOVELNTTP      RVNTTP
     C                     MOVELPBID      RVPBID
     C                     MOVELPANO      RVPANO
     C                     MOVELACTP      RVACTP
     C                     MOVELACNOW     RVACNO
     C                     MOVELPLAC      RVPLAC
     C                     MOVELNTNO      RVNTNO
     C                     Z-ADDDUDT      RVDUDT
     C                     Z-ADDRAMT      RVRAMT
     C                     MOVELSBID      RVSBID
     C                     MOVELSANO      RVSANO
     C*                    MOVELAMCD      RVAMCD
     C                     MOVELRLNO      RVRLNO
     C                     MOVELTXAR      RVTXAR
     C                     MOVELRVID      RVRVID
     C                     Z-ADDUDATE     RVTXDT
     C                     UPDATRVREC
     CSR         UP#RVZ    ENDSR
     C***
     CSR         UP#IV     BEGSR
     C******
     C                     MOVEL'C'       IVFLAG
     C                     Z-ADD0         IVDEDT
     C                     MOVEL*BLANK    IVDECD
     C                     Z-ADDDATE      IVACDT
     C                     Z-SUBAAMT      IVAMT
     C                     MOVELORNO      IVORNO
     C                     MOVEL*BLANK    IVAPNO
     C                     MOVELNO        IVAPNO
     C                     MOVEL*BLANK    IVACNO
     C                     MOVELTXAR      IVTXAR
     C                     MOVEL'I'       IVFL03
     C                     MOVEL'Y'       IVFL02
     C                     Z-ADDUDATE     IVTXDT
     C                     UPDATIVREC
     C***
     CSR         UP#IVZ    ENDSR
     *****
     CSR         UP#IN     BEGSR
     C                     MOVELINVNO     INNO
     C           INNO      CHAININVMST               97
     C   97                GOTO UP#INZ
     C                     SUB  AAMT      INEAMT
     C                     SUB  AAMT      INNBAL
     C           INNBAL    IFEQ 0
     C                     MOVEL'Y'       INFL01
     C                     END
     C                     MOVEL'C'       INFLAG
     C                     MOVELNO        INRCNO
     C                     Z-ADDDATE      INRCDT
     C                     Z-ADDUDATE     INTXDT
     C                     MOVELTXAR      INTXAR
     C                     UPDATINREC
     CSR         UP#INZ    ENDSR
     C****:
     CSR         DL#RV     BEGSR
     C                     MOVELNO        RVNO
     C                     Z-ADD0         RVITEM
     C           RVKEY     SETLLRCVDTL               97
     C  N97      NO        READERCVDTL                   97
     C           *IN97     DOWEQ'0'
     C                     MOVEL'D'       RVDECD
     C                     Z-ADDUDATE     RVDEDT
     C                     MOVEL'D'       RVFLAG
     C                     MOVELTXAR      RVTXAR
     C                     Z-ADDUDATE     RVTXDT
     C                     UPDATRVREC
     C           NO        READERCVDTL                   97
     C                     ENDDO
     C****
     CSR         DL#RVZ    ENDSR
     C****
     CSR         DL#IV     BEGSR
     C                     MOVEL'6'       IVACNT
     C                     MOVEL*BLANK    IVAPNO
     C                     MOVELNO        IVAPNO
     C                     MOVEL*BLANK    IVNO
     C                     Z-ADD0         IVITEM
     C           IV1KEY    SETLLINVDTLL1             97
     C  N97      IV1TKY    READEINVDTLL1                 97
     C           *IN97     DOWEQ'0'
     C           IVFLAG    IFNE 'D'
     C                     MOVELIVNO      INNO
     C           INNO      CHAININVMST               96
     C  N96                SUB  IVAMT     INEAMT
     C  N96                SUB  IVAMT     INNBAL
     C  N96                MOVEL'C'       INFLAG
     C  N96                MOVEL' '       INFL01
     C  N96                MOVEL*BLANK    INRCNO
     C  N96                Z-ADDUDATE     INTXDT
     C  N96                MOVELTXAR      INTXAR
     C  N96                UPDATINREC
     C                     ENDIF
     ******
     C                     MOVEL'D'       IVFLAG
     C                     MOVELTXAR      IVTXAR
     C                     Z-ADDUDATE     IVTXDT
     C                     MOVEL'D'       IVDECD
     C                     MOVEL'Y'       IVFL02
     C                     Z-ADDUDATE     IVDEDT
     C                     UPDATIVREC1
     C           IV1TKY    READEINVDTLL1                 97
     C                     ENDDO
     CSR         DL#IVZ    ENDSR
     C****
** ARRX
A
B
C
D
E
F
G
H
I
J
** ARRY
A1
A2
A3
A4
A5
B1
B2
C1
C2
C3
D1
D2
D3
D4
D5
Z1
Z2
Z3
Z4
Z5
** ERR
０１－功能代碼必須輸入．
０２－功能代碼必須為１，２，３，４
０３－繳款號碼必須輸入　　　　
０４－繳款號碼已存在，不可新增
０５－繳款號碼不存在
０６－此張繳款已作廢，不可再修改
０７－此張繳款已作廢，不可再作廢
０８－客戶編號不存在
０９－票款別必須輸入
１０－無此種票款別　　　　　
１１－款項別必須輸入
１２－無此種款項別　　　　　
１３－發票號碼不存在　　　　
１４－繳款總金額不等於沖銷總金額
１５－此張發票已繳完款，不可再沖銷
１６－發票號碼重複
１７－繳款日期必須輸入
１８－繳款日期錯誤
１９－借貸別錯誤
２０－會計科目錯誤
２１－付款地必須為Ｄ，Ｅ
２２－支票號碼必須輸入
２３－到期日期必須輸入
２４－到期日期錯誤
２５－繳款金額不可為０
２６－訂單編號不存在
２７－銀行代號不存在
２８－帳戶編號不存在
２９－資金代號必須輸入
３０－資金代號不存在
３１－新增時，繳款號碼不可輸入　　　　
３２－此張發票未收餘額比沖銷金額還小，不可沖銷
３３－此張發票之客戶編號不等於繳款客戶編號
３４－此張繳款單編號非屬本廠區．
３５－明細項至少必須輸入一筆
３６－此張繳款單非本程式輸入，請查核
３７－收款業務員必須輸入．
３８－此張發票已作廢不可沖銷．
３９－會計科目為存入保證時，相關號碼必須為訂單編號．
４０－信用狀編號不存在
４１－會計科目為２１５９時，相關號碼必須輸入
４２－信用狀餘額不可小於支票金額．
