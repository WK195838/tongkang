     H****************************************************************
     H*
     H*            UPDATE  DATE  103/03/28  2014AR816 S00WCJ (0303A)
     H*                           幣別增加人民幣C
     H*
     H***************************************************************
     H        1   Y                                     1                 BR003
     FARE022S CF  E                    WORKSTN
     FBANKAC  UF  E           K        DISK                      A
     FACBANK  IF  E           K        DISK
     FBANKID  IF  E           K        DISK
     E*************************************************************
     E                    TAB1    1   5  1   TABR1  12
0303AE                    TAB2    1  11  1   TABR2  10
     E                    HD      1   1 10
     E                    ERR     1  11 70
     I*************************************************************
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           ACKEY     KLIST
     C                     KFLD           ACTYPE
     C                     KFLD           ACCURR
     C                     KFLD           ACACNO
     C           BAKEY     KLIST
     C                     KFLD           BAID1
     C                     KFLD           BAID2
     C           A1KEY     KLIST
     C                     KFLD           ACBKNO
     C**************************************************************
     C  N90                MOVE '1'       SCRN    1
     C  N90                MOVE '1'       *IN,90
     C**************************************************************
     C           *IN03     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     CSR         SR#01     BEGSR
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C                     EXSR CK#01
     C  N99                MOVE '2'       SCRN
     C  N99                EXSR PR#02
     C   99                MOVE '1'       SCRN
     CSR         SR#01Z    ENDSR
     C******
     CSR         SR#02     BEGSR
     C                     EXFMTDETAIL
     C   KC                EXSR KC#01
     C   KL                MOVE '1'       SCRN
     C   KL                GOTO SR#02Z
     C   KJ                EXSR KJ#02
     C   KJ                GOTO SR#02Z
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C                     ELSE
     C           CODE      IFEQ '4'
     C                     MOVE '1'       SCRN
     C                     ENDIF
     C                     ENDIF
     CSR         SR#02Z    ENDSR
     C*****
     CSR         KC#01     BEGSR
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C****
     CSR         KJ#02     BEGSR
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C                     ENDIF
     C  N99                EXSR FL#02
     C  N99                MOVE '1'       SCRN
     CSR         KJ#02Z    ENDSR
     C****
     CSR         CK#01     BEGSR
     C                     SETOF                     414299
     C                     SETOF                     4344
     C                     MOVE *BLANK    ERRMSG
     C                     SELEC
     C           CODE      WHEQ ' '
     C                     SETON                     4199
     C                     MOVE ERR,1     ERRMSG
     C           CODE      WHEQ '1'
     C                     MOVE '新增'  MOD
     C                     MOVE '0'       *IN70
     C           CODE      WHEQ '2'
     C                     MOVE '更正'  MOD
     C                     MOVE '0'       *IN70
     C           CODE      WHEQ '3'
     C                     MOVE '作廢'  MOD
     C                     MOVE '1'       *IN70
     C           CODE      WHEQ '4'
     C                     MOVE '查詢'  MOD
     C                     MOVE '1'       *IN70
     C                     OTHER
     C                     SETON                     4199
     C                     MOVE ERR,2     ERRMSG
     C                     ENDSL
     C******
     C           TYPE      IFEQ *BLANK
     C                     SETON                     4299
     C                     MOVE ERR,3     ERRMSG
     C                     ELSE
     C           TYPE      LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     TYPEC
     C  N31                SETON                     4299
     C  N31                MOVELERR,4     ERRMSG
     C                     ENDIF
     C**
     C           CURR      IFEQ *BLANK
     C                     SETON                     4399
     C                     MOVE ERR,5     ERRMSG
     C                     ELSE
     C           CURR      LOKUPTAB2      TABR2          31
     C   31                MOVELTABR2     CURRC
     C  N31                SETON                     4399
     C  N31                MOVELERR,6     ERRMSG
     C                     ENDIF
     C**
     C           NO        IFEQ *BLANK
     C                     SETON                     4499
     C                     MOVE ERR,7     ERRMSG
     C                     ENDIF
     C**
     C           *IN99     IFEQ '0'
     C                     MOVELTYPE      ACTYPE
     C                     MOVELCURR      ACCURR
     C                     MOVELNO        ACACNO
     C           ACKEY     CHAINBANKAC              N97
     C                     SELEC
     C           CODE      WHEQ '1'
     C           *IN97     ANDEQ'0'
     C                     SETON                     4299
     C                     SETON                     4344
     C                     MOVE ERR,8     ERRMSG
     C           CODE      WHNE '1'
     C           *IN97     ANDEQ'1'
     C                     SETON                     4299
     C                     SETON                     4344
     C                     MOVE ERR,9     ERRMSG
     C                     ENDSL
     C                     ENDIF
     CSR         CK#01Z    ENDSR
     C******
     CSR         PR#02     BEGSR
     C**
     C           CODE      CASEQ'1'       PR#ADD
     C                     CAS            PR#OTH
     C                     ENDCS
     CSR         PR#02Z    ENDSR
     C******
     C******
     CSR         PR#ADD    BEGSR
     C                     MOVELTXAR      AREA
     C                     MOVEL*BLANK    BNAM
     C                     MOVEL*BLANK    NAME
     C                     MOVEL*BLANK    BKNO
     CSR         PR#ADZ    ENDSR
     C******
     CSR         PR#OTH    BEGSR
     C                     MOVELACAREA    AREA
     C                     MOVELACBNAM    BNAM
     C                     MOVELACNAME    NAME
     C                     MOVELACOLID    BKNO
     C                     MOVELACBAID    BAID
     C                     MOVELBAID      BAID1
     C                     MOVE BAID      BAID2
     C           BAKEY     CHAINBANKID               97
     C  N97                MOVELBABNAM    BAIDN
     CSR         PR#OTZ    ENDSR
     C******
     CSR         CK#02     BEGSR
     C                     SETOF                     99
     C                     MOVEA'000'     *IN,45
     C                     MOVE *BLANK    ERRMSG
     C                     MOVELBAID      BAID1
     C                     MOVE BAID      BAID2
     C           BAKEY     CHAINBANKID               97
     C  N97                MOVELBABNAM    BAIDN
     C   97                MOVE *BLANK    BAIDN
     C   97                SETON                     9946
     C   97                MOVELERR,10    ERRMSG
     C***
     C           BNAM      IFEQ HD,1
     C                     MOVELBAIDN     BNAM
     C                     ENDIF
     C***
     C           BKNO      IFNE *BLANK
     C                     MOVELBKNO      ACBKNO
     C           A1KEY     CHAINACBANK               97
     C  N97                MOVELACBKNM    NAME
     C   97                MOVE *BLANK    BAIDN
     C   97                SETON                     9947
     C   97                MOVELERR,11    ERRMSG
     C                     ENDIF
     C******
     CSR         CK#02Z    ENDSR
     C******
     CSR         FL#02     BEGSR
     C           CODE      CASEQ'3'       DL#02
     C           CODE      CASEQ'1'       WR#02
     C           CODE      CASEQ'2'       UP#02
     C                     ENDCS
     CSR         FL#02Z    ENDSR
     C****
     CSR         DL#02     BEGSR
     C***
     C                     MOVELTYPE      ACTYPE
     C                     MOVELCURR      ACCURR
     C                     MOVELNO        ACACNO
     C           ACKEY     CHAINBANKAC               97
     C  N97                DELETACREC
     CSR         DL#02Z    ENDSR
     *****
     CSR         WR#02     BEGSR
     C***
     C                     CLEARACREC
     C                     MOVELTYPE      ACTYPE
     C                     MOVELCURR      ACCURR
     C                     MOVELNO        ACACNO
     C                     MOVELBNAM      ACBNAM
     C                     MOVELNAME      ACNAME
     C                     MOVELAREA      ACAREA
     C                     MOVELBKNO      ACOLID
     C                     MOVELBAID      ACBAID
     C                     WRITEACREC
     C***
     CSR         WR#02Z    ENDSR
     *****
     CSR         UP#02     BEGSR
     C                     MOVELTYPE      ACTYPE
     C                     MOVELCURR      ACCURR
     C                     MOVELNO        ACACNO
     C           ACKEY     CHAINBANKAC               97
     C  N97                MOVELBNAM      ACBNAM
     C  N97                MOVELNAME      ACNAME
     C  N97                MOVELAREA      ACAREA
     C  N97                MOVELBKNO      ACOLID
     C  N97                MOVELBAID      ACBAID
     C  N97                UPDATACREC
     C***
     CSR         UP#02Z    ENDSR
** TAB1
A甲存
B乙存
C借款虛帳號
T定存
D款券劃撥
** TAB2
A澳大利幣
C人民幣
D德國馬克
F法國法朗
H港幣
J日幣
L英鎊
N新台幣
S新加坡幣
U美金
E歐元
** HD
　　　　
** ERR
功能代碼必須輸入．
功能代碼必須為１，２，３，４，
存款別必須輸入
存款別必須為Ａ，Ｂ，Ｃ，Ｔ，
幣別必須輸入
幣別必須為Ｄ，Ｆ，Ｈ，Ｊ，Ｌ，Ｎ，Ｓ，Ｕ
帳號代號必須輸入
此銀行帳號已存在
此銀行帳號不存在
此銀行代號不存在
此舊帳號代號不存在
