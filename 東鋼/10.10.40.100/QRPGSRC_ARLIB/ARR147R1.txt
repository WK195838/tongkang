     H*****************************************************************
     H*
     H*            *****   PROGRAM INTRODUCTION   *****
     H*
     H*            1.PROGRAM-ID    ARR147R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION     訂單別出貨統計表─交貨地點
     H*            5.DATE-WRITTEN  113/04/30 2024AR00015
     H*            6.UPDATE-DATE
     H*
     H*****************************************************************
     FARR147F IP  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FARR147T O   E             60     PRINTER      KINFDS PRTCTL
     FARR147T1O   E             61     PRINTER
     F*
     E*
     IWFREC
     I                                              WFCUNOL3
     I                                              WFORNOL3
     I                                              WFMSMNL2
     I                                              WFWTNOL1
     IPRTCTL      DS
     I                                    B 367 3680CURLIN
     I                                    B 369 3720CURPAG
     I            DS
     I                                        1   8 WFWTNO
     I                                        1   1 D#TXAR
     I            DS
     I                                        1   2 OEOF
     I                                        1   1 OE
     I                                        2   2 OF
     I            DS
     I                                        1  10 W#MSMN
     I                                        1   1 W#OE
     I                                       10  10 W#OF
     I           UDS
     I                                      401 4080R#DATS
     I                                      411 4180R#DATE
     I                                      421 426 R#CUNS
     I                                      431 436 R#ORNS
     I                                      441 450 R#MSMN
     I                                      451 451 R#AREA
     I                                      452 452 R#INVT
     I*
     I                                      951 985 COMP
     I                                     10111020 S#DEVI
     I                                     10011010 U#USID
     I                                     10111020 DEVNM
     C*
     C****************************************************************
     C*          *ENTRY    PLIST
     C*                    PARM           R#NO   10
     C*
     C*****************************************************************
     C*          Main      Program
     C*****************************************************************
     C  N10                EXSR SR0000
     C                     EXSR SR1000
     C                     EXSR SR2000
     CL1                   EXSR SR3000
     CL2                   EXSR SR4000
     CL3                   EXSR SR5000
     CLR                   EXSR SR6000
     C*****************************************************************
     C           SR0000    BEGSR
     C*****************************************************************
     C                     CLEARAR147T1M
     C                     CLEARAR47T11M
     C                     CLEARAR147T2M
     C                     CLEARAR47T21M
     C                     CLEARAR147T3M
     C                     CLEARAR47T31M
     C                     MOVEL*ON       *IN10
     C           R#CUNS    IFNE *BLANKS
     C           R#CUNS    CHAINCBCUST              N97
     C  N97                MOVELCBCUNM    R#CUNM
     C                     ENDIF
     C*
     C                     WRITEAR147T1H
     C                     WRITEAR47T11H
     C                     MOVEL*OFF      *IN60
     C                     MOVEL*OFF      *IN61
     C                     SETON                     515253
     C                     SETON                     414243
     C                     MOVEL*ALL'='   R#LINE
     C                     ENDSR
     C*****************************************************************
     C           SR1000    BEGSR
     C*****************************************************************
     C*
     C                     MOVE ''      OEOF
     C*
     C                     MOVEL*BLANKS   W#MSMN
     C                     MOVELOE        W#OE
     C                     MOVELOF        W#OF
     C*
     C           R#MSMN    IFNE W#MSMN
     C           WFMSMN    ANDNER#MSMN
     C                     GOTO END100
     C                     ENDIF
     C*
     C           R#AREA    IFNE *BLANKS
     C           D#TXAR    ANDNER#AREA
     C                     GOTO END100
     C                     ENDIF
     C*
     C           R#INVT    IFEQ 'Y'
     C           WFIVNO    ANDEQ*BLANKS
     C                     GOTO END100
     C                     ENDIF
     C*
     C           R#INVT    IFEQ 'N'
     C           WFIVNO    ANDNE*BLANKS
     C                     GOTO END100
     C                     ENDIF
     C*
     C                     MOVELWFCUNO    R#CUNO
     C           R#CUNO    IFNE *BLANKS
     C           R#CUNO    CHAINCBCUST              N97
     C  N97                MOVELCBCUNM    R1CUNM
     C                     ENDIF
     C                     MOVELWFORNO    R#ORNO
     C                     MOVELWFMSMN    R1MSMN
     C                     Z-ADDWFDATE    R1DATE
     C                     MOVELWFWTNO    R#TXNO
     C                     MOVELWFVRTM    R#ITEM
     C                     Z-ADDWFQTY     R#QTY
     C                     ADD  R#QTY     R#SQTY
     C                     MOVELWFVTNO    R#VTNO
     C                     MOVELWFIVNO    R#IVNO
     C                     MOVELWFAPNO    R#APNO
     C                     MOVELWFPLE1    R#PLE1
     C*
     C           W#T11L    IFEQ 'Y'
     C                     WRITEAR47T11L
     C                     MOVEL*BLANKS   W#T11L
     C                     ENDIF
     C*
     C           W1T11L    IFEQ 'Y'
     C                     WRITEAR47T11L
     C                     MOVEL*BLANKS   W1T11L
     C                     ENDIF
     C*
     C                     WRITEAR47T11D
     C*
     C                     SETOF                     414243
     C*                    SETOF                     515253
     C                     ADD  R#QTY     R#TQTY
     C                     ADD  R#QTY     R1TQTY
     C                     ADD  R#QTY     R2TQTY
     C*
     C           END100    ENDSR
     C*****************************************************************
     C           SR2000    BEGSR
     C*****************************************************************
     C*
     C   60                WRITEAR147T1X
     C   60                WRITEAR147T1H
     C   60                SETON                     515253
     C   60                SETOF                     60
     C   61                WRITEAR47T11X
     C   61                WRITEAR47T11H
     C   61                SETON                     414243
     C   61                SETOF                     61
     C                     ENDSR
     C*
     C****************************************************************
     C           SR3000    BEGSR
     C*****************************************************************
     C*
     C           R#SQTY    IFNE 0
     C           W#T1L     IFEQ 'Y'
     C                     WRITEAR147T1L
     C                     MOVEL*BLANKS   W#T1L
     C                     ENDIF
     C*
     C           W1T1L     IFEQ 'Y'
     C                     WRITEAR147T1L
     C                     MOVEL*BLANKS   W1T1L
     C                     ENDIF
     C*
     C                     WRITEAR147T1D
     C                     CLEARAR147T1D
     C                     SETOF                     515253
     C                     ENDIF
     C                     ENDSR
     C*****************************************************************
     C           SR4000    BEGSR
     C*****************************************************************
     C*
     C           R#TQTY    IFNE 0
     C                     EXSR SR2000
     C                     WRITEAR147T1M
     C                     WRITEAR47T11M
     C  NL3                MOVEL'Y'       W#T1L   1
     C  NL3                MOVEL'Y'       W#T11L  1
     C                     SETON                     5343
     C                     Z-ADD0         R#TQTY
     C                     ENDIF
     C                     ENDSR
     C*****************************************************************
     C           SR5000    BEGSR
     C*****************************************************************
     C*
     C           R1TQTY    IFNE 0
     C                     EXSR SR2000
     C                     WRITEAR147T2M
     C                     WRITEAR47T21M
     C  NLR                MOVEL'Y'       W1T1L   1
     C  NLR                MOVEL'Y'       W1T11L  1
     C                     SETON                     5253
     C                     SETON                     4243
     C                     Z-ADD0         R1TQTY
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR6000    BEGSR
     C*****************************************************************
     C*
     C           R2TQTY    IFNE 0
     C                     EXSR SR2000
     C                     WRITEAR147T3M
     C                     WRITEAR47T31M
     C                     ELSE
     C                     EXSR SR2000
     C                     SETON                     59
     C                     WRITEAR147T3M
     C                     WRITEAR47T31M
     C                     ENDIF
     C*
     C                     ENDSR
