     H****************************************************************
     H*
     H*            UPDATE  DATE  99/09/02  2010AR517  S00WCJ (9909A)
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FRCVDTL  IP  E                    DISK
     FACDEPT  IF  E           K        DISK
 9210FINVDTLL1IF  E           K        DISK
     FQPRINT  O   F     158            PRINTER
     E*************************************************************
     E                    TAB1    1  10  1   TABR1  10
     I*************************************************************
     IRVREC
     I                                              RVDPNOL3
     I                                              RVRVIDL2
     I                                              RVNO  L1
     I****
     IAADS        DS
     I              'も部門別繳款明細表-C         CON3                       X
     I              '（業務）も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     IBBDS        DS
     I                                        1  18 TAIL1
     I                                        1   40YY1
     I                                        5   5 DET1
     I                                        6   70MM1
     I                                        8   8 DET2
     I                                        9  100DD1
     I                                       11  18 DET3
     I                                       21  38 TAIL2
     I                                       21  21 OE
     I                                       22  31 DEPT
     I                                       32  37 DET4
     I                                       38  38 OF
     ICCDS        DS
     I                                       21  280YMD
     I                                       21  260YM
     I                                       21  240YY
     I                                       25  260MM
     I                                       27  280DD
     I           UDS
     I                                       21  21 U#RCV1
     I                                       22  22 U#RCV2
     I                                       31  36 U#CUN1
     I                                       37  42 U#CUN2
 9210I                                       43  48 U#ORNO
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
9909AC           *DATE     SUB  19000000  U#SYSD  80
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L3                EXSR DL#L3
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     CL3                   EXSR TL#L3
     CLR                   EXCPTET#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C           *ENTRY    PLIST
     C                     PARM           DATE1   80
     C                     PARM           DATE2   80
     C*          BKKEY     KLIST
     C*                    KFLD           BAID1
     C*                    KFLD           BAID2
 9210C*FILE==>INVDTL
  .  C           K#INVD    KLIST
  .  C                     KFLD           IVACNT
 9210C                     KFLD           IVAPNO
     C*
     C                     MOVEL'/'       DET1
     C                     MOVEL'/'       DET2
     C                     MOVEL'合計：'DET3
     C                     MOVEL''      OE
     C                     MOVE ''      OF
     C                     MOVE DET3      TEMP    7
     C                     MOVELTEMP      DET4
     C                     Z-ADD0         AMTT4  110
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD56        TCNT    30
     C                     MOVE *ALL'='   ALS   138
     C                     MOVE *ALL'-'   ALQ   126
     C*
 9210C           U#ORNO    IFEQ *BLANK
  .  C                     MOVEL'全部'  ORNO    6
  .  C                     ELSE
  .  C                     MOVELU#ORNO    ORNO
 9210C                     ENDIF
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD8         LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L3                             **
     C**----------------------------------------------**
     CSR         DL#L3     BEGSR
     C                     MOVELRVDPNO    W#DPNO  8
     C           W#DPNO    CHAINACDEPT               97
     C   97                MOVE *BLANK    ACDPNM
     C                     MOVE ACDPNM    DPNM   13
     C                     MOVELDPNM      DEPT
     C                     Z-ADD0         AMTT3  110
     C                     Z-ADDTCNT      LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         AMTT2  110
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         AMTT1  110
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
 9210C           U#ORNO    IFNE *BLANK
  .  C                     MOVEL'6'       IVACNT
  .  C                     MOVELRVNO      IVAPNO
  .  C           K#INVD    CHAINIVREC                40
  .  C           IVORNO    IFNE U#ORNO
  .  C                     GOTO DP#01Z
  .  C                     ENDIF
 9210C                     ENDIF
     C*
     C                     MOVE *BLANK    CHAR10 10
     C           RVNTTP    LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     CHAR10
     C  N31                MOVEL'其他'  CHAR10
      *
     C                     MOVE *BLANK    CHAR6   6
     C           RVPLAC    COMP 'D'                      31
     C   31                MOVEL'本埠'  CHAR6
     C           RVPLAC    COMP 'E'                      31
     C   31                MOVEL'外埠'  CHAR6
      *
     C           RVACTP    COMP 'C'                      31
     C   31                MOVEL'C'       DC      1
     C   31                SUB  RVRAMT    AMTT1
     C  N31                MOVEL'D'       DC
     C  N31                ADD  RVRAMT    AMTT1
      *
     C                     Z-ADDRVRAMT    DEC9    90
      *
     C                     MOVEL*BLANK    R#NTNO 20
     C                     MOVELRVNTNO    R#NTNO
      *
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#01
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
 9210C           AMTT1     IFNE 0
     C                     ADD  AMTT1     AMTT2
     C                     EXCPTET#L1
     C                     ADD  1         LCNT
 9210C                     ENDIF
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
 9210C           AMTT2     IFNE 0
     C                     Z-ADDRVDATE    YMD
     C                     Z-ADDYY        YY1
     C                     Z-ADDMM        MM1
     C                     Z-ADDDD        DD1
     C                     ADD  AMTT2     AMTT3
     C                     EXCPTET#L2
     C                     ADD  3         LCNT
 9210C                     ENDIF
     CSR         TL#L2Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L3 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L3     BEGSR
 9210C           AMTT3     IFNE 0
     C                     ADD  AMTT3     AMTT4
     C                     EXCPTET#L3
     C                     ADD  2         LCNT
 9210C                     ENDIF
     CSR         TL#L3Z    ENDSR
      ***********************************************************
      ****   DEFINE OUPUT FORMAT                             ****
      ***********************************************************
     OQPRINT  E   02           EH#01
     O                                   17 '報表編號: AR055'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
9909AO                         U#SYSDZ  132
     O        E 1              EH#01
     O                                   11 '訂單編號:'
     O                         ORNO      17
     O                         CON3      77                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 2              EH#01
     O                                   11 '繳款部門:'
     O                         RVDPNO  +  1
     O                         ACDPNM  +  1
     O                                   58 '繳款期間:'
     O                         DATE1   +  1 '    /  /  '
     O                                 +  1 '-'
     O                         DATE2   +  1 '    /  /  '
     O                                 + 14 '業務:'
     O                         U#RCV1  +  1
     O                                 +  1 '-'
     O                         U#RCV2  +  1
     O                                 +  4 '客戶:'
     O                         U#CUN1  +  1
     O                                 +  1 '-'
     O                         U#CUN2  +  1
     O        E 1              EH#01
     O                         ALS      138
     O        E 1              EH#01
     O                                   11 '繳款日期'
     O                                   22 '繳款單號'
     O                                   32 '業務員'
     O                                   44 '客戶編號'
     O                                   56 '客戶名稱'
     O                                   66 '款項別'
     O                                   76 '票款別'
     O                                   90 '票據號碼'
     O                                  112 '到期日期'
     O                                  120 '付款地'
     O                                  132 '金　額'
     O                                  138 '借貸'
     O        E 1              EH#01
     O                         ALS      138
     O        E 1              ED#01
9008 O                         RVDATE    10 '    /  /  '
     O                 L1      RVNO      19
     O                 75      RVNO      19
     O                 L2      RVRVID    30
     O                 75      RVRVID    30
     O                         RVCUNO    41
     O                         RVCUNM    56
     O                         RVUSTP    63
     O                         CHAR10    78
     O                         R#NTNO   101
9008 O                         RVDUDT   111 '    /  /  '
     O                         CHAR6    119
     O                         DEC9  J  132
     O                         DC       135
     O        E 1              ET#L1
     O*       E 1              ET#L1
     O*                                 107 '繳款單小計:'
     O*                        AMTT1 J  132
     O*       E 1              ET#L1
     O        E 1              ET#L2
     O                         ALQ      138
     O        E 1              ET#L2
     O                         TAIL1    107
     O                         AMTT2 J  132
     O        E 1              ET#L2
     O                         ALS      138
     O        E 1              ET#L3
     O                         TAIL2    107
     O                         AMTT3 J  132
     O        E 1              ET#L3
     O                         ALS      138
     O        E 1              ET#LR
     O                                  107 '各部門總合計:'
     O                         AMTT4 J  132
     O        E 1              ET#LR
     O                         ALS      138
     O*****************************************************************
** TAB1
A支票
B背書支票
C本票
D背書本票
E銀行本票
F匯票
G匯款
H現金
I信用狀
J銀行存款
