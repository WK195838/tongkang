     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR121RS
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     折讓別押匯明細表
     H*            5.DATE-WRITTEN  94/08/08
     H*            6.DATE-UPDATE
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR121S CF  E                    WORKSTN
     FCBCUST  IF  E           K        DISK
     E*************************************************************
     E                    ERR     1   7 70
     I*************************************************************
     I           UDS
     I                                      300 305 U#CUNO
     I                                      306 313 U#DAT1
     I                                      314 321 U#DAT2
     I                                      322 322 U#AREA
     I                                     10011010 U#USID
     C**************************************************************
     C*   程式參數傳遞區
     C**************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P#IN03  1
     C**************************************************************
     C*   主程式開始
     C**************************************************************
     C                     EXSR SR0000                     初始程式
     C*
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C                     ENDCS
     C                     ENDDO
     C*
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C           SR0000    BEGSR
     C**************************************************************
     C                     MOVEL'01'      W#PRID  2        畫面一
     C*
     C                     ENDSR
     C**************************************************************
     C           SR1000    BEGSR
     C**************************************************************
     C*W#PRID='01'-->畫面一
     C           W#PRID    DOWEQ'01'
     C                     EXFMTAR121F1                    螢幕輸入
     C                     MOVEL*BLANK    S#ERR
     C*
     C           *IN03     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     MOVEL'1'       P#IN03           回前畫面
     C                     MOVEL'00'      W#PRID           結束
     C                     LEAVE
     C                     ENDIF
     C*檢核
     C                     EXSR SR1100                     檢核
     C                     ENDDO
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         SR1100    BEGSR
     C**************************************************************
     C                     SETOF                     303132
     C                     SETOF                     99
     C           S#CUNO    IFEQ *BLANK
     C                     MOVELERR,1     S#ERR
     C                     SETON                     3099
     C                     ENDIF
     C*
     C           *IN99     IFEQ *OFF
     C           S#CUNO    CHAINCBREC                40
     C   40                SETON                     3099
     C   40                MOVELERR,2     S#ERR
     C                     ENDIF
     C*
     C  N99      S#DAT1    IFEQ 0
     C                     SETON                     3199
     C                     MOVELERR,3     S#ERR
     C                     ENDIF
     C*
     C  N99      S#DAT2    IFEQ 0
     C                     SETON                     3299
     C                     MOVELERR,4     S#ERR
     C                     ENDIF
     C*
     C           *IN99     IFEQ *OFF
     C                     MOVEL*BLANK    P#DATE
     C                     MOVE S#DAT1    P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM ' '       P#FLAG  1
     C           P#FLAG    IFNE '0'
     C                     SETON                     3199
     C                     MOVELERR,5     S#ERR
     C                     ENDIF
     C                     ENDIF
     C*
     C           *IN99     IFEQ *OFF
     C                     MOVEL*BLANK    P#DATE
     C                     MOVE S#DAT2    P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM ' '       P#FLAG  1
     C           P#FLAG    IFNE '0'
     C                     SETON                     3299
     C                     MOVELERR,6     S#ERR
     C                     ENDIF
     C                     ENDIF
     C*
     C  N99      S#DAT1    IFGT S#DAT2
     C                     SETON                     313299
     C                     MOVELERR,7     S#ERR
     C                     ENDIF
     C*
     C  N99                MOVEL'00'      W#PRID
     C  N99                MOVELS#CUNO    U#CUNO
     C  N99                MOVELS#DAT1    U#DAT1
     C  N99                MOVELS#DAT2    U#DAT2
     C  N99                MOVELS#AREA    U#AREA
     C*
     C                     ENDSR
     C*****************************************************************
** ERR
０１－請輸入客戶代號！
０２－客戶代號不存在！
０３－請輸入折讓起日！
０４－請輸入折讓止日！
０５－折讓起日輸入錯誤！
０６－折讓止日輸入錯誤！
０７－折讓起日不可大於折讓止日！
