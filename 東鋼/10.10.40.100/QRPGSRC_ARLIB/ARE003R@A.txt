     H*****************************************************************
     H* 920723  若ARAPLG中的單據別(A1CODE)有新增或修改時，
     H*         則ARP076P也需連同一並修改。
     H* 950328  新增若請款日為30號，且又遇到2月份時，則請款
     H*         日不應為3月30號，應為2月底最後一天，修改
     H*          SR3111
     H*            UPDATE  DATE  99/08/20  2010AR517 S00WCJ (9908A)
     H*                         102/10/24  S00WCJ (0210A)
     H*                         增加日期檢核不可大於150年
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARE003S CF  E                    WORKSTN
     F                                        RRN   KSFILE SF1
     FINVMST  UF  E           K        DISK
     FINVMSTL1IF  E           K        DISK
     F            INREC                             KRENAMEINREC1
 9207FINVMSTL5IF  E           K        DISK
 9207F            INREC                             KRENAMEINREC5
     FTRNDTLL3IF  E           K        DISK
     FGENSEQ  UF  E           K        DISK                      A
8906 FARR001N UF  E           K        DISK
9010 FARCUDT  IF  E           K        DISK
9010 FARINVM  UF  E           K        DISK                      A
9010 FARINVD  UF  E           K        DISK                      A
9010 FINVDTL  IF  E           K        DISK
9010 FHSCONT  IF  E           K        DISK
9207 FARAPLG  UF  E           K        DISK                      A
     E*----------------------------------------------------------------
9506 E                    ERR     1  25 70
     I*****************************************************************
     IINREC5
     I              INAPNO                          F#APNO
     I              INCUNO                          F#CUNO
     I*
     IAADS        DS
     I                                        1  10 GRP
     I                                        1   1 GRP1
     I                                        1   1 GE1
     I                                        2   2 GE2
     I                                        3   3 GE3
     I                                        4  10 GE4
     I                                       11  16 APNOG
     I                                       11  11 APNO1
     I                                       12  160APNO2
9008 I                                       21  280YMD
LYW  I                                       21  240YY
 .   I                                       25  260MM
 .   I                                       27  280DD
9010 I            DS
 .   I                                        1   80D#INDT
 .   I                                        1   40D#YEAR
     I                                        5   60D#MONT
     I                                        7   80D#DATE
9503 I            DS
 .   I                                        1   80W#DAT1
 .   I                                        1   40D#YEA1
 .   I                                        5   60D#MON1
 .   I                                        7   80D#DAT1
 .   I            DS
 .   I                                        1   80W#DAT2
 .   I                                        1   40D#YEA2
 .   I                                        5   60D#MON2
 .   I                                        7   80D#DAT2
 .   I            DS
 .   I                                        1   80W#DAT3
 .   I                                        1   40D#YEA3
 .   I                                        5   60D#MON3
 .   I                                        7   80D#DAT3
 .   I            DS
 .   I                                        1  24 P#MT1
9503 I                                        3   40D#MTDT
0210AI            DS
0210AI                                        1   80W#APDT
0210AI                                        1   40D#APDT
     I           UDS
     I                                      951 985 COMP
     I                                     10011010 D#USER
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C*****************************************************************
     C*          KEY       LIST
     C*****************************************************************
     C* FILE -> INVMSTL1(發票主檔)
     C*
     C           IN2KEY    KLIST
     C                     KFLD           INCUNO           *客戶代號
     C                     KFLD           INAPNO           *請款單號
     C*
     C* FILE -> GENSEQ(號碼順序檔)
     C*
     C           GEKEY     KLIST
     C                     KFLD           GEKIND           *編碼種類
     C                     KFLD           GEPRIN           *編碼原則
     C*
     C* FILE -> TRNDTLL3(銷貨明細檔)
     C*
     C           TXKEY     KLIST
     C                     KFLD           TXIVNO           *發票號碼
     C*
     C* FILE -> ARR001N(請款單下載檔)
     C*
8907 C           ARKEY     KLIST
 .   C                     KFLD           ANAPNO           *請款單號
 .   C                     KFLD           ANAPIT           *請款項次
 .   C*
 .   C* FILE -> ARINVM(承購發票明細檔)
 .   C*
9010 C           K#INVD    KLIST
 .   C                     KFLD           AVINNO           *發票號碼
 .   C                     KFLD           AVACNT           *類別
 .   C                     KFLD           AVITEM           *項次
 .   C*
 .   C* FILE -> HSCONT(買賣合約書檔)
 .   C*
9010 C           K#CONT    KLIST
 .   C                     KFLD           SBAREA           *合編地區
 .   C                     KFLD           SBCNUM           *合編流水
     C*
     C* FILE -> INVMSTL5(發票主檔)
     C*
     C           K#INVM    KLIST
     C                     KFLD           F#APNO           *請款單號
     C                     KFLD           F#CUNO           *客戶代號
     C*
     C* FILE==>ARAPLG(請款單記錄檔)
     C*
     C           K#APLG    KLIST
     C                     KFLD           A1APNO           *請款單號
     C                     KFLD           A1ITEM           *請款項次
     C*****************************************************************
     C*          INITIAL   VALUE
     C*****************************************************************
9908AC           *DATE     SUB  19000000  U#SYSD  80
9908AC* N90                Z-ADDUDATE     YMD
9908AC  N90                Z-ADDU#SYSD    YMD
     C  N90                MOVE 'N'       S#YN
     C  N90                MOVE 'N'       S#OPT
     C  N90                MOVE '1'       SCRN    1
     C  N90                MOVE '1'       *IN,90
     C*
     C*****************************************************************
     C*          MAIN      PROGRAM
     C*****************************************************************
     C           *IN03     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C*****************************************************************
     CSR         SR#01     BEGSR
     C*****************************************************************
     C                     EXFMTTITLE
     C   12                EXSR KC#01
     C   KC                EXSR KC#01
     C                     EXSR CK#01                      *檢核畫面
     C*
     C  N99                SELEC                           *畫面無誤
     C           CODE      WHEQ '5'                        *列印
     C                     CALL 'ARR001P'
     C                     PARM           CUNO             *客戶代號
     C                     PARM           APNO             *請款單號
     C                     PARM 'Y'       P#OPT   1
     C                     PARM           S#YN
     C                     PARM           S#OPT
     C                     FREE 'ARR001P'
     C                     MOVE '1'       SCRN
     C                     MOVE ERR,1     ERRMSG
     C*
     C           CODE      WHEQ '6'                        *傳真
     C                     CALL 'ARR001PX'
     C                     PARM           CUNO             *客戶代號
     C                     PARM           APNO             *請款單號
     C                     FREE 'ARR001PX'
     C                     MOVE '1'       SCRN
     C                     MOVE ERR,1     ERRMSG
     C*
     C                     OTHER
     C                     MOVE '2'       SCRN
     C                     EXSR PR#02
     C                     ENDSL
     C*
     C   99                MOVE '1'       SCRN
     C*
     CSR         SR#01Z    ENDSR
     C*****************************************************************
     CSR         SR#02     BEGSR
     C*****************************************************************
     C           TTLAMT    IFEQ 0
     C                     SETON                     74
     C                     ELSE
     C                     SETOF                     74
     C                     ENDIF
     C*
     C                     WRITEUND01
     C                     EXFMTSFCTL1
     C*
     C   KC                EXSR KC#01                      離開
     C   KL                MOVE '1'       SCRN             回上頁
     C   KL                GOTO SR#02Z
     C   KJ                EXSR KJ#02                      存檔檢核
     C   KJ                GOTO SR#02Z                           　　
     C*
     C**IN07==>LC編號
     C*
     C           *IN07     IFEQ *ON
     C                     EXSR SR4000
     C                     GOTO SR#02Z
     C                     ENDIF
     C*
     C           CODE      IFEQ '1'                        *新增
     C           CODE      OREQ '2'                        *修改
     C                     EXSR CK#02
     C                     ELSE
     C           CODE      IFEQ '4'                        *查詢
     C                     MOVE '1'       SCRN
     C                     ENDIF
     C                     ENDIF
     C*
     CSR         SR#02Z    ENDSR
     C*****************************************************************
     CSR         KC#01     BEGSR
     C*****************************************************************
     C                     SETON                     LR
     C                     RETRN
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         KJ#02     BEGSR
     C*****************************************************************
     C           CODE      IFEQ '1'                        *新增
     C           CODE      OREQ '2'                        *修改
     C                     EXSR CK#02
     C                     ENDIF
     C*
     C  N99                EXSR FL#02
     C  N99                MOVE '1'       SCRN
     C*
     CSR         KJ#02Z    ENDSR
     C*****************************************************************
     CSR         CK#01     BEGSR
     C*****************************************************************
     C                     SETOF                     414299
     C                     SETOF                     4344
     C                     SETOF                     4546
     C                     MOVE *BLANK    ERRMSG
     C*
     C                     SELEC
     C           CODE      WHEQ ' '
     C                     SETON                     4199
     C                     MOVE ERR,2     ERRMSG
     C           CODE      WHEQ '1'
     C                     MOVE '新增'  MOD
     C                     MOVE '0'       *IN70
     C           CODE      WHEQ '2'
     C                     MOVE '更正'  MOD
     C                     MOVE '0'       *IN70
     C           CODE      WHEQ '3'
     C                     MOVE '作廢'  MOD
     C                     MOVE '1'       *IN70
     C           CODE      WHEQ '4'
     C                     MOVE '查詢'  MOD
     C                     MOVE '1'       *IN70
     C           CODE      WHEQ '5'
     C                     MOVE '列表'  MOD
     C                     MOVE '1'       *IN70
     C           CODE      WHEQ '6'
     C                     MOVE '傳真'  MOD
     C                     MOVE '1'       *IN70
     C                     OTHER
     C                     SETON                     4199
     C                     MOVE ERR,3     ERRMSG
     C                     ENDSL
     C*
     C*--------------------*
     C*客戶代號不可為空白*
     C*--------------------*
     C*
     C           CUNO      IFEQ *BLANK                     *客戶代號
     C                     SETON                     4299
     C                     MOVE ERR,4     ERRMSG
     C                     ENDIF
     C*
     C*--------------------*
     C*訂單編號不可為空白*
     C*--------------------*
     C*
     C           ORNO      IFEQ *BLANK                     *訂單編號
     C                     SETON                     4499
     C                     MOVE ERR,12    ERRMSG
     C                     ENDIF
     C*
     C*
     C*--------------------*
     C*列印選擇不可為空白*
     C*--------------------*
     C*
     C  N99      S#YN      IFEQ *BLANK                     *出貨列印
     C                     SETON                     4599
     C                     MOVE ERR,24    ERRMSG
     C                     ENDIF
     C*
     C  N99      S#OPT     IFEQ *BLANK                     *出貨列印
     C                     SETON                     4699
     C                     MOVE ERR,25    ERRMSG
     C                     ENDIF
     C*
     C*------------------------------*
     C*不為新增時，請款單號不可空白*
     C*，反之，新增時，單號需為空白*
     C*------------------------------*
     C*
     C           CODE      IFNE '1'                        *新增
     C           APNO      IFEQ *BLANK                     *請款單號
     C                     SETON                     4399
     C                     MOVE ERR,5     ERRMSG
     C                     ENDIF
     C                     ELSE
     C           APNO      IFNE *BLANK
     C                     SETON                     4399
     C                     MOVE ERR,13    ERRMSG
     C                     ENDIF
     C                     ENDIF
     C**
     C**
     C*          CODE      IFEQ '2'
     C*          CODE      OREQ '3'
     C*          CODE      OREQ '5'
     C*                    MOVELAPNO      NOAREA  1
     C*          NOAREA    IFNE TXAR
     C*                    SETON                     4399
     C*                    MOVE ERR,14    ERRMSG
     C*                    ENDIF
     C*                    ENDIF
     C*
     C*--------------------------------*
     C*９９燈號ＯＦＦ，且不為新增時，*
     C*依輸入之客戶代號、請款單號，判*
     C*斷該筆資料是否存在於發票檔中。*
     C*--------------------------------*
     C*
     C           *IN99     IFEQ '0'
     C           CODE      ANDNE'1'
     C                     MOVELCUNO      INCUNO           *客戶代號
     C                     MOVELAPNO      INAPNO           *請款單號
     C           IN2KEY    CHAININVMSTL1             97
     C           *IN97     DOWEQ'0'
     C           INDECD    IFNE 'D'                        *作廢碼
     C                     LEAVE
     C                     ENDIF
     C           IN2KEY    READEINVMSTL1                 97
     C                     ENDDO
     C*
     C*----------------------------------*
     C*如為新增，且燈號９７為ＯＦＦ時，*
     C*表示該筆資料已存在，不可再新增。*
     C*----------------------------------*
     C*
     C                     SELEC
     C           CODE      WHEQ '1'
     C           *IN97     ANDEQ'0'
     C                     SETON                     434299
     C                     MOVE ERR,6     ERRMSG
     C*
9506 C*          CODE      WHEQ '2'
     C*          *IN97     ANDEQ'0'
9506 C*          INNBAL    ANDEQ0
     C*                    SETON                     4399
     C*                    MOVE ERR,10    ERRMSG
     C*
     C*------------------------------*
     C*如不為新增，且９７燈號ＯＮ，*
     C*則表示該筆資料不存在於檔中。*
     C*------------------------------*
     C*
     C           CODE      WHNE '1'
     C           *IN97     ANDEQ'1'
     C                     SETON                     434299
     C                     MOVE ERR,7     ERRMSG
9506 C*
 .   C*------------------------------*
 .   C*如為修改，且９７燈號ＯＦＦ，*
 .   C*且主檔之訂單與畫面輸入不同，*
 .   C*則表示該筆資料不存在於檔中。*
 .   C*------------------------------*
 .   C*
 .   C           CODE      WHNE '1'
 .   C           *IN97     ANDEQ'0'
 .   C           ORNO      ANDNEINORNO
 .   C                     SETON                     434499
9506 C                     MOVE ERR,23    ERRMSG
     C                     ENDSL
     C                     ENDIF
     C*
     CSR         CK#01Z    ENDSR
     C*****************************************************************
     CSR         PR#02     BEGSR
     C*****************************************************************
     C                     SETON                     80
     C                     WRITESFCTL1
     C                     SETOF                     80
     C*
     C                     Z-ADD0         RRN     30
     C                     MOVE *BLANK    CUNM             *客戶名稱
     C                     MOVE *BLANK    RVID             *收款業務
     C                     Z-ADD0         TTLAMT           *合計金額
     C*
     C           CODE      CASEQ'1'       PR#ADD           *新增
     C           CODE      CASEQ'2'       PR#ADD           *修改
     C                     CAS            PR#OTH           *其他
     C                     ENDCS
     C*
     C           RRN       IFEQ 0
     C                     SETON                     99
     C                     MOVE ERR,9     ERRMSG
     C                     ENDIF
     C*
     C           CODE      IFEQ '1'
     C           *IN99     ANDEQ'1'
     C                     MOVE *BLANK    APNO             *請款單號
     C                     ENDIF
     C*
     CSR         PR#02Z    ENDSR
     C*****************************************************************
     CSR         PR#ADD    BEGSR
     C*****************************************************************
     C           CODE      IFEQ '1'
     C                     EXSR @GETNO
9908AC*                    Z-ADDUDATE     APDT             *請款日期
9908AC                     Z-ADDU#SYSD    APDT             *請款日期
     C                     ELSE
     C                     Z-ADDINAPDT    APDT
     C                     ENDIF
     C*
     C                     SETOF                     61
     C*
     C*------------------------*
     C*寫入畫面二相關欄位資料*
     C*------------------------*
     C*
     C           APNO      IFNE *BLANK                     *請款單號
     C                     MOVELAPNO      W#APNO  6
     C                     EXSR SR1000                     讀入已請款
     C                     ENDIF
     C*
     C                     MOVEL*BLANK    W#APNO  6
     C                     EXSR SR1000                     讀入未請款
     C                     Z-ADDRRN       S#INCT
     C*
     C                     SETON                     10
     C*
     CSR         PR#ADZ    ENDSR
     C*****************************************************************
     CSR         SR1000    BEGSR
     C*****************************************************************
     C*  搬入營幕變數    W#APNO *
     C*---------------------------*
     C*
     C                     MOVELCUNO      INCUNO           *客戶代號
     C                     MOVELW#APNO    INAPNO           *請款單號
     C*
     C*----------------------------------*
     C*以輸入之條件，抓取符合條件之資料*
     C*----------------------------------*
     C*
     C           IN2KEY    SETLLINVMSTL1             97
     C           *IN97     DOWEQ'0'
     C           IN2KEY    READEINVMSTL1                 97
     C   97                LEAVE
     C*
     C*--------------------------*
     C*讀取資料之作廢碼如為Ｄ或*
     C*所讀取之訂單編號與輸入之*
     C*訂單編號不同時，則ITER*
     C*--------------------------*
     C*
     C                     SELEC
     C           INDECD    WHEQ 'D'                        *作廢碼
     C                     ITER
     C           INORNO    WHNE ORNO                       *訂單編號
     C                     ITER
     C*          INNBAL    WHEQ 0
 9301C*          INAPNO    ANDNE*BLANK
     C*                    ITER
     C                     ENDSL
     C*
     C                     ADD  1         RRN
     C*
     C                     MOVE INNO      INVNO            *發票號碼
     C                     Z-ADDINAAMT    AAMT             *出貨金額
     C                     Z-ADDINCAMT    BAMT             *折銷退額
     C                     Z-ADDINBAMT    CAMT             *扣預收款
     C                     ADD  INDAMT    CAMT             *扣預額沖
     C                     Z-ADDINATAX    DAMT             *銷項稅額
     C                     ADD  INCTAX    DAMT             *折銷退稅
     C                     ADD  INDTAX    DAMT             *扣預稅沖
     C                     Z-ADDINEAMT    EAMT             *繳款金額
     C                     ADD  INFAMT    EAMT             *退票金額
     C*
     C*--------------------------*
     C*計算畫面各畫面之金額欄位*
     C*--------------------------*
     C*
     C                     EXSR @GET
     C*
     C*--------------*
     C*計算發票餘額*
     C*--------------*
     C*
     C                     Z-ADDAAMT      NBAL             *發票餘額
     C                     ADD  BAMT      NBAL
     C                     ADD  CAMT      NBAL
     C                     ADD  DAMT      NBAL
     C                     ADD  EAMT      NBAL
     C*
     C*------------------------------*
     C*發票主檔中之請款單號如與畫面*
     C*輸入之請款單號相同，則於OPT *
     C*及OPT1前上Y碼並合計該畫 *
     C*面所有發票資料之發票餘額    *
     C*------------------------------*
     C*
     C           INAPNO    IFEQ APNO
     C                     MOVE 'Y'       OPT
     C                     MOVE 'Y'       OPT1
     C                     ADD  NBAL      TTLAMT
     C                     ELSE
     C                     MOVE *BLANK    OPT
     C                     MOVE *BLANK    OPT1
     C                     ENDIF
     C*
     C                     SETOF                     51
     C                     WRITESF1
     C  N61                MOVELINCUNM    CUNM             *客戶簡稱
     C  N61                MOVELINRVID    RVID             *收款業務
     C  N61                SETON                     61
     C*
     C                     ENDDO
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         @GETNO    BEGSR
     C*****************************************************************
     C                     MOVEL'03'      GEKIND           *編碼種類
     C                     MOVE *BLANK    GEPRIN           *編碼原則
     C                     MOVE *BLANK    GRP              *編碼原則
     C                     MOVELTXAR      GE1              *編第一碼
     C*
     C*                    MOVE YY        GE2
     C*          MM        IFLT 10
     C*                    MOVE MM        GE3
     C*                    ELSE
     C*          MM        IFEQ 10
     C*                    MOVE 'A'       GE3
     C*                    ELSE
     C*          MM        IFEQ 11
     C*                    MOVE 'B'       GE3
     C*                    ELSE
     C*          MM        IFEQ 12
     C*                    MOVE 'C'       GE3
     C*                    ENDIF
     C*                    ENDIF
     C*                    ENDIF
     C*                    ENDIF
     C*
     C                     MOVELGRP       GEPRIN
     C*
     C*------------------------------------*
     C*如為新增，則讀取符合條件之請款號碼*
     C*如讀取不到，則寫入一筆新請款號碼。*
     C*------------------------------------*
     C*
     C           GEKEY     CHAINGENSEQ              N97
     C   97                Z-ADD0         GECUNO           *目前號碼
     C                     MOVELGRP1      APNO1
     C           GECUNO    ADD  1         APNO2
     C                     MOVELAPNOG     APNO             *請款號碼
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         PR#OTH    BEGSR
     C*****************************************************************
     C                     Z-ADDINAPDT    APDT             *請款日期
     C                     MOVELINCUNM    CUNM             *客戶簡稱
     C                     MOVELINRVID    RVID             *收款業務
     C                     MOVELCUNO      INCUNO           *客戶代號
     C                     MOVELAPNO      INAPNO           *請款單號
     C*
     C           IN2KEY    SETLLINVMSTL1             97
     C           *IN97     DOWEQ'0'
     C           IN2KEY    READEINVMSTL1                 97
     C   97                LEAVE
     C*
CLJ  C           INDECD    IFEQ 'D'                        避開已作廢
     C                     ITER
---  C                     ENDIF
     C*
     C                     ADD  1         RRN
     C*
     C                     MOVEL'Y'       OPT
     C                     MOVELINNO      INVNO            *發票號碼
     C                     Z-ADDINAAMT    AAMT             *出貨金額
     C                     Z-ADDINCAMT    BAMT             *折銷退額
     C                     Z-ADDINBAMT    CAMT             *扣預收款
     C                     ADD  INDAMT    CAMT             *扣預額沖
     C                     Z-ADDINATAX    DAMT             *銷項稅額
     C                     ADD  INCTAX    DAMT             *折銷退稅
     C                     ADD  INDTAX    DAMT             *扣預稅沖
     C                     Z-ADDINEAMT    EAMT             *繳款金額
     C                     ADD  INFAMT    EAMT             *退票金額
     C*
     C*--------------------------*
     C*計算畫面各畫面之金額欄位*
     C*--------------------------*
     C*
     C                     EXSR @GET
     C*
     C*--------------*
     C*計算發票餘額*
     C*--------------*
     C*
     C                     Z-ADDAAMT      NBAL             *發票餘額
     C                     ADD  BAMT      NBAL
     C                     ADD  CAMT      NBAL
     C                     ADD  DAMT      NBAL
     C                     ADD  EAMT      NBAL
     C                     ADD  NBAL      TTLAMT
     C*
     C                     SETOF                     51
     C                     WRITESF1
     C                     ENDDO
     C*
     C                     Z-ADDRRN       S#INCT
     C                     SETON                     10
     C*
     CSR         PR#OTZ    ENDSR
     C*****************************************************************
     CSR         @GET      BEGSR
     C*****************************************************************
     C*  GET DISCOUNT AMT *
     C*-------------------*
     C*
     C                     MOVELINNO      TXIVNO           *發票號碼
     C*
     C*--------------------*
     C*讀取符合條件之資料*
     C*--------------------*
     C*
     C           TXKEY     SETLLTRNDTLL3             96
     C           *IN97     DOWEQ'0'
     C           TXIVNO    READETRNDTLL3                 96
     C   96                LEAVE
     C*
     C*----------------------------*
     C*排除已折讓或是被刪除之資料*
     C*----------------------------*
     C*
     C           TXFL01    IFEQ 'Y'                        *折確認碼
     C           TXFLAG    OREQ 'D'                        *處理代碼
     C                     ITER
     C                     ENDIF
     C*
     C*--------------------------------*
     C*發票類別如為預收，則折讓退回金*
     C*額需再扣減銷貨明細金額方為正確*
     C*--------------------------------*
     C*
     C           INTYPE    IFEQ '2'                        *預收
     C                     SUB  TXAMT     BAMT             *折退回額
     C                     ELSE
     C*
     C*------------------------------------*
     C*如類別不為預收且銷貨調整不為其他，*
     C*則折讓退回金額需再扣減銷貨明細金額*
     C*，反之，銷貨調整如為其他，則預收貨*
     C*款額需再扣減銷貨明細金額方為正確。*
     C*------------------------------------*
     C*
     C           TXACNT    IFNE '4'                        *其他
     C                     SUB  TXAMT     BAMT             *折退回額
     C                     ELSE
     C                     SUB  TXAMT     CAMT             *預收貨款
     C                     ENDIF
     C                     ENDIF
     C*
     C*--------------------------------------*
     C*稅額需再扣減銷貨明細檔之稅額方屬正確*
     C*--------------------------------------*
     C*
     C                     SUB  TXTAX     DAMT             *稅額
     C                     ENDDO
     C*
     CSR         @GETZ     ENDSR
     C*****************************************************************
     CSR         CK#02     BEGSR
     C*****************************************************************
     C*  CHECK INPUT DATA  *
     C*--------------------*
     C*
     C                     SETOF                     99
     C                     MOVE *BLANK    ERRMSG
     C                     Z-ADD0         TTLAMT
     C*
     C*                    CALL 'C01'
     C*                    PARM           APDT
     C*                    PARM           FLAG1   1
     C*                    FREE 'C01'
     C*
     C*--------------*
     C*檢核請款日期*
     C*--------------*
     C*
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE APDT      P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           FLAG1   1
     C*
     C           FLAG1     IFNE '0'
     C                     SETON                     9944
     C                     MOVELERR,11    ERRMSG
     C                     ENDIF
     C*
0210AC                     Z-ADDAPDT      W#APDT
0210AC           D#APDT    IFGT 150
0601AC           D#APDT    ORLT 100
0210AC                     SETON                     9944
0210AC                     MOVELERR,11    ERRMSG
0210AC                     ENDIF
0210AC*
     C                     Z-ADD0         RRN
     C                     Z-ADD0         W#CNT   30
     C                     SETOF                     97
     C*
     C           *IN97     DOWEQ'0'
     C                     MOVEA'0'       *IN,51
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  97
     C   97                LEAVE
     C           OPT       IFEQ *BLANK
     C                     UPDATSF1
     C                     ITER
     C                     ELSE
     C                     ADD  NBAL      TTLAMT
     C                     ADD  1         W#CNT
     C                     ENDIF
     C                     ENDDO
     C*
 9303C******改判斷挑選筆數
  .  C           W#CNT     IFEQ 0
  .  C                     Z-ADD1         RRN
  .  C           RRN       CHAINSF1                  97
  .  C  N97                SETON                     5199
  .  C  N97                UPDATSF1
  .  C  N97                MOVELERR,8     ERRMSG
 9303C                     ENDIF
     C******
     C*          TTLAMT    IFEQ 0
     C*                    Z-ADD1         RRN
     C*          RRN       CHAINSF1                  97
     C* N97                SETON                     5199
     C* N97                UPDATSF1
     C* N97                MOVELERR,8     ERRMSG
     C*                    ENDIF
9010 C*
9010 C                     EXSR SR3000                     承購檢核
9010 C*
     CSR         CK#02Z    ENDSR
     C*****************************************************************
     CSR         FL#02     BEGSR
     C*****************************************************************
     C           CODE      CASEQ'3'       DL#02            *刪除
     C           CODE      CASEQ'1'       WR#02            *新增
     C           CODE      CASEQ'2'       UP#02            *修改
     C                     ENDCS
     C*
     CSR         FL#02Z    ENDSR
     C*****************************************************************
     CSR         DL#02     BEGSR
     C*****************************************************************
9010 C*承購檢核
  .  C                     EXSR SR3000
  .  C*
  .  C           *IN99     IFEQ *OFF
     C                     Z-ADD0         RRN
     C                     SETOF                     97
     C           *IN97     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  97
     C   97                LEAVE
     C***
     C           INVNO     CHAININVMST               96
     C  N96                MOVE *BLANK    INAPNO
     C  N96                Z-ADD0         INAPDT
     C  N96                MOVEL'C'       INFLAG
     C  N96                MOVELTXAR      INTXAR
9908AC* N96                Z-ADDUDATE     INTXDT
9908AC  N96                Z-ADDU#SYSD    INTXDT
     C  N96                UPDATINREC
     C                     ENDDO
     C*
8907 C*註記刪除之請款單(ARR001N)
8907 C                     EXSR SR2000
     C*
9010 C*承購發票存檔
 .   C                     EXSR SR3100
     C*
9207 C*請款單Ｌ／Ｃ存檔
 .   C                     EXSR SR4300
 .   C                     ENDIF
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         WR#02     BEGSR
     C*****************************************************************
     C                     CLEARGEREC
     C*
     C                     MOVEL'03'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVELTXAR      GE1
     C*
     C*                    MOVE YY        GE2
     C*          MM        IFLT 10
     C*                    MOVE MM        GE3
     C*                    ELSE
     C*          MM        IFEQ 10
     C*                    MOVE 'A'       GE3
     C*                    ELSE
     C*          MM        IFEQ 11
     C*                    MOVE 'B'       GE3
     C*                    ELSE
     C*          MM        IFEQ 12
     C*                    MOVE 'C'       GE3
     C*                    ENDIF
     C*                    ENDIF
     C*                    ENDIF
     C*                    ENDIF
     C*
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ               97
     C                     ADD  1         GECUNO
     C  N97                UPDATGEREC
     C   97                WRITEGEREC
     C***
     C                     MOVELGRP1      APNO1
     C                     Z-ADDGECUNO    APNO2
     C                     MOVELAPNOG     APNO
     C***
     C                     Z-ADD0         RRN
     C                     SETOF                     97
     C           *IN97     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  97
     C   97                LEAVE
     C***
     C           OPT       IFEQ *BLANK
     C                     ITER
     C                     ENDIF
     C****
     C           INVNO     CHAININVMST               96
     C  N96                MOVELAPNO      INAPNO
     C  N96                Z-ADDAPDT      INAPDT
     C  N96                MOVEL'C'       INFLAG
     C  N96                MOVELTXAR      INTXAR
9908AC* N96                Z-ADDUDATE     INTXDT
9908AC  N96                Z-ADDU#SYSD    INTXDT
     C  N96                UPDATINREC
     C                     ENDDO
     C***
     C                     CALL 'ARR001P'
     C                     PARM           CUNO
     C                     PARM           APNO
     C                     PARM 'Y'       P#OPT
     C                     PARM           S#YN
     C                     PARM           S#OPT
     C                     FREE 'ARR001P'
     C                     MOVE '1'       SCRN
     C                     MOVE ERR,1     ERRMSG
9010 C*承購發票存檔
  .  C                     EXSR SR3100
  .  C*
     CSR         WR#02Z    ENDSR
     C*****************************************************************
     CSR         UP#02     BEGSR
     C*****************************************************************
     C                     Z-ADD0         RRN
     C                     SETOF                     97
     C***
     C           *IN97     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  97
     C   97                LEAVE
     C***
     C           OPT       IFEQ *BLANK
     C           OPT1      ANDEQ*BLANK
     C*          OPT       ORNE *BLANK
     C*          OPT1      ANDNE*BLANK
     C                     ITER
     C                     ENDIF
     C***** FILE I/O
     C           OPT       IFEQ *BLANK
     C****  DEL
     C           INVNO     CHAININVMST               96
     C  N96                MOVE *BLANK    INAPNO
     C  N96                MOVEL'C'       INFLAG
     C  N96                MOVELTXAR      INTXAR
9908AC* N96                Z-ADDUDATE     INTXDT
9908AC  N96                Z-ADDU#SYSD    INTXDT
     C  N96                UPDATINREC
     C                     ELSE
     C****  ADD
     C           INVNO     CHAININVMST               96
     C  N96                MOVELAPNO      INAPNO
     C  N96                Z-ADDAPDT      INAPDT
     C  N96                MOVEL'C'       INFLAG
     C  N96                MOVELTXAR      INTXAR
9908AC* N96                Z-ADDUDATE     INTXDT
9908AC  N96                Z-ADDU#SYSD    INTXDT
     C  N96                UPDATINREC
     C                     ENDIF
     C                     ENDDO
9010 C*承購發票存檔
  .  C                     EXSR SR3100
  .  C*
     CSR         UP#02Z    ENDSR
     C*****************************************************************
     CSR         SR2000    BEGSR
     C*****************************************************************
     C*8907  START------------------
     C*
     C                     MOVELAPNO      W#APNO  6        *請款單號
     C                     MOVE W#APNO    ANAPNO
     C                     Z-ADD0         ANAPIT
     C*
     C           ARKEY     SETLLRARR001N
     C                     READ RARR001N                 50
     C           *IN50     DOWEQ*OFF
     C*
     C           W#APNO    IFNE ANAPNO
     C                     LEAVE
     C                     ENDIF
     C*
     C                     MOVE *BLANK    ANFLAG           *下旗標碼
     C                     MOVE 'D'       ANCODE           * ACD碼
     C                     UPDATRARR001N
     C*
     C                     READ RARR001N                 50
     C                     ENDDO
     CSR                   ENDSR
     C*****************************************************************
     C           SR3000    BEGSR
     C*****************************************************************
     C*應收帳款承購檢核開始(承購之客戶以集團方式進行)
     C*
9011 C                     MOVELCUNO      W#CUNO  5
9011 C                     MOVELW#CUNO    K#CUNO  6
     C*
9011 C           K#CUNO    CHAINRARCUDT              95
     C           *IN95     IFEQ *OFF                       承購客戶
     C                     Z-ADD0         RRN
     C                     SETOF                     97
     C*
     C           *IN97     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  97
     C   97                LEAVE
     C           INVNO     CHAINRARINVM             N94
     C           *IN94     IFEQ *OFF
     C           AMAPCD    ANDNE*BLANK                     承購確認
     C                     SETON                     5199
     C                     UPDATSF1
     C                     MOVELERR,15    ERRMSG
     C                     ENDIF
     C                     ENDDO
     C                     ENDIF
     C*
     C*應收帳款承購檢核結束
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         SR3100    BEGSR
     C*****************************************************************
     C*應收帳款承購存檔開始(承購之客戶以集團方式進行)
     C*
9011 C                     MOVELCUNO      W#CUNO
9011 C                     MOVELW#CUNO    K#CUNO
     C*
9011 C           K#CUNO    CHAINRARCUDT              95
     C           *IN95     IFEQ *OFF                       承購客戶
     C                     Z-ADD0         RRN
     C                     SETOF                     97
     C*
     C           *IN97     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  97
     C   97                LEAVE
     C                     CLEARRARINVM
     C           INVNO     CHAINRARINVM              94
     C*
     C           OPT       IFEQ *BLANK
     C           CODE      OREQ '3'
     C*主檔
     C                     MOVE 'D'       AMFLAG           處理代號
     C  N94                UPDATRARINVM
     C*明細檔
     C                     MOVE *OFF      *IN89
     C                     MOVE INVNO     AVINNO           *發票號碼
     C                     MOVE *BLANK    AVACNT           *類別
     C                     Z-ADD0         AVITEM           *項次
     C*
     C           K#INVD    SETLLRARINVD
     C           *IN89     DOWEQ*OFF
     C                     READ RARINVD                  89
     C   89                LEAVE
     C           AVINNO    IFNE INVNO
     C                     LEAVE
     C                     ENDIF
     C                     MOVE 'D'       AVFLAG           處理代號
     C                     UPDATRARINVD
     C                     ENDDO
     C                     ELSE
     C                     EXSR SR3110
     C                     ENDIF
     C                     ENDDO
     C                     ENDIF
     C*
     C*應收帳款承購存檔結束
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         SR3110    BEGSR
     C*****************************************************************
     C*該客戶已終止承購
     C*
     C           ININDT    IFGE ACDAT2
     C           ACDAT2    ANDNE0
     C                     GOTO EN3110
     C                     ENDIF
     C*
     C*針對華熊營造單一張訂單做承購
     C*
     C           INCUNO    IFEQ 'DP032'
     C           INORNO    IFNE 'T01997'
     C           INORNO    ANDNE'T02060'
     C           INORNO    ANDNE'M01488'
     C                     GOTO EN3110
     C                     ENDIF
     C                     ENDIF
     C*
     C*針對工信工程單一張訂單做承購
     C*
     C           W#CUNO    IFEQ 'DP055'
     C           INORNO    IFNE 'P03892'
     C           INORNO    ANDNE'K03408'
     C                     GOTO EN3110
     C                     ENDIF
     C                     ENDIF
     C*
     C           INVNO     CHAININREC                88
     C   94                MOVE 'A'       AMFLAG           處理代號
     C  N94                MOVE 'C'       AMFLAG           處理代號
     C                     MOVE INVNO     AMINNO           發票號碼
     C                     Z-ADDININDT    AMINDT           發票日期
     C                     MOVE INKIND    AMINTP           發票聯式
     C                     MOVE INDECD    AMDELT           做廢碼
     C                     Z-ADDINDEDT    AMDELD           做廢日期
     C                     MOVELW#CUNO    AMCUNO           客戶代號
     C                     MOVE INCUNM    AMCUNM           客戶簡稱
     C                     Z-ADDINAAMT    AMAAMT           銷貨金額
     C                     Z-ADDINATAX    AMATAX           銷貨稅額
     C                     Z-ADDINCAMT    AMBAMT           折讓金額
     C                     Z-ADDINCTAX    AMBTAX           折讓稅額
     C                     Z-ADDINNBAL    AMDAMT           承購金額
     C                     Z-ADD0         AMEAMT           已融資金額
     C                     Z-ADD0         AMFAMT           已沖銷金額
     C                     Z-ADDINBAMT    AMCAMT           扣預收金額
     C                     MOVELINAPNO    AMAPN1           承購單號
     C                     Z-ADDINAPDT    AMAPD1           承購日期
     C*
 9112C*判斷該客戶若接受多張請款單，則該客戶承購該單號即為請款單號
  .  C           ACCOD1    IFNE *BLANK
  .  C                     MOVELINAPNO    AMAPN2           請款單號
  .  C                     Z-ADDINAPDT    AMAPD2           請款日期
  .  C                     ELSE
  .  C                     MOVEL*BLANK    AMAPN2
  .  C                     Z-ADD0         AMAPD2
  .  C                     ENDIF
 9112C*
     C                     MOVE INORNO    AMORNO           訂單號碼
     C                     MOVE INAREA    AMAREA           開立廠區
     C                     MOVE D#USER    AMUPDM
9908AC*                    MOVE UDATE     AMUPDD
9908AC                     MOVE U#SYSD    AMUPDD
     C                     TIME           AMUPDT
     C*計算發票到期日期
TEST C                     MOVELINNO      W#IN1   2
 .   C                     MOVE INNO      W#IN2   80
 .   C           W#IN1     IFEQ 'LD'
 .   C           W#IN2     ANDEQ87872089
 .   C                     Z-ADD0         W#TEST  10
 .   C                     ENDIF
     C*
     C                     EXSR SR3111
     C*
     C   94                WRITERARINVM
     C  N94                UPDATRARINVM
     C*處理明細檔(先全部刪除後再全部重新寫入)
     C*刪除
     C                     MOVE *OFF      *IN89
     C                     MOVE AMINNO    AVINNO
     C                     MOVE *BLANK    AVACNT
     C                     Z-ADD0         AVITEM
     C           K#INVD    SETLLRARINVD
     C           *IN89     DOWEQ*OFF
     C                     READ RARINVD                  89
     C   89                LEAVE
     C           AVINNO    IFNE INVNO
     C                     LEAVE
     C                     ENDIF
     C                     DELETRARINVD
     C                     ENDDO
     C*重新寫入
     C                     MOVE *OFF      *IN89
     C                     MOVE AMINNO    AVINNO
     C                     MOVE *BLANK    AVACNT
     C                     Z-ADD0         AVITEM
     C           K#INVD    SETLLIVREC
     C           *IN89     DOWEQ*OFF
     C                     READ IVREC                    89
     C   89                LEAVE
     C           IVNO      IFNE AMINNO
     C                     LEAVE
     C                     ENDIF
     C*
     C                     MOVE 'A'       AVFLAG
     C                     MOVE IVNO      AVINNO
     C                     MOVE IVACNT    AVACNT
     C                     Z-ADDIVITEM    AVITEM
     C                     Z-ADDIVACDT    AVACDT
     C                     MOVE IVORNO    AVORNO
     C                     Z-ADDIVQTY     AVQTY
     C                     Z-ADDIVUPRC    AVUPRC
     C                     Z-ADDIVAMT     AVAMT
     C                     MOVE IVDECD    AVDECD
     C                     MOVE IVDEDT    AVDEDT
     C                     MOVE IVAPNO    AVAPNO
     C                     MOVE IVACNO    AVACNO
     C                     MOVE IVFL01    AVFL01
     C                     MOVE IVFL02    AVFL02
     C                     MOVE IVFL03    AVFL03
     C                     MOVE IVTXAR    AVTXAR
     C                     Z-ADDIVTXDT    AVTXDT
     C                     MOVE IVRESV    AVRESV
     C*
     C                     WRITERARINVD
     C                     ENDDO
     C*
     CSR         EN3110    ENDSR
     C*****************************************************************
     CSR         SR3111    BEGSR
     C*****************************************************************
     C*計算發票到期日
     C*
     C                     MOVELAMORNO    SBAREA           *合編地區
     C                     MOVE AMORNO    SBCNUM           *合編流水
     C*
     C           K#CONT    CHAINRHSCONT              88
     C                     MOVE AMINDT    D#INDT           *發票日期
     C*
     C                     Z-ADDSBRTD1    D#DATE
     C                     Z-ADDD#INDT    W#DAT1  80       請款一
     C                     Z-ADDSBRTD2    D#DATE
     C                     Z-ADDD#INDT    W#DAT2  80       請款二
     C                     Z-ADDSBRTD3    D#DATE
     C                     Z-ADDD#INDT    W#DAT3  80       請款三
     C                     Z-ADDSBRTD1    D#DATE
     C                     ADD  1         D#MONT
     C*
     C           D#MONT    IFEQ 13
     C                     Z-ADD1         D#MONT
     C                     ADD  1         D#YEAR
     C                     ENDIF
     C*
     C                     Z-ADDD#INDT    W#DAT4  80       次月請款一
     C*
     C*----------------------------*
     C* 2月份請款日如超過29號，*
     C*則請款日必須為當月(2月) *
     C*----------------------------*
     C*
     C           D#MON1    IFEQ 2
     C           D#DAT1    ANDGT28
     C                     MOVE W#DAT1    P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM *BLANK    P#MT1  24
     C                     PARM *BLANK    P#LEAP  1
     C                     PARM *BLANK    P#ERR   1
     C           P#ERR     IFEQ '0'
     C                     Z-ADDD#MTDT    D#DAT1
     C                     ENDIF
     C                     ENDIF
     C*
     C                     MOVE W#DAT1    P#DATE
     C                     CALL 'UTS104R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MOD1  1
     C                     PARM '1'       P#MOD2  1
     C                     PARM '0005'    P#DAYS  4
     C                     PARM *BLANK    P#RTND  8
     C                     PARM *BLANK    P#ERR   1
     C           P#ERR     IFEQ '0'
     C                     MOVE P#RTND    W#DAT1           理論請款一
     C                     ELSE
     C                     Z-ADD0         W#DAT1
     C                     ENDIF
     C*
     C*----------------------------*
     C* 2月份請款日如超過29號，*
     C*則請款日必須為當月(2月) *
     C*----------------------------*
     C*
     C           D#MON2    IFEQ 2
     C           D#DAT2    ANDGT28
     C                     MOVE W#DAT2    P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM *BLANK    P#MT1  24
     C                     PARM *BLANK    P#LEAP  1
     C                     PARM *BLANK    P#ERR   1
     C           P#ERR     IFEQ '0'
     C                     Z-ADDD#MTDT    D#DAT2
     C                     ENDIF
     C                     ENDIF
     C*
     C                     MOVE W#DAT2    P#DATE
     C                     CALL 'UTS104R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MOD1  1
     C                     PARM '1'       P#MOD2  1
     C                     PARM '0005'    P#DAYS  4
     C                     PARM *BLANK    P#RTND  8
     C                     PARM *BLANK    P#ERR   1
     C           P#ERR     IFEQ '0'
     C                     MOVE P#RTND    W#DAT2           理論請款二
     C                     ELSE
     C                     Z-ADD0         W#DAT2
     C                     ENDIF
     C*
     C*----------------------------*
     C* 2月份請款日如超過29號，*
     C*則請款日必須為當月(2月) *
     C*----------------------------*
     C*
     C           D#MON3    IFEQ 2
     C           D#DAT3    ANDGT28
     C                     MOVE W#DAT3    P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM *BLANK    P#MT1  24
     C                     PARM *BLANK    P#LEAP  1
     C                     PARM *BLANK    P#ERR   1
     C           P#ERR     IFEQ '0'
     C                     Z-ADDD#MTDT    D#DAT3
     C                     ENDIF
     C                     ENDIF
     C*
     C                     MOVE W#DAT3    P#DATE
     C                     CALL 'UTS104R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MOD1  1
     C                     PARM '1'       P#MOD2  1
     C                     PARM '0005'    P#DAYS  4
     C                     PARM *BLANK    P#RTND  8
     C                     PARM *BLANK    P#ERR   1
     C           P#ERR     IFEQ '0'
     C                     MOVE P#RTND    W#DAT3           理論請款三
     C                     ELSE
     C                     Z-ADD0         W#DAT3
     C                     ENDIF
     C*
     C           AMINDT    IFLE W#DAT1                     以付款日一
     C           SBRTD1    ANDNE0
     C                     Z-ADDW#DAT1    D#INDT           理論請款一
     C                     Z-ADDSBPDT1    D#DATE
     C                     ADD  1         D#MONT
     C           D#MONT    IFEQ 13
     C                     Z-ADD1         D#MONT
     C                     ADD  1         D#YEAR
     C                     ENDIF
     C                     Z-ADDD#INDT    AMDUDT           發票到期日
     C                     ENDIF
     C*
     C           AMINDT    IFLE W#DAT2                     以付款日二
     C           AMINDT    ANDGEW#DAT1
     C           SBRTD2    ANDNE0
     C                     Z-ADDW#DAT2    D#INDT           理論請款二
     C                     Z-ADDSBPDT2    D#DATE
     C                     ADD  1         D#MONT
     C           D#MONT    IFEQ 13
     C                     Z-ADD1         D#MONT
     C                     ADD  1         D#YEAR
     C                     ENDIF
     C                     Z-ADDD#INDT    AMDUDT           發票到期日
     C                     ENDIF
     C*
     C           AMINDT    IFLE W#DAT3                     以付款日三
     C           AMINDT    ANDGEW#DAT2
     C           AMINDT    ANDGEW#DAT1
     C           SBRTD3    ANDNE0
     C                     Z-ADDW#DAT3    D#INDT           理論請款三
     C                     Z-ADDSBPDT3    D#DATE
     C                     ADD  1         D#MONT
     C           D#MONT    IFEQ 13
     C                     Z-ADD1         D#MONT
     C                     ADD  1         D#YEAR
     C                     ENDIF
     C                     Z-ADDD#INDT    AMDUDT           發票到期日
     C                     ENDIF
     C*
     C           AMINDT    IFLE W#DAT4                     次月請款一
     C           AMINDT    ANDGEW#DAT3
     C           AMINDT    ANDGEW#DAT2
     C           AMINDT    ANDGEW#DAT1
     C                     Z-ADDW#DAT4    D#INDT
     C                     Z-ADDSBPDT1    D#DATE
     C                     ADD  1         D#MONT
     C           D#MONT    IFEQ 13
     C                     Z-ADD1         D#MONT
     C                     ADD  1         D#YEAR
     C                     ENDIF
     C                     Z-ADDD#INDT    AMDUDT           發票到期日
     C                     ENDIF
     C*
     C           SBPYCK    IFNE ' '                        支票繳款
     C                     MOVE AMDUDT    P#DATE
     C                     Z-ADDSBPERD    W#PERD  40       *票期
     C                     MOVE W#PERD    P#DAYS
     C                     CALL 'UTS104R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MOD1  1
     C                     PARM '2'       P#MOD2  1
     C                     PARM           P#DAYS  4
     C                     PARM *BLANK    P#RTND  8
     C                     PARM *BLANK    P#ERR   1
     C           P#ERR     IFEQ '0'
     C                     MOVE P#RTND    AMDUDT           預估兌現日
     C                     ELSE
     C                     Z-ADD0         AMDUDT
     C                     ENDIF
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         SR4000    BEGSR
     C*****************************************************************
     C*9207  START------------------
     C*
     C                     MOVE 'Y'       W#LOOP  1
     C                     EXSR SR4100
     C*
     C           W#LOOP    DOWEQ'Y'
     C                     EXFMTAR003W1
     C                     SETOF                     9198
     C                     MOVE *BLANK    S#ERW1
     C*
     C           *IN03     IFEQ *ON
     C           *IN12     OREQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C                     EXSR SR4200                     檢核
     C*ＬＣ存檔
     C  N98      *IN11     IFEQ *ON
     C                     EXSR SR4300                     存檔
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         SR4100    BEGSR
     C*****************************************************************
     C                     MOVELAPNO      A1APNO           請款單號
     C                     Z-ADD1         A1ITEM           請款項次
     C*
     C           K#APLG    CHAINRARAPLG              53
     C           *IN53     IFEQ *OFF
     C                     MOVELA1CODE    S#CODE           單據類別
     C                     MOVELA1VNNO    S#VNNO           單據編號
     C                     MOVELA1BANK    S#BANK           開立銀行
     C                     Z-ADDA1DAT1    S#DAT1           開立日期
     C                     Z-ADDA1DAT2    S#DAT2           押匯日期
     C                     Z-ADDA1BAMT    S#BAMT           單據餘額
     C                     MOVELA1CURY    S#CURY           幣別
     C                     ELSE
     C                     MOVEL*BLANK    S#CODE           單據類別
     C                     MOVEL*BLANK    S#VNNO           單據編號
     C                     MOVEL*BLANK    S#BANK           開立銀行
     C                     Z-ADD0         S#DAT1           開立日期
     C                     Z-ADD0         S#DAT2           押匯日期
     C                     Z-ADD0         S#BAMT           單據餘額
     C                     MOVEL'NTS'     S#CURY           幣別
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         SR4200    BEGSR
     C*****************************************************************
     C                     SETOF                     9198
     C                     MOVE *BLANK    S#ERW1
     C*
     C*先檢核該張單據所屬的請款單單號是否已存在
     C*
     C                     MOVELAPNO      F#APNO           *請款單號
     C                     MOVELINCUNO    F#CUNO           *客戶代號
     C*
     C           K#INVM    CHAININREC5               52
     C           *IN52     IFEQ *ON
     C                     SETON                     9198
     C                     MOVELERR,16    S#ERW1
     C                     ENDIF
     C*
     C*單據類別不可空白
     C*
     C  N98      S#CODE    IFEQ *BLANK
     C                     SETON                     9198
     C                     MOVELERR,22    S#ERW1
     C                     ENDIF
     C*
     C*單據類別輸入錯誤
     C*
     C  N98      S#CODE    IFNE '1'
     C           S#CODE    ANDNE'2'
     C           S#CODE    ANDNE'3'
     C                     SETON                     9198
     C                     MOVELERR,17    S#ERW1
     C                     ENDIF
     C*
     C*單據編號不可空白
     C*
     C  N98      S#VNNO    IFEQ *BLANK
     C                     SETON                     9198
     C                     MOVELERR,18    S#ERW1
     C                     ENDIF
     C*
     C*檢核開立日期
     C*
     C                     MOVE S#DAT1    P#DATE           開立日期
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           P#ERR   1
     C*
     C  N98      P#ERR     IFNE '0'
     C                     SETON                     9198
     C                     MOVELERR,19    S#ERW1
     C                     ENDIF
     C*
     C*檢核押匯日期
     C*
     C                     MOVE S#DAT2    P#DATE           押匯日期
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           P#ERR   1
     C*
     C  N98      P#ERR     IFNE '0'
     C                     SETON                     9198
     C                     MOVELERR,20    S#ERW1
     C                     ENDIF
     C*
     C*幣別不可空白
     C*
     C  N98      S#CURY    IFEQ *BLANK
     C                     SETON                     9198
     C                     MOVELERR,21    S#ERW1
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         SR4300    BEGSR
     C*****************************************************************
     C                     MOVELAPNO      A1APNO           請款單號
     C                     Z-ADD1         A1ITEM           請款項次
     C*
     C           K#APLG    CHAINRARAPLG              53
     C   53                Z-ADD1         A1ITEM           項次
     C                     MOVELAPNO      A1APNO           請款單號
     C                     MOVELCUNO      A1CUNO           客戶編號
     C                     MOVELCUNM      A1CUNM           客戶名稱
     C                     MOVELORNO      A1ORNO           訂單編號
     C                     MOVELS#CODE    A1CODE           單據類別
     C                     MOVELS#VNNO    A1VNNO           單據編號
     C                     MOVELS#BANK    A1BANK           開立銀行
     C                     Z-ADDS#DAT1    A1DAT1           開立日期
     C                     Z-ADDS#DAT2    A1DAT2           押匯日期
     C                     MOVELS#CURY    A1CURY           幣別
     C                     Z-ADDS#BAMT    A1BAMT           單據餘額
     C                     MOVELD#USER    A1UPDM           異動人員
9908AC*                    MOVE UDATE     A1UPDD           異動日期
9908AC                     MOVE U#SYSD    A1UPDD           異動日期
     C                     TIME           A1UPDT           異動時間
     C*
     C                     SELEC
     C           CODE      WHEQ '1'
     C                     MOVEL'A'       A1FLAG
     C                     MOVE *OFF      *IN92
     C           CODE      WHEQ '2'
     C                     MOVEL'C'       A1FLAG
     C                     MOVE *OFF      *IN92
     C           CODE      WHEQ '3'
     C                     MOVEL'D'       A1FLAG
     C                     MOVE *ON       *IN92
     C                     ENDSL
     C*
     C   53N92             WRITERARAPLG
     C  N53                UPDATRARAPLG
     C*
     CSR                   ENDSR
     C*****************************************************************
** ERR
請款單已送至批次佇列執行。
功能代碼必須輸入．
功能代碼必須為１，２，３，４，５
客戶編號必須輸入
請款單號必須輸入
此客戶此張請款單已存在，不可再新增
此客戶此張請款單不存在．
新增或修改時，必須至少選擇一筆發票
此客戶此張請款單已收完款，或此客戶已無未繳清之發票
此張請款單已收完款，不可再更正
日期錯誤
訂單號碼必須輸入
新增時請款單號不可輸入
此張請款單非屬本廠區，不可更改，刪除或列印
此張發票已進入承購程序，不允許任何異動
請先開立請款單後再輸入單據資料
單據類別輸入錯誤
單據編號不可空白
開立日期格式錯誤
押匯日期格式錯誤
請輸入幣別
單據類別不可空白
此訂單不存在於此請款單中
２４－請輸入請款單列印時是否列印出貨地點！
２５－請輸入列印時是否列印折讓單號！
