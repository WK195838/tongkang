     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP094R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION     進倉履約折讓自動處理分攤金額檢核表
     H*            5.DATE-WRITTEN  103/01/20  2014AR796
     H*            6.UDPTAE  DATE
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARDSDT  IP  E           K        DISK
     FARSAID  IF  E           K        DISK
     FARR094T1O   E             69     PRINTER
     E                    ARY        50 12               類別數量金額
     IRARDSDT     01
     I                                              ASCUNOL3
     I                                              ASORNOL2
     I                                              ASAREAL2
     I                                              ASINNOL1
     I           UDS
     I                                      309 309 D#PROD
     I                                     10011010 T#USID
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C   L3                EXSR L3CLR
     C   L2                EXSR L2CLR
     C   L1                EXSR L1CLR
     C   01                EXSR DTLSR
     CL1 99                EXSR L1SR
     CL2 99                EXSR L2SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C* KEY LIST
     C           KEY01     KLIST
     C                     KFLD           ASORNO
     C                     KFLD           ASYYMM
     C                     KFLD           ASCONT
     C*初始報表
     C*          D#PROD   IFNE  ' '
     C                     MOVE *ALL'='   T#LIN1
     C                     MOVE *ALL'='   T#LIN2
     C                     SETON                     6999
     C*
     C                     ENDSR
     C*****************************************************************
     C           L3CLR     BEGSR
     C*****************************************************************
     C                     SETON                     69
     C                     SETOF                     60
     C                     MOVE *BLANK    W#CUNO
     C                     ENDSR
     C*****************************************************************
     C           L2CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR941T3
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR941T2
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     MOVE ASSALE    T#SAID           業務代號
     C           ASSALE    CHAINRARSAID             N40
     C  N40                MOVELSANAME    T#SANM           業務姓名
     C   40                MOVE *BLANK    T#SANM
     C*
     C           ASCUNO    IFNE W#CUNO
     C                     MOVE *OFF      *IN60
     C                     MOVE ASCUNO    W#CUNO  6
     C                     ELSE
     C                     MOVE *ON       *IN60
     C                     ENDIF
     C*
     C                     MOVELASPDNM    W#PDNM  1
     C*
     C           D#PROD    IFNE ' '
     C           D#PROD    ANDNEW#PDNM
     C                     GOTO ENDDL
     C                     ENDIF
     C*
     C                     MOVE ASCUNO    T#CUNO           客戶代號
     C                     MOVE ASCUNM    T#CUNM           客戶名稱
     C                     MOVE ASORNO    T#ORNO           訂單號碼
     C                     MOVE ASAREA    T#AREA           廠區
     C                     Z-ADDASYYMM    T#YYMM           處理年月
     C                     Z-ADDASCONT    T#CONT           處理次數
     C                     MOVELASINNO    T#INNO           發票號碼
     C                     MOVELASTXNO    T#TXNO           磅單編號
     C                     Z-ADDASTXIT    T#TXIT           磅單項次
     C                     MOVELASPDNM    T#PDNM           產品名稱
     C                     Z-ADDASQTY1    T#QTY1           銷貨數量
     C                     Z-ADDASAMT4    T#AMT4           折讓單價
     C                     Z-ADDASDMT1    T#DMT1           折讓金額
     C*
     C                     ADD  ASDMT1    T#DMTA
     C                     ADD  ASDMT1    T1DMTA
     C*
     C   69                WRITEAR941TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR941T1
     C*
     C           ENDDL     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C   69                WRITEAR941TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR941T2
     C*
     C                     ENDSR
     C*****************************************************************
     C           L2SR      BEGSR
     C*****************************************************************
     C   69                WRITEAR941TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR941T3
     C*
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C   69                WRITEAR941TH
     C                     MOVE *OFF      *IN69
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C   69                WRITEAR941TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR941TR
     C*
     C                     ENDSR
