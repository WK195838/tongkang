     H        1   Y                                     1                 BR003
     FARE021S CF  E                    WORKSTN
     FGENSEQ  UF  E           K        DISK                      A
     E*************************************************************
     E                    ERR     1  22 70
     I*************************************************************
     IAADS        DS
     I                                        1   4 YYMM1
     I                                        1   20YY
     I                                        3   40MM
     I                                       11  20 GRP
     I                                       11  11 GE1
     I                                       12  15 GE2
     I                                       16  20 GE3
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           GEKEY     KLIST
     C                     KFLD           GEKIND
     C                     KFLD           GEPRIN
     C**************************************************************
     C  N90                MOVE '1'       SCRN    1
     C  N90                MOVE '1'       *IN,90
     C**************************************************************
     C           *IN03     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     CSR         SR#01     BEGSR
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C                     EXSR CK#01
     C  N99                MOVE '2'       SCRN
     C  N99                EXSR PR#02
     C   99                MOVE '1'       SCRN
     CSR         SR#01Z    ENDSR
     C******
     CSR         SR#02     BEGSR
     C                     EXFMTDETAIL
     C   KC                EXSR KC#01
     C   KL                MOVE '1'       SCRN
     C   KL                GOTO SR#02Z
     C   KJ                EXSR KJ#02
     C   KJ                GOTO SR#02Z
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C                     ELSE
     C           CODE      IFEQ '4'
     C                     MOVE '1'       SCRN
     C                     ENDIF
     C                     ENDIF
     CSR         SR#02Z    ENDSR
     C*****
     CSR         KC#01     BEGSR
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C****
     CSR         KJ#02     BEGSR
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C                     ENDIF
     C  N99                EXSR FL#02
     C  N99                MOVE '1'       SCRN
     CSR         KJ#02Z    ENDSR
     C****
     CSR         CK#01     BEGSR
     C                     SETOF                     414299
     C                     SETOF                     7071
     C                     Z-ADD0         STNO2   80
     C                     Z-ADD0         STNO3   80
     C                     Z-ADD0         CUNO2   80
     C                     Z-ADD0         CUNO3   80
     C                     MOVE *BLANK    ERRMSG
9008 C                     MOVE YYMM      YYMM1
     C                     SELEC
     C           CODE      WHEQ ' '
     C                     SETON                     4199
     C                     MOVE ERR,1     ERRMSG
     C           CODE      WHEQ '1'
     C                     MOVE '新增'  MOD
     C                     MOVE '0'       *IN70
     C           CODE      WHEQ '2'
     C                     MOVE '更正'  MOD
     C                     MOVE '0'       *IN70
     C           CODE      WHEQ '3'
     C                     MOVE '作廢'  MOD
     C                     MOVE '1'       *IN70
     C           CODE      WHEQ '4'
     C                     MOVE '查詢'  MOD
     C                     MOVE '1'       *IN70
     C                     OTHER
     C                     SETON                     4199
     C                     MOVE ERR,2     ERRMSG
     C                     ENDSL
     C******
     C           YYMM      IFEQ *ZERO
     C                     SETON                     4299
     C                     MOVE ERR,3     ERRMSG
     C                     ENDIF
     C**
     C           YY        IFEQ *ZERO
     C                     SETON                     4299
     C                     MOVE ERR,4     ERRMSG
     C                     ENDIF
     C**
     C           MM        IFLT 1
     C           MM        ORGT 12
     C                     SETON                     4299
     C                     MOVE ERR,5     ERRMSG
     C                     ENDIF
     C**
     C           *IN99     IFEQ '0'
     C                     MOVEL'01'      GEKIND
     C                     MOVEL*BLANK    GEPRIN
     C                     MOVEL*BLANK    GRP
     C                     MOVELTXAR      GE1
9008 C                     MOVE YYMM      GE2
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ              N97
     C  N97                Z-ADDGESTNO    STNO2
     C  N97                Z-ADDGECUNO    CUNO2
     C                     MOVEL'02'      GEKIND
     C           GEKEY     CHAINGENSEQ              N97
     C  N97                Z-ADDGESTNO    STNO3
     C  N97                Z-ADDGECUNO    CUNO3
     C                     SELEC
     C           CODE      WHEQ '1'
     C           *IN97     ANDEQ'0'
     C                     SETON                     4299
     C                     MOVE ERR,6     ERRMSG
     C           CODE      WHEQ '2'
     C           *IN97     ANDEQ'0'
     C           STNO2     IFLE CUNO2
     C           STNO3     ORLE CUNO3
     C                     SETON                     71
     C                     ENDIF
     C           CODE      WHEQ '3'
     C           *IN97     ANDEQ'0'
     C           STNO2     IFLE CUNO2
     C           STNO3     ORLE CUNO3
     C                     SETON                     4299
     C                     MOVE ERR,16    ERRMSG
     C                     ENDIF
     C           CODE      WHNE '1'
     C           *IN97     ANDEQ'1'
     C                     SETON                     4299
     C                     MOVE ERR,7     ERRMSG
     C                     ENDSL
     C                     ENDIF
     CSR         CK#01Z    ENDSR
     C******
     CSR         PR#02     BEGSR
CLJ  C                     SETOF                     4344
     C                     SETOF                     4546
---  C                     SETOF                     4748
     C*
     C                     MOVE *BLANK    PRE2
     C                     MOVE *BLANK    PRE3
     C                     Z-ADD0         SNO2
     C                     Z-ADD0         SNO3
     C                     Z-ADD0         ENO2
     C                     Z-ADD0         ENO3
     C                     Z-ADD0         CNO2
     C                     Z-ADD0         CNO3
     C**
     C           CODE      CASNE'1'       PR#OTH
     C                     ENDCS
     ***
     CSR         PR#02Z    ENDSR
     C******
     C******
     CSR         PR#OTH    BEGSR
     C                     MOVEL'01'      GEKIND
     C                     MOVEL*BLANK    GEPRIN
     C                     MOVEL*BLANK    GRP
     C                     MOVELTXAR      GE1
9008 C                     MOVE YYMM      GE2
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ              N97
     C  N97                MOVELGEPRE     PRE2
     C  N97                Z-ADDGESTNO    SNO2
     C  N97                Z-ADDGEENNO    ENO2
     C  N97                Z-ADDGECUNO    CNO2
     C***
     C                     MOVEL'02'      GEKIND
     C           GEKEY     CHAINGENSEQ              N97
     C  N97                MOVELGEPRE     PRE3
     C  N97                Z-ADDGESTNO    SNO3
     C  N97                Z-ADDGEENNO    ENO3
     C  N97                Z-ADDGECUNO    CNO3
     C***
     CSR         PR#OTZ    ENDSR
     C******
     CSR         CK#02     BEGSR
     C                     SETOF                     99
     C                     MOVE *BLANK    ERRMSG
     C***
     C                     MOVEA'000000'  *IN,43
     C***
     C           PRE2      IFEQ *BLANK
     C                     SETON                     9943
     C                     MOVELERR,8     ERRMSG
     C                     ENDIF
     C*****
     C           PRE3      IFEQ *BLANK
     C                     SETON                     9944
     C                     MOVELERR,9     ERRMSG
     C                     ENDIF
     C*****
     C           SNO2      IFEQ 0
     C                     SETON                     9945
     C                     MOVELERR,10    ERRMSG
     C                     ENDIF
     C*****
     C           SNO3      IFEQ 0
     C                     SETON                     9946
     C                     MOVELERR,11    ERRMSG
     C                     ENDIF
     C*****
     C           ENO2      IFEQ 0
     C                     SETON                     9947
     C                     MOVELERR,12    ERRMSG
     C                     ENDIF
     C*****
     C           ENO3      IFEQ 0
     C                     SETON                     9948
     C                     MOVELERR,13    ERRMSG
     C                     ENDIF
     C*****
     C           SNO2      IFGT ENO2
     C                     SETON                     994547
     C                     MOVELERR,14    ERRMSG
     C                     ENDIF
     C*****
     C           SNO3      IFGT ENO3
     C                     SETON                     994648
     C                     MOVELERR,15    ERRMSG
     C                     ENDIF
     C*****
     C  N99N71   SNO2      SUB  1         CNO2
     C  N99N71   SNO3      SUB  1         CNO3
     C           CNO2      IFGT ENO2
     C                     SETON                     9947
     C                     MOVELERR,17    ERRMSG
     C                     ENDIF
     C           CNO3      IFGT ENO3
     C                     SETON                     9948
     C                     MOVELERR,18    ERRMSG
     C                     ENDIF
     C*
     C*-----------------------------
     C* 870321 加入檢核碼確認
     C*-----------------------------
     C           S#CKS2    IFNE SNO2
     C           *IN71     ANDEQ'0'
     C                     MOVELERR,19    ERRMSG
     C                     SETON                     9945
     C                     GOTO CK#02Z
     C                     ENDIF
     C           S#CKS3    IFNE SNO3
     C           *IN71     ANDEQ'0'
     C                     MOVELERR,21    ERRMSG
     C                     SETON                     9946
     C                     GOTO CK#02Z
     C                     ENDIF
     C           S#CKE2    IFNE ENO2
     C                     MOVELERR,20    ERRMSG
     C                     SETON                     9947
     C                     GOTO CK#02Z
     C                     ENDIF
     C           S#CKE3    IFNE ENO3
     C                     MOVELERR,22    ERRMSG
     C                     SETON                     9948
     C                     GOTO CK#02Z
     C                     ENDIF
     C**
     C*****
     CSR         CK#02Z    ENDSR
     C******
     CSR         FL#02     BEGSR
     C           CODE      CASEQ'3'       DL#02
     C           CODE      CASEQ'1'       WR#02
     C           CODE      CASEQ'2'       UP#02
     C                     ENDCS
     CSR         FL#02Z    ENDSR
     C****
     CSR         DL#02     BEGSR
     C                     MOVEL'01'      GEKIND
     C                     MOVEL*BLANK    GEPRIN
     C                     MOVEL*BLANK    GRP
     C                     MOVELTXAR      GE1
9008 C                     MOVE YYMM      GE2
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ               97
     C  N97                DELETGEREC
     C***
     C                     MOVEL'02'      GEKIND
     C           GEKEY     CHAINGENSEQ               97
     C  N97                DELETGEREC
     C******
     CSR                   ENDSR
     *****
     CSR         WR#02     BEGSR
     C***
     C                     MOVEL'01'      GEKIND
     C                     MOVEL*BLANK    GEPRIN
     C                     MOVEL*BLANK    GRP
     C                     MOVELTXAR      GE1
9008 C                     MOVE YYMM      GE2
     C                     MOVELGRP       GEPRIN
     C                     MOVELPRE2      GEPRE
     C                     Z-ADDSNO2      GESTNO
     C                     Z-ADDENO2      GEENNO
     C                     Z-ADDCNO2      GECUNO
     C                     WRITEGEREC
     C****
     C                     MOVEL'02'      GEKIND
     C                     MOVEL*BLANK    GEPRIN
     C                     MOVEL*BLANK    GRP
     C                     MOVELTXAR      GE1
9008 C                     MOVE YYMM      GE2
     C                     MOVELGRP       GEPRIN
     C                     MOVELPRE3      GEPRE
     C                     Z-ADDSNO3      GESTNO
     C                     Z-ADDENO3      GEENNO
     C                     Z-ADDCNO3      GECUNO
     C                     WRITEGEREC
     C****
     CSR         WR#02Z    ENDSR
     *****
     CSR         UP#02     BEGSR
     C***
     C                     MOVEL'01'      GEKIND
     C                     MOVEL*BLANK    GEPRIN
     C                     MOVEL*BLANK    GRP
     C                     MOVELTXAR      GE1
9008 C                     MOVE YYMM      GE2
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ               97
     C  N97                MOVELPRE2      GEPRE
     C  N97                Z-ADDSNO2      GESTNO
     C  N97                Z-ADDENO2      GEENNO
     C  N97                Z-ADDCNO2      GECUNO
     C  N97                UPDATGEREC
     C***
     C                     MOVEL'02'      GEKIND
     C           GEKEY     CHAINGENSEQ               97
     C  N97                MOVELPRE3      GEPRE
     C  N97                Z-ADDSNO3      GESTNO
     C  N97                Z-ADDENO3      GEENNO
     C  N97                Z-ADDCNO3      GECUNO
     C  N97                UPDATGEREC
     C***
     CSR         UP#02Z    ENDSR
** ERR
功能代碼必須輸入．
功能代碼必須為１，２，３，４
年月必須輸入
年度錯誤
月份錯誤
此年月已輸入發票號碼，不可再新增
此年月並未輸入發票號碼．
二聯式發票字軌不可空白
三聯式發票字軌不可空白
二聯式發票起始號碼不可為０
三聯式發票起始號碼不可為０
二聯式發票截止號碼不可為０
三聯式發票截止號碼不可為０
二聯式發票起始號碼不可大於截止號碼
三聯式發票起始號碼不可大於截止號碼
此年月之發票號碼已開立發票，不可再刪除
二聯式發票截止號碼不可小於目前號碼
三聯式發票截止號碼不可小於目前號碼
１９－二聯式發票起始號碼輸入與檢核不符
２０－二聯式發票截止號碼輸入與檢核不符
２１－三聯式發票起始號碼輸入與檢核不符
２２－三聯式發票截止號碼輸入與檢核不符
