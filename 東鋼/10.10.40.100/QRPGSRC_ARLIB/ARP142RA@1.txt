     A*****************************************************************
     A*
     A*            *****   PROGRAM INTRODUCTION   *****
     A*
     A*            1.PROGRAM-ID    ARP142RA
     A*            2.PROGRAM-TYPE  RPG
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     台北INENRB-->INENRBT
     A*            5.DATE-WRITTEN  107/12/10 2018AR00031
     A*            6.UPDATE  DATE  108/05/09 S00WCJ (0805A)
     A*                            增加寫入電子發票介面LOG異動資訊
     A*
     A*****************************************************************
     H            Y                                     1                 BR100
     FINENRB  UP  E           K        DISK
     F            RINENRB                           KRENAMENRBECD
     FINENRBT UF  E           K        DISK                      A
     FINENRBG O   E           K        DISK
     F            RINENRB                           KRENAMENRBG
     INRBECD      01
     I              NBFLAG                          FLAG
     I              NBMSTE                          MSTE
     I              NBHEBN                          HEBN
     I              NBBRBN                          BRBN
     I              NBINTE                          INTE
     I              NBYEMH                          YEMH
     I              NBINTK                          INTK
     I              NBINBN                          INBN
     I              NBINEN                          INEN
     I              NBINB1                          INB1
     I              NBINE1                          INE1
     I              NBINBT                          INBT
     I              NBFACT                          FACT
     I              NBTMDT                          TMDT
     I              NBTMTM                          TMTM
     I              NBTMOP                          TMOP
     I              NBTAWS                          TAWS
     I              NBSTCL                          STCL
     I              NBXPDD                          XPDD
     I              NBXPDT                          XPDT
     I              NBXUSR                          XUSR
     I              NBMAR1                          MAR1
     I              NBPTYN                          PTYN
     I*
0805AI           UDS
0805AI                                     10011010 U#USID
     C           KEY       KLIST
     C                     KFLD           YEMH
     C                     KFLD           FACT
     C*
     C                     MOVEA'000'     *IN,11
     C           FLAG      COMP 'A'                      11
     C           FLAG      COMP 'C'                      12
     C           FLAG      COMP 'D'                      13
     C*
     C           STCL      IFNE ' '
     C           KEY       CHAINRINENRB              32
     C                     EXSR MOVE1
     C           *IN32     IFEQ '1'
     C           FACT      IFEQ 'U'
     C           FACT      OREQ 'H'
     C           FACT      OREQ 'K'
     C                     WRITERINENRB
0805AC                     MOVE 'ARP142RA'NBAPNM
0805AC           *DATE     SUB  19000000  NBCHDD
0805AC                     TIME           NBCHDT
0805AC                     MOVELU#USID    NBCUSR
     C                     WRITENRBG
     C                     END
     C                     END
     C*
     C           *IN32     IFEQ '0'
     C           FACT      IFEQ 'U'
     C           FACT      OREQ 'H'
     C           FACT      OREQ 'K'
     C                     UPDATRINENRB
0805AC                     MOVE 'ARP142RA'NBAPNM
0805AC           *DATE     SUB  19000000  NBCHDD
0805AC                     TIME           NBCHDT
0805AC                     MOVELU#USID    NBCUSR
     C                     WRITENRBG
     C                     END
     C                     END
     C                     ENDIF
     C           *IN11     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     MOVE ' '       FLAG
     C                     UPDATNRBECD
     C                     END
     C           *IN13     IFEQ '1'
     C                     DELETNRBECD
     C                     END
     C**************************************************************
     C           MOVE1     BEGSR
     C                     MOVELFLAG      NBFLAG
     C                     MOVELMSTE      NBMSTE
     C                     MOVELHEBN      NBHEBN
     C                     MOVELBRBN      NBBRBN
     C                     MOVELINTE      NBINTE
     C                     MOVELYEMH      NBYEMH
     C                     MOVELINTK      NBINTK
     C                     MOVELINBN      NBINBN
     C                     MOVELINEN      NBINEN
     C                     MOVELINB1      NBINB1
     C                     MOVELINE1      NBINE1
     C                     MOVELINBT      NBINBT
     C                     MOVELFACT      NBFACT
     C                     Z-ADDTMDT      NBTMDT
     C                     Z-ADDTMTM      NBTMTM
     C                     MOVELTMOP      NBTMOP
     C                     MOVELTAWS      NBTAWS
     C                     MOVELSTCL      NBSTCL
     C                     Z-ADDXPDD      NBXPDD
     C                     Z-ADDXPDT      NBXPDT
     C                     MOVELXUSR      NBXUSR
     C                     MOVELMAR1      NBMAR1
     C                     MOVELPTYN      NBPTYN
     C                     ENDSR
