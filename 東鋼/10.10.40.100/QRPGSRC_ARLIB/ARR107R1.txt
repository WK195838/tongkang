     H*****************************************************************
     H*
     H*          >>  PROGRAM INTRODUCTION  <<
     H*          1.PROGRAM-ID    ARR107R
     H*          2.PROGRAM-TYPE  RPG/400
     H*          3.AUTHOR        S02CSF
     H*          4.FUNCTION     應收帳款承購(E-FACTOTING)託收票據明細表
     H*                          (先前處理)
     H*          5.DATE-WRITTEN  92/02/16
     H*          6.DATE-MODIFY
     H*
     H*****************************************************************
     H        1   Y                                     1
     FARCUDT  IP  E           K        DISK
     FARCVCK  IF  E           K        DISK
     FARCVMS  IF  E           K        DISK
     FARR107F O   E           K        DISK
     IRARCUDT     01
     C*****************************************************************
     C*          KEY   LIST
     C*****************************************************************
     C*FILE==>ARCVCKN
     C           W#CVCK    KLIST
     C                     KFLD           AKRCNO           沖銷單號
     C                     KFLD           AKRCTM           沖銷項次
     C*****************************************************************
     C*         主程式開始
     C*****************************************************************
     C                     EXSR DTLSR
     CLR                   RETRN
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C*
     C           *LOVAL    SETLLRARCVMS
     C                     MOVE *OFF      *IN40
     C           *IN40     DOWEQ*OFF
     C                     READ RARCVMS                  40
     C   40                LEAVE
     C*
     C           ASCUNO    IFEQ ACCUNO
     C                     EXSR SR1000
     C                     ENDIF
     C                     ENDDO
     C*
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           SR1000    BEGSR
     C*****************************************************************
     C                     CLEARRAR107F
     C*
     C                     MOVELASRCNO    AKRCNO           沖銷單號
     C                     Z-ADD0         AKRCTM           沖銷項次
     C           W#CVCK    SETLLRARCVCK
     C                     MOVE *OFF      *IN41
     C           *IN41     DOWEQ*OFF
     C                     READ RARCVCK                  41
     C           ASRCNO    IFNE AKRCNO
     C           *IN41     OREQ *ON
     C                     LEAVE
     C                     ENDIF
     C* G:現金
     C           AKNTTP    IFEQ 'G'                        票款別
     C           AKNTTP    OREQ 'J'
     C           AKNTTP    OREQ ' '
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVELACCUNO    AFCUNO           客戶代號
     C                     MOVELACCUNM    AFCUNM           客戶名稱
     C                     MOVELACBKID    AFBKID           銀行代號
     C                     MOVELACBKNM    AFBKNM           銀行名稱
     C                     MOVELAKRCNO    AFRCNO           繳款單號
     C                     Z-ADDASACDT    AFACDT           繳款日期
     C                     Z-ADDAKDUDT    AFDUDT           票據到期日
     C                     MOVELAKNTNO    AFRLNO           票據號碼
     C                     Z-ADDAKRAMT    AFRAMT           票據金額
     C                     MOVELAKPLAC    AFPLAC           付款地
     C                     MOVELAKBANM    AFBANM           付款銀行
     C                     MOVELAKPANO    AFPANO           付款帳號
     C                     MOVELAKNTCD    AFNTCD           兌現碼
     C*
     C                     WRITERAR107F
     C*
     C                     ENDDO
     C*
     CSR                   ENDSR
