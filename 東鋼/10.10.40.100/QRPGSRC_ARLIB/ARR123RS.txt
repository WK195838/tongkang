     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR123RS
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     客戶銷售排行榜（財會用）
     H*            5.DATE-WRITTEN  95/01/03
     H*            6.DATE-UPDATE
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR123S CF  E                    WORKSTN
     E*************************************************************
     E                    ERR     1   9 70
     I*************************************************************
     I            DS
     I                                        1   60D#YM1
     I                                        1   40D#YY1
     I                                        5   60D#MM1
     I            DS
     I                                        1   60D#YM2
     I                                        1   40D#YY2
     I                                        5   60D#MM2
     I           UDS
     I                                      300 3050U#YM1
     I                                      306 3110U#YM2
     I                                      312 3140U#RAK1
     I                                      315 3170U#RAK2
     I                                      318 3280U#AMT
     I                                     10011010 U#USID
     C**************************************************************
     C*   程式參數傳遞區
     C**************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P#IN03  1
     C**************************************************************
     C*   主程式開始
     C**************************************************************
     C                     EXSR SR0000                     初始程式
     C*
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C                     ENDCS
     C                     ENDDO
     C*
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C           SR0000    BEGSR
     C**************************************************************
     C                     MOVEL'01'      W#PRID  2        畫面一
     C*
     C                     Z-ADD1         S#RAK1           銷貨排名
     C                     Z-ADD10        S#RAK2
     C*
     C                     ENDSR
     C**************************************************************
     C           SR1000    BEGSR
     C**************************************************************
     C*W#PRID='01'-->畫面一
     C           W#PRID    DOWEQ'01'
     C                     EXFMTAR123F1                    螢幕輸入
     C                     MOVEL*BLANK    S#ERR
     C*
     C           *IN03     IFEQ '1'
     C           *IN12     OREQ '1'
     C                     MOVEL'1'       P#IN03           回前畫面
     C                     MOVEL'00'      W#PRID           結束
     C                     LEAVE
     C                     ENDIF
     C*檢核
     C                     EXSR SR1100                     檢核
     C                     ENDDO
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         SR1100    BEGSR
     C**************************************************************
     C                     MOVEA'00000'   *IN,30
     C*
     C                     SETOF                     99
     C           S#YM1     IFEQ 0
     C                     MOVELERR,1     S#ERR
     C                     SETON                     3099
     C                     ENDIF
     C*
     C  N99                Z-ADDS#YM1     D#YM1
     C  N99      D#MM1     IFLT 1
     C           D#MM1     ORGT 12
     C                     MOVELERR,2     S#ERR
     C                     SETON                     3099
     C                     ENDIF
     C*
     C  N99      S#YM2     IFEQ 0
     C                     MOVELERR,3     S#ERR
     C                     SETON                     3199
     C                     ENDIF
     C*
     C  N99                Z-ADDS#YM2     D#YM2
     C  N99      D#MM2     IFLT 1
     C           D#MM2     ORGT 12
     C                     MOVELERR,4     S#ERR
     C                     SETON                     3199
     C                     ENDIF
     C*
     C  N99      S#YM1     IFGT S#YM2
     C                     MOVELERR,5     S#ERR
     C                     SETON                     303199
     C                     ENDIF
     C*
     C  N99      S#RAK1    IFEQ 0
     C                     MOVELERR,6     S#ERR
     C                     SETON                     3299
     C                     ENDIF
     C*
     C  N99      S#RAK2    IFEQ 0
     C                     MOVELERR,7     S#ERR
     C                     SETON                     3399
     C                     ENDIF
     C*
     C  N99      S#RAK1    IFGT S#RAK2
     C                     MOVELERR,8     S#ERR
     C                     SETON                     323399
     C                     ENDIF
     C*
     C  N99      S#AMT     IFEQ 0
     C                     MOVELERR,9     S#ERR
     C                     SETON                     3499
     C                     ENDIF
     C*
     C  N99                MOVEL'00'      W#PRID
     C  N99                Z-ADDS#YM1     U#YM1
     C  N99                Z-ADDS#YM2     U#YM2
     C  N99                Z-ADDS#RAK1    U#RAK1
     C  N99                Z-ADDS#RAK2    U#RAK2
     C  N99                Z-ADDS#AMT     U#AMT
     C*
     C                     ENDSR
     C*****************************************************************
** ERR
０１－請輸入銷貨年月起始值！
０２－銷貨年月起始值輸入錯誤！
０３－請輸入銷貨年月終止值！
０４－銷貨年月終止值輸入錯誤！
０５－銷貨年月起始值不可大於終止值！
０６－請輸入排行名次起始值！
０７－請輸入排行名次終止值！
０８－排行名次起始值不可大於終止值！
０９－請輸入銷貨淨額！
