     H*****************************************************************
     H*
     H*            *****   PROGRAM INTRODUCTION   *****
     H*
     H*            1.PROGRAM-ID    RCVDTLCVTR
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION    RCVDTL&RCADTL     (會計科目轉換4->6)
     H*            5.DATE-WRITTEN  101/12/20 2012AR704 S00WCJ
     H*
     H*****************************************************************
     FRCVDTL  UP  E           K        DISK
     FRCADTL  UF  E           K        DISK
     FACNCMPL1IF  E           K        DISK
     IRVACNO      DS
     I                                        5   6 ACNO56
     IRAACNO      DS
     I                                        5   6 ACN156
     C*****************************************************************
     C*          Parameter List
     C*****************************************************************
     C*P#PRC='Y'執行轉換
     C           *ENTRY    PLIST
     C                     PARM           P#PRC   1        *處理碼
     C*****************************************************************
     C*          Main      Program
     C*****************************************************************
     C           P#PRC     IFNE 'Y'
     C                     MOVEL*ON       *INLR
     C                     ENDIF
     C*CHAIN ACNCMP新舊會科對照表X(4)->X(6)
     C           RVDATE    IFGE 1010101
     C           RVACNO    ANDNE*BLANKS
     C           ACNO56    ANDEQ*BLANKS                    *4碼會計科目
     C*
     C                     MOVELRVACNO    W#ACNO  9        *ROC 會計科目
     C           W#ACNO    CHAINRACNCMP              60    *NR
     C  N60                MOVELAZNO      RVACNO           *IFRS會計科目
     C*
     C                     UPDATRVREC
     C*
     C           RVNO      SETLLRAREC
     C                     MOVEL*OFF      *IN50
     C           *IN50     DOWEQ*OFF
     C           RVNO      READERAREC                    50
     C   50                LEAVE
     C*
     C  N50      ACN156    IFEQ *BLANKS
     C                     MOVELRAACNO    W#ACNO
     C           W#ACNO    CHAINRACNCMP              60
     C  N60                MOVELAZNO      RAACNO
     C*
     C                     UPDATRAREC
     C                     ENDIF
     C                     ENDDO
     C*
     C                     ENDIF
