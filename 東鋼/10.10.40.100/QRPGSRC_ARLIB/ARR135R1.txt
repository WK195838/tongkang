     H*****************************************************************
     H*
     H*          >>  PROGRAM INTRODUCTION  <<
     H*          1.PROGRAM-ID    ARR135R1
     H*          2.PROGRAM-TYPE  RPG/400
     H*          3.AUTHOR        S02LBU
     H*          4.FUNCTION     新桃工程自用發票開立明細表
     H*          5.DATE-WRITTEN  98/02/26
     H*          6.DATE-MODIFY   104/11/12  2015AR953 S00WCJ (0411A)
     H*                           產品代碼3碼擴5碼
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FUENDTL  IP  E           K        DISK
     FARR135F UF  E           K        DISK                      A
     E*****************************************************************
     IUVREC       01
     I*
     I           UDS
     I                                      301 3060D#YMS
     I                                      307 3120D#YME
     I                                      313 3220D#PJNO
     I                                     10011010 U#USID
     C*****************************************************************
     C*          KEY       LIST
     C*****************************************************************
     C           K#KEY1    KLIST
     C                     KFLD           K#NO   10        發票號碼
     C                     KFLD           K#AREA  1        類別
     C                     KFLD           K#TRCD  2        異動代號
0411AC                     KFLD           K#PDNM  5        品名
     C*
     C*****************************************************************
     C*          MAIN      PROGRAM
     C*****************************************************************
     C   01                EXSR DP#01
     CLR                   RETRN
     C*
     C*****************************************************************
     CSR         DP#01     BEGSR
     C*****************************************************************
     C                     MOVELUVNO      K#NO             發票號碼
     C                     MOVELUVAREA    K#AREA           廠區
     C                     MOVELUVTRCD    K#TRCD           異動代號
     C                     MOVELUVPDNM    K#PDNM           品名
     C*
     C                     MOVEL*OFF      *IN30
     C           K#KEY1    CHAINWVREC                30
     C*
     C           *IN30     IFEQ *ON
     C                     MOVELUVNO      W1NO             發票號碼
     C                     MOVELUVAREA    W1AREA           來源廠區
     C                     MOVELUVTRCD    W1TRCD           異動代號
     C                     MOVELUVPDNM    W1PDNM           品名
     C                     MOVELUVPJNO    W1PJNO           專案編號
     C                     Z-ADDUVNODD    W1DATE           發票日期
     C                     Z-ADDUVUPRC    W1UPRC           單價
     C                     Z-ADDUVQTY     W1QTY            數量
     C                     Z-ADDUVAMT     W1AMT            金額
     C                     Z-ADDUVTAX     W1TAX            稅額
     C                     WRITEWVREC
     C                     ELSE
     C                     ADD  UVQTY     W1QTY            數量
     C                     ADD  UVAMT     W1AMT            金額
     C                     ADD  UVTAX     W1TAX            稅額
     C                     UPDATWVREC
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*****************************************************************
