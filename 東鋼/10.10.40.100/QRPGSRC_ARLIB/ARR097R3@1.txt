     H*****************************************************************
     H*
     H*            >>  PROGRAM INTRODUCTION  <<
     H*
     H*            1.PROGRAM-ID      ARR097R3
     H*            2.PROGRAM-TYPE    RPG
     H*            3.AUTHOR          S02LBU
     H*            4.FUNCTION       應收帳款明細表（查帳用）
     H*            5.DATE-WRITTE     91/09/12
     H*
     H*****************************************************************
     H        1    Y                                    1
     FARR097F IP  E           K        DISK
 9302FSAMAST  IF  E           K        DISK
     FARR097T O   E             66     PRINTER
     F*****************************************************************
     IRARR097F
     I                                              AFCUNOL3
     I                                              AFORNOL2
     I                                              AFIVNOL1
     I*----------------------------------------------------------------
     I            DS
     I                                        1   6 AFORNO
     I                                        1   1 D#AREA
     I                                        2   60D#ORNO
     I           UDS
     I                                      300 3030R#YME
     I                                     10011010 U#USID
     I*
     C*****************************************************************
     C*        KEY      LIST
     C*****************************************************************
 9302C*FILE==>SAMAST(訂單主檔)
   . C           K#SAMT    KLIST
   . C                     KFLD           S1OREA
 9302C                     KFLD           S1ORNO
     C*****************************************************************
     C*        MAIN  PROGRAM
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C   L3                EXSR L3CLR
     C   L2                EXSR L2CLR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CL2 99                EXSR L2SR
     CL3 99                EXSR L3SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     Z-ADD0         W#THAT 120
     C                     MOVE *ALL'='   R#LIN1
     C                     MOVE *ALL'='   R#LIN2
     C                     SETON                     66
     C*
     C                     ENDSR
     C*****************************************************************
     C           L3CLR     BEGSR
     C*****************************************************************
     C                     Z-ADD0         W#FMT  110
     C                     Z-ADD0         W#GMT  110
     C                     Z-ADD0         W#HMT  110
     C*
     C                     ENDSR
     C*****************************************************************
     C           L2CLR     BEGSR
     C*****************************************************************
     C                     Z-ADD0         W#FAT  110
     C                     Z-ADD0         W#GAT  110
     C                     Z-ADD0         W#HAT  110
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR097T1D
     C                     MOVE *BLANK    W#IVNO 10
     C                     Z-ADD0         W#HAMT 110
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C           W#IVNO    IFNE AFIVNO
     C           W#HAMT    ORNE AFHAMT
     C                     MOVELAFIVNO    W#IVNO
     C                     Z-ADDAFHAMT    W#HAMT
     C                     ADD  AFAAMT    R#AAMT           貨款
     C                     ADD  AFBAMT    R#BAMT           扣預收款
     C                     ADD  AFCAMT    R#CAMT           銷項稅額
     C                     ADD  AFDAMT    R#DAMT           發票金額
     C                     ADD  AFEAMT    R#EAMT           銷／折金額
     C                     ADD  AFFAMT    R#FAMT           應收貨款
     C                     ADD  AFGAMT    R#GAMT           已收金額
     C                     ADD  AFHAMT    R#HAMT           應收帳款
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C                     MOVELAFCUNM    R#CUNM           客戶名稱
     C                     MOVELAFCUNO    R#CUNO           客戶編號
     C                     MOVELAFORNO    R#ORNO           訂單編單
     C                     MOVELAFRVID    R#RVID           業務員
     C                     MOVELAFIVNO    R#IVNO           發票號碼
 9302C*
   . C                     MOVELD#AREA    S1OREA
   . C                     Z-ADDD#ORNO    S1ORNO
   . C           K#SAMT    CHAINRSAMAST              40
   . C  N40                MOVELS1CTNO    R#CTNO           工程案號
 9302C*
     C   66                WRITEAR097T1H
     C                     SETOF                     66
     C                     WRITEAR097T1D
     C*金額累計
     C                     ADD  R#FAMT    W#FMT            客應貨小計
     C                     ADD  R#GAMT    W#GMT            客已金小計
     C                     ADD  R#HAMT    W#HMT            客應收小計
     C                     ADD  R#FAMT    W#FAT            訂應貨小計
     C                     ADD  R#GAMT    W#GAT            訂已金小計
     C                     ADD  R#HAMT    W#HAT            訂應帳小計
     C                     ADD  R#HAMT    W#THAT           應收帳款總
     C*
     C                     ENDSR
     C*****************************************************************
     C           L2SR      BEGSR
     C*****************************************************************
     C                     Z-ADDW#FAT     R#SOFT
     C                     Z-ADDW#GAT     R#SOGT
     C                     Z-ADDW#HAT     R#SOHT
     C*
     C   66                WRITEAR097T1H
     C                     SETOF                     66
     C                     WRITEAR097T1S
     C*
     C                     ENDSR
     C*****************************************************************
     C           L3SR      BEGSR
     C*****************************************************************
     C                     Z-ADDW#FMT     R#SFMT
     C                     Z-ADDW#GMT     R#SGMT
     C                     Z-ADDW#HMT     R#SHMT
     C*
     C   66                WRITEAR097T1H
     C                     SETOF                     66
     C                     WRITEAR097T1X
     C*
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C                     Z-ADDW#THAT    R#THMT
     C*
     C   66                WRITEAR097T1H
     C                     SETOF                     66
     C                     WRITEAR097T1T
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C*
     C   66                WRITEAR097T1H
     C                     SETOF                     66
     C                     WRITEAR097T1M
     C*
     C                     ENDSR
