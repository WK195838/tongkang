     H        1   Y                                     1                 BR003
     FARE023S CF  E                    WORKSTN
     FBANKID  UF  E           K        DISK                      A
     E*************************************************************
     E                    ERR     1   5 70
     I*************************************************************
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           BAKEY     KLIST
     C                     KFLD           BAID1
     C                     KFLD           BAID2
     C**************************************************************
     C  N90                MOVE '1'       SCRN    1
     C  N90                MOVE '1'       *IN,90
     C**************************************************************
     C           *IN03     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     CSR         SR#01     BEGSR
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C                     EXSR CK#01
     C  N99                MOVE '2'       SCRN
     C  N99                EXSR PR#02
     C   99                MOVE '1'       SCRN
     CSR         SR#01Z    ENDSR
     C******
     CSR         SR#02     BEGSR
     C                     EXFMTDETAIL
     C   KC                EXSR KC#01
     C   KL                MOVE '1'       SCRN
     C   KL                GOTO SR#02Z
     C   KJ                EXSR KJ#02
     C   KJ                GOTO SR#02Z
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C                     ELSE
     C           CODE      IFEQ '4'
     C                     MOVE '1'       SCRN
     C                     ENDIF
     C                     ENDIF
     CSR         SR#02Z    ENDSR
     C*****
     CSR         KC#01     BEGSR
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C****
     CSR         KJ#02     BEGSR
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C                     ENDIF
     C  N99                EXSR FL#02
     C  N99                MOVE '1'       SCRN
     CSR         KJ#02Z    ENDSR
     C****
     CSR         CK#01     BEGSR
     C                     SETOF                     414299
     C                     SETOF                     4344
     C                     MOVE *BLANK    ERRMSG
     C                     SELEC
     C           CODE      WHEQ ' '
     C                     SETON                     4199
     C                     MOVE ERR,1     ERRMSG
     C           CODE      WHEQ '1'
     C                     MOVE '新增'  MOD
     C                     MOVE '0'       *IN70
     C           CODE      WHEQ '2'
     C                     MOVE '更正'  MOD
     C                     MOVE '0'       *IN70
     C           CODE      WHEQ '3'
     C                     MOVE '作廢'  MOD
     C                     MOVE '1'       *IN70
     C           CODE      WHEQ '4'
     C                     MOVE '查詢'  MOD
     C                     MOVE '1'       *IN70
     C                     OTHER
     C                     SETON                     4199
     C                     MOVE ERR,2     ERRMSG
     C                     ENDSL
     C******
     C           BID1      IFEQ *BLANK
     C                     SETON                     4299
     C                     MOVE ERR,3     ERRMSG
     C                     ENDIF
     C**
     C           *IN99     IFEQ '0'
     C                     MOVELBID1      BAID1
     C                     MOVELBID2      BAID2
     C           BAKEY     CHAINBANKID              N97
     C                     SELEC
     C           CODE      WHEQ '1'
     C           *IN97     ANDEQ'0'
     C                     SETON                     429943
     C                     MOVE ERR,4     ERRMSG
     C           CODE      WHNE '1'
     C           *IN97     ANDEQ'1'
     C                     SETON                     429943
     C                     MOVE ERR,5     ERRMSG
     C                     ENDSL
     C                     ENDIF
     CSR         CK#01Z    ENDSR
     C******
     CSR         PR#02     BEGSR
     C**
     C           CODE      CASEQ'1'       PR#ADD
     C                     CAS            PR#OTH
     C                     ENDCS
     CSR         PR#02Z    ENDSR
     C******
     C******
     CSR         PR#ADD    BEGSR
     C                     MOVEL*BLANK    AREA
     C                     MOVEL*BLANK    RESV1
     C                     MOVEL*BLANK    RESV2
     C                     MOVEL*BLANK    BNAM
     C                     MOVEL*BLANK    NAME
     CSR         PR#ADZ    ENDSR
     C******
     CSR         PR#OTH    BEGSR
     C                     MOVELBAAREA    AREA
     C                     MOVELBARESV    RESV1
     C                     MOVE BARESV    RESV2
     C                     MOVELBABNAM    BNAM
     C                     MOVELBANAME    NAME
     CSR         PR#OTZ    ENDSR
     C******
     CSR         CK#02     BEGSR
     C                     SETOF                     99
     C                     MOVEA'00'      *IN,44
     C                     MOVE *BLANK    ERRMSG
     C***
     C******
     CSR         CK#02Z    ENDSR
     C******
     CSR         FL#02     BEGSR
     C           CODE      CASEQ'3'       DL#02
     C           CODE      CASEQ'1'       WR#02
     C           CODE      CASEQ'2'       UP#02
     C                     ENDCS
     CSR         FL#02Z    ENDSR
     C****
     CSR         DL#02     BEGSR
     C***
     C                     MOVELBID1      BAID1
     C                     MOVELBID2      BAID2
     C           BAKEY     CHAINBANKID               97
     C  N97                DELETBAREC
     CSR         DL#02Z    ENDSR
     *****
     CSR         WR#02     BEGSR
     C***
     C                     CLEARBAREC
     C                     MOVELBID1      BAID1
     C                     MOVELBID2      BAID2
     C                     MOVELBNAM      BABNAM
     C                     MOVELNAME      BANAME
     C                     MOVELAREA      BAAREA
     C                     MOVELRESV1     BARESV
     C                     MOVE RESV2     BARESV
     C                     WRITEBAREC
     C***
     CSR         WR#02Z    ENDSR
     *****
     CSR         UP#02     BEGSR
     C                     MOVELBID1      BAID1
     C                     MOVELBID2      BAID2
     C           BAKEY     CHAINBANKID               97
     C  N97                MOVELBNAM      BABNAM
     C  N97                MOVELNAME      BANAME
     C  N97                MOVELAREA      BAAREA
     C  N97                MOVELRESV1     BARESV
     C  N97                MOVE RESV2     BARESV
     C  N97                UPDATBAREC
     C***
     CSR         UP#02Z    ENDSR
** ERR
功能代碼必須輸入．
功能代碼必須為１，２，３，４，
總行別必須輸入
此銀行代號已存在
此銀行代號不存在
