     H        1   Y                                     1                 BR003
     FCURCPR  IP  E                    DISK
     FQPRINT  O   F      80            PRINTER
     I*************************************************************
     ICUREC
     I                                              CUAREAL3
     I                                              CURVIDL2
     I                                              CUCUNOL1
     I****
     IAADS        DS
     I              'も客戶請領款提示表-C         CON3                       X
     I              'も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     I           UDS
     I                                        1   2 RVID
     I                                        3   3 FLAG
     I                                        5  100YDATE
     I                                       13  13 AREA
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L3 90             EXSR DL#L3
     C   L2 90N91          EXSR DL#L2
     C   L1 90N91          EXSR DL#L1
     C      90N91          EXSR DP#01
     CL1    90N91          EXSR TL#L1
     CL2    90N91          EXSR TL#L2
     CL3    90N91          EXSR TL#L3
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    20
     C                     Z-ADD55        TCNT    20
     C                     Z-ADD0         CNT
     C                     MOVE *ALL'='   ALS    80
     C                     MOVE *ALL'-'   ALQ    50
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L3                             **
     C**----------------------------------------------**
     CSR         DL#L3     BEGSR
     C                     Z-ADD0         AMTT3   90
     C                     SETOF                     91
     C           AREA      IFNE ' '
     C           CUAREA    ANDNEAREA
     C                     SETON                     91
     C                     ENDIF
     C  N91                SELEC
     C           CUAREA    WHEQ 'P'
     C                     MOVEL'台北'  AREAC   6
     C           CUAREA    WHEQ 'T'
     C                     MOVEL'桃園'  AREAC
     C           CUAREA    WHEQ 'K'
     C                     MOVEL'高雄'  AREAC
     C           CUAREA    WHEQ 'H'
     C                     MOVEL'苗栗'  AREAC
     C           CUAREA    WHEQ 'M'
     C                     MOVEL'台中'  AREAC
     C                     OTHER
     C                     MOVEL'　　'  AREAC
     C                     ENDSL
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         AMTTT   90
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         AMTT    90
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     ADD  CURAMT    AMTT
     C                     ADD  CURAMT    AMTTT
     C                     ADD  CULAMT    AMTT
     C                     ADD  CULAMT    AMTTT
     C                     ADD  CURAMT    AMTT3
     C                     ADD  CULAMT    AMTT3
     C                     Z-ADDCURAMT    AMTP    90
     C                     Z-ADDCULAMT    AMTC    90
     C           CUFL01    IFEQ 'Y'
     C                     MOVEL'ˇ'    FL01    4
     C                     ELSE
     C                     MOVEL'　'    FL01
     C                     ENDIF
     C***
     C           CUFL02    IFEQ 'Y'
     C                     MOVEL'ˇ'    FL02    4
     C                     ELSE
     C                     MOVEL'　'    FL02
     C                     ENDIF
     C***
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD10        LCNT
     C                     EXCPTED#01
     C                     ADD  1         LCNT
     C                     ADD  1         CNT     30
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C                     EXCPTET#L1
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C                     EXCPTET#L2
     C                     ADD  2         LCNT
     CSR         TL#L2Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L3 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L3     BEGSR
     C                     EXCPTET#L3
     C                     Z-ADD80        LCNT
     CSR         TL#L3Z    ENDSR
     C******
     OQPRINT  E   02           EH#01
     O                                   17 '報表編號: AR031'
     O                         CON4      55                                    X
     O                                   71 '製表日期'
     O                                   72 ':'
     O                         UDATE Y   80
     O        E 1              EH#01
     O                         CON3      53                                    X
     O                                   71 '頁　　次'
     O                                   72 ':'
     O                         PAG   Z   75
     O        E 1              EH#01
     O                                    9 '地區別:'
     O                         CUAREA  +  1
     O                         AREAC   +  1
     O                                 +  2 '請領款日期：'
     O                         YDATE Y +  1
     O        E 1              EH#01
     O                         ALS       80
     O        E 1              EH#01
     O                                    6 '業務'
     O                                   14 '客　戶'
     O                                   24 '客戶名稱'
     O                                   30 '訂單'
     O                                   42 '上月未收款'
     O                                   55 '本月未收款'
     O                                   68 '客戶小計'
     O                                   74 '請款'
     O                                   80 '領款'
     O        E 1              EH#01
     O                         ALS       80
     O        E 1              ED#01
     O                 L2      CURVID     4
     O                 75      CURVID     4
     O                 L1      CUCUNO    13
     O                 75      CUCUNO    13
     O                 L1      CUCUNM    24
     O                 75      CUCUNM    24
     O                         CUORNO    30
     O                         AMTP  J   42
     O                         AMTC  J   55
     O                         FL01      72
     O                         FL02      78
     O        E 00             ET#L1
     O                         AMTT  J   68
     O        E 1              ET#L1
     O                         ALQ       80
     O*****
     O        E 1              ET#L2
     O                                   54 '業務員合計：'
     O                         AMTTT J   68
     O        E 1              ET#L2
     O                         ALQ       80
     O*****
     O        E 1              ET#L3
     O                                   54 '課別合計：'
     O                         AMTT3 J   68
     O        E 1              ET#L3
     O                         ALQ       80
     O*****
