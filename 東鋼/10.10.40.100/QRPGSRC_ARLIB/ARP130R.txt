     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP130R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION     空白發票上送電子發票介面檔
     H*            5.DATE-WRITTEN  107/03/28  2017AR1112
     H*            6.UPDATE  DATE  107/06/19  2018AR1162 S00WCJ (0706A)
     H*                            電子發票介面檔增加處理代號及廠區
     H*                            108/05/09  S00WCJ (0805A)
     H*                            增加寫入電子發票介面LOG異動資訊
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FINENBU  UF  E           K        DISK                      A
     FINENBUG O   E           K        DISK
     F            RINENBU                           KRENAMEINENUG
     FGENSEQ  IF  E           K        DISK
     F*INENRB  IF  E           K        DISK
     F*****************************************************************
     E                    HD      1   3 10
     I            DS
     I                                        1   80W#DATE
     I                                        1   60D#YYMM
     I                                        1   40D#YY
     I                                        5   60D#MM
     I            DS
     I                                        1   60W#YYMM
     I                                        5   60D#WMM
     I            DS
     I                                        1  10 GRP
     I                                        1   1 GE1
     I                                        2   7 GE2
     I                                        8  10 GE3
     I           UDS
     I                                        1   6 D#YMD
     I                                       11  12 D#CHYN
     I                                     10011010 D#USER
     I                                     10211021 U#AREA
     I*
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C*
     C           GEKEY     KLIST
     C                     KFLD           GEKIND
     C                     KFLD           GEPRIN
     C*
     C           KEY01     KLIST
     C                     KFLD           W#AREA  1
     C                     KFLD           W#YYMM
     C*
     C           KEY02     KLIST
     C                     KFLD           W#AREA
     C                     KFLD           W#YYMM
     C                     KFLD           GEKIND
     C*
     C           *DATE     SUB  19000000  U#SYSD  80
     C                     Z-ADDU#SYSD    W#DATE  80
     C*
     C           D#YMD     IFEQ *BLANKS
     C           D#MM      IFNE 1
     C                     SUB  1         D#MM
     C                     ELSE
     C                     SUB  1         D#YY
     C                     Z-ADD12        D#MM
     C                     ENDIF
     C                     Z-ADDD#YYMM    W#YYMM  60
     C                     ELSE
     C                     MOVELD#YMD     W#YYMM
     C                     ENDIF
     C*
     C                     MOVELU#AREA    W#AREA
     C                     MOVEL'01'      GEKIND
     C                     MOVEL*BLANKS   GEPRIN
     C                     MOVELU#AREA    GE1
     C                     MOVELW#YYMM    GE2
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ              N95
     C  N95                EXSR SR1000
     C                     MOVEL'02'      GEKIND
     C           GEKEY     CHAINGENSEQ              N95
     C  N95                EXSR SR1000
     C           U#AREA    IFEQ 'P'
     C                     MOVEL'M'       W#AREA
     C                     MOVEL'01'      GEKIND
     C                     MOVEL*BLANKS   GEPRIN
     C                     MOVELW#AREA    GE1
     C                     MOVELW#YYMM    GE2
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ              N95
     C  N95                EXSR SR1000
     C                     MOVEL'02'      GEKIND
     C           GEKEY     CHAINGENSEQ              N95
     C  N95                EXSR SR1000
     C                     ENDIF
     C*
     C                     SETON                     LR
     C                     RETRN
     C****************************************************************
     C           SR1000    BEGSR
     C****************************************************************
     C*
     C           KEY02     CHAINRINENBU              68
0706AC* N68                GOTO END200
0706AC   68                CLEARRINENBU
0706AC   68                MOVEL'A'       NUFLAG
0706AC  N68                MOVEL'C'       NUFLAG
     C                     MOVEL'E0402'   NUMSTE           訊息代碼
     C                     SELEC
     C           W#AREA    WHEQ 'P'
     C           W#AREA    OREQ 'U'
     C           W#AREA    OREQ 'M'
     C                     MOVELHD,1      NUHEBN           總公司統編
     C                     MOVELHD,1      NUBRBN           分支機構統編
     C           W#AREA    WHEQ 'H'
     C                     MOVELHD,1      NUHEBN
     C                     MOVELHD,2      NUBRBN
     C           W#AREA    WHEQ 'K'
     C                     MOVELHD,1      NUHEBN
     C                     MOVELHD,3      NUBRBN
     C                     ENDSL
     C                     MOVEL'07'      NUINTE           發票類別
     C*判斷單數期則+1
     C           D#WMM     DIV  2         W#1     20
     C                     MVR            W#2     20       取餘數
     C           W#2       IFEQ 0
     C                     MOVE W#YYMM    NUYEMH           發票期別
     C                     ELSE
     C                     ADD  1         D#WMM
     C                     MOVE W#YYMM    NUYEMH
     C                     SUB  1         D#WMM            改回原來的值
     C                     ENDIF
     C                     MOVELGEPRE     NUINTK           發票字軌
     C           GECUNO    ADD  1         W#GENO  80
     C                     MOVELW#GENO    NUINBN           空白發票起
     C                     MOVELGEENNO    NUINEN           空白發票迄
     C                     MOVELW#AREA    NUFACT           廠區
     C                     Z-ADDW#YYMM    NUYYMM           使用年月
     C                     MOVELGEKIND    NUKIND           編碼種類
     C*
     C                     Z-ADDU#SYSD    NUXPDD           上送日期
     C                     TIME           NUXPDT           上送時間
     C                     MOVELD#USER    NUXUSR           上送人員
0706AC   68                WRITERINENBU
0706AC  N68                UPDATRINENBU
0805AC                     MOVEL'ARP130R' NUAPNM
0805AC                     Z-ADDU#SYSD    NUCHDD
0805AC                     TIME           NUCHDT
0805AC                     MOVELD#USER    NUCUSR
     C                     WRITEINENUG
     C*
     C           END200    ENDSR
     C****************************************************************
** HD
75280209
84707108
07989206
