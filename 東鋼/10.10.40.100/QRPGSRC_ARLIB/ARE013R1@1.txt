     H        1   Y                                     1                 BR003
     FARE013S1CF  E                    WORKSTN
     F                                        RR1   KSFILE SF1
     F                                        RR2   KSFILE SF2
     F                                        RR3   KSFILE SF3
     FINVMST  UF  E           K        DISK
     FINVMSTL7IF  E           K        DISK
     F            INREC                             KRENAMEINREC1
     FINVDTLL1UF  E           K        DISK                      A
     F            IVREC                             KRENAMEIVREC1
     FRCVDTLL4UF  E           K        DISK
     FRCVDTLL5UF  E           K        DISK
     F            RVREC                             KRENAMERVREC5
     FRCADTL  UF  E           K        DISK                      A
     FCBCUST  IF  E           K        DISK
     FGENSEQ  UF  E           K        DISK                      A
     E*************************************************************
     E                    ARRO       99 10
     E                    R@         36  6
     E                    ERR     1  39 70
     I*************************************************************
     IAADS        DS
     I                                        1   5 W1CUNO
     I                                        1   1 S1KIND
     I                                        2   2 S1CUN1
     I                                        3   5 S1CUN2
9008 I                                       11  16 ORNO1
 .   I                                       11  11 ORNO11
 .   I                                       12  160ORNO12
     I                                       21  30 SBID
     I                                       21  23 SBID1
     I                                       24  27 SBID2
     I                                       28  30 SBID3
     I                                       31  40 SANO
     I                                       31  31 SANO1
     I                                       32  32 SANO2
     I                                       33  40 SANO3
     IBBDS        DS
     I                                        1  10 GRP
     I                                        1   1 GE1
     I                                        2  10 GE2
     I                                       11  16 NOG
     I                                       11  11 NOA
     I                                       12  160NOB
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           GEKEY     KLIST
     C                     KFLD           GEKIND
     C                     KFLD           GEPRIN
     C           INKEY     KLIST
     C                     KFLD           INNO
     C           K#KEY1    KLIST
     C                     KFLD           CUN
     C                     KFLD           RNO
     C           IVKEY     KLIST
     C                     KFLD           IVACNT
     C                     KFLD           IVAPNO
     C                     KFLD           IVNO
     C                     KFLD           IVITEM
     C           IVTKY     KLIST
     C                     KFLD           IVACNT
     C                     KFLD           IVAPNO
     C           IVT1KY    KLIST
     C                     KFLD           IVACNT
     C                     KFLD           IVAPNO
     C                     KFLD           IVNO
     C           CBKEY     KLIST
     C                     KFLD           CBCUNO
     C           RV4KY     KLIST
     C                     KFLD           RVWRNO
     C                     KFLD           RVNTNO
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C           RV5KY     KLIST
     C                     KFLD           RVNTNO
     C                     KFLD           RVCUNO
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C           RV5TKY    KLIST
     C                     KFLD           RVNTNO
     C                     KFLD           RVCUNO
     C           RAKEY     KLIST
     C                     KFLD           RANO
     C                     KFLD           RAITEM
     C           RATKY     KLIST
     C                     KFLD           RANO
     C**************************************************************
     C  N90                MOVE '1'       SCRN    1
     C  N90                Z-ADD36        R#L1    30
     C  N90                Z-ADD10        R#L2    30
     C  N90                Z-ADD99        R#L3    30
     C  N90                MOVE '1'       *IN,90
     C**************************************************************
     C           *INKC     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C*****----------------------*****
     C***** SCREEN 01 MAIN LOGIC *****
     C*****----------------------*****
     CSR         SR#01     BEGSR
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C                     EXSR CK#01
     C  N99                EXSR PR#02
     C  N99                MOVE '2'       SCRN
     CSR         SR#01Z    ENDSR
     C*****----------------------*****
     C***** SCREEN 02 MAIN LOGIC *****
     C*****----------------------*****
     CSR         SR#02     BEGSR
     C                     WRITEUND03
     C                     WRITESFCTL3
     C                     WRITESFCTL2
     C                     WRITEUND01
     C                     EXFMTSFCTL1
     C                     READ SFCTL2                   31
     C                     READ SFCTL3                   31
     C   KC                EXSR KC#01
     C   KC                GOTO SR#02Z
     C   KL                MOVE *BLANK    ERRMSG
     C   KL                MOVE '1'       SCRN
     C   KL                GOTO SR#02Z
     C   KJ                EXSR KJ#02
     C   KJ                GOTO SR#02Z
     C           ACT       IFEQ '1'
     C           ACT       OREQ '2'
     C                     EXSR CK#02
     C  N99                EXSR CK#021
     C  N99                EXSR CK#022
     C                     ENDIF
     CSR         SR#02Z    ENDSR
     C*****---------------------*****
     C*****                     *****
     C*****  PF03  EXIT         *****
     C*****                     *****
     C*****---------------------*****
     CSR         KC#01     BEGSR
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C*****---------------------*****
     C*****                     *****
     C*****  PF10  SAVE         *****
     C*****                     *****
     C*****---------------------*****
     CSR         KJ#02     BEGSR
     C           ACT       IFEQ '1'
     C           ACT       OREQ '2'
     C                     EXSR CK#02
     C  N99                EXSR CK#021
     C  N99                EXSR CK#022
     C                     ENDIF
     C  N99      ACT       IFNE '4'
     C                     EXSR FL#02
     C                     ENDIF
     C  N99                MOVE '1'       SCRN
     CSR         KJ#02Z    ENDSR
     C*****-----------------------*****
     C*****   SCREEN 01 CHECK     *****
     C*****-----------------------*****
     CSR         CK#01     BEGSR
     C                     SETOF                     414299
     C                     SETOF                     6061
     C                     MOVE *BLANK    ERRMSG
     C                     EXSR ACTCTL
     C                     SELEC
     C           ACT       WHEQ ' '
     C                     SETON                     4199
     C                     MOVE ERR,1     ERRMSG
     C           ACT       WHEQ '1'
     C                     MOVE '新增'  MOD
     C           ACT       WHEQ '2'
     C                     MOVE '修改'  MOD
     C           ACT       WHEQ '3'
     C                     MOVE '刪除'  MOD
     C           ACT       WHEQ '4'
     C                     MOVE '查詢'  MOD
     C                     OTHER
     C                     SETON                     4199
     C                     MOVE ERR,2     ERRMSG
     C                     ENDSL
     C******
     C           ACT       IFNE '1'
     C           WRNO      ANDEQ*BLANK
     C                     SETON                     4299
     C                     MOVE ERR,3     ERRMSG
     C                     ENDIF
     C**
     C           ACT       IFEQ '1'
     C           WRNO      ANDNE*BLANK
     C                     SETON                     4299
     C                     MOVE ERR,18    ERRMSG
     C                     ENDIF
     C**
     C           ACT       IFEQ '1'
     C           CUN       IFNE *BLANK
     C           RNO       IFEQ *BLANK
     C                     SETON                     6199
     C                     MOVE ERR,32    ERRMSG
     C                     ENDIF
     C                     ENDIF
     C           RNO       IFNE *BLANK
     C           CUN       IFEQ *BLANK
     C                     SETON                     6099
     C                     MOVE ERR,32    ERRMSG
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C**
     C**
     C           *IN99     IFEQ '0'
     C                     MOVELWRNO      RVWRNO
     C                     MOVE *BLANK    RVNTNO
     C                     MOVE *BLANK    RVNO
     C                     MOVE *BLANK    RVITEM
     C           RV4KY     SETLLRCVDTLL4             97
     C  N97      WRNO      READERCVDTLL4                 97
     C                     SELEC
     C           *IN97     WHEQ '0'
     C           ACT       COMP '1'                      31
     C   31                SETON                     4299
     C   31                MOVE ERR,4     ERRMSG
     C           *IN97     WHEQ '1'
     C           ACT       COMP '2'                      31
     C  N31      ACT       COMP '3'                      31
     C  N31      ACT       COMP '4'                      31
     C   31                SETON                     4299
     C   31                MOVE ERR,5     ERRMSG
     C                     ENDSL
     C                     ENDIF
     CSR         CK#01Z    ENDSR
      ********
      ********
      ********
     CSR         ACTCTL    BEGSR
     C                     SETOF                     7071
     C                     SELEC
     C           ACT       WHEQ '1'
     C                     SETON                     71
     C           ACT       WHEQ '3'
     C                     SETON                     7071
     C           ACT       WHEQ '4'
     C                     SETON                     7071
     C                     ENDSL
     CSR                   ENDSR
     C******
     C*****----------------------*****
     C*****  SCREEN 02 PREPARE   *****
     C*****----------------------*****
     CSR         PR#02     BEGSR
     C**
     C** SCREEN TOP
     C**
     C                     MOVE *BLANK    ERRMSG
     C                     SETON                     80
     C                     WRITESFCTL1
     C                     WRITESFCTL2
     C                     WRITESFCTL3
     C                     SETOF                     80
     C                     MOVEA'000000'  *IN,43
     C                     MOVEA'00000000'*IN,51
     C                     MOVEA'0000000' *IN,59
     C                     MOVE *BLANK    CUNO
     C                     MOVE *BLANK    CUNM
     C                     Z-ADDUDATE     DATE
     C                     Z-ADD0         TTLAMT
     C                     Z-ADD0         TAAMT
     C                     Z-ADD0         RR1     20
     C                     Z-ADD0         RR2     20
     C                     Z-ADD0         RR3     20
     C** SCREEN UPPER
     C           ACT       CASEQ'1'       P2#ADD
     C                     CAS            P2#OTH
     C                     ENDCS
     C** SCREEN MIDDLE
     C           ACT       CASEQ'1'       P4#ADD
     C                     CAS            P4#OTH
     C                     ENDCS
     C** SCREEN LOWER
     C           ACT       CASEQ'1'       P3#ADD
     C                     CAS            P3#OTH
     C                     ENDCS
     CSR         PR#02Z    ENDSR
     C*****------------------------*****
     C***** SCREEN 02 TOP FOR ADD  *****
     C*****------------------------*****
     CSR         P2#ADD    BEGSR
     C                     EXSR @GETNO
     C                     CLEARSF1
     C                     EXSR ACTCTL
     C           RR1       DOWLTR#L1
     C                     ADD  1         RR1
     C                     Z-ADDRR1       SRL1
     C                     WRITESF1
     C                     ENDDO
     C                     SETON                     10
     CSR         P2#ADZ    ENDSR
     C*****-----------------------*****
     C*****  NO. AUTO-CODE        *****
     C*****-----------------------*****
     CSR         @GETNO    BEGSR
     C                     MOVEL'07'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVE *BLANK    GRP
     C                     MOVELTXAR      GE1
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ              N97
     C   97                Z-ADD0         GECUNO
     C                     MOVELTXAR      NOA
     C           GECUNO    ADD  1         NOB
     C                     MOVELNOG       WRNO
      *
     C           CUN       IFNE *BLANK
     C                     MOVE CUN       CUNO
     C                     ENDIF
     CSR                   ENDSR
     C*****------------------------*****
     C*****
     C***** SCREEN 02 PREPARE(U,D,I)****
     C*****    TOP
     C*****------------------------*****
     CSR         P2#OTH    BEGSR
     C                     MOVELWRNO      RVWRNO
     C                     MOVE *BLANK    RVNTNO
     C                     Z-ADD0         RVDUDT
     C                     SETOF                     97
     C           RV4KY     SETLLRCVDTLL4             97
     ****
     C           *IN97     DOWNE'1'
     C           WRNO      READERCVDTLL4                 97
     C   97                LEAVE
     C           RVFLAG    COMP 'D'                      31
     C   31                ITER
     C           RVNTCD    IFEQ '2'
     C                     MOVELRVFLAG    ACD1
     C                     ADD  RVRAMT    TTLAMT
     C                     ELSE
     C                     MOVEL'D'       ACD1
     C                     END
     C                     MOVELRVNTNO    NTNO
     C                     MOVELRVPBID    PBID
     C                     MOVELRVPANO    PANO
     C                     MOVELRVNTTP    NTTP
     C                     MOVELRVUSTP    USTP
     C                     MOVELRVPLAC    PLAC
     C                     MOVELRVRLNO    RLNO
     C                     Z-ADDRVRAMT    RAMT
     C                     MOVELRVDPNO    DPNO
     C                     MOVELRVSANO    SANO
     C                     Z-ADDRVDUDT    DUDT
     C                     MOVELRVAMCD    AMCD
     C                     ADD  1         RR1
     C                     Z-ADDRR1       SRL1
     C                     WRITESF1
     C                     ENDDO
     C****
     C                     MOVELRVCUNO    CUNO
     C                     MOVELCUNO      CBCUNO
     C           CBKEY     CHAINCBCUST               97
     C  N97                MOVELCBCUNM    CUNM
     C   97                MOVE *BLANK    CUNM
     C                     Z-ADDRVNTDT    DATE
      *
     C           ACT       IFEQ '1'
     C           ACT       OREQ '2'
     C           RR1       OREQ 0
     C                     CLEARSF1
     C                     EXSR ACTCTL
     C           RR1       DOWLTR#L1
     C                     ADD  1         RR1
     C                     Z-ADDRR1       SRL1
     C                     WRITESF1
     C                     ENDDO
     C                     ENDIF
     C****
     C                     SETON                     10
     CSR         P2#OTZ    ENDSR
     C******
     C******
     CSR         P3#ADD    BEGSR
     C           RNO       IFEQ *BLANK
     C           CUN       ANDEQ*BLANK
     C                     CLEARSF3
     C                     EXSR ACTCTL
     C           RR3       DOWLTR#L3
     C                     ADD  1         RR3
     C                     Z-ADDRR3       SRL3
     C                     WRITESF3
     C                     ENDDO
     C                     SETON                     10
     C                     ENDIF
      *
     C           RNO       IFNE *BLANK
     C           CUN       ANDNE*BLANK
     C                     SETOF                     97
     C                     Z-ADD0         W#AMT  110
     C           K#KEY1    SETLLINREC1               97
     C           *IN97     DOWNE'1'
     C           K#KEY1    READEINREC1              N    97
     C   97                LEAVE
     C           INFLAG    IFNE 'D'
     C                     Z-SUBINEAMT    W#AMT
     C                     SUB  INFAMT    W#AMT
     C                     ADD  W#AMT     TAAMT
     C                     END
     C                     MOVELINNO      INVNO
     C                     Z-ADDW#AMT     AAMT
     C                     MOVELINORNO    ORNO
     C                     MOVELINFLAG    ACD3
     C                     ADD  1         RR3
     C                     Z-ADDRR3       SRL3
     C                     WRITESF3
     C                     ENDDO
     C                     SETON                     10
     C                     ENDIF
     CSR         P3#ADZ    ENDSR
     C******
     C******
     C******
     CSR         P3#OTH    BEGSR
     C                     MOVEL'7'       IVACNT
     C                     MOVE *BLANK    IVAPNO
     C                     MOVELWRNO      IVAPNO
     C                     MOVE *BLANK    IVNO
     C                     Z-ADD0         IVITEM
     C                     SETOF                     97
     C           IVKEY     SETLLINVDTLL1             97
      ***
     C           *IN97     DOWNE'1'
     C           IVTKY     READEINVDTLL1            N    97
     C   97                LEAVE
     C           IVFLAG    IFNE 'D'
     C                     ADD  IVAMT     TAAMT
     C                     END
     C                     MOVELIVNO      INVNO
     C                     Z-ADDIVAMT     AAMT
     C                     MOVELIVORNO    ORNO
     C                     MOVELIVFLAG    ACD3
     C                     ADD  1         RR3
     C                     Z-ADDRR3       SRL3
     C                     WRITESF3
     C                     ENDDO
     C***
     C           ACT       IFEQ '1'
     C           ACT       OREQ '2'
     C           RR3       OREQ 0
     C                     CLEARSF3
     C                     EXSR ACTCTL
     C           RR3       DOWLTR#L3
     C                     ADD  1         RR3
     C                     Z-ADDRR3       SRL3
     C                     WRITESF3
     C                     ENDDO
     C                     ENDIF
     C                     SETON                     10
     CSR                   ENDSR
     C******
     CSR         P4#ADD    BEGSR
     C                     CLEARSF2
     C                     EXSR ACTCTL
     C           RR2       DOWLTR#L2
     C                     ADD  1         RR2
     C                     Z-ADDRR2       SRL2
     C                     WRITESF2
     C                     ENDDO
     C*                    SETON                     10
     CSR         P4#ADZ    ENDSR
     C******
     C******
     C******
     CSR         P4#OTH    BEGSR
     C                     MOVELWRNO      RANO
     C                     Z-ADD0         RAITEM
     C                     SETOF                     97
     C           RAKEY     SETLLRCADTL               97
      ***
     C           *IN97     DOWNE'1'
     C           RATKY     READERCADTL              N    97
     C   97                LEAVE
     C           RAFLAG    IFEQ 'D'
     C                     ITER
     C                     ENDIF
     C                     MOVELRAACTP    DC
     C                     MOVELRAACNO    GLNO
     C                     MOVELRAWAYC    WAY
     C                     Z-ADDRADUDT    DUE1
     C                     MOVELRARLNO    RLNO
     C                     MOVELRARESV    ACNO1
     C                     MOVELRAMUID    CSNM
     C                     Z-ADDRAAAMT    AMT2
     C                     ADD  1         RR2
     C                     Z-ADDRR2       SRL2
     C                     WRITESF2
     C           RAACTP    IFEQ 'C'
     C                     ADD  RAAAMT    TTLAMT
     C                     ELSE
     C                     SUB  RAAAMT    TTLAMT
     C                     ENDIF
     C                     ENDDO
     C***
     C           ACT       IFEQ '1'
     C           ACT       OREQ '2'
     C           RR2       OREQ 0
     C                     CLEARSF2
     C                     EXSR ACTCTL
     C           RR2       DOWLTR#L2
     C                     ADD  1         RR2
     C                     Z-ADDRR2       SRL2
     C                     WRITESF2
     C                     ENDDO
     C                     ENDIF
     C*                    SETON                     10
     CSR                   ENDSR
     C*****----------------------*****
     C***** SCREEN 02 CHECK      *****
     C*****----------------------*****
     CSR         CK#02     BEGSR
     C                     SETOF                     99
     C                     MOVEA'000000'  *IN,43
     C                     MOVEA*BLANK    R@
     C                     MOVE *BLANK    ERRMSG
     C                     MOVE *BLANK    CUNM
     C                     Z-ADD0         TTLAMT
     C***
     C           CUNO      IFNE *BLANK
     C                     MOVELCUNO      CBCUNO
     C           CBKEY     CHAINCBCUST               97
     C  N97                MOVELCBCUNM    CUNM
     C   97                MOVE ERR,8     ERRMSG
     C   97                SETON                     9944
     C   97                GOTO CK#02Z
     C                     ENDIF
     C***
     C                     SELEC
     C           DATE      WHEQ 0
     C                     SETON                     9945
     C                     MOVELERR,13    ERRMSG
     C                     GOTO CK#02Z
     C                     OTHER
     C*                    CALL 'C01'
     C*                    PARM           DATE    60
     C*                    PARM ' '       FLAG1   1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DATE      P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           FLAG1   1
     C           FLAG1     IFNE '0'
     C                     SETON                     9945
     C                     MOVELERR,14    ERRMSG
     C                     GOTO CK#02Z
     C                     ENDIF
     C                     ENDSL
      **
     C                     Z-ADD0         RR1
     C           RR1       DOWLTR#L1
     C                     ADD  1         RR1
     C           RR1       CHAINSF1                  97
     C   97                LEAVE
     C                     MOVEA'00000000'*IN,51
     C                     MOVEA'0000000' *IN,59
     C***
     C           ACD1      IFEQ 'D'
     C                     UPDATSF1
     C                     ITER
     C                     ENDIF
     C***
     C           NTNO      IFEQ *BLANK
     C                     CLEARSF1
     C                     EXSR ACTCTL
     C                     Z-ADDRR1       SRL1
     C                     UPDATSF1
     C                     ITER
     C                     END
      *
     C           ACD1      IFEQ *BLANK
     C                     MOVEL'C'       ACD1
     C                     ENDIF
      *
     C                     MOVELNTNO      RVNTNO
     C                     MOVELCUNO      RVCUNO
     C                     MOVE *BLANK    RVNO
     C                     MOVE *BLANK    RVITEM
     C           RV5KY     SETLLRCVDTLL5             31
     C           CK#02A    TAG
     C  N31      NTNO      READERCVDTLL5            N    31
     C   31                SETON                     5899
     C   31                MOVELERR,21    ERRMSG
     C   31                UPDATSF1
     C   31                ITER
     C           RVFLAG    COMP 'D'                      31
     C   31                GOTO CK#02A
     C           CUNO      IFNE RVCUNO
     C                     SETON                     5899
     C                     MOVELERR,26    ERRMSG
     C                     UPDATSF1
     C                     ITER
     C                     ENDIF
      ***
     C*                    MOVELRVNO      TXARW   1
     C                     MOVELRVAREA    TXARW   1
     C           TXAR      COMP TXARW                3131
     C   31                SETON                     5899
     C   31                MOVELERR,25    ERRMSG
     C   31                UPDATSF1
     C   31                ITER
      ***
     C           RVWRNO    COMP *BLANK               3131
     C   31      RVWRNO    COMP WRNO                 3131
     C   31                SETON                     5899
     C   31                MOVELERR,24    ERRMSG
     C   31                UPDATSF1
     C   31                ITER
     C***
     C                     MOVELRVNO      R@,RR1
     C***
     C                     ADD  RVRAMT    TTLAMT
     C                     MOVELRVPBID    PBID
     C                     MOVELRVPANO    PANO
     C                     MOVELRVNTTP    NTTP
     C                     MOVELRVUSTP    USTP
     C                     MOVELRVPLAC    PLAC
     C                     MOVELRVRLNO    RLNO
     C                     Z-ADDRVRAMT    RAMT
     C                     MOVELRVDPNO    DPNO
     C                     MOVELRVSANO    SANO
     C                     Z-ADDRVDUDT    DUDT
     C                     MOVELRVAMCD    AMCD
     C***
     C           RVACNO    IFNE '1131'
     C           RVACNO    ANDNE'1544'
     C                     SETON                     5899
     C                     MOVE ERR,22    ERRMSG
     C                     END
     C                     UPDATSF1
     C***
     C                     ENDDO
     C***
     C* N99      TTLAMT    IFEQ 0
     C*                    SETON                     99
     C*                    MOVELERR,16    ERRMSG
     C*                    ENDIF
     C***
     CSR         CK#02Z    ENDSR
     C******
     C*****----------------------*****
     C***** SCREEN 02 CHECK      *****
     C*****----------------------*****
     CSR         CK#021    BEGSR
     C                     SETOF                     99
     C                     MOVEA'000000'  *IN,21
     C                     MOVE *BLANK    ERRMSG
     C***
     C                     Z-ADD0         RR2
     C           RR2       DOWLTR#L2
     C                     ADD  1         RR2
     C           RR2       CHAINSF2                  97
     C   97                LEAVE
     C                     MOVEA'000000'  *IN,21
     C***
     C           DC        IFEQ *BLANK
     C                     CLEARSF1
     C                     EXSR ACTCTL
     C                     Z-ADDRR2       SRL2
     C                     UPDATSF2
     C                     ITER
     C                     END
      *
     C           DC        IFNE 'D'
     C           DC        ANDNE'C'
     C                     SETON                     9921
     C                     MOVELERR,35    ERRMSG
     C                     ENDIF
     **
     C           GLNO      IFEQ *BLANK
     C                     SETON                     9922
     C                     MOVELERR,36    ERRMSG
     C                     ENDIF
     **
     C           WAY       IFEQ *BLANK
     C                     SETON                     9923
     C                     MOVELERR,37    ERRMSG
     C                     ENDIF
     C           DUE1      IFNE 0
     C*                    CALL 'C01'
     C*                    PARM           DUE1
     C*                    PARM ' '       FLAG1   1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DUE1      P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE
     C                     PARM '1'       P#MODE
     C                     PARM           P#MTL
     C                     PARM           P#LY
     C                     PARM           FLAG1
     C           FLAG1     IFNE '0'
     C                     SETON                     9925
     C                     MOVELERR,38    ERRMSG
     C                     ENDIF
     C                     ENDIF
     **
     C           AMT2      IFEQ 0
     C                     SETON                     9926
     C                     MOVELERR,39    ERRMSG
     C                     ENDIF
     C           DC        IFEQ 'C'
     C                     ADD  AMT2      TTLAMT
     C                     ELSE
     C                     SUB  AMT2      TTLAMT
     C                     ENDIF
     C                     ENDDO
      *
     C* N99      TTLAMT    IFEQ 0
     C*                    SETON                     99
     C*                    MOVELERR,16    ERRMSG
     C*                    ENDIF
     C***
     CSR         CK#02S    ENDSR
     C******
     C******
     CSR         CK#022    BEGSR
     C                     CLEARARRO
     C                     SETOF                     996667
     C                     Z-ADD0         TAAMT
     C                     MOVE *BLANK    ERRMSG
     C***
     C                     Z-ADD0         RR3
     C           RR3       DOWLER#L3
     C                     ADD  1         RR3
     C           RR3       CHAINSF3                  97
     C   97                LEAVE
     C                     MOVEA'00'      *IN,66
     C           ACD3      IFEQ 'D'
     C                     UPDATSF3
     C                     ITER
     C                     ENDIF
      *
     C           INVNO     IFEQ *BLANK
     C                     CLEARSF3
     C                     EXSR ACTCTL
     C                     Z-ADDRR3       SRL3
     C                     UPDATSF3
     C                     ITER
     C                     ENDIF
      *
     C           ACD3      IFEQ *BLANK
     C                     MOVEL'C'       ACD3
     C                     ENDIF
      *
     C                     MOVELINVNO     INNO
     C           INKEY     CHAININVMST              N97
     C   97                SETON                     9966
     C   97                MOVELERR,9     ERRMSG
     C   97                UPDATSF3
     C   97                ITER
     C  N97      IVDECD    COMP *BLANK               3131
     C  N97 31             SETON                     9966
     C  N97 31             MOVELERR,7     ERRMSG
     C  N97 31             UPDATSF3
     C  N97 31             ITER
     C                     MOVELINORNO    ORNO
      *
      * CHECK發票之繳款單號須存在於票據之繳款單號
      *
     C*                    SETON                     32
     C*                    Z-ADD0         I       30
     C*          I         DOWLTR#L1
     C*                    ADD  1         I
     C*          R@,I      IFEQ *BLANK
     C*                    ITER
     C*                    ENDIF
     C*                    MOVEL'6'       IVACNT
     C*                    MOVE *BLANK    IVAPNO
     C*                    MOVELR@,I      IVAPNO
     C*                    MOVELINVNO     IVNO
     C*                    Z-ADD0         IVITEM
     C*                    SETOF                     31
     C*          IVKEY     SETLLINVDTLL1             31
      *
     C*          *IN31     DOWNE'1'
     C*          IVT1KY    READEINVDTLL1            N    31
     C*  31                LEAVE
     C*          IVDECD    IFNE *BLANK
     C*          IVFLAG    ORNE *BLANK
     C*                    ITER
     C*                    ENDIF
     C*                    SETOF                     32
     C*                    ENDDO
      *
     C*                    ENDDO
     C*  32                SETON                     9966
     C*  32                MOVELERR,23    ERRMSG
      ***********************
     C           *LIKE     DEFN INEAMT    WWX
     C                     Z-SUBINEAMT    WWX
     C                     SUB  INFAMT    WWX
     C           AAMT      IFEQ 0
     C                     Z-ADDWWX       AAMT
     C                     ENDIF
      *
     C           AAMT      IFGT WWX
     C                     SETON                     9966
     C                     MOVELERR,19    ERRMSG
     C                     ENDIF
      *
     C           INCUNO    IFNE CUNO
     C                     SETON                     9966
     C                     MOVELERR,20    ERRMSG
     C                     ENDIF
     C                     ADD  AAMT      TAAMT
     C           INVNO     LOKUPARRO                     97
     C      97             SETON                     9966
     C      97             MOVELERR,12    ERRMSG
     C     N97             Z-ADDRR3       Y       20
     C     N97             MOVELINVNO     ARRO,Y
     C                     UPDATSF3
     C                     ENDDO
     C  N99      TAAMT     IFNE TTLAMT
     C                     SETON                     99
     C                     MOVELERR,10    ERRMSG
     C                     ENDIF
     CSR         CK#02R    ENDSR
     C******
     C******
     CSR         FL#02     BEGSR
     C           ACT       CASEQ'3'       DL#02
     C           ACT       CASEQ'2'       UP#02
     C           ACT       CASEQ'1'       WR#02
     C                     ENDCS
     CSR         FL#02Z    ENDSR
      *****
      *****
      *****
     CSR         WR#02     BEGSR
     C                     CLEARGEREC
     C                     MOVEL'07'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVE *BLANK    GRP
     C                     MOVELTXAR      GE1
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ               97
     C   97                Z-ADD1         GECUNO
     C   97                WRITEGEREC
     C  N97                ADD  1         GECUNO
     C  N97                UPDATGEREC
     C                     MOVELTXAR      NOA
     C                     Z-ADDGECUNO    NOB
     C                     MOVELNOG       WRNO
      ***
     C                     EXSR DL#RV
     C                     EXSR DL#RA
     C                     EXSR DL#IV
     C                     EXSR UP#RV
     C                     EXSR UP#RA
     C                     EXSR UP#IV
     CSR         WR#02Z    ENDSR
     C***
     C***
     C***
     CSR         DL#02     BEGSR
     C                     EXSR DL#RV
     C                     EXSR DL#RA
     C                     EXSR DL#IV
     CSR         DL#02Z    ENDSR
     C***
     C***
     C***
     CSR         UP#02     BEGSR
     C                     EXSR DL#RV
     C                     EXSR DL#RA
     C                     EXSR DL#IV
     C                     EXSR UP#RV
     C                     EXSR UP#RA
     C                     EXSR UP#IV
     CSR         UP#02Z    ENDSR
     C***
     C***
     C***
     CSR         UP#RV     BEGSR
     C                     Z-ADD0         RR1
     C                     MOVEL*BLANK    DEPT    4
     C                     SETOF                     97
     C           *IN97     DOWEQ'0'
     C                     ADD  1         RR1
     C           RR1       CHAINSF1                  97
     C   97                LEAVE
      *
     C           NTNO      IFEQ *BLANK
     C           ACD1      OREQ 'D'
     C                     ITER
     C                     ENDIF
      *
     C                     MOVELNTNO      RVNTNO
     C                     MOVELCUNO      RVCUNO
     C                     MOVE *BLANK    RVNO
     C                     MOVE *BLANK    RVITEM
     C           RV5KY     SETLLRCVDTLL5             31
     C  N31      RV5TKY    READERCVDTLL5                 31
     C   31                ITER
     C                     MOVELRVDPNO    DEPT    4
     C                     MOVEL'C'       RVFLAG
     C                     MOVEL'2'       RVNTCD
     C                     Z-ADDDATE      RVNTDT
     C                     MOVELWRNO      RVWRNO
     C                     MOVELTXAR      RVTXAR
     C                     Z-ADDUDATE     RVTXDT
     C                     UPDATRVREC5
     C                     ENDDO
     CSR         UP#RVZ    ENDSR
     C*****
     C*****
     C*****
     CSR         UP#RA     BEGSR
     C                     Z-ADD0         RR2
     C                     SETOF                     97
     C           *IN97     DOWEQ'0'
     C                     ADD  1         RR2
     C           RR2       CHAINSF2                  97
     C   97                LEAVE
      *
     C           DC        IFEQ *BLANK
     C                     ITER
     C                     ENDIF
      *
     C                     MOVELWRNO      RANO
     C                     Z-ADDRR2       RAITEM
     C           RAKEY     CHAINRCADTL               97
      *
     C                     CLEARRAREC
     C                     MOVEL'A'       RAFLAG
     C                     MOVELWRNO      RANO
     C                     Z-ADDRR2       RAITEM
     C                     MOVELDC        RAACTP
     C                     MOVELGLNO      RAACNO
     C                     MOVELWAY       RAWAYC
     C                     MOVELDEPT      RADEPT
     C                     MOVELRLNO      RARLNO
     C                     MOVELCSNM      RAMUID
     C                     Z-ADDDUE1      RADUDT
     C                     Z-ADDAMT2      RAAAMT
     C                     MOVELTXAR      RATXAR
     C                     Z-ADDUDATE     RATXDT
     C   97                WRITERAREC
     C  N97                UPDATRAREC
     C                     ENDDO
     CSR         UP#RAZ    ENDSR
     C*****
     C*****
     C*****
     CSR         UP#IV     BEGSR
     C                     Z-ADD0         RR3
     C                     SETOF                     97
     C           *IN97     DOWEQ'0'
     C                     ADD  1         RR3
     C           RR3       CHAINSF3                  97
     C   97                LEAVE
      *
     C           INVNO     IFEQ *BLANK
     C           ACD3      OREQ 'D'
     C                     ITER
     C                     ENDIF
      *
     C                     MOVEL'7'       IVACNT
     C                     MOVE *BLANK    IVAPNO
     C                     MOVELWRNO      IVAPNO
     C                     MOVELINVNO     IVNO
     C                     Z-ADD0         IVITEM
     C                     SETOF                     96
     C           IVKEY     SETLLINVDTLL1             96
     C  N96      IVT1KY    READEINVDTLL1                 96
      *
     C   96                CLEARIVREC1
     C   96                MOVEL'7'       IVACNT
     C   96                MOVE *BLANK    IVAPNO
     C   96                MOVELWRNO      IVAPNO
     C   96                MOVELINVNO     IVNO
     C   96                Z-ADD1         IVITEM
     C   96                MOVEL'A'       IVFLAG
     C  N96                MOVE 'C'       IVFLAG
     C                     Z-ADDDATE      IVACDT
     C                     MOVELORNO      IVORNO
     C                     MOVE *BLANK    IVPDCD
     C                     Z-ADD0         IVQTY
     C                     Z-ADD0         IVUPRC
     C                     Z-ADDAAMT      IVAMT
     C                     MOVE *BLANK    IVDECD
     C                     Z-ADD0         IVDEDT
     C   96                MOVE *BLANK    IVACNO
     C                     MOVE *BLANK    IVFL01
     C                     MOVE 'Y'       IVFL02
     C                     MOVE 'J'       IVFL03
     C                     MOVE TXAR      IVTXAR
     C                     Z-ADDUDATE     IVTXDT
     C  N96                UPDATIVREC1
     C                     SETON                     31
     C           *IN96     DOWEQ'1'
     C           *IN31     ANDEQ'1'
     C                     WRITEIVREC1                 31
     C   31                ADD  1         IVITEM
     C                     ENDDO
      ***
     C                     MOVELIVNO      INNO
     C           INNO      CHAININVMST               96
     C   96                ITER
     C                     ADD  IVAMT     INFAMT
     C                     ADD  IVAMT     INNBAL
     C                     MOVEL'C'       INFLAG
     C           INNBAL    COMP 0                        31
     C   31                MOVEL'Y'       INFL01
     C                     MOVELTXAR      INTXAR
     C                     Z-ADDUDATE     INTXDT
     C                     UPDATINREC
     C                     ENDDO
     CSR         UP#IVZ    ENDSR
     C*****----------------------------*****
     C***** DELETE RV DATA (LOGICAL)   *****
     C*****----------------------------*****
     CSR         DL#RV     BEGSR
     C                     MOVELWRNO      RVWRNO
     C                     MOVE *BLANK    RVNTNO
     C                     MOVE *BLANK    RVNO
     C                     MOVE *BLANK    RVITEM
     C                     SETOF                     97
     C           RV4KY     SETLLRCVDTLL4             97
     C           *IN97     DOWNE'1'
     C           WRNO      READERCVDTLL4                 97
     C   97                LEAVE
      *
     C           RVNTCD    IFNE '2'
     C                     ITER
     C                     END
      *
     C                     MOVEL'C'       RVFLAG
     C                     MOVE *BLANK    RVNTCD
     C**                   Z-ADD0         RVNTDT
     C**                   MOVE *BLANK    RVWRNO
     C                     MOVELTXAR      RVTXAR
     C                     Z-ADDUDATE     RVTXDT
     C                     UPDATRVREC
     C                     ENDDO
     CSR         DL#RVZ    ENDSR
     C*****----------------------------*****
     C***** DELETE RA DATA (LOGICAL)   *****
     C*****----------------------------*****
     CSR         DL#RA     BEGSR
     C                     MOVELWRNO      RANO
     C                     Z-ADD0         RAITEM
     C                     SETOF                     97
     C           RAKEY     SETLLRCADTL               97
      ***
     C           *IN97     DOWNE'1'
     C           RATKY     READERCADTL                   97
     C   97                LEAVE
      *
     C           RAFLAG    IFEQ 'D'
     C                     ITER
     C                     END
      *
     C                     MOVEL'D'       RAFLAG
     C                     MOVELTXAR      RATXAR
     C                     Z-ADDUDATE     RATXDT
     C                     UPDATRAREC
      *
     C                     ENDDO
     CSR         DL#RAZ    ENDSR
     C*****----------------------------*****
     C***** DELETE IV DATA (LOGICAL)   *****
     C*****----------------------------*****
     CSR         DL#IV     BEGSR
     C                     MOVEL'7'       IVACNT
     C                     MOVE *BLANK    IVAPNO
     C                     MOVELWRNO      IVAPNO
     C                     MOVE *BLANK    IVNO
     C                     Z-ADD0         IVITEM
     C                     SETOF                     97
     C           IVKEY     SETLLINVDTLL1             97
      ***
     C           *IN97     DOWNE'1'
     C           IVTKY     READEINVDTLL1                 97
     C   97                LEAVE
      *
     C           IVFLAG    IFEQ 'D'
     C                     ITER
     C                     END
      *
     C                     MOVEL'D'       IVFLAG
     C                     MOVELTXAR      IVTXAR
     C                     Z-ADDUDATE     IVTXDT
     C                     MOVEL'Y'       IVFL02
     C                     UPDATIVREC1
      *
     C                     MOVELIVNO      INNO
     C           INNO      CHAININVMST               96
     C   96                ITER
     C                     SUB  IVAMT     INFAMT
     C                     SUB  IVAMT     INNBAL
     C                     MOVE 'C'       INFLAG
     C           INNBAL    COMP 0                        31
     C   31                MOVEL'Y'       INFL01
     C                     MOVELTXAR      INTXAR
     C                     Z-ADDUDATE     INTXDT
     C                     UPDATINREC
      *****
     C                     ENDDO
     CSR         DL#IVZ    ENDSR
      *******************************************************
** ERR
功能代碼必須輸入．
功能代碼必須為１，２，３，４
非新增時，退回單號必須輸入　　　　
退回單號已存在，不可新增
退回單號不存在
繳款單已作廢，不可再處理
此張發票已作廢，不可再處理
客戶編號不存在
發票號碼不存在　　　　
沖銷總金額不等於退回總金額
此張發票已繳完款，不可再沖銷
發票號碼重複
申請日期必須輸入
申請日期錯誤
支票號碼必須輸入
退回金額不可為零
訂單編號不存在
新增時，退回單號不可輸入　　　　
發票收款金額比退回金額還小，不可沖銷
發票之客戶編號不等於繳款客戶編號
票據號碼不存在
此筆資料非為票據資料
發票之繳款單號不存在於票據資料中
此筆票據已退回不可再退回
此筆票據為不同廠區之資料不可處理
票據號碼非為此客戶之繳款資料
借貸方必須為苠疿虳恛苠捑
會計科目必須輸入
沖帳方式必須輸入
到期日期錯誤
金額必須輸入
以繳款單帶出發票時,繳款單及客戶代號不得空白　　　　
