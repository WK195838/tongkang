     H*****************************************************************
     H*
     H*          >>  PROGRAM INTRODUCTION  <<
     H*          1.PROGRAM-ID    ARR118R1
     H*          2.PROGRAM-TYPE  RPG/400
     H*          3.AUTHOR        S02CSF
     H*          4.FUNCTION     應收票據資負同在查核明細表
     H*          5.DATE-WRITTEN  94/04/13
     H*          6.DATE-MODIFY
     H*
     H*
     H*****************************************************************
     H        1   Y                                     1
     FRCVDTL  IP  E           K        DISK
     FRCVDTY  IF  E           K        DISK
     F            RVREC                             KRENAMERVRECY
     FINVDTLL1IF  E           K        DISK
     FINVMST  IF  E           K        DISK
     FARR118F O   E           K        DISK
     F*****************************************************************
     IRVREC
     I                                              RVNO  L1
     IRVRECY
     I              RVFLAG                          F#FLAG
     I              RVNO                            F#NO
     I              RVITEM                          F#ITEM
     I              RVDATE                          F#DATE
     I              RVCUNO                          F#CUNO
     I              RVCUNM                          F#CUNM
     I              RVTYPE                          F#TYPE
     I              RVDPNO                          F#DPNO
     I              RVVNO1                          F#VNO1
     I              RVDECD                          F#DECD
     I              RVDEDT                          F#DEDT
     I              RVUSTP                          F#USTP
     I              RVNTTP                          F#NTTP
     I              RVPBID                          F#PBID
     I              RVPANO                          F#PANO
     I              RVACTP                          F#ACTP
     I              RVACNO                          F#ACNO
     I              RVRLNO                          F#RLNO
     I              RVPLAC                          F#PLAC
     I              RVNTNO                          F#NTNO
     I              RVDUDT                          F#DUDT
     I              RVRAMT                          F#RAMT
     I              RVSBID                          F#SBID
     I              RVSANO                          F#SANO
     I              RVAMCD                          F#AMCD
     I              RVWRNO                          F#WRNO
     I              RVNTCD                          F#NTCD
     I              RVNTDT                          F#NTDT
     I              RVPRCD                          F#PRCD
     I              RVPRDT                          F#PRDT
     I              RVTBID                          F#TBID
     I              RVTANO                          F#TANO
     I              RVVNO2                          F#VNO2
     I              RVTXAR                          F#TXAR
     I              RVTXDT                          F#TXDT
     I              RVAREA                          F#AREA
     I              RVRVID                          F#RVID
     I              RVRESV                          F#RESV
     I              RVOLRV                          F#OLRV
     I              RVOLDT                          F#OLDT
     I              RVRES2                          F#RES2
     I           UDS
     I                                      300 3070U#DUDT
     C*****************************************************************
     C*          KET  LIST
     C*****************************************************************
     C*FILE => INVDTLL1(發票明細檔)
     C           K#IVNO    KLIST
     C                     KFLD           IVACNT
     C                     KFLD           IVAPNO
     C                     KFLD           IVNO
     C                     KFLD           IVITEM
     C*FILE => RCVDTL(繳款明細檔)
     C           K#RCVY    KLIST
     C                     KFLD           F#NO
     C                     KFLD           F#ITEM
     C*****************************************************************
     C*         主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     MOVE *ON       *IN99
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     CLEARRAR118F
     C                     MOVEL*BLANK    W#FLAG  1
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C*TEST
     C           RVNO      IFEQ 'M55399'
     C                     Z-ADD0         W#TMP   10
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C                     MOVELRVNO      F#NO
     C                     Z-ADD0         F#ITEM
     C           K#RCVY    SETLLRVRECY
     C                     MOVE *OFF      *IN40
     C           *IN40     DOWEQ*OFF
     C                     READ RVRECY                   40
     C   40                LEAVE
     C*
     C           F#NO      IFNE RVNO
     C                     LEAVE
     C                     ENDIF
     C*
     C           F#USTP    IFEQ 'A1'
     C                     EXSR SR1000
     C                     ENDIF
     C*
     C           F#USTP    IFEQ 'A2'
     C                     MOVEL'X'       W#FLAG
     C                     ENDIF
     C*
     C                     MOVELF#ACNO    W#ACNO  1
     C           F#ACTP    IFEQ 'C'
     C           W#ACNO    IFEQ '1'
     C           W#ACNO    OREQ '2'
     C                     MOVEL'X'       W#FLAG
     C                     ENDIF
     C                     ENDIF
     C*
     C                     EXSR SR2000
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR1000    BEGSR
     C*****************************************************************
     C*找票據沖預收
     C                     MOVEL'6'       IVACNT
     C                     MOVELF#NO      IVAPNO
     C                     MOVEL*BLANK    IVNO
     C                     Z-ADD0         IVITEM
     C           K#IVNO    SETLLIVREC
     C                     MOVE *OFF      *IN41
     C           *IN41     DOWEQ*OFF
     C                     READ IVREC                    41
     C   41                LEAVE
     C*
     C           IVACNT    IFNE '6'
     C           IVAPNO    ORNE F#NO
     C                     LEAVE
     C                     ENDIF
     C*
     C           IVNO      CHAININREC                42
     C  N42      INTYPE    IFEQ '2'
     C                     MOVEL'X'       W#FLAG
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2000    BEGSR
     C*****************************************************************
     C*最後一筆不為借方或是應該票據沖預收
     C           W#FLAG    IFNE *BLANK
     C                     MOVELF#CUNO    A8CUNO
     C                     MOVELF#CUNM    A8CUNM
     C                     Z-ADDF#DATE    A8RVDT
     C                     MOVELF#NO      A8RVNO
     C                     Z-ADDF#ITEM    A8ITEM
     C                     MOVELF#ACNO    A8ACNO
     C                     MOVELF#ACTP    A8ACTP
     C                     MOVELF#USTP    A8USTP
     C                     MOVELF#VNO1    A8VNO1
     C                     MOVELF#NTNO    A8NTNO
     C                     Z-ADDF#DUDT    A8DUDT
     C                     Z-ADDF#RAMT    A8AMT
     C                     WRITERAR118F
     C                     ENDIF
     C*
     C                     ENDSR
