     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP109R3
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     電子商務--發票作廢下載處理
     H*            5.DATE-WRITTEN  93/08/09
     H*            6.DATE-UPDATE
     H*
     H*****************************************************************
     H        1   Y                                     1
     FINVMST  IP  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FTRNDTL  IF  E           K        DISK
     FTNDLOG  UF  E           K        DISK                      A
     FTNDLOGL1IF  E           K        DISK
     F            RTNDLOG                           KRENAMETNDLGX
     FARCBDW  UF  E           K        DISK
     FARTXDW  UF  E           K        DISK                      A
     IINREC
     I                                              INCUNOL1
     C*****************************************************************
     ITNDLGX
     I              TWIVNO                          TGIVNO
     I              TWCODE                          TGCODE
     I              TWNO                            TGNO
     I              TWITEM                          TGITEM
     C*****************************************************************
     C*          KEY   LIST
     C*****************************************************************
     C*FILE==>ARTXDW(銷貨明細檔下載)
     C           K#TXDW    KLIST
     C                     KFLD           AXVNNO           客戶統編
     C                     KFLD           AXFILE           檔案序號
     C                     KFLD           AXSEQN           資料處理序號
     C*
     C*FILE==>TNDLOG(銷貨下載ＬＯＧ檔)
     C           K#TDLG    KLIST
     C                     KFLD           TWVNNO           客戶統編
     C                     KFLD           TWFILE           檔案序號
     C                     KFLD           TWSEQN           資料處理序號
     C*
     C*FILE==>TRNDTL(銷貨明細檔)
     C           K#TRND    KLIST
     C                     KFLD           TXCODE           單據代碼
     C                     KFLD           TXNO             單據號碼
     C                     KFLD           TXITEM           項次
     C*
     C*****************************************************************
     C*          Main      Program
     C*****************************************************************
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     MOVE *ON       *IN99
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     Z-ADD0         W#TXSQ  50
     C                     MOVEL*BLANK    W#MUID 10
     C*
     C*先判斷該客戶是否要做資料交換
     C*
     C           TXCUNO    CHAINCBREC                40
     C           *IN40     IFEQ *OFF
     C                     MOVELCBMUID    W#MUID
     C           W#MUID    CHAINRARCBDW              41
     C  N41      ADFLE5    IFEQ 'Y'
     C                     ADD  1         ADSEQ5
     C                     UPDATRARCBDW
     C                     ENDIF
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C* 1.先判斷此筆作廢發票所對應的銷貨是否已下載（已存於TNDLOG檔）
     C* 2.若此筆銷貨已下載，則將異動寫入ＬＯＧ檔，並且也寫入銷貨下載檔
     C*
     C  N41      ADFLE5    IFEQ 'Y'                        銷貨下載
     C           INNO      CHAINTNDLGX               42
     C  N42                EXSR TXDSR                      寫入銷貨下載
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C*
     C                     ENDSR
     C*****************************************************************
     C           TXDSR     BEGSR
     C*****************************************************************
     C*寫入銷貨下載檔
     C           INNO      SETLLTNDLGX
     C                     MOVE *OFF      *IN43
     C           *IN43     DOWEQ*OFF
     C                     READ TNDLGX                   43
     C   43                LEAVE
     C*
     C           TGIVNO    IFNE INNO
     C                     LEAVE
     C                     ENDIF
     C*
     C                     MOVELTGCODE    TXCODE
     C                     MOVELTGNO      TXNO
     C                     Z-ADDTGITEM    TXITEM
     C           K#TRND    CHAINTXREC                44
     C           *IN40     IFEQ *OFF
     C*
     C                     ADD  1         W#TXSQ
     C                     MOVELW#MUID    AXVNNO
     C                     Z-ADDADSEQ5    AXFILE
     C                     Z-ADDW#TXSQ    AXSEQN
     C           K#TXDW    CHAINRARTXDW              45
     C                     CLEARRARTXDW
     C                     MOVELTXFLAG    AXFLAG           處理代碼
     C                     MOVELTXCODE    AXCODE           單據代碼
     C                     MOVELTXNO      AXNO             單據號碼
     C                     Z-ADDTXITEM    AXITEM           項次
     C                     MOVELTXACNT    AXACNT           原因別
     C           19110000  ADD  TXDATE    AXDATE           單據日期
     C                     MOVELTXCUNO    AXCUNO           客戶代號
     C                     MOVELTXCUNM    AXCUNM           客戶名稱
     C                     MOVELTXORNO    AXORNO           訂單編號
     C                     MOVELTXIVNO    AXIVNO           發票號碼
     C                     MOVELTXPCNO    AXPCNO           磅單號碼
     C                     MOVELTXSALE    AXSALE           出貨業務
     C                     MOVELTXPDNM    AXPDNM           品名
     C                     Z-ADDTXQTY     AXQTY            數量
     C                     Z-ADDTXUPRC    AXUPRC           單價
     C                     Z-ADDTXAMT     AXAMT            金額
     C                     Z-ADDTXTAX     AXTAX            稅額
     C                     Z-ADDW#TXSQ    AXSEQN           資料處理序號
     C                     Z-ADDADSEQ5    AXFILE           檔案序號
     C                     MOVELW#MUID    AXVNNO           客戶統編
     C   45                WRITERARTXDW
     C*
     C                     EXSR LOGSR
     C                     ENDIF
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           LOGSR     BEGSR
     C*****************************************************************
     C*寫入銷貨ＬＯＧ檔
     C                     MOVELW#MUID    TWVNNO
     C                     Z-ADDADSEQ5    TWFILE
     C                     Z-ADDW#TXSQ    TWSEQN
     C           K#TDLG    CHAINRTNDLOG              46
     C                     CLEARRTNDLOG
     C                     MOVELTXFLAG    TWFLAG           處理代碼
     C                     MOVELTXCODE    TWCODE           單據代碼
     C                     MOVELTXNO      TWNO             單據號碼
     C                     Z-ADDTXITEM    TWITEM           項次
     C                     MOVELTXACNT    TWACNT           原因別
     C           19110000  ADD  TXDATE    TWDATE           單據日期
     C                     MOVELTXCUNO    TWCUNO           客戶代號
     C                     MOVELTXCUNM    TWCUNM           客戶名稱
     C                     MOVELTXORNO    TWORNO           訂單編號
     C                     MOVELTXIVNO    TWIVNO           發票號碼
     C                     MOVELTXPCNO    TWPCNO           磅單號碼
     C                     MOVELTXSALE    TWSALE           出貨業務
     C                     MOVELTXPDNM    TWPDNM           品名
     C                     Z-ADDTXQTY     TWQTY            數量
     C                     Z-ADDTXUPRC    TWUPRC           單價
     C                     Z-ADDTXAMT     TWAMT            金額
     C                     Z-ADDTXTAX     TWTAX            稅額
     C                     Z-ADDW#TXSQ    TWSEQN           資料處理序號
     C                     Z-ADDADSEQ5    TWFILE           檔案序號
     C                     MOVELW#MUID    TWVNNO           客戶統編
     C   46                WRITERTNDLOG
     C*
     C                     ENDSR
