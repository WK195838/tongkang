     H*****************************************************************
     H*
     H*          >>  PROGRAM INTRODUCTION  <<
     H*          1.PROGRAM-ID    ARE053R
     H*          2.PROGRAM-TYPE  RPG/400
     H*          3.AUTHOR        S02CSF
     H*          4.FUNCTION     應收帳款承購人工票據兌現　
     H*          5.DATE-WRITTEN  92/02/11
     H*          6.DATE-MODIFY
     H*
     H*****************************************************************
     H        1   Y                                     1
     FARCVMS  IP  E           K        DISK
     FARCVCK  IF  E           K        DISK
     FARE053F1IF  E           K        DISK
     FARE053F2O   E           K        DISK
     IRARCVMS     01
     C   01                EXSR DTLSR
     C*****************************************************************
     C*          KET  LIST
     C*****************************************************************
     C*FILE==>ARCVCK
     C           W#CVCK    KLIST
     C                     KFLD           AKRCNO
     C                     KFLD           AKRCTM
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     CLEARRAR053F2
     C*
     C                     MOVELASCUNO    W#CUNO  5
     C           W#CUNO    CHAINRAR053F1             50
     C           *IN50     IFEQ *ON
     C                     GOTO DTLEND
     C                     ENDIF
     C*
     C                     MOVELASRCNO    AKRCNO
     C                     Z-ADD0         AKRCTM
     C           W#CVCK    SETLLRARCVCK
     C                     MOVE *OFF      *IN40
     C           *IN40     DOWEQ*OFF
     C                     READ RARCVCK                  40
     C           AKRCNO    IFNE ASRCNO
     C           *IN40     OREQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C           AKNTTP    IFEQ 'J'
     C           AKNTTP    OREQ 'G'
     C           AKNTTP    OREQ ' '
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVELA1CUNO    A2CUNO           客戶代號
     C                     MOVELA1CUNM    A2CUNM           客戶簡稱
     C                     MOVELA1BKID    A2BKID           銀行代號
     C                     MOVELA1BKNM    A2BKNM           銀行簡稱
     C                     Z-ADDAKDUDT    A2DUDT           票據到期日
     C                     Z-ADDASACDT    A2ACDT           繳款日期
     C                     MOVELAKNTNO    A2RLNO           票據號碼
     C                     Z-ADDAKRAMT    A2RAMT           票據金額
     C                     MOVELAKRCNO    A2RCNO           沖銷單號
     C                     Z-ADDAKRCTM    A2RCTM           沖銷項次
     C                     MOVELAKPLAC    A2PLAC           付款地
     C                     MOVELAKPBID    A2BANM           付款銀行
     C                     MOVELAKPANO    A2PANO           付款帳號
     C                     MOVELAKNTCD    A2NTCD           兌現碼
     C*
     C                     WRITERAR053F2
     C                     ENDDO
     C*
     C           DTLEND    ENDSR
     C*****************************************************************
