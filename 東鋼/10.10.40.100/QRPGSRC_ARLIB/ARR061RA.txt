     H****************************************************************
     H*
     H*            UPDATE  DATE  99/09/02 2010AR517 S00WCJ (9909A)
     H*                         102/12/10 S00WCJ (0212A)
     H*                          訂單M01848、M01853排除不印
     H*                         103/01/21 S00WCJ (0301A)
     H*                          修正印表日期
     H*
     H****************************************************************
     H                                                                    PY116
     FWFILE1  IP  F      63            DISK
     FBSYSDT  IF  E           K        DISK
     FQPRINT  O   F     132     OV     PRINTER
     IWFILE1  AA  01
     I                                        1   2 W1SALEL4
     I                                        3  11 W1ORNOL2
     I                                       12  17 W1CUNOL3
     I                                       18  27 W1CUNM
     I                                       28  350W1ACDT
     I                                       28  330ACDT  L1
     I                                    P  36  410W1GAMT
     I                                       42  51 W1NO
     I                                       52  63 W1BK12
     I           UDS
     I                                       50  50 U#TYPE
     I                                      801 8080U#SYSD
     I                                      951 986 COMP
     C*
     C           *ENTRY    PLIST
     C                     PARM           P#HEAD 12
     C*
     C*****************************************************************
9909AC*          *DATE     SUB  19000000  U#SYSD  80
     C   L1                Z-ADD0         AMTL1  110
9010 C           W1NO      CHAINYSREC                40
     C  N40      YSCODE    IFEQ ' '
     C                     MOVE *ON       *IN40
     C                     ENDIF
0212AC                     MOVELW1ORNO    W#ORNO  6
0212AC           W#ORNO    IFEQ 'M01848'
0212AC           W#ORNO    OREQ 'M01853'
0212AC                     MOVE *OFF      *IN40
0212AC                     ENDIF
     C   40                ADD  W1GAMT    AMTL1
     C   40                ADD  W1GAMT    AMTLR  130
9010 C                     Z-ADDW1ACDT    W#ACDT  80
     C   L1      AMTL1     IFEQ 0
     C                     MOVE *OFF      *IN50
     C                     ELSE
     C                     MOVE *ON       *IN50
     C                     ENDIF
     C*                    EXCPTPRT#
     OQPRINT  H  1 4   1P
     O       OR        OV
9010 O                                   14 '編號: ARR061'
     O                         COMP      64
     O                                   80 '頁數:'
     O                         PAGE2 Z +  1
     O        H  2     1P
     O       OR        OV
     O                         P#HEAD    13
     O                                   55 '逾齡應收帳款明細表'
     O                                   80 '印表日期:'
9909AO                         U#SYSD  +  1 '    /  /  '
     O        H  1     1P
     O       OR        OV
     O                                   12 '客戶名稱'
     O                                   27 '訂單編號'
     O                                   44 '業務員代號'
     O                                   59 '出貨日期'
     O                                   74 '發票號碼'
     O                                   91 '應收款金額'
     O        H  1     1P
     O       OR        OV
     O                                   11 '--------'
     O                                   26 '--------'
     O                                   43 '----------'
     O                                   58 '--------'
     O                                   73 '--------'
     O                                   90 '----------'
     O        D  1     01 40
     O                         W1CUNM    12
     O                         W1ORNO    27
     O                         W1SALE    39
     O                         W#ACDT    58 '    /  /  '
     O                         W1NO      73
     O                         W1GAMTJ   91
     O        T  2     L1 50
     O                                   12 '小計:'
     O                         AMTL1 J   91
     O        T  2     LR
     O                                   12 '合計:'
     O                         AMTLR J   91
