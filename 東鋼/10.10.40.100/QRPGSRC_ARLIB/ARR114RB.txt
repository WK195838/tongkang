     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR114RA
     H*            2.PROGRAM-TYPE  RPG
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     折讓單統計表（含銷折／銷退／預收）
     H*                            ***彙總表***
     H*            5.DATE-WRITTEN  93/01/08
     H*            6.DATE-MODIFY
     H*
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR114F IP  E           K        DISK
     FARR114TBO   E             69     PRINTER
     E                    TABA1   1   5  1   TABA2  10
     E*****************************************************************
     IRAR114F
     I                                              A4ACNTL1
     I           UDS
     I                                      300 300 U#AREA
     I                                      301 3060U#YYMM
     I                                      307 307 U#KIND
     I                                     10011010 U#USID
     I*****************************************************************
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     SETON                     6999
     C*
     C                     MOVEL*ALL'='   T#LIN1
     C                     MOVEL*ALL'='   T#LIN2
     C                     MOVEL*ALL'-'   T#LIN3
     C*
     C                     MOVELU#AREA    T#AREA
     C                     Z-ADDU#YYMM    T#YYMM
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR114T1D
     C*
     C           A4ACNT    LOKUPTABA1     TABA2          60
     C   60                MOVE TABA2     T#ACNT
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     ADD  A4AMT     T#AMT            未稅
     C                     ADD  A4TAX     T#TAX            含稅
     C                     ADD  A4TOT     T#TOT            金額
     C                     ADD  1         T#CONT           筆數
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C           T#TOT     IFNE 0
     C   69                WRITEAR114T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR114T1D
     C                     ENDIF
     C*總計
     C                     ADD  T#AMT     T#AMTA           未稅
     C                     ADD  T#TAX     T#TAXA           含稅
     C                     ADD  T#TOT     T#TOTA           金額
     C                     ADD  T#CONT    T#CONA           筆數
     C*
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C           T#TOTA    IFNE 0
     C   69                WRITEAR114T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR114T1A
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C*
     C   69                WRITEAR114T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR114T1M
     C*
     C                     ENDSR
     C*****************************************************************
**  TABA1 TABA2
2銷貨退回
3銷貨折讓
4預收折讓
5扣預沖回
6預折手切
