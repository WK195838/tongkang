     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR150R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION     勤美公司出貨補價差統計表寫入工作檔
     H*            5.DATE-WRITTEN  107/01/26 2018AR1126
     H*            6.UPDATE  DATE
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTL  IP  E           K        DISK
     FWTMASPL2IF  E           K        DISK
     FARR150F1IF  E           K        DISK
     FARR150F O   E           K        DISK
     F*
     ITXREC       01
     I                                              TXNO  L1
     I            DS
     I                                        1   8 D#TXNO
     I                                        1   1 D#TXN1
     I                                        2   3 D#TXN2
     I                                        4   80D#TXN3
     I            DS
     I                                        1   9 TXORNO
     I                                        1   6 D#ORNO
     I            DS
     I                                        1   4 W6STNO
     I                                        4   4 W#STNO
     I           UDS
     I                                     10011010 T#USID
     I                                     10211021 U#AREA
     C*****************************************************************
     C*              MAIN  PROGRAM
     C*****************************************************************
     C           KEY01     KLIST
     C                     KFLD           D#TXN1
     C                     KFLD           D#TXN2
     C                     KFLD           D#TXN3
     C*
     C                     CLEARRAR150F
     C                     MOVELTXCUNO    A1CUNO           客戶編號
     C                     MOVELTXCUNM    A1CUNM           客戶簡稱
     C                     MOVELD#ORNO    A1ORNO           訂單編號
     C                     MOVELTXNO      A1TXNO           磅單編號
     C                     Z-ADDTXITEM    A1ITEM           項次
     C                     Z-ADDTXDATE    A1DATE           單據日期
     C                     Z-ADDTXACDT    A1ACDT           入帳日期
     C                     MOVEL'U'       D#AREA  1
     C                     MOVELTXNO      D#TXNO
     C           D#TXN1    IFEQ 'U'
     C           D#TXN1    OREQ 'D'
     C           D#TXN1    OREQ 'X'
     C           D#TXN1    OREQ 'R'
     C           D#TXN1    OREQ 'J'
     C           D#TXN1    OREQ 'N'
     C           D#TXN1    OREQ 'V'
     C           D#TXN1    OREQ 'Q'
     C*
     C           KEY01     CHAINWTMASPL2            N51
     C           *IN51     IFEQ *OFF
     C                     MOVELW6STNO    A1STNO           庫別
     C                     ELSE
     C                     MOVEL*BLANKS   A1STNO
     C                     ENDIF
     C                     ENDIF
     C                     MOVELTXPDNM    A1PDNM           品名
     C                     Z-ADDTXQTY     A1QTY
     C           A1ORNO    CHAINRAR150F1            N50
     C  N50                Z-ADDA0SAMT    A1SAMT           價差
     C   50                Z-ADD0         A1SAMT
     C           A1QTY     MULT A1SAMT    A1AAMT    H      金額
     C*
     C                     WRITERAR150F
