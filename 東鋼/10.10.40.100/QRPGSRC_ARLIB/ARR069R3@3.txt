     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR069R3
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     訂單別出貨及發票明細表（報表）
     H*            5.DATE-WRITTEN  86/10/26
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR069F IP  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FINVMST  IF  E           K        DISK
     FTRNDTLL2IF  E           K        DISK
     FWTSBMP  IF  E           K        DISK
     FARR069T O   E             66     PRINTER
     F*****************************************************************
8906 E                    ARY1     1000 24
 .   E                    TAB1    1   8  9   TAB1R  40
     E*****************************************************************
     IWFREC
     I                                              WFORNOL2
     I                                              WFDATEL1
     I*---------------------------------------------------------------
     I           UDS
     I*                                       1   6 R#ORNO
     I                                      407 4140R#YMDS
     I                                      415 4220R#YMDE
     I*                                      18  23 R#CUNO
     I*
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     I*
8906 I            DS
 .   I                                        1   9 D#TAB
 .   I                                        1   3 D#TAB1
 .   I                                        4   9 D#TAB2
8906 I            DS
 .   I                                        1   8 D#WTNO
 .   I                                        1   1 D#WTN1
 .   I                                        2   3 D#WTN2
 .   I                                        4   80D#WTN3
8906 I            DS
 .   I                                        1   8 D#WTNX
 .   I                                        1   1 D#WTX1
 .   I                                        2   6 D#WTX2
     I            DS
     I                                        1  24 W#ARY1
     I                                        1   3 D#PDNM
     I                                        5   8 D#SPE1
     I                                        9  160D#QTY
8906 I                                       17  24 D#QULT
9101 I            DS
 .   I                                        1   5 D#FULL
 .   I                                        2   4 D#PART
9101 I            DS
 .   I                                        1   7 D#SPEX
 .   I                                        1   3 D#H
 .   I                                        4   4 D#X
 .   I                                        5   7 D#B
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C   L2                EXSR L2CLR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CL2 99                EXSR L2SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*
     C*********************************
     C*  初始程式
     C*********************************
     CSR         INISR     BEGSR
     C                     Z-ADD0         W#NUM   50
     C           K#SBMP    KLIST
     C                     KFLD           W7AREA
     C                     KFLD           W7VNO1
     C                     KFLD           W7VNO2
     C                     KFLD           W7SQIT
     C*
     CSR                   ENDSR
     C*
     C*********************************
     C*  L2清除函式
     C*********************************
     CSR         L2CLR     BEGSR
     C                     SETON                     66    首頁表頭
     C                     Z-ADD0         W#PN    30       品名種類數
     C                     Z-ADD0         I       30       索引指標
     C                     MOVEA*ALL'9'   ARY1             清陣列
     C*
     C                     Z-ADD0         R#SQTY           清合計值
     C                     Z-ADD0         R#TQTY
     C*
     CSR                   ENDSR
     C*
     C*********************************
     C*  L1清除函式
     C*********************************
     CSR         L1CLR     BEGSR
     C                     Z-ADD0         R#SQTY           清合計值
     CSR                   ENDSR
     C*
     C*********************************
     C*  DETAIL TIME 函式
     C*********************************
     CSR         DTLSR     BEGSR
     C           WFIVNO    IFNE *BLANK
     C                     MOVELWFIVNO    W#IVNO 10        發票號碼
     C                     ENDIF
     C                     CLEARAR069T1D
     C                     Z-ADDWFDATE    R#DATE           日期
     C                     MOVELWFWTNO    D#WTNX           磅單
     C                     MOVE D#WTX2    D#WTN3           磅單
     C                     MOVEL'11'      D#WTN2           磅單
     C                     MOVELD#WTX1    D#WTN1           磅單
     C                     MOVE D#WTNO    R#WTNO
     C                     MOVELWFPDNM    R#PDNM           品名
     C                     MOVELWFQULT    R#QULT           材質
     C*                    MOVELWFSPE1    R#SPE1           徑度
     C*                    MOVE WFWTNO    D#WTNO
     C                     MOVE D#WTN1    W7AREA
     C                     MOVE '11'      W7VNO1
     C                     MOVE D#WTN3    W7VNO2
     C                     Z-ADD0         W7SQIT
     C           K#SBMP    SETLLRWTSBMP
     C                     MOVE *OFF      *IN40
     C           *IN40     DOWEQ*OFF
     C                     READ RWTSBMP                  40
     C   40                LEAVE
     C           W7AREA    IFNE D#WTN1
     C           W7VNO2    ORNE D#WTN3
     C                     LEAVE
     C                     ENDIF
     C           W7NAME    IFEQ WFPDNM
     C           W7STDW    ANDEQWFQTY
     C                     MOVE W7SPE1    D#FULL
     C                     MOVE D#PART    D#H
     C                     MOVE 'X'       D#X
     C                     MOVE W7SPE2    D#FULL
     C                     MOVE D#PART    D#B
     C                     MOVE D#SPEX    R#SPE1
     C                     ENDIF
     C                     ENDDO
     C                     Z-ADDWFQTY     R#QTY            數量
     C                     MOVELWFIVNO    R#IVNO           發票號碼
     C           WFIVNO    CHAININREC               N41
     C                     Z-ADDININDT    R#IVDT
     C                     Z-ADDWFIVAM    R#IVAM           出貨金額
     C*                    Z-ADDWFAR06    R#AR06           折退金額
     C*                    Z-ADDWFPRRV    R#PRRV           預收金額
     C                     Z-ADDWFIVTX    R#IVTX           稅額
     C*                    Z-ADDWFIVAR    R#IVAR           發票餘額
     C                     MOVELWFAPNO    R#APNO           繳款單號
     C*
     C           W#IVNO    SETLLTXREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ TXREC                    90
     C           *IN90     IFEQ *ON
     C           TXIVNO    ORNE W#IVNO
     C                     LEAVE
     C                     ENDIF
     C           TXNO      IFEQ R#WTNO
     C                     Z-ADDTXAMT     R#AMT
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C*
     C           R#QTY     IFNE 0
     C                     MOVE WFORNO    R#ORNO
     C                     MOVE WFCUNO    R#CUNO
     C           *IN66     IFEQ '1'                        印出
     C           R#CUNO    CHAINCBCUST               69    設客戶名稱
     C                     MOVEL*BLANK    R#CUNM
     C                     MOVELCBCUNM    R#CUNM
     C                     WRITEAR069T1H
     C                     SETOF                     66
     C                     ENDIF
     C                     ADD  1         W#NUM
     C                     WRITEAR069T1D
     C*
     C                     ADD  R#QTY     R#SQTY           小計
     C                     ADD  R#QTY     R#TQTY           合計
     C*
     C                     Z-ADD1         I                設定陣列
     C           I         DOWLEW#PN
     C                     MOVE ARY1,I    W#ARY1
     C           WFPDNM    IFEQ D#PDNM
9101 C*          WFSPE1    ANDEQD#SPE1
8906 C           WFQULT    ANDEQD#QULT
     C                     LEAVE
     C                     ENDIF
     C                     ADD  1         I
     C                     ENDDO
     C*
     C           I         IFGT W#PN                       找不到
     C                     ADD  1         W#PN             新增
     C                     Z-ADDW#PN      I
     C                     MOVE *ALL'0'   W#ARY1
     C                     MOVELWFPDNM    D#PDNM
     C                     MOVELWFSPE1    D#SPE1
8906 C                     MOVELWFQULT    D#QULT
     C                     ENDIF
     C*
     C                     ADD  WFQTY     D#QTY
     C                     MOVE W#ARY1    ARY1,I
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*********************************
     C*  L1 函式  印日期小計
     C*********************************
     CSR         L1SR      BEGSR
 9206C           R#SQTY    IFNE 0
     C           *IN66     IFEQ '1'
     C                     WRITEAR069T1H
     C                     SETOF                     66
     C                     ENDIF
     C                     WRITEAR069T1S
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*********************************
     C*  L2 函式  印合計
     C*********************************
     CSR         L2SR      BEGSR
     C           *IN66     IFEQ '1'
     C                     WRITEAR069T1H
     C                     SETOF                     66
     C                     ENDIF
     C                     WRITEAR069T1T
     C*                                                    印陣列
     C                     SORTAARY1
     C                     Z-ADD0         I
     C*
     C                     DO   W#PN
     C                     ADD  1         I
     C                     MOVE ARY1,I    W#ARY1
     C                     CLEARAR069T1X
     C                     MOVELD#PDNM    R#XPDN
9101 C*                    MOVELD#SPE1    R#XSPE
     C                     Z-ADDD#QTY     R#XQTY
     C*
     C                     MOVE *BLANK    D#TAB
8906 C                     MOVELD#PDNM    D#TAB1
8906 C                     MOVELD#QULT    D#TAB2
8906 C           D#TAB     LOKUPTAB1      TAB1R          69
     C                     MOVEL*BLANK    R#XNAM
     C   69                MOVELTAB1R     R#XNAM
     C*
     C   66                WRITEAR069T1H
     C   66                SETOF                     66
     C                     WRITEAR069T1X
     C                     ENDDO
     C*
     CSR                   ENDSR
     C*
     C*********************************
     C*  LR 函式  印合計
     C*********************************
     CSR         LRSR      BEGSR
     C           W#NUM     IFEQ 0
     C                     EXSR MSGSR
     C                     ENDIF
     C*
     CSR                   ENDSR
     C*
     C*********************************
     C*  查無資料函式
     C*********************************
     CSR         MSGSR     BEGSR
     C                     WRITEAR069T1H
     C                     WRITEAR069T1M
     CSR                   ENDSR
     C*
     C*********************************
     C*  子函式集
     C*********************************
** TAB1
212SD280 SD280/G40中級鋼筋
212SD280WSD280/G40中級可銲鋼筋
213SD420 SD420/G60高拉力鋼筋
218SD420WSD420W/A706高級可銲鋼筋
252SD280 SD280/G40中級鋼筋（定尺）
252SD280WSD280/G40中級可銲鋼筋（定尺）
253SD420 SD420/G60高拉力鋼筋（定尺）
258SD420WSD420W/A706高拉可銲鋼筋（定尺）
