     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR077R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     銷貨帳款客戶帳齡分析表
     H*            5.DATE-WRITTEN  88/06/17
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARSTTX  IP  E           K        DISK
     FARSAID  IF  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FARR077T O   E             66     PRINTER
     I*****************************************************************
     IAXREC
     I                                              AXORNOL1
     I*------------------------------------------------------
     I           UDS
     I                                        1   4 U#DAY1
     I                                        5   8 U#DAY2
     I                                        9  12 U#DAY3
     I*
     I                                       16  16 U#FLG1
     I                                       17  17 U#FLG2
     I                                       18  18 U#FLG3
     I                                       19  19 U#FLG4
     I*
     I                                       21  21 U#STC1
     I                                       22  22 U#STC2
     I                                       23  23 U#STC3
     I                                       24  24 U#STC4
     I*
     I                                       31  38 U#ETXD
     I*
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C  N99                SETON                     99
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     C*
     C*********************************
     C*  初始程式
     C*********************************
     CSR         INISR     BEGSR
     C                     MOVE U#DAY1    R#DAY1
     C                     MOVE U#DAY2    R#DAY2
     C                     MOVE U#DAY3    R#DAY3
     C                     MOVE U#ETXD    R#ETXD
     C*
     C                     Z-ADD1         R#DA1B
     C           R#DAY1    SUB  1         R#DA1E
     C                     Z-ADDR#DAY1    R#DA2B
     C           R#DAY2    SUB  1         R#DA2E
     C                     Z-ADDR#DAY2    R#DA3B
     C           R#DAY3    SUB  1         R#DA3E
     C                     Z-ADDR#DAY3    R#DA4B
     C*
     C                     Z-ADDR#DA1B    R#TA1B
     C                     Z-ADDR#DA1E    R#TA1E
     C                     Z-ADDR#DA2B    R#TA2B
     C                     Z-ADDR#DA2E    R#TA2E
     C                     Z-ADDR#DA3B    R#TA3B
     C                     Z-ADDR#DA3E    R#TA3E
     C                     Z-ADDR#DA4B    R#TA4B
     C*
     C                     MOVELU#FLG1    R#FLG1
     C                     MOVELU#FLG2    R#FLG2
     C                     MOVELU#FLG3    R#FLG3
     C                     MOVELU#FLG4    R#FLG4
     C*
     C                     MOVELU#STC1    R#STC1
     C                     MOVELU#STC2    R#STC2
     C                     MOVELU#STC3    R#STC3
     C                     MOVELU#STC4    R#STC4
     C*
     C                     Z-ADD0         W#ITEM  30
     C                     CLEARAR077T1T
     C                     SETON                     66    首頁表頭
     CSR                   ENDSR
     C*
     C*********************************
     C*  L1清除函式換訂單
     C*********************************
     CSR         L1CLR     BEGSR
     C                     CLEARAR077T1D
     CSR                   ENDSR
     C*
     C*********************************
     C*  DETAIL TIME 函式
     C*********************************
     CSR         DTLSR     BEGSR
     C                     SELEC                           判斷狀態碼
     C           AXSTCD    WHEQ '1'
     C           U#STC1    IFEQ ' '
     C                     GOTO ESDTL
     C                     ENDIF
     C           AXSTCD    WHEQ '2'
     C           U#STC2    IFEQ ' '
     C                     GOTO ESDTL
     C                     ENDIF
     C           AXSTCD    WHEQ '3'
     C           U#STC3    IFEQ ' '
     C                     GOTO ESDTL
     C                     ENDIF
     C           AXSTCD    WHEQ '4'
     C           AXSTCD    OREQ '*'
     C           U#STC4    IFEQ ' '
     C                     GOTO ESDTL
     C                     ENDIF
     C                     ENDSL
     C*
     C                     SELEC
     C           AXINDU    WHLT R#DAY1
     C                     ADD  AXINAM    R#AMTA
     C           AXINDU    WHLT R#DAY2
     C                     ADD  AXINAM    R#AMTB
     C           AXINDU    WHLT R#DAY3
     C                     ADD  AXINAM    R#AMTC
     C                     OTHER
     C                     ADD  AXINAM    R#AMTD
     C                     ENDSL
     CSR         ESDTL     ENDSR
     C*
     C*********************************
     C*  L1 函式  印出訂單
     C*********************************
     CSR         L1SR      BEGSR
     C           R#FLG1    IFEQ 'Y'
     C           R#AMTA    ANDNE0
     C                     GOTO CK#OK
     C                     ENDIF
     C*
     C           R#FLG2    IFEQ 'Y'
     C           R#AMTB    ANDNE0
     C                     GOTO CK#OK
     C                     ENDIF
     C*
     C           R#FLG3    IFEQ 'Y'
     C           R#AMTC    ANDNE0
     C                     GOTO CK#OK
     C                     ENDIF
     C*
     C           R#FLG4    IFEQ 'Y'
     C           R#AMTD    ANDNE0
     C                     GOTO CK#OK
     C                     ENDIF
     C*
     C                     GOTO ESL1
     C*
     C           CK#OK     TAG                             合條件要印
     C*
     C           R#AMTA    ADD  R#AMTB    R#AMTE
     C                     ADD  R#AMTC    R#AMTE
     C                     ADD  R#AMTD    R#AMTE
     C*
     C                     ADD  1         W#ITEM
     C                     Z-ADDW#ITEM    R#ITEM
     C           AXCUNO    CHAINCBCUST               69
     C  N69                MOVELCBCUNM    R#CUNM           客戶名稱
     C                     MOVELAXORNO    R#ORNO
     C                     MOVELAXRVID    R#RVID
     C           R#RVID    CHAINARSAID               69    業務名稱
     C  N69                MOVELSANAME    R#RVNM
     C                     MOVELAXDEPT    R#DEPT
     C*
     C   66                WRITEAR077T1H
     C   66                SETOF                     66
     C                     WRITEAR077T1D
     C*
     C                     ADD  R#AMTA    R#TAMA           合計
     C                     ADD  R#AMTB    R#TAMB
     C                     ADD  R#AMTC    R#TAMC
     C                     ADD  R#AMTD    R#TAMD
     CSR         ESL1      ENDSR
     C*
     C*********************************
     C*  LR 函式 （印出合計）
     C*********************************
     CSR         LRSR      BEGSR
     C           R#TAMA    ADD  R#TAMB    R#TAME
     C                     ADD  R#TAMC    R#TAME
     C                     ADD  R#TAMD    R#TAME
     C*
     C   66                WRITEAR077T1H
     C   66                SETOF                     66
     C                     WRITEAR077T1T
     CSR                   ENDSR
     C*
     C*********************************
     C*  查無資料函式
     C*********************************
     CSR         MSGSR     BEGSR
     C                     WRITEAR077T1H
     C                     WRITEAR077T1M
     CSR                   ENDSR
     C*
     C*********************************
     C*  子函式集
     C*********************************
