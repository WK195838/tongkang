     H****************************************************************
     H*
     H*            UPDATE  DATE  99/08/31  2010AR517  S00WCJ (9908A)
     H*
     H****************************************************************
     H            Y                                     1                 BR100
     FARINVM  UP  E           K        DISK
     FCBCUST  IF  E           K        DISK
9103 FINVMST  IF  E           K        DISK
     FARCUDT  UF  E           K        DISK
     FARDLOD  O   E                    DISK
     FARP083T O   E             69     PRINTER
     IRARINVM     01
     I                                              AMAPD1L1
     I                                              AMAPN1L1
     I            DS
     I                                        1  46 D#CUM1
     I                                        1  20 D#MUI1
     I                                       21  30 D#CONT
     I                                       31  43 D#SAMT
     I                                       44  44 D#DOT1
     I                                       45  46 D#ZER1
     I            DS
     I                                        1 100 D#CUM2
     I                                        1  20 D#MUI2
     I                                       21  40 D#MUI3
     I                                       41  60 D#INNO
     I                                       61  63 D#PRCE
     I                                       64  76 D#DAMT
     I                                       77  77 D#DOT2
     I                                       78  79 D#ZER2
     I                                       80  83 D#INYR
     I                                       84  84 D#DAS1
     I                                       85  86 D#INMN
     I                                       87  87 D#DAS2
     I                                       88  89 D#INDT
     I                                       90  93 D#DUYR
     I                                       94  94 D#DAS3
     I                                       95  96 D#DUMN
     I                                       97  97 D#DAS4
     I                                       98  99 D#DUDT
 9406I                                      100 100 D#EXPT
     I            DS
     I                                        1   8 D#DATE
     I                                        1   4 D#YY
     I                                        5   6 D#MM
     I                                        7   8 D#DD
     I           UDS
     I                                      301 303 U#BKID
     I                                     10011010 U#USID
     C****************************************************************
9908AC           *DATE     SUB  19000000  U#SYSD  80
     C  N99                EXSR INISR
     C   L1                EXSR L1CLR
     C   01                EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR 99                EXSR LRSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     SETON                     6999
     C                     MOVE *ALL'='   T#LIN1
     C                     MOVE *ALL'='   T#LIN2
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     Z-ADD0         T#AMT1
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     CLEARRARDLOD
     C                     MOVE *BLANK    D#CUM2
     C                     MOVE '1'       ADCUM1
     C                     MOVEL'75280209'D#MUI2           東鋼統編
     C           AMCUNO    CHAINCBREC                41
     C                     MOVELCBMUID    D#MUI3           買受人編號
     C                     MOVELAMINNO    D#INNO           發票號碼
     C                     MOVE 'TWD'     D#PRCE           幣別
     C                     MOVE *ALL'0'   D#DAMT           發票金額
     C                     MOVE AMDAMT    D#DAMT           發票金額
     C* 911126 應銀行要求將金額後之.00改為三位空白
     C*                    MOVE '.'       D#DOT2           小數點
     C*                    MOVE *ALL'0'   D#ZER2           小數金額
     C                     MOVE *BLANK    D#DOT2           小數點
     C                     MOVE *BLANK    D#ZER2           小數金額
     C*
     C           19110000  ADD  AMINDT    W#DATE  80       發票日期
     C                     MOVE W#DATE    D#DATE           發票日期
     C                     MOVE D#YY      D#INYR
     C                     MOVE '/'       D#DAS1
     C                     MOVE D#MM      D#INMN
     C                     MOVE '/'       D#DAS2
     C                     MOVE D#DD      D#INDT
     C*
     C           19110000  ADD  AMDUDT    W#DATE           發票日期
     C                     MOVE W#DATE    D#DATE           發票日期
     C                     MOVE D#YY      D#DUYR
     C                     MOVE '/'       D#DAS3
     C                     MOVE D#MM      D#DUMN
     C                     MOVE '/'       D#DAS4
     C                     MOVE D#DD      D#DUDT
     C*940630--銀行新增欄位-大陸出口記號(D#EXPT)
     C                     MOVE '0'       D#EXPT
     C*
     C                     MOVE D#CUM2    ADCUM2
     C                     WRITERARDLOD
     C*
     C                     ADD  1         W#CONT  40
     C                     ADD  AMDAMT    W#TAMT 120
     C*
     C                     MOVE 'Y'       AMDLC1           承購下載碼
9908AC                     MOVE U#SYSD    AMDLD1           承購日期
     C                     MOVE U#USID    AMDLM1           下載人員
     C                     MOVE 'Y'       AMAPCD           承購確認碼
     C                     UPDATRARINVM
     C*列印
9103 C           AMINNO    CHAININREC                40
LYW  C                     MOVELINRVID    T#RVID           業務員
     C*
     C                     MOVE AMORNO    T#ORNO
     C*
     C                     ADD  1         T#ITEM
     C           19110000  ADD  AMINDT    T#INDT           發票日期
     C           19110000  ADD  AMDUDT    T#DUDT           到期日期
     C   69                WRITEAR083TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR083T1
     C*
     C                     ADD  AMDAMT    T#AMT1           請款單小計
     C                     ADD  AMDAMT    T#AMTT           合計
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C   69                WRITEAR083TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR083T2
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C           W#CONT    IFNE 0
     C                     CLEARRARDLOD
     C                     MOVE *BLANK    D#CUM1
     C                     MOVE '0'       ADCUM1
     C                     MOVEL'75280209'D#MUI1           東鋼統編
     C                     MOVE *ALL'0'   D#CONT           發票張數
     C                     MOVE W#CONT    D#CONT           發票張數
     C                     MOVE *ALL'0'   D#SAMT           發票金額
     C                     MOVE W#TAMT    D#SAMT           發票金額
     C                     MOVE '.'       D#DOT1           小數點
     C                     MOVE *ALL'0'   D#ZER1           小數金額
     C                     MOVELD#CUM1    ADCUM2
     C                     WRITERARDLOD
     C                     ENDIF
     C*
     C   69                WRITEAR083TH
     C                     MOVE *OFF      *IN69
     C                     WRITEAR083T3
     C*更新客戶檔餘額
     C                     MOVELAMCUNO    W#CUNX  5
     C                     MOVELW#CUNX    W#CUNY  6
     C           W#CUNY    CHAINRARCUDT              40
     C                     ADD  T#AMTT    ACAAM1
     C                     UPDATRARCUDT
     C*
     C                     ENDSR
