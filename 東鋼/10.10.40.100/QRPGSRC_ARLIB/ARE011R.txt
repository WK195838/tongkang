     H****************************************************************
     H*
     H*            UPDATE  DATE  99/08/23  2010AR517  S00WCJ (9908A)
     H*                         101/09/18  2012AR682  S00WCJ (0109A)
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FARE011S CF  E                    WORKSTN
     F                                        RRN   KSFILE SF1
     FRCVDTLL1UF  E           K        DISK
     F*ANKID  IF  E           K        DISK
     FBANKAC  IF  E           K        DISK
     E*************************************************************
     E                    ERR     1   8 70
     E                    TAB1    1   6  1   TABR1  10
     I*************************************************************
     IAADS        DS
9008 I                                       21  280YMD
LYW  I                                       21  240YY
 .   I                                       25  260MM
 .   I                                       27  280DD
     IBBDS        DS
     I                                       11  20 W02
     I                                       11  11 ACTYPE
     I                                       12  12 ACCURR
     I                                       13  20 ACACNO
     I            DS
     I                                        1   6 D#RVNO
     I                                        2   2 TXARV
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
0109AIIFRS       UDS                             50
0109AI                                        1   80D#IFRS
     C**************************************************************
     C           RVKEY     KLIST
     C                     KFLD           RVDATE
     C                     KFLD           RVNTNO
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C**         BKKEY     KLIST
     C**                   KFLD           BAID1
     C**                   KFLD           BAID2
     C           ACKEY     KLIST
     C                     KFLD           ACTYPE
     C                     KFLD           ACCURR
     C                     KFLD           ACACNO
     C********************************
     C*****                     ******
     C*****  INITIAL VALUES     ******
     C*****                     ******
     C********************************
0109AC           *DATE     SUB  19000000  U#SYSD  80
9908AC* N90                Z-ADDUDATE     YMD
9908AC  N90      *DATE     SUB  19000000  YMD
9908AC* N90                Z-ADDUDATE     DATE0
9908AC  N90      *DATE     SUB  19000000  DATE0
9908AC* N90                Z-ADDUDATE     DATE1
9908AC  N90      *DATE     SUB  19000000  DATE1
9908AC* N90                Z-ADDUDATE     DATE2
9908AC  N90      *DATE     SUB  19000000  DATE2
0109AC           *NAMVAR   DEFN ARIFRSCTL IFRS
0109AC           U#SYSD    IFLT D#IFRS
0109AC                     MOVEL*ON       *IN77
0109AC                     ELSE
0109AC                     MOVEL*OFF      *IN77
0109AC                     ENDIF
0109AC                     UNLCKIFRS
     C  N90                MOVE '1'       SCRN    1
     C  N90                Z-ADD990       R#L01   30
     C  N90                MOVE '1'       *IN,90
     C********************************
     C*****                     ******
     C*****     MAIN LOGIC      ******
     C*****                     ******
     C********************************
     C           *IN03     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C********************************
     C*****                     ******
     C***** SCREEN 01 MAIN CTRL ******
     C*****                     ******
     C********************************
     CSR         SR#01     BEGSR
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C   KC                GOTO SR#01Z
     C                     EXSR CK#01
     C  N99                EXSR PR#02
     C  N99                MOVE '2'       SCRN
     C   99                MOVE '1'       SCRN
     CSR         SR#01Z    ENDSR
     C********************************
     C*****                     ******
     C***** SCREEN 02 MAIN CTRL ******
     C*****                     ******
     C********************************
     CSR         SR#02     BEGSR
     C                     WRITEUND01
     C                     EXFMTSFCTL1
     C   KC                EXSR KC#01
     C   KC                GOTO SR#02Z
     C   KJ                EXSR KJ#02
     C   KJ                GOTO SR#02Z
     C   KL                EXSR KL#02
     C   KL                GOTO SR#02Z
     C                     EXSR CK#02
     CSR         SR#02Z    ENDSR
     C********************************
     C*****                     ******
     C*****   CF03 EXIT         ******
     C*****                     ******
     C********************************
     CSR         KC#01     BEGSR
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C********************************
     C*****                     ******
     C*****   CF10 SAVE         ******
     C*****                     ******
     C********************************
     CSR         KJ#02     BEGSR
     C                     EXSR CK#02
     C  N99                EXSR FL#02
     C  N99                MOVE '1'       SCRN
     CSR         KJ#02Z    ENDSR
     C********************************
     C*****                     ******
     C*****   CF12 PREVIOUS     ******
     C*****                     ******
     C********************************
     CSR         KL#02     BEGSR
     C                     MOVE *BLANK    ERRMSG
     C                     MOVE '1'       SCRN
     CSR         KL#02Z    ENDSR
     C********************************
     C*****                     ******
     C*****   SCREEN 01 CKECK   ******
     C*****                     ******
     C********************************
     CSR         CK#01     BEGSR
     C                     SETOF                     414299
     C                     SETOF                     434044
     C                     MOVE *BLANK    ERRMSG
      **
     C           DATE1     IFGT DATE2
     C                     SETON                     9941
     C                     MOVELERR,2     ERRMSG
     C                     ENDIF
      **
     C           DATE1     IFNE 0
     C*                    CALL 'C01'
     C*                    PARM DATE1     APDT    60
     C*                    PARM ' '       FLAG    1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DATE1     P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           FLAG    1
     C           FLAG      IFNE '0'
     C                     SETON                     9941
     C                     MOVELERR,1     ERRMSG
     C                     ENDIF
     C                     ENDIF
      **
     C*                    CALL 'C01'
     C*                    PARM DATE2     APDT    60
     C*                    PARM ' '       FLAG    1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DATE2     P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE
     C                     PARM '1'       P#MODE
     C                     PARM           P#MTL
     C                     PARM           P#LY
     C                     PARM           FLAG
     C           FLAG      IFNE '0'
     C                     SETON                     9942
     C                     MOVELERR,1     ERRMSG
     C                     ENDIF
     CSR         CK#01Z    ENDSR
     C********************************
     C*****                      *****
     C*****   SCREEN 02 PREPARE  *****
     C*****                      *****
     C********************************
     CSR         PR#02     BEGSR
     C                     Z-ADD1         DSPREC
     C                     MOVE *BLANK    ERRMSG
     C                     SETON                     80
     C                     WRITESFCTL1
     C                     SETOF                     801051
     C                     SETOF                     97
     C                     Z-ADD0         RRN     30
     C                     Z-ADDDATE1     RVDATE
     C                     MOVE *BLANK    RVNTNO
     C           RVKEY     SETLLRCVDTLL1             97
     C           *IN97     DOWEQ'0'
     C                     READ RCVDTLL1            N    97
     C  N97      RVDATE    COMP DATE2                97
     C   97                LEAVE
0109AC   77      RVACNO    COMP '1131'               3131
0109AC  N77      RVACNO    COMP '111001'             3131
     C  N31      RVNTCD    COMP *BLANK               3131
     C  N31      RVVNO2    COMP *BLANK               3131
     C  N31      RVNTNO    COMP *BLANK                   31
     C   31                ITER
     C                     MOVE RVNO      D#RVNO
     C                     MOVELRVNO      TXARW   1
     C           TXAR      IFEQ 'P'
     C           TXARW     IFNE 'P'
     C           TXARW     ANDNE'M'
     C           TXARW     ANDNE'H'
     C                     ITER
     C                     ENDIF
     C                     ENDIF
      *
     C           TXAR      IFEQ 'P'
     C           TXARV     ANDGE'A'
     C           TXARV     ANDLE'Z'
     C                     ITER
     C                     ENDIF
      *
     C                     MOVELRVTBID    H01
     C                     MOVELRVTANO    H02
     C                     MOVELRVNO      H03
     C                     MOVELRVITEM    H04
     C                     MOVEL*BLANK    ACT
     C                     MOVELRVTBID    W01
     C                     MOVELRVTANO    W02
     C           ACKEY     CHAINBANKAC               31
     C   31                MOVE *BLANK    W01X
     C  N31                MOVELACBAID    W01
     C  N31                MOVELACBNAM    W01X
     C                     MOVELRVNTNO    W03
     C                     MOVE *BLANK    W04
     C           RVNTTP    LOKUPTAB1      TABR1          31
     C   31                MOVELTABR1     W04
     C  N31                MOVEL'其他'  W04
     C                     Z-ADDRVDUDT    W05
     C                     Z-ADDRVRAMT    W06
     C                     MOVELRVPRCD    W07
     C                     MOVELRVPBID    W21
     C                     MOVELRVPANO    W22
     C                     MOVELRVNO      W23
     C                     MOVELRVCUNM    W24
     C                     MOVELRVDATE    W25
     C                     MOVE *BLANK    W26
     C           RVPLAC    COMP 'D'                      31
     C   31                MOVEL'本埠'  W26
     C           RVPLAC    COMP 'E'                      31
     C   31                MOVEL'外埠'  W26
     C                     MOVELRVUSTP    W27
     C                     ADD  1         RRN
     C           RVPRCD    COMP 'Y'                      51
     C                     WRITESF1
     C           RRN       IFGT R#L01
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C           RRN       IFEQ 0
     C                     CLEARSF1
     C                     MOVEL'END DATA'W01X
     C                     ADD  1         RRN
     C                     SETON                     51
     C                     WRITESF1
     C                     SETOF                     51
     C                     ENDIF
     C                     SETON                     10
     CSR         PR#02Z    ENDSR
     C********************************
     C*****                      *****
     C*****   SCREEN 02 CHECK    *****
     C*****                      *****
     C********************************
     CSR         CK#02     BEGSR
     C                     MOVEL*BLANK    W02T   10
     C                     Z-ADD1         DSPREC
     C                     SETOF                     31
     C           *IN31     DOWEQ'0'
     C                     READCSF1                      31
     C  N31                Z-ADDRRN       DSPREC
     C                     END
      *
     C                     SETOF                     414299
     C                     SETOF                     434044
     C                     MOVE *BLANK    ERRMSG
     C*                    CALL 'C01'
     C*                    PARM DATE0     APDT    60
     C*                    PARM ' '       FLAG    1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DATE0     P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE
     C                     PARM '1'       P#MODE
     C                     PARM           P#MTL
     C                     PARM           P#LY
     C                     PARM           FLAG
     C           FLAG      IFNE '0'
     C                     SETON                     9940
     C                     MOVELERR,1     ERRMSG
     C                     ENDIF
     C***
     C                     Z-ADD0         RRN
     C                     SETOF                     97
     C           *IN97     DOWEQ'0'
     C                     ADD  1         RRN
     C                     SETOF                     414243
     C           RRN       CHAINSF1                  97
     C   97                LEAVE
     C   21                MOVELW02T      W02
     C           W02       IFNE *BLANK
     C                     MOVELW02       W02T
     C                     ENDIF
     C           ACT       COMP 'Y'                      31
     C   31      W07       COMP 'Y'                      31
     C   31                SETON                     4199
     C   31                MOVELERR,7     ERRMSG
      *
     C           ACT       COMP 'Y'                  3131
     C   31      W07       COMP 'C'                      31
     C   31                SETON                     4199
     C   31                MOVELERR,8     ERRMSG
      *
     C           ACT       COMP ' '                      31
     C  N31      ACT       COMP 'Y'                      31
     C  N31      ACT       COMP 'C'                      31
     C  N31                SETON                     4199
     C  N31                MOVELERR,4     ERRMSG
      *
     C*          W01       IFNE *BLANK
     C*          ACT       OREQ 'Y'
     C*          BKKEY     CHAINBANKID               31
     C*  31                SETON                     4299
     C*  31                MOVELERR,5     ERRMSG
     C*                    ENDIF
      *
     C           W02       IFNE *BLANK
     C           ACT       OREQ 'Y'
     C           ACKEY     CHAINBANKAC               31
     C   31                SETON                     4399
     C   31                MOVELERR,6     ERRMSG
     C   31                MOVE *BLANK    W01
     C   31                MOVE *BLANK    W01X
     C  N31                MOVELACBAID    W01
     C  N31                MOVELACBNAM    W01X
     C                     ENDIF
     C           W07       COMP 'Y'                      51
     C                     UPDATSF1
     C                     ENDDO
     CSR         CK#02Z    ENDSR
     C********************************
     C*****                      *****
     C*****   WRITE DATA TO FILE *****
     C*****                      *****
     C********************************
     CSR         FL#02     BEGSR
     C                     Z-ADD0         RRN
     C                     SETOF                     97
     C           *IN97     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  97
     C   97                LEAVE
     C**         H01       IFNE W01
     C           H02       IFNE W02
     C           ACT       ORNE *BLANK
     C                     Z-ADDW25       RVDATE
     C                     MOVELW03       RVNTNO
     C                     MOVELH03       RVNO
     C                     MOVELH04       RVITEM
     C           RVKEY     CHAINRCVDTLL1             31
     C   31                ITER
      *
     C           ACT       COMP 'Y'                      31
     C   31                MOVEL'Y'       RVPRCD
     C   31                Z-ADDDATE0     RVPRDT
     C           ACT       COMP 'C'                      31
     C   31                MOVE *BLANK    W01
     C   31                MOVE *BLANK    W01X
     C   31                MOVE *BLANK    W02
     C                     MOVEL'C'       RVFLAG
     C                     MOVELW01       RVTBID
     C                     MOVELW02       RVTANO
     C                     MOVELTXAR      RVTXAR
9908AC*                    Z-ADDUDATE     RVTXDT
9908AC           *DATE     SUB  19000000  RVTXDT
     C           W01       COMP *BLANK                   31
     C   31      W02       COMP *BLANK                   31
     C   31                Z-ADD0         RVPRDT
     C   31                MOVE *BLANK    RVPRCD
     C                     UPDATRVREC
     C                     ENDIF
     C                     ENDDO
     CSR         FL#02Z    ENDSR
      ******************************* END PROGRAM *****************
** ERR
日期錯誤
日期範圍錯誤
無符合之資料
處理代碼錯誤
銀行代號錯誤
銀行帳號錯誤
此筆資料已託收不可再託收
此筆資料未託收不可取消
** ECK
A支票
B背書支票
C本票
D背書本票
E銀行本票
I信用狀　
