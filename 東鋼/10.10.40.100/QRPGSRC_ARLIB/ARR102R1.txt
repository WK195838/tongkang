     H*****************************************************************
     H*
     H*          >>  PROGRAM INTRODUCTION  <<
     H*          1.PROGRAM-ID    ARR102R
     H*          2.PROGRAM-TYPE  RPG/400
     H*          3.AUTHOR        S02CSF
     H*          4.FUNCTION     補運費發票開立檢核表
     H*          5.DATE-WRITTEN  91/12/26
     H*          6.DATE-MODIFY
     H*
     H*****************************************************************
     H        1   Y                                     1
     FARDLWT  IP  E           K        DISK
     FTRNDTL  IF  E           K        DISK
     FARR102T O   E           K        DISK
     IRARDLWT     01
     I           UDS
     I                                     10011010 U#USID
     I            DS
     I                                        1   8 D#WTNO
     I                                        1   1 D#WTN1
     I                                        2   3 D#WTN2
     I                                        4   8 D#WTN3
     I*****************************************************************
     C*         主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C   01                EXSR DTLSR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C*          初始程式
     C*****************************************************************
     CSR         INISR     BEGSR
     C                     SETON                     6699
     C                     MOVE *ALL'='   T#LIN1
     C                     MOVE *ALL'='   T#LIN2
     C*FILE==>TRNDTL
     C           W#TRND    KLIST
     C                     KFLD           TXCODE
     C                     KFLD           TXNO
     C                     KFLD           TXITEM
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     CLEARAR102T1D
     C                     Z-ADD0         W#QTY
     C*
     C                     MOVELAWCUNO    T#CUNO           客戶編號
     C                     MOVELAWCUNM    T#CUNM           客戶名稱
     C                     MOVELAWORNO    T#ORNO           訂單號碼
     C                     MOVELAWDLVY    T#DLVY           交單編號
     C                     MOVELAWWTNO    T#WTNO           磅單號碼
     C                     MOVELAWTXNO    T#TXNO           調整單號
     C*
     C                     MOVELAWWTNO    D#WTNO
     C                     MOVEL'11'      D#WTN2
     C                     MOVEL'SA04'    TXCODE
     C                     MOVELD#WTNO    TXNO
     C                     Z-ADD0         TXITEM
     C           W#TRND    SETLLTXREC
     C                     MOVE *OFF      *IN50
     C           *IN50     DOWEQ*OFF
     C                     READ TXREC                    50
     C   50                LEAVE
     C*
     C           TXNO      IFNE D#WTNO
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ADD  TXQTY     W#QTY   70
     C                     ENDDO
     C*
     C                     MOVELAWIVNO    T#IVNO           發票號碼
     C*
     C                     Z-ADDAWWTDT    T#WTDT           出貨日期
     C                     Z-ADDW#QTY     T#QTY            重量
     C*
     C   66                WRITEAR102T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR102T1D
     C*
     CSR                   ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C   66                WRITEAR102T1H
     C                     MOVE *OFF      *IN66
     C                     WRITEAR102T1M
     C*
     CSR                   ENDSR
