     H        1   Y                                     1                 BR003
     FARE002S CF  E                    WORKSTN
     F                                        RRN   KSFILE SF1
     F                                        RRN2  KSFILE SF2
     FTRNDTL  UF  E           K        DISK                      A
     FTRNDTLL5IF  E           K        DISK
     F            TXREC                             KRENAMETXREC5
     FSAMAST  IF  E           K        DISK
     FHIPROD  IF  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FGENSEQ  UF  E           K        DISK                      A
9112 FARDLWT  UF  E           K        DISK
8902 FAMCTRL  IF  E           K        DISK
  .  FACCTRL  IF  E           K        DISK
     E*************************************************************
     E                    ERR     1  29 70
     E                    ARY1       99 15
     I*************************************************************
8902 I            DS
9008 I                                        1   80D#DATE
LYW  I                                        1   60D#YYMM
     IAADS        DS
     I                                        1   6 W1CUNO
     I                                        1   1 S1KIND
     I                                        2   2 S1CUN1
     I                                        3   5 S1CUN2
9008 I                                        6   6 S1CD01
     I                                       11  20 GRP
     I                                       11  11 GE1
     I                                       12  20 GE2
     I                                       21  26 NOG
     I                                       21  21 NOA
     I                                       22  260NOB
     I            DS
     I                                        1  15 W#ARYE
     I                                        1  11 D#ARNO
     I                                       12  15 D#ARRN
     I            DS
     I                                        1  15 TXRESV
     I                                        1   3 D#PDNO
     I            DS
     I                                        1  10 TXIVNO
     I                                        1   1 D#IVN1
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           GEKEY     KLIST
     C                     KFLD           GEKIND
     C                     KFLD           GEPRIN
     C*
     C           TXKEY     KLIST
     C                     KFLD           TXCODE
     C                     KFLD           TXNO
     C                     KFLD           TXITEM
     C*
     C           TX1KEY    KLIST
     C                     KFLD           TXCODE
     C                     KFLD           TXNO
     C*
     C           S1KEY     KLIST
9510 C                     KFLD           KOREA   1
9510 C                     KFLD           KORNO   50
9510 C                     KFLD           KORTM   30
     C*
     C           CBKEY     KLIST
     C                     KFLD           CBCUNO
     C*
8901 C           K#CTRL    KLIST
 .   C                     KFLD           A5AREA
 .   C                     KFLD           A5YYMM
 .   C*
8901 C           K#CTRC    KLIST
 .   C                     KFLD           AAAREA
 .   C                     KFLD           AAYYMM
 .   C                     KFLD           AAFSYS
 .   C*
9112 C*FILE==>ARDLWT
 .   C           W#DLWT    KLIST
 .   C                     KFLD           AWORNO
9112 C                     KFLD           AWWTNO
     C**************************************************************
     C  N90                MOVE '1'       SCRN    1
     C  N90                MOVE '1'       *IN,90
9112 C*用50燈號來判斷是要寫入ARDLWT調整單號或清空ARDLWT調整單號
9112 C                     MOVE *OFF      *IN50
     C**************************************************************
     C           *IN03     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01            畫面一
     C           SCRN      CASEQ'2'       SR#02            畫面二
     C           SCRN      CASEQ'3'       SR#03            畫面三
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     CSR         SR#01     BEGSR
     C                     EXFMTTITLE
     C   KC                EXSR KC#01                      離開
     C                     EXSR CK#01                      CHECK畫面一
     C  N99                EXSR PR#02
     C  N99                MOVE '2'       SCRN
     C*
     CSR         SR#01Z    ENDSR
     C**************************************************************
     CSR         SR#02     BEGSR
     C**************************************************************
     C                     WRITEUND01
     C                     EXFMTSFCTL1
     C                     MOVE *BLANK    ERRMSG
     C*
     C   KC                EXSR KC#01                      離開
     C   KL                MOVE '1'       SCRN             回上頁
     C   KL                Z-ADD0         W#QTY
     C   KL                GOTO SR#02Z
     C   KJ                EXSR KJ#02                      存檔
     C   KJ                GOTO SR#02Z
     C   08                EXSR KH#02                      載入
     C   08                GOTO SR#02Z
     C*
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C                     ELSE
     C           CODE      IFEQ '4'
     C                     MOVE '1'       SCRN
     C                     ENDIF
     C                     ENDIF
     C*
     CSR         SR#02Z    ENDSR
     C**************************************************************
     CSR         KC#01     BEGSR                           離開
     C**************************************************************
     C                     SETON                     LR
     C                     RETRN
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         KJ#02     BEGSR
     C**************************************************************
     C           CODE      IFEQ '1'
     C           CODE      OREQ '2'
     C                     EXSR CK#02
     C                     ENDIF
     C  N99                EXSR FL#02
     C  N99                MOVE '1'       SCRN
     C*
     CSR         KJ#02Z    ENDSR
     C**************************************************************
     CSR         KH#02     BEGSR
     C**************************************************************
     C                     MOVEL'3'       SCRN
     C                     EXSR SR3100                     初始畫面三
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         SR#03     BEGSR
     C**************************************************************
     C           S#SFN2    IFEQ 0
     C                     SETOF                     81
     C                     SETON                     8274
     C                     MOVELERR,27    S#ERR
     C                     ELSE                            有資料
     C                     SETOF                     8182
     C                     SETON                     74
     C                     ENDIF
     C*
     C                     WRITEUND02
     C                     EXFMTSFCTL2                     螢幕輸入
     C*
     C                     MOVE *BLANK    S#ERR
     C           S#CRN2    IFNE 0
     C                     Z-ADDS#CRN2    S#NBR2           記錄位置
     C                     ENDIF
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C                     EXSR KC#01                      結束
     C           *IN12     WHEQ '1'
     C                     MOVE '2'       SCRN             回前畫面
     C           *IN05     WHEQ '1'
     C                     EXSR SR3400                     全部挑選
     C           *IN10     WHEQ '1'
     C                     EXSR SR3220
     C                     EXSR SR3200                     存檔
     C                     OTHER
     C                     EXSR SR3300                     執行
     C                     ENDSL
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         SR3100    BEGSR
     C**************************************************************
     C* INISR畫面三
     C                     MOVE *BLANK    S#ERR
     C                     Z-ADD1         S#NBR2
     C                     SETON                     8182
     C                     WRITESFCTL2
     C*
     C                     Z-ADD0         S#SFN2
     C                     Z-ADD0         RRN2
     C                     Z-ADD0         S#SQTY
     C                     Z-ADD0         S#SAMT
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         SR3200    BEGSR
     C**************************************************************
     C                     Z-ADD0         RRN2    30
     C                     Z-ADD0         RRN
     C                     Z-ADD0         W#SVN2  20
     C*
     C                     DO   S#SFN2
     C                     ADD  1         RRN2
     C           RRN2      CHAINSF2                  30
     C*
     C           S#OPT     IFEQ ' '
     C                     ITER
     C                     ENDIF
     C*
     C           1         DOWEQ1
     C                     ADD  1         RRN
     C*
     C           RRN       IFGT 90
     C                     MOVELERR,25    ERRMSG           項次超過
     C                     MOVEL'2'       SCRN
     C                     GOTO EN3200
     C                     ENDIF
     C*
     C           RRN       CHAINSF1                  30
     C           PNO       IFEQ *BLANK
     C                     EXSR SR3210                     轉換內容
     C                     ADD  1         W#SVN2
     C                     UPDATSF1
     C                     LEAVE
     C                     ELSE
     C                     EXSR SR3211                     數量重新算
     C                     UPDATSF1
     C                     ENDIF
     C                     ENDDO
     C*
     C                     ENDDO
     C                     MOVELERR,26    ERRMSG
     C                     MOVELW#SVN2    W#SVC2  2
     C           ERRMSG    CAT  W#SVC2:0  ERRMSG
     C           ES3200    TAG
     C*
     C                     MOVEL'2'       SCRN
     C*
     CSR         EN3200    ENDSR
     C**************************************************************
     CSR         SR3210    BEGSR
     C**************************************************************
     C*畫面三存檔搬移變數
     C*
     C                     Z-ADDRRN       ITEM             項次
     C                     Z-ADDS#QTY     QTY              數量
     C                     MOVELW#JUST    JUST             調整別
     C                     MOVELW#PDNM    PDNM             調整品名
     C                     MOVELS#PDNO    PDNO             原品名
     C                     MOVELF#ORNO    ORNO             訂單編號
     C                     MOVELS#NO      PNO              磅單編號
     C                     MOVELS#CUNO    CUNO             客戶代號
     C                     MOVELS#CUNM    CUNM             客戶簡稱
     C                     MOVEL'A'       FLAG              FLAG碼
     C*
     C                     Z-ADD0         W#AVQT  76       平均數量
     C           S#QTY     DIV  W#QTY     W#AVQT    H      平均數量
     C           W#AVQT    MULT W#TAMT    AMT       H      金額
     C           AMT       DIV  QTY       PRC       H      平均單價
     C*
     C                     ADD  QTY       S#QTYS
     C                     ADD  AMT       TTLAMT
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         SR3211    BEGSR
     C**************************************************************
     C                     Z-ADD0         W#AVQT           平均數量
     C           QTY       DIV  W#QTY     W#AVQT    H      平均數量
     C           W#AVQT    MULT W#TAMT    AMT       H      金額
     C           AMT       DIV  QTY       PRC       H      平均單價
     C*
     C           RRN       IFEQ 1
     C                     Z-ADD0         TTLAMT
     C                     ENDIF
     C*
     C                     ADD  AMT       TTLAMT
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         SR3220    BEGSR
     C**************************************************************
     C                     Z-ADD1         RRN
     C                     Z-ADD1         RRN2
     C                     Z-ADD0         W#QTY
     C                     Z-ADD0         S#QTYS
     C                     Z-ADD0         TTLAMT
     C*
     C*重新合計畫面二重量、金額
     C*
     C           1         DOWEQ1
     C           RRN       CHAINSF1                  31
     C*
     C   31                LEAVE
     C*
     C                     ADD  1         RRN
     C                     ADD  QTY       W#QTY
     C                     ADD  QTY       S#QTYS
     C                     ADD  AMT       TTLAMT
     C                     ENDDO
     C*
     C*累計畫面三重量至畫面二
     C*
     C           1         DOWEQ1
     C           RRN2      CHAINSF2                  31
     C*
     C   31                LEAVE
     C*
     C                     ADD  1         RRN2
     C           S#OPT     IFEQ ' '
     C                     ITER
     C                     ENDIF
     C*
     C                     ADD  S#QTY     W#QTY   90
     C                     ENDDO
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         SR3300    BEGSR
     C**************************************************************
     C*畫面三執行
     C*
     C           S#ORNO    IFEQ *BLANK                     訂單號碼
     C           S#JUST    OREQ *BLANK                     調整別
     C           S#PDNM    OREQ *BLANK                     調整品名
     C           S#ACDS    OREQ 0                          入帳起日
     C           S#ACDE    OREQ 0                          入帳止日
     C           S#TAMT    OREQ 0                          調整金額
     C                     GOTO EN3300
     C                     ELSE
     C                     EXSR SR3100
     C                     ENDIF
     C*
     C                     Z-ADD0         W#TAMT  90
     C                     MOVEL*BLANK    F#ORNO  6
     C                     MOVEL*BLANK    W#JUST  1
     C                     MOVEL*BLANK    W#PDNM  3
     C                     Z-ADDS#TAMT    W#TAMT
     C                     MOVELS#JUST    W#JUST
     C                     MOVELS#PDNM    W#PDNM
     C                     MOVELS#ORNO    F#ORNO
     C           F#ORNO    SETLLTRNDTLL5
     C*
     C           1         DOWEQ1
     C           F#ORNO    READETRNDTLL5            N    30
     C   30                LEAVE
     C*
     C           TXACDT    IFLT S#ACDS
     C           TXACDT    ORGT S#ACDE
     C           TXFLAG    OREQ 'D'
     C           D#IVN1    OREQ '#'
     C           D#IVN1    OREQ '0'
     C           TXAMT     ORLE 0
     C                     ITER
     C                     ENDIF
     C*
     C                     ADD  1         S#SFN2
     C                     ADD  1         RRN2
     C                     Z-ADDRRN2      S#ITEM           項次
     C                     MOVEL*BLANK    S#OPT            挑選碼
     C                     MOVELTXNO      S#NO             磅單號碼
     C                     MOVELTXPDNM    S#PDNO           品名
     C                     MOVELTXCUNO    S#CUNO           客戶代號
     C                     MOVELTXCUNM    S#CUNM           客戶名稱
     C                     Z-ADDTXQTY     S#QTY            數量
     C                     Z-ADDTXAMT     S#AMT            金額
     C                     Z-ADDTXUPRC    S#UPRC           單價
     C*
     C                     WRITESF2
     C                     ADD  S#QTY     S#SQTY           數量小計
     C                     ADD  S#AMT     S#SAMT           金額小計
     C                     ENDDO
     C*
     C                     MOVEL*BLANK    S#ORNO
     C                     MOVEL*BLANK    S#JUST
     C                     MOVEL*BLANK    S#PDNM
     C                     Z-ADD0         S#ACDS
     C                     Z-ADD0         S#ACDE
     C                     Z-ADD0         S#TAMT
     C*
     CSR         EN3300    ENDSR
     C**************************************************************
     CSR         SR3400    BEGSR
     C**************************************************************
     C                     Z-ADD0         RRN2
     C                     MOVE *OFF      *IN34
     C*
     C           1         DOWEQ1
     C                     ADD  1         RRN2
     C           RRN2      CHAINSF2                  34
     C*
     C   34                LEAVE
     C*
     C                     MOVEL'Y'       S#OPT
     C                     UPDATSF2
     C                     ENDDO
     C*
     CSR                   ENDSR
     C**************************************************************
     CSR         CK#01     BEGSR
     C**************************************************************
     C*CHECK畫面一
     C                     SETOF                     414299
     C                     SETOF                     4369
     C                     MOVE *BLANK    ERRMSG
     C*
     C                     SELEC
     C           CODE      WHEQ ' '
     C                     SETON                     4199
     C                     MOVE ERR,1     ERRMSG
     C*
     C           CODE      WHEQ '1'
     C                     MOVE '新增'  MOD
     C                     MOVE '0'       *IN71
     C*
     C           CODE      WHEQ '2'
     C                     MOVE '更正'  MOD
     C                     MOVE '0'       *IN71
     C*
     C           CODE      WHEQ '3'
     C                     MOVE '作廢'  MOD
     C                     MOVE '1'       *IN71
     C                     MOVE '1'       *IN69
     C*
     C           CODE      WHEQ '4'
     C                     MOVE '查詢'  MOD
     C                     MOVE '1'       *IN71
     C                     MOVE '1'       *IN69
     C*
     C                     OTHER
     C                     SETON                     4199
     C                     MOVE ERR,2     ERRMSG
     C                     ENDSL
     C******
     C*非新增時，需輸入調整單號
     C           CODE      IFNE '1'
     C           NO        ANDEQ*BLANK
     C                     SETON                     4299
     C                     MOVE ERR,3     ERRMSG
     C                     ENDIF
     C**
     C*新增時，不可輸入調整單號
     C           CODE      IFEQ '1'
     C           NO        ANDNE*BLANK
     C                     SETON                     4299
     C                     MOVE ERR,15    ERRMSG
     C                     ENDIF
     C**
　 C*若此張調整單非屬本廠區，不可更改或刪除
     C           CODE      IFEQ '2'
     C           CODE      OREQ '3'
     C                     MOVELNO        NOAREA  1        調整單號第一碼
     C           NOAREA    IFNE TXAR
     C                     SETON                     4299
     C                     MOVE ERR,16    ERRMSG
     C                     ENDIF
     C                     ENDIF
     C**
     C           *IN99     IFEQ '0'
     C                     MOVEL'AR05'    TXCODE
     C                     MOVEL*BLANK    TXNO
     C                     MOVELNO        TXNO
     C                     Z-ADD1         TXITEM
     C           TXKEY     CHAINTRNDTL              N97
     C                     SELEC
　 C*此張調整單已存在
     C           CODE      WHEQ '1'
     C           *IN97     ANDEQ'0'
     C                     SETON                     4299
     C                     MOVE ERR,4     ERRMSG
　 C*此張調整單已開發票，不可再修改
     C           CODE      WHEQ '2'
     C           *IN97     ANDEQ'0'
     C           TXFL02    ANDNE' '
     C                     SETON                     4299
     C                     MOVE ERR,5     ERRMSG
　 C*此張調整單已開發票，不可再刪除
     C           CODE      WHEQ '3'
     C           *IN97     ANDEQ'0'
     C           TXFL02    ANDNE' '
     C                     SETON                     4299
     C                     MOVE ERR,6     ERRMSG
　 C*此張調整單不存在
     C           CODE      WHNE '1'
     C           *IN97     ANDEQ'1'
     C                     SETON                     4299
     C                     MOVE ERR,7     ERRMSG
     C                     ENDSL
8902 C*檢核是否關帳 (台北用ACCTRL 廠區用AMCTRL)
  .  C           *IN97     IFEQ *OFF
  .  C           CODE      ANDNE'4'
  .  C           TXAR      IFNE 'P'
  .  C                     MOVELTXNO      A5AREA
  .  C                     Z-ADDTXDATE    D#DATE
  .  C                     Z-ADDD#YYMM    A5YYMM
  .  C           K#CTRL    CHAINRAMCTRL             N60
  .  C           *IN60     IFEQ *OFF
  .  C           A5FG12    ANDEQ'Y'
  .  C                     SETON                     4299
  .  C                     MOVE ERR,19    ERRMSG
  .  C                     ENDIF
  .  C                     ELSE
  .  C                     MOVELTXNO      AAAREA
  .  C                     Z-ADDTXDATE    D#DATE
  .  C                     Z-ADDD#YYMM    AAYYMM
  .  C                     MOVE 'SA'      AAFSYS
  .  C           K#CTRC    CHAINRACCTRL             N60
  .  C           *IN60     IFEQ *OFF
  .  C           AAESFG    ANDEQ'Y'
  .  C                     SETON                     4299
  .  C                     MOVE ERR,19    ERRMSG
  .  C                     ENDIF
  .  C*
  .  C                     ENDIF                           * TXAR
  .  C*
  .  C                     ENDIF                           * IN97
  .  C*
     C                     ENDIF                           * IN99
     C*
     CSR         CK#01Z    ENDSR
     C*****************************************************************
     CSR         PR#02     BEGSR
     C*****************************************************************
     C                     SETON                     84    SFL CLEAR
     C                     WRITESFCTL1
     C                     SETOF                     84
     C*
     C                     Z-ADD0         RRN     20
     C                     Z-SUB11        S#NBR1
     C                     MOVE *BLANK    ORNO             訂單編號
     C                     MOVE *BLANK    CUNO             客戶代號
     C                     MOVE *BLANK    CUNM             客戶名稱
     C                     Z-ADDUDATE     DATE
     C                     Z-ADD0         TTLAMT           合計
     C                     Z-ADD0         S#QTYS           合計
     C**
     C           CODE      CASEQ'1'       PR#ADD
     C                     CAS            PR#OTH
     C                     ENDCS
     C*
     CSR         PR#02Z    ENDSR
     C*****************************************************************
     CSR         PR#ADD    BEGSR
     C*****************************************************************
     C                     EXSR @GETNO
     C*
     C           RRN       DOWLT90
     C                     ADD  1         RRN
     C                     CLEARSF1
     C                     Z-ADDRRN       ITEM
     C                     WRITESF1
     C                     ENDDO
     C*
     C           RRN       IFGT 0
     C                     ADD  12        S#NBR1
     C                     ENDIF
     C*
     C                     SETON                     75
     C*
     CSR         PR#ADZ    ENDSR
     C****************************************************************
     CSR         @GETNO    BEGSR
     C****************************************************************
     C*取得調整單號
     C                     MOVEL'05'      GEKIND           編碼種類
     C                     MOVE *BLANK    GEPRIN           編碼原則
     C                     MOVE *BLANK    GRP
     C                     MOVELTXAR      GE1
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ              N97
     C   97                Z-ADD0         GECUNO           目前號碼
     C                     MOVELTXAR      NOA
     C           GECUNO    ADD  1         NOB
     C                     MOVELNOG       NO
     C*
     CSR                   ENDSR
     C****************************************************************
     CSR         PR#OTH    BEGSR
     C****************************************************************
     C                     MOVELTXORNO    ORNO
     C                     MOVELTXCUNO    CUNO
     C                     MOVELTXCUNM    CUNM
     C                     Z-ADDTXDATE    DATE
     C                     MOVEL'AR05'    TXCODE
     C                     MOVEL*BLANK    TXNO
     C                     MOVELNO        TXNO
     C                     Z-ADD0         TXITEM
     C*
     C           TXKEY     SETLLTRNDTL               97
     C  N97      TX1KEY    READETRNDTL                   97
     C           *IN97     DOWEQ'0'
     C*
     C                     CLEARSF1
     C   71                SETON                     70
     C                     ADD  1         RRN
     C*
     C           RRN       IFNE TXITEM
     C           RRN       DOWLTTXITEM
     C                     Z-ADDRRN       ITEM
     C                     WRITESF1
     C                     ADD  1         RRN
     C                     ENDDO
     C                     ENDIF
     C*
     C                     Z-ADDTXITEM    ITEM
     C                     MOVELTXACNT    JUST
     C                     MOVELTXPDNM    PDNM             調整品名
     C                     MOVELD#PDNO    PDNO             原品名
     C                     Z-ADDTXQTY     QTY
     C                     Z-ADDTXUPRC    PRC
     C                     Z-ADDTXAMT     AMT
     C                     MOVELTXPCNO    PNO
     C                     MOVELTXFLAG    FLAG
     C*
     C           TXFLAG    IFNE 'D'
     C                     ADD  TXAMT     TTLAMT
     C                     ADD  TXQTY     S#QTYS
     C                     ENDIF
     C                     WRITESF1
     C           TX1KEY    READETRNDTL                   97
     C                     ENDDO
     C****
     C           CODE      IFEQ '2'
     C                     CLEARSF1
     C           RRN       DOWLE89
     C                     ADD  1         RRN
     C                     Z-ADDRRN       ITEM
     C                     WRITESF1
     C                     ENDDO
     C                     ENDIF
     C*
     C           RRN       IFGT 0
     C                     ADD  12        S#NBR1
     C                     ENDIF
     C*
     C                     SETON                     75
     C*
     CSR         PR#OTZ    ENDSR
     C****************************************************************
     CSR         CK#02     BEGSR
     C****************************************************************
     C                     SETOF                     99
     C                     MOVEA'00'      *IN,43
     C                     MOVE *BLANK    ERRMSG
     C                     Z-ADD0         TTLAMT
     C                     Z-ADD0         S#QTYS
     C***
　 C*訂單編號必須輸入
     C                     MOVE *BLANK    CUNO
     C                     MOVE *BLANK    CUNM
     C           ORNO      IFEQ *BLANK
     C                     SETON                     4399
     C                     MOVELERR,12    ERRMSG
     C                     ELSE
9510 C                     MOVELORNO      KOREA
9510 C                     MOVE ORNO      KORNO
9510 C                     Z-ADD0         KORTM
9510 C*          S1KEY     CHAINSAMAST               97
     C*
9510 C           S1KEY     SETLLRSAMAST              24
9510 C           *IN24     DOUEQ'1'
9510 C                     READ RSAMAST                  24
9510 C           *IN24     IFEQ '0'
9510 C           S1OREA    ANDEQKOREA
9510 C           S1ORNO    ANDEQKORNO
9510 C                     SETOF                     97
9510 C                     ELSE
9510 C                     SETON                     97
9510 C                     END
9510 C                     SETON                     24
9510 C                     END
     C*訂單編號不存在
     C   97                SETON                     4399
     C   97                MOVELERR,8     ERRMSG
9008 C  N97                MOVELS1CD01    BK05    1        分公司碼
     C  N97                MOVELW1CUNO    CUNO
     C  N97                MOVE BK05      CUNO
     C  N97                MOVELS1RECV    RVID    1        收款業務員
     C  N97                MOVELS1SND     SALE    1        交款業務員
     C***
     C  N97                MOVELCUNO      CBCUNO
     C  N97      CBKEY     CHAINCBCUST               97
     C  N97                MOVELCBCUNM    CUNM             訂單編號
     C  N97                MOVELCBSANO    TYPE    1        銷售別
     C  N97                MOVELCBIVCO    CHK     1        發票別
     C                     ENDIF
     C***
     C*                    CALL 'C01'
     C*                    PARM           DATE
     C*                    PARM           FLAG1   1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DATE      P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           FLAG1   1
　 C*單據日期錯誤
     C           FLAG1     IFNE '0'
     C                     SETON                     9944
     C                     MOVELERR,13    ERRMSG
     C                     ENDIF
     C***
     C                     Z-ADD0         RRN
     C                     MOVE '0'       *IN95
     C           *IN95     DOWEQ'0'
     C                     MOVEA'000000'  *IN,51
     C                     MOVEA'0'       *IN,58
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  95
     C   95                LEAVE
     C           JUST      IFEQ *BLANK                     調整別
     C           PDNM      ANDEQ*BLANK                     品名
     C                     ITER
     C                     ENDIF
     C***
     C                     SELEC
     C*CHECK調整別
     C           JUST      WHEQ ' '
     C                     SETON                     9951
     C                     MOVE ERR,9     ERRMSG
     C           JUST      WHNE '1'
     C           JUST      ANDNE'2'
     C           JUST      ANDNE'3'
     C           JUST      ANDNE'4'
     C                     SETON                     9951
     C                     MOVE ERR,10    ERRMSG
     C                     ENDSL
     C***
     C*CHECK調整品名代號
     C           PDNM      IFNE *BLANK
     C           PDNM      CHAINHIPROD               97
     C   97                SETON                     9952
     C   97                MOVE ERR,11    ERRMSG
     C                     ENDIF
     C***
     C*CHECK金額不可空白
     C           AMT       IFEQ 0
     C           QTY       IFEQ 0
     C           PRC       OREQ 0
     C                     SETON                     9955
     C                     MOVELERR,14    ERRMSG
     C                     ENDIF
     C                     ENDIF
     C****
     C           FLAG      IFNE 'D'                        標記
     C                     ADD  AMT       TTLAMT
     C                     ADD  QTY       S#QTYS
     C                     ENDIF
     C***
     C*CHECK磅單編號不可空白
     C           PNO       IFEQ *BLANK                     磅單編號
     C                     SETON                     9956
     C                     MOVELERR,17    ERRMSG
     C                     ELSE
     C                     MOVEL'SA04'    TXCODE
     C                     MOVEL*BLANK    TXNO
     C                     MOVELPNO       TXNO             磅單編號
     C                     Z-ADD1         TXITEM
     C           TXKEY     CHAINTRNDTL              N97
     C*CHECK磅單編號必須存在
     C   97                SETON                     9956
     C   97                MOVELERR,18    ERRMSG
     C                     ENDIF
     C***
     C*CHECK原品名代號
     C           PDNM      IFEQ '701'
     C                     MOVE *OFF      *IN33
     C           TX1KEY    SETLLTRNDTL
     C           *IN97     DOWEQ*OFF
     C           TX1KEY    READETRNDTL                   97
     C   97                LEAVE
     C*
     C           TXPDNM    IFNE PDNO
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVE *ON       *IN33
     C                     LEAVE
     C                     ENDDO
     C*
     C  N33                SETON                     9958
     C  N33                MOVE ERR,11    ERRMSG
     C                     ENDIF
     C***
     C           AMT       IFEQ 0
     C           QTY       MULT PRC       AMT       H
     C                     ENDIF
     C***
     C                     UPDATSF1
     C                     ENDDO
     C***
     C* CHECK磅單是否重覆
     C                     MOVEL*ALL'9'   ARY1
     C                     MOVEL*OFF      *IN32
     C                     Z-ADD0         RRN
     C*
     C*          *IN32     DOWEQ*OFF
     C*                    ADD  1         RRN
     C*          RRN       CHAINSF1                  32
     C*
     C*          *IN32     IFEQ *ON
     C*          PNO       OREQ *BLANK
     C*                    LEAVE
     C*                    ENDIF
     C*
     C*                    MOVELPNO       D#ARNO
     C*                    MOVE PDNO      D#ARNO
     C*                    MOVE RRN       D#ARRN
     C*                    Z-ADDRRN       I       40
     C*                    MOVE W#ARYE    ARY1,I
     C*                    ENDDO
     C*
     C*                    SORTAARY1                       磅單重覆檢
     C*                    MOVEL*BLANK    W#TMNO 11
     C*                    Z-ADD0         W#TMRN  40
     C*                    Z-ADD0         I
     C*
     C*                    MOVE *OFF      *IN32
     C*                    DO   90
     C*                    ADD  1         I
     C*                    MOVE ARY1,I    W#ARYE
     C*          D#ARRN    IFEQ '9999'
     C*                    LEAVE
     C*                    ENDIF
     C*
     C*          D#ARNO    IFEQ W#TMNO
     C*                    MOVE W#TMRN    RRN
     C*          RRN       CHAINSF1                  32    前一
     C*                    SETON                     56
     C*                    UPDATSF1
     C*                    MOVE D#ARRN    RRN
     C*          RRN       CHAINSF1                  32    後一
     C*                    SETON                     56
     C*                    UPDATSF1
     C*                    SETON                     99
     C*                    MOVELERR,28    ERRMSG           磅單號重覆
     C*                    Z-ADDRRN       S#NBR1
     C*                    GOTO CK#02Z
     C*                    ENDIF
     C*
     C*                    MOVE D#ARRN    W#TMRN
     C*                    MOVE D#ARNO    W#TMNO
     C*                    ENDDO
     C******
     CSR         CK#02Z    ENDSR
     C*****************************************************************
     CSR         FL#02     BEGSR
     C*****************************************************************
     C           CODE      CASEQ'3'       DL#02            刪除
     C           CODE      CASEQ'1'       WR#02            新增
     C           CODE      CASEQ'2'       UP#02            更新
     C                     ENDCS
     C*
     CSR         FL#02Z    ENDSR
     C*****************************************************************
     CSR         DL#02     BEGSR
     C*****************************************************************
     C                     MOVE 'AR05'    TXCODE
     C                     MOVEL*BLANK    TXNO
     C                     MOVELNO        TXNO
     C                     Z-ADD0         TXITEM
     C           TXKEY     SETLLTRNDTL               97
     C  N97      TX1KEY    READETRNDTL                   97
     C           *IN97     DOWEQ'0'
     C                     MOVE 'D'       TXFLAG
     C                     MOVE TXAR      TXTXAR           異動廠區
     C                     Z-ADDUDATE     TXTXDT           異動日期
     C                     UPDATTXREC
 9112C                     MOVE *ON       *IN50
 9112C                     EXSR UP#DW                      清空ARDLWT調整單
     C           TX1KEY    READETRNDTL                   97
     C                     ENDDO
     C*
     CSR         DL#02Z    ENDSR
     C*****************************************************************
     CSR         WR#02     BEGSR
     C*****************************************************************
     C                     CLEARGEREC
     C                     MOVEL'05'      GEKIND
     C                     MOVE *BLANK    GEPRIN
     C                     MOVE *BLANK    GRP
     C                     MOVELTXAR      GE1
     C                     MOVELGRP       GEPRIN
     C           GEKEY     CHAINGENSEQ               97
     C   97                Z-ADD1         GECUNO
     C   97                WRITEGEREC
     C  N97                ADD  1         GECUNO
     C  N97                UPDATGEREC
     C                     MOVELTXAR      NOA
     C                     Z-ADDGECUNO    NOB
     C                     MOVELNOG       NO
     C***
     C                     SETOF                     95
     C                     Z-ADD0         RRN
     C           *IN95     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  95
     C   95                LEAVE
     C***
     C           JUST      IFEQ *BLANK                     調整別
     C           PDNM      ANDEQ*BLANK                     品名
     C           FLAG      OREQ 'D'                        標記
     C                     ITER
     C                     ENDIF
     C****
     C                     EXSR WR#TX                      寫入TRNDTL
 9112C                     MOVE *OFF      *IN50
 9112C                     EXSR UP#DW                      寫入ARDLWT
     C***
     C                     ENDDO
     C***
     CSR         WR#02Z    ENDSR
     C*****************************************************************
     CSR         UP#02     BEGSR
     C*****************************************************************
     C                     Z-ADD0         RRN
     C                     SETOF                     95
     C           *IN95     DOWEQ'0'
     C                     ADD  1         RRN
     C           RRN       CHAINSF1                  95
     C   95                LEAVE
     C                     MOVE 'AR05'    TXCODE
     C                     MOVEL*BLANK    TXNO
     C                     MOVELNO        TXNO
     C                     Z-ADDRRN       TXITEM
     C           TXKEY     CHAINTRNDTL               96
     C           JUST      IFEQ *BLANK                     調整別
     C           PDNM      ANDEQ*BLANK                     品名
     C           FLAG      OREQ 'D'                        標記
     C   96                ITER
     C                     MOVE 'D'       TXFLAG
     C                     MOVE TXAR      TXTXAR
     C                     Z-ADDUDATE     TXTXDT
     C                     UPDATTXREC
 9112C                     MOVE *ON       *IN50
 9112C                     EXSR UP#DW                      清空ARDLWT調整單
     C                     ELSE
     C  N96                EXSR UP#TX
     C   96                EXSR WR#TX
 9112C                     MOVE *OFF      *IN50
 9112C                     EXSR UP#DW                      寫入ARDLWT
     C                     ENDIF
     C***
     C                     ENDDO
     C***
     CSR         UP#02Z    ENDSR
     C*****************************************************************
     C***  WRITE  TO  TRNDTL  ***
     C***************************
     CSR         WR#TX     BEGSR
     C*
     C                     CLEARTXREC
     C                     MOVE 'AR05'    TXCODE
     C                     MOVELNO        TXNO
     C                     MOVEL'A'       TXFLAG
     C                     Z-ADDRRN       TXITEM
     C                     MOVELJUST      TXACNT
     C                     Z-ADDDATE      TXDATE
     C                     Z-ADDDATE      TXACDT
     C                     MOVELCUNO      TXCUNO
     C                     MOVELCUNM      TXCUNM
     C                     MOVELORNO      TXORNO
     C                     MOVELTYPE      TXSATP
     C                     MOVELRVID      TXRVID
     C                     MOVELSALE      TXSALE
     C                     MOVELCHK       TXIVTP
     C                     MOVELPDNM      TXPDNM           調整品名
     C                     MOVELPDNO      D#PDNO           原品名
     C                     Z-ADDQTY       TXQTY
     C                     Z-ADDPRC       TXUPRC
     C                     Z-ADDAMT       TXAMT
     C                     MOVELPNO       TXPCNO           磅單號碼
     C                     MOVELTXAR      TXTXAR
     C                     Z-ADDUDATE     TXTXDT
     C                     WRITETXREC
     C*
     CSR         WR#TXZ    ENDSR
     C*****************************************************************
     C***  UPDATE TO  TRNDTL  ***
     C***************************
     CSR         UP#TX     BEGSR
     C                     MOVE 'C'       TXFLAG
     C                     MOVELJUST      TXACNT
     C                     Z-ADDDATE      TXDATE
     C                     Z-ADDDATE      TXACDT
     C                     MOVELCUNO      TXCUNO
     C                     MOVELCUNM      TXCUNM
     C                     MOVELORNO      TXORNO
     C                     MOVELTYPE      TXSATP
     C                     MOVELRVID      TXRVID
     C                     MOVELSALE      TXSALE
     C                     MOVELCHK       TXIVTP
     C                     MOVELPDNM      TXPDNM           調整品名
     C                     MOVELPDNO      D#PDNO           原品名
     C                     Z-ADDQTY       TXQTY
     C                     Z-ADDPRC       TXUPRC
     C                     Z-ADDAMT       TXAMT
     C                     MOVELPNO       TXPCNO
     C                     MOVELTXAR      TXTXAR
     C                     Z-ADDUDATE     TXTXDT
     C                     UPDATTXREC
     C*
     CSR         UP#TXZ    ENDSR
 9112C*****************************************************************
  .  C***  UPDATE TO ARDLWT  ***
  .  C**************************
  .  CSR         UP#DW     BEGSR
  .  C*寫入ARDLWT調整單號
  .  C                     MOVELTXORNO    AWORNO           訂單編號
  .  C                     MOVELTXPCNO    AWWTNO           磅單編號
  .  C           W#DLWT    CHAINRARDLWT              40
  .  C           *IN50     IFEQ *OFF
  .  C                     MOVELTXNO      AWTXNO           調整單號
  .  C                     Z-ADDTXDATE    AWTXDT           調整單開立日
  .  C                     MOVEL'Y'       AWCOD1           調整開立碼
  .  C                     ELSE
  .  C*清空ARDLWT調整單號
  .  C                     MOVEL*BLANK    AWTXNO           調整單號
  .  C                     Z-ADD0         AWTXDT           調整單開立日
  .  C                     MOVEL*BLANK    AWCOD1           調整開立碼
  .  C                     ENDIF
  .  C*
  .  C  N40                UPDATRARDLWT
  .  CSR                   ENDSR
 9112C*
     C*****************************************************************
** ERR
01-功能代碼必須輸入．
02-功能代碼必須為１，２，３，４，
03-非新增時，調整單號必須輸入
04-此張調整單已存在
05-此張調整單已開發票，不可再修改
06-此張調整單已開發票，不可再刪除
07-此張調整單不存在
08-訂單編號不存在
09-調整別必須輸入
10-調整別必須為１，２，３，４
11-品名代號不存在
12-訂單編號必須輸入
13-單據日期錯誤
14-金額必須輸入
15-新增時，調整單號不可輸入
16-此張調整單非屬本廠區，不可更改或刪除
17-磅單編號必須輸入
18-磅單編號不存在
19-財會單位已關帳,不允許修改或作廢,請通知財會相關人員
20-輸入之訂單號碼不存在
21-入帳日期輸入錯誤
22-調整金額必須要輸入
23-調整別必須要輸入
24-調整品名輸入錯誤
25-載入之磅單筆數多於調整畫面內空白的項次，載入不完整
26-載入之磅單已存回調整畫面，合計存回筆數：
27-資料不存在
28-調整之磅單號碼有重覆情形，請查核（同一張磅單只能填一次）
29-調整別輸入錯誤
