     H****************************************************************
     H*
     H*            UPDATE  DATE  99/08/31  2010AR517  S00WCJ (9908A)
     H*                         101/09/21  2012AR682  S00WCJ (0109A)
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FARQ013S CF  E                    WORKSTN
     F                                        RRN   KSFILE SF1
     FBANKAC  IF  E           K        DISK
     FRCVDTLL2IF  E           K        DISK
     E*************************************************************
     E                    ERR     1  13 70
     I*************************************************************
     IAADS        DS
     I                                       11  20 PID
     I                                       11  11 PID1
     I                                       12  12 PID2
     I                                       13  20 PID3
     I                                       21  260YMD
     I                                       21  220YY
     I                                       23  240MM
     I                                       25  260DD
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C           RVKEY     KLIST
     C                     KFLD           RVDUDT
     C                     KFLD           RVNTNO
     C                     KFLD           RVNO
     C                     KFLD           RVITEM
     C           ACKEY     KLIST
     C                     KFLD           ACTYPE
     C                     KFLD           ACCURR
     C                     KFLD           ACACNO
     C********************************
     C*****                     ******
     C*****  INITIAL VALUES     ******
     C*****                     ******
     C********************************
9908AC           *DATE     SUB  19000000  U#SYSD  80
9908AC  N90                Z-ADDU#SYSD    DATE1
9908AC  N90                Z-ADDU#SYSD    DATE2
     C  N90                MOVE '1'       SCRN    1
     C  N90                Z-ADD990       R#L01   30
     C  N90                MOVE '1'       *IN,90
     C********************************
     C*****                     ******
     C*****     MAIN LOGIC      ******
     C*****                     ******
     C********************************
     C           *IN03     DOUEQ'1'
     C           SCRN      CASEQ'1'       SR#01
     C           SCRN      CASEQ'2'       SR#02
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C********************************
     C*****                     ******
     C***** SCREEN 01 MAIN CTRL ******
     C*****                     ******
     C********************************
     CSR         SR#01     BEGSR
     C                     EXFMTTITLE
     C   KC                EXSR KC#01
     C   KC                GOTO SR#01Z
     C                     EXSR CK#01
     C  N99                EXSR PR#02
     C  N99                MOVE '2'       SCRN
     C   99                MOVE '1'       SCRN
     CSR         SR#01Z    ENDSR
     C********************************
     C*****                     ******
     C***** SCREEN 02 MAIN CTRL ******
     C*****                     ******
     C********************************
     CSR         SR#02     BEGSR
     C                     WRITEUND01
     C                     EXFMTSFCTL1
     C   KC                EXSR KC#01
     C   KC                GOTO SR#02Z
     C   KL                EXSR KL#02
     C   KL                GOTO SR#02Z
     CSR         SR#02Z    ENDSR
     C********************************
     C*****                     ******
     C*****   CF03 EXIT         ******
     C*****                     ******
     C********************************
     CSR         KC#01     BEGSR
     C                     SETON                     LR
     C                     RETRN
     CSR                   ENDSR
     C********************************
     C*****                     ******
     C*****   CF12 PREVIOUS     ******
     C*****                     ******
     C********************************
     CSR         KL#02     BEGSR
     C                     MOVE *BLANK    ERRMSG
     C                     MOVE '1'       SCRN
     CSR         KL#02Z    ENDSR
     C********************************
     C*****                     ******
     C*****   SCREEN 01 CKECK   ******
     C*****                     ******
     C********************************
     CSR         CK#01     BEGSR
     C                     SETOF                     414299
     C                     SETOF                     434440
     C                     MOVE *BLANK    ERRMSG
      **
     C           DATE1     IFGT DATE2
     C                     SETON                     9941
     C                     MOVELERR,2     ERRMSG
     C                     ENDIF
      **
     C           DATE1     IFNE 0
     C*                    CALL 'C01'
     C*                    PARM DATE1     APDT    60
     C*                    PARM ' '       FLAG    1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DATE1     P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM ' '       FLAG    1
     C           FLAG      IFNE '0'
     C                     SETON                     9941
     C                     MOVELERR,1     ERRMSG
     C                     ENDIF
     C                     ENDIF
      **
     C*                    CALL 'C01'
     C*                    PARM DATE2     APDT    60
     C*                    PARM ' '       FLAG    1
     C*                    FREE 'C01'
     C                     MOVE *ALL'0'   P#DATE
     C                     MOVE DATE2     P#DATE
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM ' '       FLAG    1
     C           FLAG      IFNE '0'
     C                     SETON                     9942
     C                     MOVELERR,1     ERRMSG
     C                     ENDIF
     CSR         CK#01Z    ENDSR
     C********************************
     C*****                      *****
     C*****   SCREEN 02 PREPARE  *****
     C*****                      *****
     C********************************
     CSR         PR#02     BEGSR
     C                     MOVE *BLANK    ERRMSG
     C                     SETON                     80
     C                     WRITESFCTL1
     C                     SETOF                     801051
     C                     SETOF                     97
     C                     Z-ADD0         RRN     30
     C                     Z-ADDDATE1     RVDUDT
     C                     MOVE *BLANK    RVNTNO
     C           RVKEY     SETLLRCVDTLL2             97
     C           *IN97     DOWEQ'0'
     C                     READ RCVDTLL2                 97
     C  N97      RVDUDT    COMP DATE2                97
     C   97                LEAVE
     C           RVACNO    COMP '1131'               3131
0109AC   31      RVACNO    COMP '111001'             3131
     C**N31      RVVNO2    COMP *BLANK               3131
     C  N31      RVNTNO    COMP *BLANK                   31
     C   31                ITER
     C           TXAR      COMP RVAREA               3131
     C   31                ITER
     C           RVNTCD    COMP '1'                  3131
     C   31                ITER
     C                     MOVELRVSANO    PID
     C                     MOVELPID1      ACTYPE
     C                     MOVELPID2      ACCURR
     C                     MOVELPID3      ACACNO
     C           ACKEY     CHAINBANKAC               31
     C   31                MOVE *BLANK    ACBNAM
     C                     MOVELACBNAM    W01
     C                     MOVELRVSANO    W02
     C                     MOVELRVNTNO    W03
     C                     Z-ADDRVDATE    W04
     C                     Z-ADDRVDUDT    W05
     C                     Z-ADDRVRAMT    W06
     C                     MOVELRVTANO    PID
     C                     MOVELPID1      ACTYPE
     C                     MOVELPID2      ACCURR
     C                     MOVELPID3      ACACNO
     C           ACKEY     CHAINBANKAC               31
     C   31                MOVE *BLANK    ACBNAM
     C                     MOVELACBNAM    W21
     C                     MOVELRVTANO    W22
     C                     MOVELRVNO      W23
     C                     MOVELRVCUNM    W24
     C                     MOVELRVVNO2    W25
     C                     MOVE *BLANK    W26
     C           RVPLAC    COMP 'D'                      31
     C   31                MOVEL'本埠'  W26
     C           RVPLAC    COMP 'E'                      31
     C   31                MOVEL'外埠'  W26
     C                     MOVELRVUSTP    W27
     C                     ADD  1         RRN
     C                     WRITESF1
     C           RRN       IFGT R#L01
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C           PR#02Y    TAG
     C           RRN       IFEQ 0
     C                     CLEARSF1
     C                     MOVEL'END DATA'W01
     C                     ADD  1         RRN
     C                     SETON                     51
     C                     WRITESF1
     C                     SETOF                     51
     C                     ENDIF
      *
     C                     SETON                     10
     CSR         PR#02Z    ENDSR
      ******************************* END PROGRAM *****************
** ERR
日期錯誤
日期範圍錯誤
處理代碼錯誤
銀行代號錯誤
銀行帳號錯誤
此筆資料已兌現不可再兌現
此筆資料未兌現不可取消
