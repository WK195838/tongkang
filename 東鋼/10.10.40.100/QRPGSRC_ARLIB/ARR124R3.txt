     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR124R3
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02LBU
     H*            4.FUNCTION     客戶暫收款餘額核對清單-以營業核對財會
     H*            5.DATE-WRITTEN  95/03/29
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FAR2159D IP  E           K        DISK
     FAR2159X IF  E           K        DISK
     FCBCUST  IF  E           K        DISK
     FARR124F O   E                    DISK
     F*****************************************************************
     IRAR2159D    01
     I*
     I           UDS
     I                                     10211021 D#AREA
     I*
     C*****************************************************************
     C*          KEY       LIST
     C*****************************************************************
     C* FILE => AR2159X (客戶暫收統計檔-財會)
     C*
     C           K#2159    KLIST
     C                     KFLD           K#CUNO  6        客戶編號
     C                     KFLD           K#ORNO 10        訂單編號
     C*
     C*****************************************************************
     C*          MAIN      PROGRAM
     C*****************************************************************
     C   01                EXSR DTLSR
     CLR                   RETRN
     C*
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     Z-ADD0         W#AMT  110
     C*
     C*給定AR2159X檔KEY值
     C*
     C                     MOVELADCUNO    K#CUNO           客戶代號
     C                     MOVELADORNO    K#ORNO           訂單編號
     C*
     C                     MOVE *OFF      *IN50
     C           K#2159    CHAINAR2159X              50
     C*
     C           ADAMT1    ADD  ADAMT3    W#AMT
     C                     ADD  ADAMT4    W#AMT
     C                     SUB  ADAMT2    W#AMT
     C*
     C*台北與廠區不同之處為不用分廠區挑選資料，
     C*台北可看所有廠資料，但廠區只能看自己。
     C*
     C           W#AMT     IFEQ 0
     C           *IN50     OREQ *OFF
     C*          ADAREA    ORNE D#AREA
     C                     GOTO ENDTSR
     C                     ENDIF
     C*
     C                     Z-ADDW#AMT     W1AMT1           營暫收餘額
     C                     Z-ADD0         W1AMT2           財暫收餘額
     C                     MOVELADCUNO    W1CUNO           客戶代號
     C                     MOVELADORNO    W1ORNO           訂單編號
     C                     MOVEL'1'       W1MODE           資料來源
     C*
     C                     MOVEL*OFF      *IN51
     C           W1CUNO    CHAINCBCUST               51
     C  N51                MOVELCBCUNM    W1CUNM           客戶簡稱
     C   51                MOVEL*ALL'*'   W1CUNM
     C*
     C                     WRITERARR124F
     C*
     C           ENDTSR    ENDSR
     C*****************************************************************
