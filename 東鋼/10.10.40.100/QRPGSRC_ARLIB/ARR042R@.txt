     H        1   Y                                     1                 BR003
     FINVDTL  IP  E                    DISK
     FINVMST  IF  E           K        DISK
     FQPRINT  O   F     132            PRINTER
      *************************************************************
     E*                   TAB1    1  10  1   TABR1  10
     I*************************************************************
     IIVREC
     I                                              IVAPNOL2
     I                                              IVORNOL1
     I****
     IAADS        DS
     I              'も收款沖銷明細表も'C         CON3                       X
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
     IBBDS        DS
     I                                        1  10 XXXX
     I                                        1   3 BAID1
     I                                        4   7 BAID2
      *
     I                                       21  260YMD
     I                                       21  240YM
     I                                       21  220YY
     I                                       23  240MM
     I                                       25  260DD
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L2                EXSR DL#L2
     C   L1                EXSR DL#L1
     C                     EXSR DP#01
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     CLR                   EXSR TL#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C           *ENTRY    PLIST
     C                     PARM           DATE1   60
     C                     PARM           DATE2   60
     C*          BKKEY     KLIST
     C*                    KFLD           BAID1
     C*                    KFLD           BAID2
      *
     C                     Z-ADD0         PAG     30
     C                     Z-ADD0         AMT    110
     C                     Z-ADD0         GAMT   110
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD56        TCNT    30
     C                     MOVE *ALL'='   ALS   132
     C                     MOVE *ALL'-'   ALQ    96
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP TCNT                 75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD8         LCNT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         TAMT   110
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         AAMT   110
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C           IVNO      CHAININVMST               31
     C   31                MOVE *BLANK    INCUNO
     C   31                MOVE *BLANK    INCUNM
     C   31                MOVE *BLANK    INAPNO
     C   31                Z-ADD0         ININDT
      *
     C                     Z-SUBIVAMT     AMT
     C                     SUB  IVAMT     AAMT
     C                     SUB  IVAMT     TAMT
     C                     SUB  IVAMT     GAMT
      *
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#01
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C                     EXCPTET#L1
     C                     ADD  3         LCNT
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C                     ADD  2         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  2         LCNT
     C                     EXCPTET#L2
     CSR         TL#L2Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME LR PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     C                     ADD  2         LCNT
     CSR         TL#LRZ    ENDSR
      ***********************************************************
      ****   DEFINE OUPUT FORMAT                             ****
      ***********************************************************
     OQPRINT  E   02           EH#01
     O                                   17 '報表編號: AR042'
     O                         CON4      75                                    X
     O                                  123 '製表日期'
     O                                  124 ':'
     O                         UDATE Y  132
     O        E 1              EH#01
     O                         CON3      71                                    X
     O                                  123 '頁　　次'
     O                                  124 ':'
     O                         PAG   Z  127
     O        E 2              EH#01
     O                                   58 '繳款日期:'
     O                         DATE1   +  1 '  /  /  '
     O                                 +  1 '-'
     O                         DATE2   +  1 '  /  /  '
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              EH#01
     O                                   12 '繳款單號'
     O                                   23 '客戶編號'
     O                                   34 '客戶名稱'
     O                                   45 '訂單編號'
     O                                   60 '發票號碼'
     O                                   75 '發票日期'
     O                                   88 '入帳日期'
     O                                  102 '請款單號'
     O                                  120 '金　額'
     O        E 1              EH#01
     O                         ALS      132
     O        E 1              ED#01
     O                 L2      IVAPNO    11
     O                 L2      INCUNO    20
     O                 L2      INCUNM    34
     O                 L1      IVORNO    43
     O                         IVNO      61
     O                         ININDT    74 '  /  /  '
     O                         IVACDT    87 '  /  /  '
     O                         INAPNO   100
     O                         AMT   J  120
     O        E 1              ET#L1
     O                         ALQ      132
     O        E 1              ET#L1
     O                                  105 '訂單小計:'
     O                         AAMT  J  120
     O        E 1              ET#L1
     O        E 1              ET#L2
     O                                  105 '繳款單合計:'
     O                         TAMT  J  120
     O        E 1              ET#L2
     O        E 1              ET#LR
     O                                  105 '總　　計:'
     O                         GAMT  J  120
     O        E 1              ET#LR
     O                         ALS      132
