     H        1   Y                                     1                 BR003
     FINVDTL  IP  E                    DISK
     FINVMST  IF  E           K        DISK
     FQPRINT  O   F     100            PRINTER
      *************************************************************
     IIVREC
     I                                              IVPDCDL2
     I                                              IVNO  L1
     I****
     IAADS        DS
     I              'も８２年度開立其他-C         CON3                       X
     I              '類發票統計表も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
      *
     I           UDS
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L1                EXSR DL#L1
     C   L2                EXSR DL#L2
     C  N31                EXSR DP#01
     C*
     CL1N31                EXSR TL#L1
     CL2                   EXSR TL#L2
     CLR                   EXSR TL#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD0         AMTR   110
     C                     Z-ADD59        OVRFLW  30
     C                     MOVE *ALL'='   ALS   100
     C                     MOVE *ALL'-'   ALQ    50
     C                     MOVE *ALL'*'   ALP    50
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP OVRFLW               75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD10        LCNT
     C   75                SETON                     21
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     SETON                     21
     C                     Z-ADD0         AMT2   110
     C                     Z-ADD0         CNT2    20
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         CNT1    20
     C                     Z-ADD0         AMT1    90
     C           IVNO      CHAININVMST               31
     C  N31      INDECD    COMP 'D'                      31
     C   31                Z-ADD0         ININDT
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     ADD  IVAMT     AMT1    90
      *
     CSR         DP#01Z    ENDSR
     C***
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#01
      *
     C                     ADD  AMT1      AMT2
     C   21                SETOF                     21
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C                     EXCPTET#L2
     C                     ADD  2         LCNT
     C                     ADD  AMT2      AMTR
     CSR         TL#L2Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME LR PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     CSR         TL#LRZ    ENDSR
      ***********************************************************
      ****   DEFINE OUPUT FORMAT                             ****
      ***********************************************************
     OQPRINT  E   03           EH#01
     O                                   17 '報表編號: AR058'
     O                         CON4      53                                    X
     O                                   75 '製表日期'
     O                                   76 ':'
     O                         UDATE Y   84
     O        E 1              EH#01
     O                         CON3      60                                    X
     O                                   75 '頁　　次'
     O                                   76 ':'
     O                         PAG   Z   79
     O        E 2              EH#01
     O                                   11 '起迄期間:'
     O                                 +  1 '82/01/01 - 82/12/31'
     O        E 1              EH#01
     O                         ALS      100
     O        E 1              EH#01
     O                                   10 '產品類別'
     O                                   24 '發票號碼'
     O                                   38 '發票日期'
     O                                   52 '客戶編號'
     O                                   66 '客戶名稱'
     O                                   80 '開立廠別'
     O                                  100 '稅基金額'
     O        E 1              EH#01
     O                         ALS      100
     O        E 1              ED#01
     O                 21      IVPDCD     7
     O                         IVNO      23
     O                         ININDTY   37
     O                         INCUNO    49
     O                         INCUNM    66
     O                         INAREA    76
     O                         AMT1  N   99
     O        E 1              ET#L2
     O                                   70 '類別小計：'
     O                         AMT2  N   99
     O        E 1              ET#L2
     O                         ALP      100
     O        E 1              ET#LR
     O                                   70 '　總合計：'
     O                         AMTR  N   99
     O        E 1              ET#LR
     O                         ALQ      100
