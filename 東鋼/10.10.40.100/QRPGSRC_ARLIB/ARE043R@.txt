     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARE043R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     客戶暫收款編修程式
     H*            5.DATE-WRITTEN  88/11/31
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARE043S CF  E                    WORKSTN
     F                                        RRN1  KSFILE AR043F1
     FAR2159F UF  E           K        DISK                      A
     FCBCUST  IF  E           K        DISK
     FCAMBALT O   E           K        DISK
     E*************************************************************
     E                    T#MSG   1   8 70
     E                    TAB1    1   5  1
     I*************************************************************
     I           UDS
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     I            DS
     I                                        1  140W#SYST
     I                                        1   60D#ST
     I                                        7  100D#SY
     I                                       11  140D#SMD
     I            DS
     I                                        1  10 W#DSPN
     I                                        9  10 D#DSIT
     C**************************************************************
     C*   檔案搜尋欄位組合
     C**************************************************************
     C           K#AR      KLIST                           暫收統計檔
     C                     KFLD           ARCUNO
     C                     KFLD           ARAREA
     C**************************************************************
     C*   主程式開始
     C**************************************************************
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C           W#PRID    CASEQ'02'      SR2000           畫面二
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C*   副程式開始
     C**************************************************************
     C*----------------------------------------
     C*  宣告及初始變數
     C*----------------------------------------
     CSR         SR0000    BEGSR
     C                     Z-ADD10        N#PAG1  20       SF1筆數
     C                     MOVEL'01'      W#PRID  2        函式代號
     C                     MOVEL'F'       W#RTNV  1        函式返回值
     C                     MOVEL'F'       W#SFE1  1        SF1結束
     C                     Z-ADD0         RRN1    40
     C*
     C                     MOVEL*BLANK    S#INK1
     C                     EXSR SR1100                     初始畫面一
     C                     SETON                     51
     CSR                   ENDSR
     C*
     C************************************
     C*  畫面一:選擇客戶編號
     C************************************
     CSR         SR1000    BEGSR
     C                     WRITEAR043F1M
     C           S#SFN1    IFEQ 0
     C                     SETOF                     7273
     C                     SETON                     71
     C                     ELSE                            有資料
     C                     SETOF                     73
     C                     SETON                     7172
     C                     ENDIF
     C           W#SFE1    IFEQ 'T'
     C                     SETON                     74
     C                     ELSE
     C                     SETOF                     74
     C                     ENDIF
     C                     EXFMTAR043F1C
     C*
     C                     SETOF                     51
     C                     MOVEL*BLANK    S#MSG1
     C           S#CRN1    IFNE 0
     C                     Z-ADDS#CRN1    S#NBR1
     C                     ENDIF
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C           *IN12     OREQ '1'
     C                     MOVEL'00'      W#PRID
     C           *IN91     WHEQ '1'                        向下翻頁
     C                     EXSR SR1300
     C           *IN92     WHEQ '1'                        向上翻頁
     C                     EXSR SR1400
     C           *IN05     WHEQ '1'                        重新顯示
     C                     EXSR SR1500
     C           *IN06     WHEQ '1'                        新增
     C                     EXSR SR1600
     C*
     C                     OTHER                           執行鍵
     C                     EXSR SR1200
     C                     ENDSL
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  初始畫面一(指向指定的記錄)
     C*----------------------------------------
     CSR         SR1100    BEGSR
     C                     MOVELS#INK1    ARCUNO
     C           ARCUNO    SETLLARREC                69
     C           *IN69     IFEQ '1'
     C           *HIVAL    SETGTARREC                69
     C                     EXSR SR1130                     向前讀一頁
     C                     ENDIF
     C*
     C                     EXSR SR1110                     清除SF
     C                     EXSR SR1120                     讀入SF
     C*
     C                     Z-ADD1         S#NBR1           指向第一筆
     CSR         ES1100    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一清除SF
     C*----------------------------------------
     CSR         SR1110    BEGSR
     C                     MOVEL*BLANK    S#MSG1
     C                     SETOF                     7172
     C                     SETON                     73    清除 SF
     C                     WRITEAR043F1C
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一向下讀入SF (從目前的檔案位置)
     C*----------------------------------------
     CSR         SR1120    BEGSR
     C                     MOVEL'F'       W#SFE1
     C                     Z-ADD0         S#SFN1
     C                     Z-ADD0         RRN1
     C*
     C           1         DOWEQ1                          讀取迴圈
     C                     READ ARREC               N    69
     C           *IN69     IFEQ '1'
     C                     MOVEL'T'       W#SFE1
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ADD  1         S#SFN1
     C           S#SFN1    IFGT N#PAG1                     已滿一頁
     C                     Z-ADDN#PAG1    S#SFN1
     C                     LEAVE
     C                     ENDIF
     C                     ADD  1         RRN1
     C                     CLEARAR043F1
     C                     EXSR SR1121                     搬移資料
     C                     WRITEAR043F1
     C                     ENDDO                           讀取迴圈
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一向讀入記錄之搬移資料
     C*----------------------------------------
     CSR         SR1121    BEGSR
     C                     MOVELARCUNO    S#CUNO
     C                     MOVEL*BLANK    S#CUNM
     C           S#CUNO    CHAINCBCUST               69
     C  N69                MOVELCBCUNM    S#CUNM
     C                     MOVELARAREA    S#AREA
     C*
9010 C           ARAMT1    ADD  ARAMT3    S#TAMT
9010 C                     ADD  ARAMT4    S#TAMT
9010 C*                    Z-ADDARAMT1    S#AMT1
     C                     Z-ADDARAMT2    S#AMT2
9010 C*                    Z-ADDARAMT3    S#AMT3
9010 C           ARAMT1    ADD  ARAMT4    S#NBAL
9010 C                     SUB  ARAMT2    S#NBAL
     C*
     C                     MOVELARRESV    S#RESV
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一向上移動記錄(從目前的檔案位置)
     C*----------------------------------------
     CSR         SR1130    BEGSR
     C                     Z-ADD0         S#SFN1
     C*
     C           1         DOWEQ1
     C                     READPARREC               N    69
     C           *IN69     IFEQ '1'
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ADD  1         S#SFN1
     C           S#SFN1    IFGT N#PAG1                     多一筆
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C*
     C           *IN69     IFEQ '1'
     C           *LOVAL    SETLLARREC                69
     C                     MOVEL'T'       W#SFB1  1        已達檔頭
     C                     ELSE
     C                     MOVEL'F'       W#SFB1           未達檔頭
     C                     ENDIF
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面一：執行
     C*----------------------------------------
     CSR         SR1200    BEGSR
     C           S#INK1    IFNE *BLANK
     C                     EXSR SR1100
     C                     MOVEL*BLANK    S#INK1
     C                     GOTO ES1200
     C                     ENDIF
     C*
     C           1         DO   S#SFN1    RRN1
     C           RRN1      CHAINAR043F1              69
     C                     SELEC
     C           S#OPT1    WHEQ '2'
     C                     Z-ADDRRN1      S#NBR1
     C                     MOVEL*BLANK    S#OPT1
     C                     UPDATAR043F1
     C                     MOVEL'02'      W#PRID
     C                     MOVEL'C'       W#EDTP
     C                     EXSR SR2100
     C                     GOTO ES1200
     C                     ENDSL
     C                     ENDDO
     C*
     C                     SETON                     51
     CSR         ES1200    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一：向下翻一頁
     C*----------------------------------------
     CSR         SR1300    BEGSR
     C           W#SFE1    IFEQ 'T'
     C                     MOVELT#MSG,2   S#MSG1           已達檔底
     C                     GOTO ES1300
     C                     ENDIF
     C*
     C           S#SFN1    IFNE 0                          本頁有資料
     C                     Z-ADDS#SFN1    RRN1
     C           RRN1      CHAINAR043F1              69
     C                     MOVELS#CUNO    ARCUNO
     C                     MOVELS#AREA    ARAREA
     C           K#AR      CHAINARREC               N69    移至本頁尾
     C                     ENDIF                           本頁有資料
     C*
     C                     EXSR SR1110                     清除SF
     C                     EXSR SR1120                     讀入SF
     C           S#NBR1    IFGT S#SFN1
     C                     Z-ADD1         S#NBR1
     C                     ENDIF
     CSR         ES1300    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一：向上翻一頁
     C*----------------------------------------
     CSR         SR1400    BEGSR
     C           S#SFN1    IFNE 0                          本頁有資料
     C                     Z-ADD1         RRN1
     C           RRN1      CHAINAR043F1              69
     C                     MOVELS#CUNO    ARCUNO
     C                     MOVELS#AREA    ARAREA
     C           K#AR      CHAINARREC               N69    移至本頁頭
     C                     ENDIF                           本頁有資料
     C*
     C                     EXSR SR1130                     向前移動
     C                     EXSR SR1110                     清除SF
     C                     EXSR SR1120                     讀入SF
     C*
     C           W#SFB1    IFEQ 'T'
     C                     MOVELT#MSG,3   S#MSG1           己達檔頭
     C                     ENDIF
     CSR         ES1400    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一：重新顯示
     C*----------------------------------------
     CSR         SR1500    BEGSR
     C           S#SFN1    IFEQ 0                          本頁無資料
     C           *HIVAL    SETGTARREC
     C                     EXSR SR1130                     向前讀一頁
     C*
     C                     ELSE
     C                     Z-ADD1         RRN1
     C           RRN1      CHAINAR043F1              69
     C                     MOVELS#CUNO    ARCUNO
     C                     MOVELS#AREA    ARAREA
     C           K#AR      CHAINARREC               N69    移至本頁頭
     C                     READPARREC               N    69前移一筆
     C           *IN69     IFEQ '1'
     C           *LOVAL    SETLLARREC
     C                     ENDIF
     C                     ENDIF
     C*
     C                     EXSR SR1110                     清除SF
     C                     EXSR SR1120                     讀入SF
     C*
     C           S#NBR1    IFGT S#SFN1
     C                     Z-ADD1         S#NBR1
     C                     ENDIF
     CSR         ES1500    ENDSR
     C*
     C*----------------------------------------
     C*  畫面一：新增
     C*----------------------------------------
     CSR         SR1600    BEGSR
     C                     MOVEL'02'      W#PRID
     C                     MOVEL'A'       W#EDTP  1
     C                     EXSR SR2100
     CSR                   ENDSR
     C*
     C************************************
     C*  畫面二:編輯資料
     C************************************
     CSR         SR2000    BEGSR
     C                     EXFMTAR043F2                    螢幕輸入
     C*
     C                     MOVEL*BLANK    S#MSG2
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C                     MOVEL'00'      W#PRID           結束
     C           *IN12     WHEQ '1'
     C                     MOVEL'01'      W#PRID           回前畫面
     C                     EXSR SR1500
     C           *IN10     WHEQ '1'
     C                     EXSR SR2300                     存檔
     C           *IN91     WHEQ '1'
     C                     EXSR SR2400                     上筆
     C           *IN92     WHEQ '1'
     C                     EXSR SR2500                     下筆
     C*
     C                     OTHER
     C                     EXSR SR2200                     檢核
     C                     ENDSL
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面二：初始
     C*----------------------------------------
     CSR         SR2100    BEGSR
     C           W#EDTP    IFEQ 'C'
     C                     MOVEL'修改'  S#EDTP
     C                     SETON                     52
     C           S#CUNO    CHAINCBCUST               69
     C  N69                MOVELCBCUNM    S#CUNM
     C*
     C                     MOVELS#CUNO    ARCUNO
     C                     MOVELS#AREA    ARAREA
     C           K#AR      CHAINARREC               N69
9010 C           ARAMT1    ADD  ARAMT3    S#TAMT
9010 C                     ADD  ARAMT4    S#TAMT
     C                     Z-ADDARAMT1    S#AMT1
     C                     Z-ADDARAMT2    S#AMT2
     C                     Z-ADDARAMT3    S#AMT3
9010 C                     Z-ADDARAMT4    S#AMT4
9010 C*          ARAMT1    ADD  ARAMT3    S#NBAL
9010 C*                    SUB  ARAMT2    S#NBAL
9010 C           ARAMT1    ADD  ARAMT4    S#NBAL
9010 C                     SUB  ARAMT2    S#NBAL
     C                     MOVELARRESV    S#RESV
     C                     ENDIF
     C*
     C           W#EDTP    IFEQ 'A'
     C                     MOVEL'新增'  S#EDTP
     C                     SETOF                     52
     C                     MOVEL*BLANK    S#CUNO
     C                     MOVEL*BLANK    S#CUNM
     C                     MOVELU#AREA    S#AREA
     C                     Z-ADD0         S#AMT1
     C                     Z-ADD0         S#AMT2
     C                     Z-ADD0         S#AMT3
9010 C                     Z-ADD0         S#AMT4
     C                     Z-ADD0         S#NBAL
     C                     MOVEL*BLANK    S#RESV
     C                     ENDIF
     C*
     C                     SETOF                     535455
     CSR                   ENDSR
     C*
     C*----------------------------------------
     C*  畫面二：檢核
     C*----------------------------------------
     CSR         SR2200    BEGSR
     C                     MOVEL'F'       W#RTNV  1
     C                     SETOF                     535455
     C*
     C           W#EDTP    IFEQ 'A'                        新增模式
     C                     MOVELS#CUNO    ARCUNO
     C                     MOVELS#AREA    ARAREA
     C           K#AR      CHAINARREC               N69
     C           *IN69     IFEQ '0'                        已存在
     C                     SETON                     53
     C                     MOVELT#MSG,4   S#MSG2
     C                     GOTO ES2200
     C                     ENDIF
     C                     ENDIF                           新增模式
     C*
     C           S#AREA    LOKUPTAB1                     69
     C           *IN69     IFEQ '0'                        廠區不符
     C                     SETON                     54
     C                     MOVELT#MSG,5   S#MSG2
     C                     GOTO ES2200
     C                     ENDIF
     C*
     C           S#AMT2    IFGT 0
     C           S#AMT1    IFGT 0
     C                     MOVELT#MSG,8   S#MSG2
     C                     SETON                     55
     C                     GOTO ES2200
     C                     ENDIF
     C                     ENDIF
     C*
     C                     MOVEL'T'       W#RTNV
     CSR         ES2200    ENDSR
     C*
     C*----------------------------------------
     C*  畫面二：存檔
     C*----------------------------------------
     CSR         SR2300    BEGSR
     C                     EXSR SR2200
     C           W#RTNV    IFEQ 'F'
     C                     GOTO ES2300
     C                     ENDIF
     C*
     C                     MOVELS#CUNO    ARCUNO
     C                     MOVELS#AREA    ARAREA
     C           K#AR      CHAINARREC                69
     C*
     C                     EXSR SR2310                     授信集中
     C*
     C  N69                MOVEL'C'       ARFLAG
     C   69                MOVEL'A'       ARFLAG
     C*
     C                     MOVELS#CUNO    ARCUNO
     C                     MOVELS#AREA    ARAREA
     C                     Z-ADDS#AMT1    ARAMT1
     C                     Z-ADDS#AMT2    ARAMT2
     C                     Z-ADDS#AMT3    ARAMT3
9010 C                     Z-ADDS#AMT4    ARAMT4
     C                     Z-ADD0         ARAMT5
     C                     Z-ADD0         ARAMT6
     C                     MOVELS#RESV    ARRESV
     C*
     C  N69                UPDATARREC
     C   69                WRITEARREC
     C*
     C           W#EDTP    IFEQ 'A'
     C                     EXSR SR2100                     初始畫面
     C                     MOVELT#MSG,7   S#MSG2
     C                     ELSE
     C                     MOVELT#MSG,1   S#MSG2
     C                     ENDIF
     CSR         ES2300    ENDSR
     C*
     C*----------------------------------------
     C*  畫面二：下筆
     C*----------------------------------------
     CSR         SR2400    BEGSR
     C           W#EDTP    IFEQ 'A'                        新增不可
     C                     MOVELT#MSG,6   S#MSG2
     C                     GOTO ES2400
     C                     ENDIF
     C*
     C                     MOVELS#CUNO    ARCUNO
     C                     MOVELS#AREA    ARAREA
     C           K#AR      CHAINARREC               N69
     C                     READ ARREC               N    69
     C           *IN69     IFEQ '0'
     C                     MOVELARCUNO    S#CUNO
     C                     MOVELARAREA    S#AREA
     C                     EXSR SR2100                     初始畫面
     C                     ELSE
     C                     MOVELT#MSG,2   S#MSG2
     C                     ENDIF
     CSR         ES2400    ENDSR
     C*
     C*----------------------------------------
     C*  畫面二：上筆
     C*----------------------------------------
     CSR         SR2500    BEGSR
     C           W#EDTP    IFEQ 'A'                        新增不可
     C                     MOVELT#MSG,6   S#MSG2
     C                     GOTO ES2500
     C                     ENDIF
     C*
     C                     MOVELS#CUNO    ARCUNO
     C                     MOVELS#AREA    ARAREA
     C           K#AR      CHAINARREC               N69
     C                     READPARREC               N    69
     C           *IN69     IFEQ '0'
     C                     MOVELARCUNO    S#CUNO
     C                     MOVELARAREA    S#AREA
     C                     EXSR SR2100                     初始畫面
     C                     ELSE
     C                     MOVELT#MSG,3   S#MSG2
     C                     ENDIF
     CSR         ES2500    ENDSR
     C*
     C*==============================================================
     C*  扣預收款寫入授信集中管控異動記錄並傳回值
     C*==============================================================
     CSR         SR2310    BEGSR
     C           *IN69     IFEQ '0'                        記錄存在
     C                     Z-ADDARAMT1    W#AMT1 110
     C                     ELSE
     C                     Z-ADD0         W#AMT1
     C                     ENDIF
     C*
     C           S#AMT1    SUB  W#AMT1    W#XAMT 110       異動金額
     C           W#XAMT    IFEQ 0
     C                     GOTO ES2310
     C                     ENDIF
     C*
     C                     MOVELS#DEVI    W#DSPN           更改終端機
     C           W#DSIT    IFEQ 99                         以避免重複
     C                     Z-ADD0         W#DSIT  20
     C                     ELSE
     C                     ADD  1         W#DSIT
     C                     ENDIF
     C                     MOVE W#DSIT    D#DSIT
     C*
     C                     CLEARCTREC
     C                     Z-ADDUDATE     CTDATE           日期
     C                     TIME           W#SYST
     C                     Z-ADDD#ST      CTTIME           時間
     C                     MOVELU#AREA    CTAREA           廠區
     C                     MOVELW#DSPN    CTDSPN           終端機
     C                     MOVELU#USID    CTUSER           使用者
     C                     MOVELS#CUNO    CTCUNO           客戶
     C                     MOVEL'AC01'    CTTXID           異動代號
     C                     MOVEL'ARE043R' CTAPNO           單據編號
     C                     Z-ADDW#XAMT    CTXAMT           異動金額
     C                     WRITECTREC
     C*
     C           'CCLIB/CC'CAT  'P300P':0 W#PGMN 13         CCP300P
     C                     CALL W#PGMN                     呼叫介面
     CSR         ES2310    ENDSR
     C*
     C**************************************************************
** T#MSG
０１－資料異動已存檔完畢。
０２－已達檔底。
０３－已達檔頭。
０４－輸入之客戶及廠區暫收款記錄已存在，請用修改的方式異動資料。
０５－輸入的廠區不存在！
０６－新增狀態不可移動記錄！
０７－新增記錄已存檔完畢，要終止新增作業請按F12。
０８－暫收繳貨款金額不正確，尚有現金可扣抵！
** TAB1
P
M
T
H
K
