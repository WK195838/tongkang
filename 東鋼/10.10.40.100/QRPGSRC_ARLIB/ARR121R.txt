     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR121R
     H*            2.PROGRAM-TYPE  RPG
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     客戶別押匯明細表
     H*            5.DATE-WRITTEN  94/08/08
     H*            6.DATE-MODIFY
     H*
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR121F IP  E           K        DISK
     FARR121T O   E             69     PRINTER
     E*****************************************************************
     IRAR121F
     I                                              A2AREAL2
     I                                              A2TXNOL1
     I           UDS
     I                                     10011010 U#USID
     I                                      300 305 U#CUNO
     I                                      306 3130U#DAT1
     I                                      314 3210U#DAT2
     I                                      322 322 U#AREA
     C*****************************************************************
     C  N99                EXSR INISR
     C   L2                EXSR L2CLR
     C   L1                EXSR L1CLR
     C                     EXSR DTLSR
     CL1 99                EXSR L1SR
     CL2 99                EXSR L2SR
     CLR 99                EXSR LRSR
     CLRN99                EXSR MSGSR
     CLR                   RETRN
     C*****************************************************************
     C           INISR     BEGSR
     C*****************************************************************
     C                     MOVE *ON       *IN99
     C                     MOVE *ON       *IN69
     C*
     C                     MOVEL*ALL'='   T#LIN1
     C                     MOVEL*ALL'='   T#LIN2
     C*
     C                     MOVELU#CUNO    T#CUNH
     C                     Z-ADDU#DAT1    T#DAT1
     C                     Z-ADDU#DAT2    T#DAT2
     C*
     C                     ENDSR
     C*****************************************************************
     C           L2CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR121T1T
     C*
     C                     MOVE *ON       *IN69
     C                     MOVEL*BLANK    W#CUNO  6
     C*
     C                     SELEC
     C           A2AREA    WHEQ 'T'
     C                     MOVEL'桃園'  T#AREA
     C           A2AREA    WHEQ 'P'
     C                     MOVEL'台北'  T#AREA
     C           A2AREA    WHEQ 'K'
     C                     MOVEL'高雄'  T#AREA
     C           A2AREA    WHEQ 'H'
     C                     MOVEL'苗栗'  T#AREA
     C           A2AREA    WHEQ 'M'
     C                     MOVEL'台中'  T#AREA
     C                     ENDSL
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     CLEARAR121T1S
     C*
     C                     MOVEL*BLANK    W#ORNO  6
     C                     MOVEL*BLANK    W#TXNO  6
     C                     MOVEL*BLANK    W#IVNO 10
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     CLEARAR121T1D
     C*
     C                     MOVELA2CUNO    T#CUNO           客戶代號
     C                     MOVELA2CUNM    T#CUNM           客戶名稱
     C                     MOVELA2ORNO    T#ORNO           訂單編號
     C                     MOVELA2TXNO    T#TXNO           折讓單號
     C                     MOVELA2IVNO    T#INNO           發票號碼
     C                     Z-ADDA2AMT1    T#AMT1           發票金額
     C                     Z-ADDA2AMT2    T#AMT2           折讓金額
     C                     Z-ADDA2AMT3    T#AMT3           繳款金額
     C*客戶代號相同不印
     C           W#CUNO    IFNE A2CUNO
     C                     MOVELA2CUNO    W#CUNO
     C                     MOVE *OFF      *IN50
     C                     ELSE
     C                     MOVE *ON       *IN50
     C                     ENDIF
     C*訂單編號相同不印
     C           W#ORNO    IFNE T#ORNO
     C                     MOVELA2ORNO    W#ORNO
     C                     MOVE *OFF      *IN51
     C                     ELSE
     C                     MOVE *ON       *IN51
     C                     ENDIF
     C*磅單編號相同不印
     C           W#TXNO    IFNE A2TXNO
     C                     MOVELA2TXNO    W#TXNO
     C                     MOVE *OFF      *IN52
     C                     ELSE
     C                     MOVE *ON       *IN52
     C                     ENDIF
     C*
     C   69                WRITEAR121T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR121T1D
     C*折讓單小計
     C*同一張折讓單內的相同發票，只加一次發票金額與繳款金額
     C           W#IVNO    IFNE A2IVNO
     C                     MOVELA2IVNO    W#IVNO
     C                     ADD  T#AMT1    T#AM1S           發票金額
     C                     ADD  T#AMT3    T#AM3S           繳款金額
     C                     ENDIF
     C                     ADD  T#AMT2    T#AM2S           折讓金額
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C   69                WRITEAR121T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR121T1S
     C*客戶合計
     C                     ADD  T#AM1S    T#AM1T
     C                     ADD  T#AM2S    T#AM2T
     C                     ADD  T#AM3S    T#AM3T
     C*
     C                     ENDSR
     C*****************************************************************
     C           L2SR      BEGSR
     C*****************************************************************
     C   69                WRITEAR121T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR121T1T
     C*
     C                     ENDSR
     C*****************************************************************
     C           LRSR      BEGSR
     C*****************************************************************
     C*
     C                     ENDSR
     C*****************************************************************
     C           MSGSR     BEGSR
     C*****************************************************************
     C                     EXSR INISR
     C*
     C   69                WRITEAR121T1H
     C                     MOVE *OFF      *IN69
     C                     WRITEAR121T1M
     C*
     C                     ENDSR
