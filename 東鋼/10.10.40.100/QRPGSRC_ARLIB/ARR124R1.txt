     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR124R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02LBU
     H*            4.FUNCTION     客戶暫收款餘額核對清單-財會
     H*            5.DATE-WRITTEN  95/03/29
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FACDYBL  IP  E           K        DISK
     FSAMAST  IF  E           K        DISK
     FHSMAST  IF  E           K        DISK
     FAR2159X O   E                    DISK
     F*****************************************************************
     IRACDYBL     01
     I*
     I                                              ACRLNOL1
     I*
     IRHSMAST
     I              S1SALE                          F#SALE
     I              S1SND                           F#SND
     I              S1RECV                          F#RECV
     I*
     I            DS
     I                                        1   6 D#ORNO
     I                                        1   1 D#ORN1
     I                                        2   60D#ORN2
     I*
     I            DS
     I                                        1   6 D#CUNO
     I                                        1   1 D#CUN1
     I                                        2   2 D#CUN2
     I                                        3   5 D#CUN3
     I                                        6   6 D#CUN4
     I*
     C*****************************************************************
     C*          KEY       LIST
     C*****************************************************************
     C* FILE => HSMAST、SAMAST (訂單主檔)
     C*
     C           K#MAST    KLIST
     C                     KFLD           K#OREA  1        地區別
     C                     KFLD           K#ORNO  50       訂單號碼
     C*
     C*****************************************************************
     C*          MAIN      PROGRAM
     C*****************************************************************
     C   L1                EXSR L1CLR
     C   01                EXSR DTLSR
     CL1                   EXSR L1SR
     CLR                   RETRN
     C*
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     Z-ADD0         W#AMT  120
     C                     MOVEL*BLANK    W#ORNO  6
     C*
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C           ACDC      IFEQ 'C'
     C                     ADD  ACAMT     W#AMT            金額
     C                     ELSE
     C                     SUB  ACAMT     W#AMT
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C                     MOVEL*OFF      *IN30
     C                     MOVEL*OFF      *IN31
     C                     CLEARRAR2159X
     C*
     C                     MOVELACRLNO    D#ORNO           訂單編號
     C                     MOVELACRLNO    AXORNO           訂單編號
     C                     MOVELD#ORN1    K#OREA           地區別
     C                     Z-ADDD#ORN2    K#ORNO           訂單號碼
     C*
     C           K#MAST    CHAINSAMAST               30
     C  N30                MOVELS1KIND    D#CUN1           客戶內外銷
     C  N30                MOVELS1CUN1    D#CUN2           客戶地區
     C  N30                MOVELS1CUN2    D#CUN3           客戶流水號
     C  N30                MOVELS1CD01    D#CUN4           客分公司碼
     C  N30                MOVELD#CUNO    AXCUNO           客戶代號
     C  N30                MOVELS1CUNO    AXCUNM           客戶簡稱
     C*
     C           *IN30     IFEQ *ON
     C           K#MAST    CHAINHSMAST               31
     C  N31                MOVELS1KIND    D#CUN1           客戶內外銷
     C  N31                MOVELS1CUN1    D#CUN2           客戶地區
     C  N31                MOVELS1CUN2    D#CUN3           客戶流水號
     C  N31                MOVELS1CD01    D#CUN4           客分公司碼
     C  N31                MOVELD#CUNO    AXCUNO           客戶代號
     C  N31                MOVELS1CUNO    AXCUNM           客戶簡稱
     C                     ENDIF
     C*
     C                     Z-ADDW#AMT     AXAMT            金額
     C*
     C                     MOVELACVRNO    W#VRNO  1
     C           W#VRNO    IFEQ 'H'
     C                     MOVE '1'       AXMODE           來源
     C                     ELSE
     C                     MOVE '2'       AXMODE
     C                     ENDIF
     C*
     C                     WRITERAR2159X
     C*
     C           ENDTSR    ENDSR
     C*****************************************************************
