     H        1   Y                                     1                 BR003
     FINVMST  IP  E                    DISK
     FTRNDTLL2IF  E           K        DISK
     FQPRINT  O   F     100            PRINTER
      *************************************************************
     IINREC
     I                                              INCUNOL2
     I                                              INORNOL1
     I****
     IAADS        DS
     I              'も發票開立與銷貨收-C         CON3                       X
     I              '入差異表も'
     I              '東和鋼鐵企業股份有-C         CON4                       X
     I              '限公司'
      *
     IBBDS        DS
     I                                       21  260YMD
     I                                       21  240YM
     I                                       21  220YY
     I                                       23  240MM
     I                                       25  260DD
     I           UDS
     I                                        1   40YMA
     I                                      951 985 COMP
     I                                     10111020 DEVNM
     I                                     10211021 TXAR
     C**************************************************************
     C  N90                EXSR INZ#01
     C  N90                SETON                     90
     C   L1                EXSR DL#L1
     C   L2                EXSR DL#L2
     C                     EXSR DP#01
     C*
     CL1                   EXSR TL#L1
     CL2                   EXSR TL#L2
     CLR                   EXSR TL#LR
     C**************************************************************
     C*** SET INITIAL
     CSR         INZ#01    BEGSR
     C                     Z-ADD0         PAG     30
     C                     Z-ADD80        LCNT    30
     C                     Z-ADD0         HAMT1   90
     C                     Z-ADD0         HAMT2   90
     C                     Z-ADD0         HAMT3   90
     C                     Z-ADD59        OVRFLW  30
     C                     MOVE *ALL'='   ALS   100
     C                     MOVE *ALL'-'   ALQ    60
     C                     MOVE *ALL'*'   ALP    60
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         @CHKCT    BEGSR
     C           LCNT      COMP OVRFLW               75  75
     C   75                ADD  1         PAG
     C   75                EXCPTEH#01
     C   75                Z-ADD10        LCNT
     C   75                SETOF                     2122
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L2                             **
     C**----------------------------------------------**
     CSR         DL#L2     BEGSR
     C                     Z-ADD0         GAMT1   90
     C                     Z-ADD0         GAMT2   90
     C                     Z-ADD0         GAMT3   90
     C                     SETOF                     22
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS L1                             **
     C**----------------------------------------------**
     CSR         DL#L1     BEGSR
     C                     Z-ADD0         FAMT1   90
     C                     Z-ADD0         FAMT2   90
     C                     Z-ADD0         FAMT3   90
     C                     SETOF                     21
     CSR                   ENDSR
     C**----------------------------------------------**
     C* DETAIL PROCESS                                **
     C**----------------------------------------------**
     CSR         DP#01     BEGSR
     C                     Z-ADD0         TOT1
     C           INKIND    IFEQ '2'
     C           INTXTP    ANDEQ'1'
     C           INAAMT    DIV  1.05      AAMT      H
     C                     ELSE
     C                     Z-ADDINAAMT    AAMT    90
     C                     ENDIF
     C                     MOVELINNO      TXIVNO
     C           INNO      SETLLTRNDTLL2             97
     C           *IN97     DOWEQ'0'
     C           INNO      READETRNDTLL2                 97
     C   97                LEAVE
     C                     ADD  TXAMT     TOT1    90
     C                     ENDDO
     C***
     C           AAMT      IFNE TOT1
     C           AAMT      SUB  TOT1      TOT2    90
     C                     ADD  1         LCNT
     C                     EXSR @CHKCT
     C   75                ADD  1         LCNT
     C                     EXCPTED#01
     C                     ADD  AAMT      FAMT1
     C                     ADD  TOT1      FAMT2
     C                     ADD  TOT2      FAMT3
     C  N21                SETON                         21
     C  N22                SETON                         22
     C                     ENDIF
     CSR         DP#01Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L1 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L1     BEGSR
     C           FAMT1     IFNE 0
     C           FAMT2     ANDNE0
     C                     EXCPTET#L1
     C                     ADD  3         LCNT
     C                     ADD  FAMT1     GAMT1
     C                     ADD  FAMT2     GAMT2
     C                     ADD  FAMT3     GAMT3
     C                     ENDIF
     CSR         TL#L1Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME L2 PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#L2     BEGSR
     C           GAMT1     IFNE 0
     C           GAMT2     ANDNE0
     C                     EXCPTET#L2
     C                     ADD  2         LCNT
     C                     ADD  GAMT1     HAMT1
     C                     ADD  GAMT2     HAMT2
     C                     ADD  GAMT3     HAMT3
     C                     ENDIF
     CSR         TL#L2Z    ENDSR
     C**-----------------------------------------**
     C**  TOTAL TIME LR PROCESS                  **
     C**-----------------------------------------**
     CSR         TL#LR     BEGSR
     C                     EXCPTET#LR
     CSR         TL#LRZ    ENDSR
      ***********************************************************
      ****   DEFINE OUPUT FORMAT                             ****
      ***********************************************************
     OQPRINT  E   03           EH#01
     O                                   17 '報表編號: AR049'
     O                         CON4      53                                    X
     O                                   75 '製表日期'
     O                                   76 ':'
     O                         UDATE Y   84
     O        E 1              EH#01
     O                         CON3      56                                    X
     O                                   75 '頁　　次'
     O                                   76 ':'
     O                         PAG   Z   79
     O        E 2              EH#01
     O                                   11 '截止年月:'
     O                         YMA     +  1 '  /  '
     O        E 1              EH#01
     O                         ALS      100
     O        E 1              EH#01
     O                                   10 '客戶編號'
     O                                   20 '客戶名稱'
     O                                   30 '訂單號碼'
     O                                   40 '發票號碼'
     O                                   50 '磅單號碼'
     O                                   66 '發票出貨金額'
     O                                   82 '銷貨收入金額'
     O                                   98 '差異金額'
     O        E 1              EH#01
     O                         ALS      100
     O        E 1              ED#01
     O                N22      INCUNO     8
     O                N22      INCUNM    20
     O                N21      INORNO    26
     O                         INNO      40
     O                         TXNO      49
     O                         AAMT  N   65
     O                         TOT1  N   81
     O                         TOT2  N   97
     O        E 1              ET#L1
     O                         ALP       98
     O        E 1              ET#L1
     O                                   50 '訂單小計：'
     O                         FAMT1 N   65
     O                         FAMT2 N   81
     O                         FAMT3 N   97
     O        E 1              ET#L1
     O                         ALP       98
     O        E 1              ET#L2
     O                                   50 '客戶小計：'
     O                         GAMT1 N   65
     O                         GAMT2 N   81
     O                         GAMT3 N   97
     O        E 1              ET#L2
     O                         ALP       98
     O        E 1              ET#LR
     O                                   50 '　總合計：'
     O                         HAMT1 N   65
     O                         HAMT2 N   81
     O                         HAMT3 N   97
     O        E 1              ET#LR
     O                         ALQ       98
