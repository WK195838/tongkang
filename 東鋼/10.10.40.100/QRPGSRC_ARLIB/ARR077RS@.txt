     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR077RS
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CLJ
     H*            4.FUNCTION     銷貨帳款客戶帳齡分析表－畫面
     H*            5.DATE-WRITTEN  88/06/10
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FARR077S CF  E                    WORKSTN
     E*************************************************************
     E                    T#MSG   1   4 70
     I*************************************************************
     IAXDTA      UDS                             28
     I                                        1   4 X#DAY1
     I                                        5   8 X#DAY2
     I                                        9  12 X#DAY3
     I                                       21  28 X#ETXD
     I           UDS
     I                                        1   4 U#DAY1
     I                                        5   8 U#DAY2
     I                                        9  12 U#DAY3
     I                                       16  16 S#FLG1
     I                                       17  17 S#FLG2
     I                                       18  18 S#FLG3
     I                                       19  19 S#FLG4
     I                                       21  21 S#STC1
     I                                       22  22 S#STC2
     I                                       23  23 S#STC3
     I                                       24  24 S#STC4
     I                                       31  38 U#ETXD
     I*
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 U#USID
     I                                     10211021 U#AREA
     C**************************************************************
     C*   程式參數接收區
     C**************************************************************
     C           *ENTRY    PLIST
     C                     PARM           P#IN03  1        結束旗標
     C**************************************************************
     C*   主程式開始
     C**************************************************************
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C                     ENDCS
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
     C**************************************************************
     C*   副程式開始
     C**************************************************************
     C*----------------------------------------
     C*  宣告及初始變數
     C*----------------------------------------
     CSR         SR0000    BEGSR
     C           *NAMVAR   DEFN ARXCTLDTA AXDTA  28
     C                     MOVE X#DAY1    S#DAY1
     C                     MOVE X#DAY2    S#DAY2
     C                     MOVE X#DAY3    S#DAY3
     C                     MOVE X#ETXD    S#ETXD
     C*
     C                     MOVEL' '       S#FLG1
     C                     MOVEL' '       S#FLG2
     C                     MOVEL'Y'       S#FLG3
     C                     MOVEL'Y'       S#FLG4
     C*
     C                     MOVEL'Y'       S#STC1
     C                     MOVEL'Y'       S#STC2
     C                     MOVEL'Y'       S#STC3
     C                     MOVEL'Y'       S#STC4
     C*
     C                     MOVEL'01'      W#PRID  2
     CSR                   ENDSR
     C*
     C*
     C************************************
     C*  畫面一:選擇要列印的營業員
     C************************************
     CSR         SR1000    BEGSR
     C                     EXFMTAR077F1                    螢幕輸入
     C*
     C                     SETOF                     515253
     C                     MOVEL*BLANK    S#MSG1
     C*
     C                     SELEC
     C           *IN03     WHEQ '1'
     C           *IN12     OREQ '1'
     C                     MOVEL'00'      W#PRID
     C                     MOVEL'1'       P#IN03
     C*
     C                     OTHER
     C                     EXSR SR1100                     檢核
     C                     ENDSL
     CSR                   ENDSR
     C*
     C*-------------------------------
     C*  畫面一：執行檢核
     C*-------------------------------
     CSR         SR1100    BEGSR
     C           S#DAY1    IFGT S#DAY2
     C                     MOVELT#MSG,1   S#MSG1
     C                     SETON                     51
     C                     GOTO ES1100
     C                     ENDIF
     C*
     C           S#DAY2    IFGT S#DAY3
     C                     MOVELT#MSG,2   S#MSG1
     C                     SETON                     51
     C                     GOTO ES1100
     C                     ENDIF
     C*
     C           S#FLG1    IFEQ ' '
     C           S#FLG2    ANDEQ' '
     C           S#FLG3    ANDEQ' '
     C           S#FLG4    ANDEQ' '
     C                     MOVELT#MSG,3   S#MSG1
     C                     SETON                     52
     C                     GOTO ES1100
     C                     ENDIF
     C*
     C           S#STC1    IFEQ ' '
     C           S#STC2    ANDEQ' '
     C           S#STC3    ANDEQ' '
     C           S#STC4    ANDEQ' '
     C                     MOVELT#MSG,4   S#MSG1
     C                     SETON                     53
     C                     GOTO ES1100
     C                     ENDIF
     C*
     C                     MOVE S#DAY1    U#DAY1
     C                     MOVE S#DAY2    U#DAY2
     C                     MOVE S#DAY3    U#DAY3
     C           S#ETXD    IFEQ 0
     C                     MOVEL*ALL'0'   U#ETXD
     C                     MOVE UDATE     U#ETXD
     C                     ELSE
     C                     MOVE S#ETXD    U#ETXD
     C                     ENDIF
     C                     MOVEL'0'       P#IN03
     C                     MOVEL'00'      W#PRID           存檔結束
     CSR         ES1100    ENDSR
     C*
     C*****************************************************************
** T#MSG
０１－逾期日數不得小於領款日數。
０２－逾期日數不得大於催收日數。
０３－請至少指定一個列印區間來列印。
０４－請至少指定一個應收狀態來列印。
