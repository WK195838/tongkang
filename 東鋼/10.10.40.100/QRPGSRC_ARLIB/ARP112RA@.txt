     H            Y                                     1                 BR100
     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARP112RA
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02CSF
     H*            4.FUNCTION     營業退款檔傳輸作業(ARRETN)－廠區異動
     H*            5.DATE-WRITTEN  93/11/01
     H*            6.DATE-UPDATE
     H*
     H*****************************************************************
     FARRETN  IP  E           K        DISK
     FARRETNY UF  E           K        DISK
     F            RARRETN                           KRENAMERARRETNY
     FARRETNX UF  E           K        DISK                      A
     IRARRETN     01
     IRARRETNY
     I              ANFLAG                          A#FLAG
     I              ANRTNO                          A#RTNO
     I              ANITEM                          A#ITEM
     I              ANCUNO                          A#CUNO
     I              ANCUNM                          A#CUNM
     I              ANORNO                          A#ORNO
     I              ANAREA                          A#AREA
     I              ANDECD                          A#DECD
     I              ANDEDT                          A#DEDT
     I              ANDC                            A#DC
     I              ANUSTP                          A#USTP
     I              ANACNO                          A#ACNO
     I              ANDPNO                          A#DPNO
     I              ANRLNO                          A#RLNO
     I              ANDUDT                          A#DUDT
     I              ANCSNM                          A#CSNM
     I              ANDSC1                          A#DSC1
     I              ANDSC2                          A#DSC2
     I              ANSAID                          A#SAID
     I              ANRTUS                          A#RTUS
     I              ANRTDT                          A#RTDT
     I              ANRTTM                          A#RTTM
     I              ANAAMT                          A#AAMT
     I              ANFL01                          A#FL01
     I              ANCFUS                          A#CFUS
     I              ANCFDT                          A#CFDT
     I              ANFL02                          A#FL02
     I              ANFL03                          A#FL03
     I              ANTRUS                          A#TRUS
     I              ANTRDT                          A#TRDT
     I              ANGLNO                          A#GLNO
     I              ANRESV                          A#RESV
     I*
     I           UDS
     I                                     10211021 U#AREA
     C*****************************************************************
     C*          KEY   LIST
     C*****************************************************************
     C           K#ARTX    KLIST
     C                     KFLD           AXRTNO
     C                     KFLD           AXITEM
     C           K#ARTY    KLIST
     C                     KFLD           A#RTNO
     C                     KFLD           A#ITEM
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
     C  N99                EXSR INISR
     C                     EXSR DTLSR
     CLR 99                EXSR LRSR
     C*****************************************************************
     CSR         INISR     BEGSR
     C*****************************************************************
     C  N99                SETON                     99
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         DTLSR     BEGSR
     C*****************************************************************
     C                     MOVELANRTNO    A#RTNO
     C                     Z-ADD0         A#ITEM
     C           K#ARTY    SETLLRARRETNY             40
     C                     MOVE *OFF      *IN40
     C           *IN40     DOWEQ*OFF
     C                     READ RARRETNY                 40
     C   40                LEAVE
     C*
     C           A#RTNO    IFNE ANRTNO
     C                     LEAVE
     C                     ENDIF
     C*
     C                     EXSR SR1000
     C*
     C                     ENDDO
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         LRSR      BEGSR
     C*****************************************************************
     C*
     CSR                   ENDSR
     C*****************************************************************
     CSR         SR1000    BEGSR
     C*****************************************************************
     C           A#FLAG    COMP 'A'                      11
     C           A#FLAG    COMP 'C'                      12
     C           A#FLAG    COMP 'D'                      13
     C*
     C                     MOVELA#RTNO    AXRTNO
     C                     Z-ADDA#ITEM    AXITEM
     C           K#ARTX    CHAINRARRETX              41
     C*
     C                     MOVELA#FLAG    AXFLAG           處理代碼
     C                     MOVELA#RTNO    AXRTNO           退款編號
     C                     Z-ADDA#ITEM    AXITEM           退款項次
     C                     MOVELA#CUNO    AXCUNO           客戶代號
     C                     MOVELA#CUNM    AXCUNM           客戶名稱
     C                     MOVELA#ORNO    AXORNO           訂單編號
     C                     MOVELA#AREA    AXAREA           退款廠區
     C                     MOVELA#DECD    AXDECD           刪除碼
     C                     Z-ADDA#DEDT    AXDEDT           刪除日期
     C                     MOVELA#DC      AXDC             借貸方
     C                     MOVELA#USTP    AXUSTP           款項別
     C                     MOVELA#ACNO    AXACNO           會計科目
     C                     MOVELA#DPNO    AXDPNO           部門代號
     C                     MOVELA#RLNO    AXRLNO           相關號碼
     C                     Z-ADDA#DUDT    AXDUDT           到期日
     C                     MOVELA#CSNM    AXCSNM           對象別
     C                     MOVELA#DSC1    AXDSC1           摘要
     C                     MOVELA#DSC2    AXDSC2           輔助摘要
     C                     MOVELA#SAID    AXSAID           領款人
     C                     MOVELA#RTUS    AXRTUS           退款人員
     C                     Z-ADDA#RTDT    AXRTDT           退款日期
     C                     Z-ADDA#RTTM    AXRTTM           退款時間
     C                     Z-ADDA#AAMT    AXAAMT           退款金額
     C                     MOVELA#FL01    AXFL01           營業確認碼
     C                     MOVELA#CFUS    AXCFUS           營業確認人員
     C                     Z-ADDA#CFDT    AXCFDT           營業確認日期
     C                     MOVELA#FL02    AXFL02           營業列印碼
     C                     MOVELA#RESV    AXRESV           預留
     C*
     C   41                WRITERARRETX
     C  N41                UPDATRARRETX
     C*
     C                     MOVEL*BLANK    A#FLAG
     C   11
     COR 12                UPDATRARRETNY
     C   13                DELETRARRETNY
     C*
     CSR                   ENDSR
