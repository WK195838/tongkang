     H****************************************************************
     H*
     H*            UPDTAE  DATE  99/12/02  2010AR556  S00WCJ (9912A)
     H*                         100/11/28  2011AR634  S00WCJ (0011A)
     H*                           配合IFRS修改
     H*                         101/12/13  2012AR704  S00WCJ (0112A)
     H*                           配合IFRS正式轉換修改
     H*                         108/12/25  S00WCJ (0812A)
     H*                          不同代開程式寫入總帳之ACCSNM發生內
     H*                          碼不一致之情形，與品妃姊討論後，寫入
     H*                          總帳前再重CHAIN CBCUST
     H*                         112/06/01  S00WCJ (1206A)
     H*                           P廠不代開N廠預收貨款，由N廠自行
     H*                          代開
     H*
     H****************************************************************
     H        1   Y                                     1                 BR003
     FINVDTL  IP  E           K        DISK
     FARWF02  O   E           K        DISK                      A
     FINVMST  IF  E           K        DISK
     FACTRAN  UF  E           K        DISK
0812AFCBCUST  IF  E           K        DISK
     F*---------------------------------------------------------------
     IIVREC
     I                                              IVNO  L1
     I           UDS
9008 I                                        1   60YM
0112AI                                      221 2260D#IFRS
     I                                     10211021 TXAR
     C*****************************************************************
     C*          KEY   LIST
     C*****************************************************************
     C           ACKEY     KLIST
     C                     KFLD           TXAR
     C                     KFLD           YM
     C                     KFLD           ACVRNO
     C                     KFLD           ACITEM
     C**-------------------------------------------**
     C**  MAIN LOGIC                               **
     C**-------------------------------------------**
     C  N90                EXSR IN#01
     C   L1                EXSR DL#01
     C  N11                EXSR DP#01
     C**-------------------------------------------**
     C**   INITIAL ROUTINE                         **
     C**-------------------------------------------**
     CSR         IN#01     BEGSR
     C                     MOVEL'F00000'  ACVRNO
     C                     Z-ADD0         ACITEM
     C           ACKEY     SETLLACTRAN               95
     C           *IN95     DOWEQ'0'
     C                     READ ACTRAN                   95
     C  N95      ACVRNO    IFGT 'F99999'
     C                     SETON                     95
     C                     ENDIF
     C   95                LEAVE
     C                     DELETRACVOUR
     C                     ENDDO
     C                     SETON                     90
     C***
0112AC           YM        IFLT D#IFRS
0011AC                     MOVEL'F00000'  W#VNO1  6
0011AC                     MOVEL'F99999'  W#VNO2  6
0011AC                     CALL 'ARS888P'
0011AC                     PARM           TXAR
0011AC                     PARM           YM
0011AC                     PARM           W#VNO1
0011AC                     PARM           W#VNO2
0112AC                     ENDIF
     CSR                   ENDSR
     C**-------------------------------------------**
     C**   L1 PROCESS                              **
     C**-------------------------------------------**
     CSR         DL#01     BEGSR
     C           IVNO      CHAININVMST               11
     C*
     C  N11                SELEC
     C           TXAR      WHEQ 'P'                        台北
     C           INAREA    IFEQ 'M'                        不代台中
     C           INAREA    OREQ 'H'                        不代苗栗
     C           INAREA    OREQ 'K'                        不代高雄
1206AC           INAREA    OREQ 'N'                        不代大業
     C                     SETON                     11
     C                     ENDIF
     C*
     C           TXAR      WHEQ 'M'                        台中
     C           INAREA    IFNE 'M'                        台中代開
     C                     SETON                     11
     C                     ENDIF
     C*
     C           TXAR      WHEQ 'T'                        八德不代
     C                     SETON                     11
9912AC*
9912AC           TXAR      WHEQ 'U'
9912AC                     SETON                     11    桃園不代
     C*
     C           TXAR      WHEQ 'H'                        苗栗
     C           INAREA    IFNE 'H'                        苗栗代開
     C                     SETON                     11
     C                     ENDIF
     C*
     C           TXAR      WHEQ 'K'                        高雄
     C           INAREA    IFNE 'K'                        高雄代開
     C                     SETON                     11
     C                     ENDIF
     C                     ENDSL
     CSR                   ENDSR
     C**--------------------------------------**
     C**   DEATIL TIME PROCESS                **
     C**--------------------------------------**
     CSR         DP#01     BEGSR
     C           IVAMT     IFEQ 0
     C           INDECD    OREQ 'D'
     C                     GOTO DP#01Z
     C                     ENDIF
     C**
     C                     CLEARRARWF02
     C                     MOVELIVFLAG    W2FLAG
     C                     MOVELIVNO      W2NO
     C                     MOVELIVACNT    W2ACNT
     C                     MOVELIVITEM    W2ITEM
     C                     MOVELIVACDT    W2ACDT
     C                     MOVELIVORNO    W2ORNO
     C                     MOVELIVPDCD    W2PDCD
     C                     MOVELIVQTY     W2QTY
     C                     Z-ADDIVUPRC    W2UPRC
     C                     MOVELIVAMT     W2AMT
     C                     MOVELIVDECD    W2DECD
     C                     MOVELIVDEDT    W2DEDT
     C                     MOVELIVAPNO    W2APNO
     C                     MOVELIVACNO    W2ACNO
     C                     MOVELIVFL01    W2FL01
     C                     MOVELIVFL02    W2FL02
     C                     MOVELIVFL03    W2FL03
     C                     MOVELIVTXAR    W2TXAR
     C                     MOVELIVTXDT    W2TXDT
     C                     MOVELIVRESV    W2RESV
     C  N11                MOVELINKIND    W2KIND
     C  N11                MOVELINAREA    W2AREA
     C  N11                MOVELINCUNO    W2CUNO
0812AC  N11      INCUNO    CHAINCBCUST              N59
0812AC  N11N59             MOVELCBCUNM    W2CUNM
0812AC  N11 59             MOVELINCUNM    W2CUNM
     C                     WRITERARWF02
     CSR         DP#01Z    ENDSR
