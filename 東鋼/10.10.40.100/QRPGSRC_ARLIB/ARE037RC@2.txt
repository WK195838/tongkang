     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARE037RC
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02YSH
     H*            4.FUNCTION     新版營業繳款輸入程式（外銷專用）
     H*            5.DATE-WRITTEN  91/05/08
     H*            6.DATE-UPDATE
     H* 說明：本程式由外銷業務輸入繳款資料至SARCVF，經過修改、
     H*       沖銷等動作，在進行確認後，便可印出報表交由財會；而
     H*       財會人員在審核無誤後，便進行過入RCVDTL的動作。
     H*
     H*****************************************************************
     H        1   Y                                     1
     FARE037SCCF  E                    WORKSTN
     F                                        RRN1  KSFILE AR037F1
     F                                        RRN2  KSFILE AR037F2
     F                                        RRN3  KSFILE AR037F3
     FINVMST  IF  E           K        DISK
     FINVMSTL2IF  E           K        DISK
     F            INREC                             KRENAMEINRECL2
     FCBCUST  IF  E           K        DISK
     FHSCINV  IF  E           K        DISK
     FGENSEQ  UF  E           K        DISK
     FSARCVF  UF  E           K        DISK                      A
     FSARCVFL4IF  E           K        DISK
     F            SRREC                             KRENAMESRRECL
     FSARVIN  UF  E           K        DISK                      A
     FSARVINL1IF  E           K        DISK
     F            SIREC                             KRENAMESIRECL
     E                    ARY        20 28                檢核重複
     E                    ERR     1  16 70                錯誤訊息
     I            DS
     I                                        1   6 SRRVNO
     I                                        1   1 D#RVNO
     I            DS
     I                                        1   6 D#RVNX
     I                                        1   1 D#RVN1
     I                                        2   6 D#RVN2
     I            DS
     I                                        1  28 D#ARY
     I                                        1   1 D#ACTP
     I                                        2   5 D#ACNO
     I                                        6  172D#RAMT
     I                                       18  254D#EXC2
     I                                       26  28 D#CURY
     I           UDS
     I                                      300 300 D#INDI
     I                                      951 985 S#COMP
     I                                     10111020 S#DEVI
     I                                     10011010 U#USID
     I                                     10011003 U#USDP
     I                                     10011001 U#USTP
     I                                     10211021 U#AREA
     C*****************************************************************
     C*        KEY   LIST
     C*****************************************************************
     C*FILE -> SARCVFL4
     C           K#SAR4    KLIST
     C                     KFLD           SRCUNO
     C                     KFLD           SRRVDT
     C                     KFLD           SRRVNO
     C                     KFLD           SRITEM
     C*FILE -> SARCVF
     C           K#SARC    KLIST
     C                     KFLD           SRRVNO
     C                     KFLD           SRITEM
     C*FILE -> SARVIN
     C           K#RVIN    KLIST
     C                     KFLD           SIRVNO
     C                     KFLD           SIITEM
     C*FILE -> GENSEQ
     C           K#NSEQ    KLIST
     C                     KFLD           GEKIND
     C                     KFLD           GEPRIN
     C*FILE -> INVMSTL2
     C           K#INL2    KLIST
     C                     KFLD           INORNO
     C                     KFLD           INNO
     C*FILE -> HSCINV
     C           K#CINV    KLIST
     C                     KFLD           C1ORNO
     C                     KFLD           C1OSEQ
     C                     KFLD           C1CITM
     C*****************************************************************
     C*        MAIN  PROGRAM
     C*****************************************************************
     C                     EXSR SR0000
     C           W#PRID    DOUEQ'00'
     C           W#PRID    CASEQ'01'      SR1000           畫面一
     C           W#PRID    CASEQ'02'      SR2000           畫面二
     C           W#PRID    CASEQ'03'      SR3000           READC
     C           W#PRID    CASEQ'04'      SR4000           畫面三
     C                     ENDCS
     C                     ENDDO
     C                     MOVE *ON       *INLR
     C*****************************************************************
     C           SR0000    BEGSR
     C*****************************************************************
     C                     MOVE '01'      W#PRID  2
     C                     MOVE *OFF      *IN99
     C*部門限定
     C                     SELEC
     C           U#USTP    WHEQ 'S'                        系統工程部
     C                     MOVEL'*'       W#HEAD  1
     C*
     C           U#USTP    WHEQ 'B'                        營業部
     C           U#USTP    OREQ 'J'                        鄭梅蘭
     C                     SELEC
     C           U#USDP    WHEQ 'B01'
     C           U#USDP    OREQ 'J00'
     C                     MOVEL'P'       W#HEAD           台北
     C           U#USDP    WHEQ 'B02'
     C                     MOVEL'T'       W#HEAD           桃園
     C           U#USDP    WHEQ 'B03'
     C                     MOVEL'K'       W#HEAD           高雄
     C           U#USDP    WHEQ 'B04'
     C                     MOVEL'M'       W#HEAD           台中
     C           U#USDP    WHEQ 'B05'
     C                     MOVEL'H'       W#HEAD           苗栗
     C                     ENDSL
     C*
     C                     OTHER                           其他
     C*
     C           U#USDP    IFEQ 'TYB'                      桃園
     C                     MOVEL'T'       W#HEAD
     C                     ELSE                            結束
     C                     MOVEL'00'      W#PRID
     C                     ENDIF
     C*
     C                     ENDSL
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR1000    BEGSR
     C*****************************************************************
     C*W#PRID='01'--->ARE037SC-1畫面
     C           *IN99     IFEQ *OFF                        無錯誤
     C                     Z-ADD0         RRN1    40
     C                     Z-SUB13        S#NBR1            SFLRCDNBR
     C*
     C                     MOVE *ON       *IN73             SFLCLR
     C                     WRITEAR037F1C
     C                     MOVE *OFF      *IN73
     C*
     C                     MOVE *ON       *IN30             DSTATR(PC)
     C*
     C                     MOVE S#KEY1    SRCUNO           客戶代號
     C                     MOVE *HIVAL    SRRVDT           日期
     C                     MOVE *HIVAL    SRRVNO           繳款單號
     C                     Z-ADD0         SRITEM           項次
     C           K#SAR4    SETLLSRRECL
     C                     EXSR SR1100                     讀入
     C                     ENDIF                            IN99
     C*迴圈開始
     C           W#PRID    DOWEQ'01'
     C*
     C           RRN1      IFEQ 0
     C                     MOVE *ON       *IN72             SFLDSP OFF
     C                     MOVELERR,1     S#ERR1
     C*
     C           S#KEY1    IFEQ *BLANK
     C                     MOVE *OFF      *IN32             DSPATR(RI)
     C                     ELSE
     C                     MOVE *ON       *IN32
     C                     ENDIF
     C*
     C                     ELSE
     C                     MOVE *OFF      *IN72
     C                     ENDIF
     C*
     C           W#CNT     IFGE 13
     C                     MOVE *ON       *IN74             SFLEND OFF
     C                     ELSE
     C                     MOVE *OFF      *IN74
     C                     ENDIF
     C*
     C                     WRITEAR037F1M
     C                     EXFMTAR037F1C
     C*
     C                     MOVE *BLANK    S#ERR1
     C*IN03 IN12
     C           *IN03     IFEQ *ON
     C           *IN12     OREQ *ON
     C                     MOVE '00'      W#PRID
     C                     ITER
     C                     ENDIF
     C*新增
     C           *IN06     IFEQ *ON
     C                     Z-ADD1         W#OPT   10        OPT暫存值
     C                     MOVE '03'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*
     C           *IN91     IFEQ *ON
     C  N90                EXSR SR1100
     C                     ITER
     C                     ENDIF
     C*
     C                     MOVE '02'      W#PRID
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR1100    BEGSR
     C*****************************************************************
     C*讀入AR037F1資料
     C                     Z-ADD0         W#CNT   20
     C           W#CNT     DOWLE12
     C                     READ SRRECL                   90
     C   90                LEAVE
     C*
     C           SRMKTP    IFNE '1'                        外銷
     C                     ITER
     C                     ENDIF
     C*
     C           W#HEAD    IFNE '*'                        依部門過濾
     C           W#HEAD    ANDNED#RVNO                     只給看自己
     C                     ITER
     C                     ENDIF
     C*
     C           SRITEM    IFNE 1                          首項即可
     C                     ITER
     C                     ENDIF
     C*
     C                     CLEARAR037F1
     C*
     C                     Z-ADDSRRVDT    S#RVDT           繳款日期
     C                     MOVELSRRVNO    S#RVNO           繳款單號
     C                     MOVELSRCUNO    S#CUNO           客戶編號
     C                     Z-ADDSRNAMT    S#NAMT           繳款金額
     C                     Z-ADDSRXAMT    S#XAMT           沖銷金額
     C                     MOVELSRFL01    S#FL01           已確認碼
     C                     MOVELSRFL02    S#FL02           已過入碼
     C                     MOVELSRFL03    S#FL03           已列印碼
     C*
     C           S#CUNO    CHAINCBCUST               69    客戶名稱
     C           *IN69     IFEQ '1'
     C                     MOVEL*BLANK    S#CUNM
     C                     ELSE
     C                     MOVELCBCUNM    S#CUNM
     C                     ENDIF
     C*
     C                     ADD  1         W#CNT
     C                     ADD  1         RRN1
     C*
     C                     WRITEAR037F1
     C*
     C                     ENDDO
     C*
     C           W#CNT     IFGE 1
     C                     ADD  14        S#NBR1            SFLRCDNBR
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2000    BEGSR
     C*****************************************************************
     C*READ C AR037F1
     C                     MOVE *OFF      *IN99
     C*                    Z-ADD1         RRN1
     C           W#PRID    DOWEQ'02'
     C                     READCAR037F1                  90
     C   90                MOVE '01'      W#PRID
     C   90                LEAVE
     C*
     C           S#OPT1    IFEQ 0
     C                     ITER
     C                     ENDIF
     C*
     C           S#OPT1    IFNE 5                          查詢
     C                     EXSR SR2100                     檢核
     C                     ENDIF
     C*有錯
     C           *IN99     IFEQ *ON
     C                     MOVE *ON       *IN31             DSPATR(RI)
     C                     UPDATAR037F1
     C                     MOVE '01'      W#PRID
     C                     LEAVE
     C                     ELSE
     C*沒錯
     C                     MOVE S#CUNO    S#KEY1
     C                     Z-ADDS#OPT1    W#OPT
     C                     Z-ADD0         S#OPT1
     C                     UPDATAR037F1
     C                     ENDIF
     C*
     C                     SELEC
     C           W#OPT     WHEQ 3                          試算
     C                     EXSR SR2400
     C                     MOVELERR,16    S#ERR1
     C                     MOVE '01'      W#PRID
     C           W#OPT     WHEQ 4                          刪除
     C                     EXSR SR2200
     C           W#OPT     WHEQ 6                          沖銷
     C                     MOVE '04'      W#PRID
     C           W#OPT     WHEQ 8                          確認
     C           W#OPT     OREQ 9                          確認還原
     C                     EXSR SR2300
     C                     OTHER
     C                     MOVE '03'      W#PRID
     C                     ENDSL
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2100    BEGSR
     C*****************************************************************
     C*
     C           S#FL02    IFNE *BLANK
     C                     MOVELERR,2     S#ERR1
     C                     MOVE *ON       *IN99
     C                     ENDIF
     C*
     C  N99      S#OPT1    IFNE 9                          確認還原
     C           S#FL01    ANDNE*BLANK                     已確認
     C                     MOVELERR,3     S#ERR1
     C                     MOVE *ON       *IN99
     C                     ENDIF
     C*
     C  N99      S#OPT1    IFEQ 9                          確認還原
     C           S#FL01    ANDEQ*BLANK                     已確認
     C                     MOVELERR,4     S#ERR1
     C                     MOVE *ON       *IN99
     C                     ENDIF
     C*
     C  N99      S#OPT1    IFEQ 8                          確認
     C           S#NAMT    IFNE S#XAMT
     C           S#NAMT    OREQ 0
     C           S#XAMT    OREQ 0
     C                     MOVELERR,13    S#ERR1
     C                     MOVE *ON       *IN99
     C                     ENDIF
     C                     ENDIF
     C*
     C   99                Z-ADDRRN1      S#NBR1
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2200    BEGSR
     C*****************************************************************
     C*刪除主檔
     C                     MOVE S#RVNO    SRRVNO
     C                     Z-ADD0         SRITEM
     C           K#SARC    SETLLSRREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SRREC                    90
     C   90                LEAVE
     C           SRRVNO    IFNE S#RVNO                     繳款單號
     C                     LEAVE
     C                     ENDIF
     C                     DELETSRREC
     C                     ENDDO
     C*刪除明細
     C                     MOVE S#RVNO    SIRVNO
     C                     Z-ADD0         SIITEM
     C           K#RVIN    SETLLSIREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SIREC                    90
     C   90                LEAVE
     C           SIRVNO    IFNE S#RVNO                     繳款單號
     C                     LEAVE
     C                     ENDIF
     C                     DELETSIREC
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2300    BEGSR
     C*****************************************************************
     C*確認或還原
     C                     MOVE S#RVNO    SRRVNO
     C                     Z-ADD0         SRITEM
     C           K#SARC    SETLLSRREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SRREC                    90
     C   90                LEAVE
     C           SRRVNO    IFNE S#RVNO                     繳款單號
     C                     LEAVE
     C                     ENDIF
     C*
     C           W#OPT     IFEQ 8                          確認
     C                     MOVE 'Y'       SRFL01
     C                     ELSE                            還原
     C                     MOVE *BLANK    SRFL01
     C                     MOVE *BLANK    SRFL03           列印碼
     C                     ENDIF
     C*
     C                     MOVE UDATE     SRCFDT           確認日期
     C                     MOVE U#USID    SRCFUS           確認人員
     C*
     C                     UPDATSRREC
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2400    BEGSR
     C*****************************************************************
     C*確認時先進行兌換損失與奇零尾數的調整
     C*再進行發票金額的轉換
     C*然後計算繳款金額與沖銷金額的關係
     C*此動作只進行一次,如果計算錯誤則以手動修改之
     C                     MOVE *OFF      *IN97            計算燈號
     C                     MOVE *ALL'9'   ARY              初始陣列
     C*先將主檔內容紀錄於ARY中
     C                     EXSR SR2410
     C*由ARY將資訊讀出並做整理
     C  N97                EXSR SR2420
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2410    BEGSR
     C*****************************************************************
     C                     MOVE S#RVNO    SRRVNO
     C                     Z-ADD0         SRITEM
     C           K#SARC    SETLLSRREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SRREC                    90
     C*
     C           *IN90     IFEQ *ON
     C           SRRVNO    ORNE S#RVNO                     繳款單號
     C                     LEAVE
     C                     ENDIF
     C*
     C                     Z-ADDSRITEM    W#ITEM  20       最後一項
     C*先將主檔內容紀錄於ARY中
     C                     MOVE SRACTP    D#ACTP           借貸別
     C                     MOVE SRACNO    D#ACNO           會計科目
     C                     Z-ADDSRRAMT    D#RAMT           收款金額
     C                     Z-ADDSREXC2    D#EXC2           銀行匯率
     C                     MOVE SRCURY    D#CURY           幣別
     C                     Z-ADDSRITEM    I       20        INDEX
     C                     MOVE D#ARY     ARY,I
     C*紀錄SR2420所需資訊
     C                     Z-ADDSREXC1    W#EXC1  84       財會匯率
     C                     Z-ADDSREXC3    W#EXC3  84       報關匯率
     C*有以下三種科目則代表已經試算,不做第二次試算
     C           SRACNO    IFEQ '8212'                     兌換損失
     C           SRACNO    OREQ '8112'                     兌換收益
     C           SRACNO    OREQ '8246'                     奇零尾差
     C                     MOVE *ON       *IN97
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2420    BEGSR
     C*****************************************************************
     C*初始各計算值
     C                     Z-ADD0         W#1114 120       外幣存款
     C                     Z-ADD0         W#7119 120       出口費用
     C                     Z-ADD0         W#8211 120       出押息
     C                     Z-ADD0         W#AMTX 122       美金總額
     C*由ARY將資訊讀出並做整理
     C                     SORTAARY
     C           1         DO   20        I
     C                     MOVE ARY,I     D#ARY
     C*
     C           D#ACTP    IFEQ '9'                        初始項次
     C                     LEAVE
     C                     ENDIF
     C*
     C                     SELEC
     C           D#ACNO    WHEQ '1114'                     外幣存款
     C           D#RAMT    MULT W#EXC1    W#AMT  120H      台幣
     C                     ADD  W#AMT     W#1114
     C                     ADD  D#RAMT    W#AMTX
     C*出口費用如為台幣,須先依銀行匯率轉成美金,再依財會匯率轉成台
     C*幣;如為美金則直接以財會匯率轉換
     C           D#ACNO    WHEQ '7119'                     出口費用
     C           D#CURY    IFEQ 'NTD'                      台幣
     C           D#CURY    OREQ *BLANK                     台幣
     C           D#RAMT    DIV  D#EXC2    W#AMT1 133H
     C                     ADD  W#AMT1    W#AMTX
     C           W#AMT1    MULT W#EXC1    W#AMT     H
     C                     ADD  W#AMT     W#7119
     C                     ELSE
     C           D#RAMT    MULT W#EXC1    W#AMT     H
     C                     ADD  W#AMT     W#7119
     C                     ADD  D#RAMT    W#AMTX
     C                     ENDIF
     C*計算方式同上
     C           D#ACNO    WHEQ '8211'                     出押息
     C           D#CURY    IFEQ 'NTD'                      台幣
     C           D#CURY    OREQ *BLANK                     台幣
     C           D#RAMT    DIV  D#EXC2    W#AMT1    H
     C                     ADD  W#AMT1    W#AMTX
     C           W#AMT1    MULT W#EXC1    W#AMT     H
     C                     ADD  W#AMT     W#8211
     C                     ELSE
     C           D#RAMT    MULT W#EXC1    W#AMT     H
     C                     ADD  W#AMT     W#8211
     C                     ADD  D#RAMT    W#AMTX
     C                     ENDIF
     C*
     C                     ENDSL
     C*
     C                     ENDDO
     C*寫入8212(兌換損失)或8112(兌換收益),與8246(奇零尾差)
     C                     EXSR SR2421
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR2421    BEGSR
     C*****************************************************************
     C*初始各項值
     C                     Z-ADD0         W#8212 120       損失暫存
     C                     Z-ADD0         W#8112 120       收益暫存
     C                     Z-ADD0         W#8246 120       尾數暫存
     C                     Z-ADD0         W#CONT  20       須寫入筆數
     C                     Z-ADD0         W#CONX  20       已寫入筆數
     C*若財會匯率大於報關匯率則為收益,反之則為損失
     C           W#EXC1    IFGE W#EXC3
     C           W#EXC1    SUB  W#EXC3    W#EXCG  84
     C           W#AMTX    MULT W#EXCG    W#8112    H
     C                     ELSE
     C           W#EXC3    SUB  W#EXC1    W#EXCG
     C           W#AMTX    MULT W#EXCG    W#8212    H
     C                     ENDIF
     C*計算奇零尾差,以台幣表達(公式如下)
     C*沖銷總額=繳款總額
     C*繳款總額=1114+7119+8211+8212-8112+8246
     C* => 8246=沖銷總額-(1114+7119+8211+8212-8112)
     C           S#XAMT    SUB  W#1114    W#8246
     C                     SUB  W#7119    W#8246
     C                     SUB  W#8211    W#8246
     C                     SUB  W#8212    W#8246
     C                     ADD  W#8112    W#8246
     C*計算須寫入筆數
     C           W#8112    IFNE 0
     C                     ADD  1         W#CONT
     C                     ENDIF
     C           W#8212    IFNE 0
     C                     ADD  1         W#CONT
     C                     ENDIF
     C           W#8246    IFNE 0
     C                     ADD  1         W#CONT
     C                     ENDIF
     C*先更新此繳款書的繳款金額然後再寫入
     C                     MOVE S#RVNO    SRRVNO
     C                     Z-ADD0         SRITEM
     C           K#SARC    SETLLSRREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SRREC                    90
     C*
     C           *IN90     IFEQ *ON
     C           SRRVNO    ORNE S#RVNO                     繳款單號
     C                     LEAVE
     C                     ENDIF
     C                     Z-ADDSRXAMT    SRNAMT
     C                     UPDATSRREC
     C*寫入新項次
     C           SRITEM    IFEQ W#ITEM                     已到最末筆
     C                     MOVE *BLANK    SRUSTP           款項別
     C                     MOVE *BLANK    SRNTTP           票據別
     C                     MOVE *BLANK    SRPBID           付款銀行
     C                     MOVE *BLANK    SRPANO           付款帳號
     C                     MOVE *BLANK    SRPLAC           付款地
     C                     MOVE *BLANK    SRNTNO           票據號碼
     C                     MOVE *BLANK    SRSBID           存入銀行
     C                     MOVE *BLANK    SRSANO           存入帳號
     C                     Z-ADD0         SRDUDT           到期日
     C                     MOVE *BLANK    SRRLNO           相關號碼
     C                     MOVEL*BLANK    SRRESV           保留碼
     C                     MOVE '1'       SRMKTP           外銷
     C                     MOVEL'NTD'     SRCURY           幣別
     C                     Z-ADDS#EXC1    SREXC1           財會匯率
     C                     Z-ADD0         SREXC2           銀行匯率
     C                     Z-ADDS#EXC3    SREXC3           報關匯率
     C*
     C           W#8112    IFNE 0                          兌換收益
     C                     ADD  1         W#ITEM
     C                     Z-ADDW#ITEM    SRITEM           項次
     C                     MOVEL'C'       SRACTP           借貸別
     C                     MOVEL'8112'    SRACNO           會計科目
     C                     Z-ADDW#8112    SRRAMT           繳款金額
     C                     WRITESRREC
     C                     ADD  1         W#CONX           已寫入筆數
     C                     ENDIF
     C*
     C           W#8212    IFNE 0                          兌換損失
     C                     ADD  1         W#ITEM
     C                     Z-ADDW#ITEM    SRITEM           項次
     C                     MOVEL'D'       SRACTP           借貸別
     C                     MOVEL'8212'    SRACNO           會計科目
     C                     Z-ADDW#8212    SRRAMT           繳款金額
     C                     WRITESRREC
     C                     ADD  1         W#CONX           已寫入筆數
     C                     ENDIF
     C*
     C           W#8246    IFNE 0                          奇零尾數
     C                     ADD  1         W#ITEM
     C                     Z-ADDW#ITEM    SRITEM           項次
     C           W#8246    IFGT 0
     C                     MOVEL'D'       SRACTP           借貸別
     C                     MOVEL'8246'    SRACNO           會計科目
     C                     Z-ADDW#8246    SRRAMT           繳款金額
     C                     ELSE
     C                     MOVEL'C'       SRACTP           借貸別
     C                     MOVEL'8246'    SRACNO           會計科目
     C                     Z-SUBW#8246    SRRAMT           繳款金額
     C                     ENDIF
     C                     WRITESRREC
     C                     ADD  1         W#CONX           已寫入筆數
     C                     ENDIF
     C*
     C                     ENDIF                            W#ITEM
     C*
     C           W#CONT    IFEQ W#CONX
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ENDDO
     C                     ENDSR
     C*****************************************************************
     C           SR3000    BEGSR
     C*****************************************************************
     C*W#PRID='01'--->ARE037SC-2畫面
     C                     Z-ADD0         RRN2    40
     C*
     C                     MOVE *ON       *IN73             SFLCLR
     C                     WRITEAR037F2C
     C                     MOVE *OFF      *IN73
     C*
     C           W#OPT     IFEQ 5                          查詢
     C                     MOVE *ON       *IN33
     C                     ELSE
     C                     MOVE *OFF      *IN33
     C                     ENDIF
     C*
     C                     MOVE *OFF      *IN61             PROTECT
     C*
     C                     SELEC
     C           W#OPT     WHEQ 1
     C                     MOVE '新增'  S#SF2T
     C           W#OPT     WHEQ 2
     C                     MOVE '修改'  S#SF2T
     C           W#OPT     WHEQ 5
     C                     MOVE '查詢'  S#SF2T
     C                     ENDSL
     C*
     C           W#OPT     IFNE 1                          新增
     C                     EXSR SR3100                     讀入檔頭
     C                     ELSE
     C                     MOVE *BLANK    S#RVNO           繳款單號
     C                     MOVE *BLANK    S#DPNO           部門別
     C                     MOVE *BLANK    S#RVID           收款業務
     C                     MOVE *BLANK    S#CUNO           客戶編號
     C                     Z-ADD0         S#RVDT           繳款日期
     C                     Z-ADD0         S#NAMT           繳款總額
     C                     Z-ADD0         S#XAMT           沖銷總額
     C                     Z-ADD0         S#EXC1           財會匯率
     C                     Z-ADD0         S#EXC3           報關匯率
     C                     ENDIF
     C*
     C                     Z-ADD1         S#NBR2            SFLRCDNBR
     C                     EXSR SR3200                     初始SFL
     C*迴圈開始
     C  N33                MOVE *ON       *IN34            DSPATR(PC)
     C           W#PRID    DOWEQ'03'
     C*
     C           RRN2      IFEQ 0
     C                     MOVE *ON       *IN72             SFLDSP OFF
     C                     MOVELERR,1     S#ERR1
     C*
     C                     ELSE
     C                     MOVE *OFF      *IN72
     C                     ENDIF
     C*
     C                     WRITEAR037F2M
     C                     EXFMTAR037F2C
     C*
     C                     SETOF                     3499
     C                     MOVEA'00000'   *IN,53           DSPATR(PC)
     C                     MOVE *OFF      *IN62
     C                     MOVE *BLANK    S#ERR2
     C*IN03
     C           *IN03     IFEQ *ON
     C                     MOVE '00'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*IN12
     C           *IN12     IFEQ *ON
     C           RRN1      IFNE 0
     C                     MOVE '02'      W#PRID
     C                     ELSE
     C                     MOVE '01'      W#PRID
     C                     MOVE *OFF      *IN99            畫面一INI
     C                     ENDIF
     C                     LEAVE
     C                     ENDIF
     C*說明
     C           *IN01     IFEQ *ON
     C                     EXFMTAR037F2W
     C                     ITER
     C                     ENDIF
     C*檢核
     C  N33                EXSR SR3300
     C*存檔
     C  N33N99   *IN10     IFEQ *ON
     C                     EXSR SR3400
     C                     MOVE '04'      W#PRID           沖銷畫面
     C                     LEAVE
     C                     ENDIF
     C*
     C   33                MOVE '04'      W#PRID
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR3100    BEGSR
     C*****************************************************************
     C*讀入檔頭
     C                     MOVE S#RVNO    SRRVNO
     C                     Z-ADD1         SRITEM
     C           K#SARC    CHAINSRREC                90
     C                     MOVELSRRVNO    S#RVNO           繳款單號
     C                     MOVELSRDPNO    S#DPNO           部門別
     C                     MOVELSRRVID    S#RVID           收款業務
     C                     MOVELSRCUNO    S#CUNO           客戶編號
     C                     Z-ADDSRRVDT    S#RVDT           繳款日期
     C                     Z-ADDSRNAMT    S#NAMT           繳款總額
     C                     Z-ADDSRXAMT    S#XAMT           沖銷總額
     C                     Z-ADDSREXC1    S#EXC1           財會匯率
     C                     Z-ADDSREXC3    S#EXC3           報關匯率
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR3200    BEGSR
     C*****************************************************************
     C*讀入明細
     C           RRN2      DOWLE19
     C                     ADD  1         RRN2
     C                     MOVE S#RVNO    SRRVNO
     C                     Z-ADDRRN2      SRITEM
     C           K#SARC    CHAINSRREC                90
     C                     Z-ADDRRN2      S#SFI2
     C           *IN90     IFEQ *OFF                       有資料
     C                     MOVELSRACTP    S#ACTP           借貸別
     C                     MOVELSRUSTP    S#USTP           款項別
     C                     MOVELSRNTTP    S#NTTP           票據別
     C                     MOVELSRACNO    S#ACNO           會計科目
     C                     MOVELSRPBID    S#PBID           付款銀行
     C                     MOVELSRNTNO    S#NTNO           票據號碼
     C                     Z-ADDSRRAMT    S#RAMT           收款金額
     C                     MOVELSRRLNO    S#RLNO           相關號碼
     C                     MOVELSRSANO    S#SANO           存入帳號
     C                     Z-ADDSRDUDT    S#DUDT           到期日
     C           SRCURY    IFEQ *BLANK
     C                     MOVE 'NTD'     S#CURY
     C                     ELSE
     C                     MOVELSRCURY    S#CURY           幣別
     C                     ENDIF
     C                     Z-ADDSREXC2    S#EXC2           銀行匯率
     C                     ELSE
     C                     MOVE *BLANK    S#ACTP           借貸別
     C                     MOVE *BLANK    S#USTP           款項別
     C                     MOVE *BLANK    S#NTTP           票據別
     C                     MOVE *BLANK    S#ACNO           會計科目
     C                     MOVE *BLANK    S#PBID           付款銀行
     C                     MOVE *BLANK    S#NTNO           票據號碼
     C                     Z-ADD0         S#RAMT           收款金額
     C                     MOVE *BLANK    S#RLNO           相關號碼
     C                     MOVE *BLANK    S#SANO           存入帳號
     C                     Z-ADD0         S#DUDT           到期日
     C                     MOVE *BLANK    S#CURY           幣別
     C                     Z-ADD0         S#EXC2           銀行匯率
     C                     ENDIF
     C  N33
     CORN90                WRITEAR037F2
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR3300    BEGSR
     C*****************************************************************
     C*清空繳款金額,於SR3320重新合計
     C                     Z-ADD0         S#NAMT           繳款金額
     C*檢核檔頭
     C           S#CUNO    IFEQ *BLANK                     客戶編號
     C                     SETON                     345399
     C                     MOVELERR,6     S#ERR2
     C                     ENDIF
     C*
     C           S#CUNO    CHAINCBREC               N90
     C  N99 90             SETON                     345399
     C  N99 90             MOVELERR,6     S#ERR2
     C  N99N90             MOVELCBCUNM    S#CUNM
     C*
     C                     MOVE S#RVDT    P#DATE           繳款日期
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           P#ERR   1
     C  N99      P#ERR     IFNE '0'
     C                     SETON                     5499
     C                     MOVELERR,6     S#ERR2
     C                     ENDIF
     C*
     C  N99      S#EXC3    IFEQ 0                          報關匯率
     C                     SETON                     6299
     C                     MOVELERR,6     S#ERR2
     C                     ENDIF
     C*
     C  N99      S#DPNO    IFEQ *BLANK                     部門別
     C                     SETON                     5599
     C                     MOVELERR,6     S#ERR2
     C                     ENDIF
     C*
     C  N99      S#RVID    IFEQ *BLANK                     業務員
     C                     SETON                     5699
     C                     MOVELERR,6     S#ERR2
     C                     ENDIF
     C*
     C  N99      S#EXC1    IFEQ 0                          財會匯率
     C                     SETON                     5799
     C                     MOVELERR,6     S#ERR2
     C                     ENDIF
     C*檢核SFL
     C                     Z-ADD0         W#CONT
     C                     MOVE *OFF      *IN98            錯誤判斷
     C  N99      W#CONT    DOWLERRN2
     C                     ADD  1         W#CONT
     C           W#CONT    CHAINAR037F2              90
     C   90                LEAVE
     C*當借貸別為空白時,自動清空所有欄位
     C*而不為空白時才進行檢核
     C           S#ACTP    IFEQ *BLANK                     借貸別
     C                     MOVE *BLANK    S#ACTP           借貸別
     C                     MOVE *BLANK    S#USTP           款項別
     C                     MOVE *BLANK    S#NTTP           票據別
     C                     MOVE *BLANK    S#ACNO           會計科目
     C                     MOVE *BLANK    S#PBID           付款銀行
     C                     MOVE *BLANK    S#NTNO           票據號碼
     C                     Z-ADD0         S#RAMT           收款金額
     C                     MOVE *BLANK    S#RLNO           相關號碼
     C                     MOVE *BLANK    S#SANO           存入帳號
     C                     Z-ADD0         S#DUDT           到期日
     C                     MOVE *BLANK    S#CURY           幣別
     C                     Z-ADD0         S#EXC2           銀行匯率
     C                     MOVEA'00000000'*IN,41
     C                     MOVEA'0000'    *IN,49
     C                     ELSE
     C                     EXSR SR3310                     明細檢核
     C                     EXSR SR3320                     計算繳款額
     C                     ENDIF
     C*
     C                     UPDATAR037F2
     C                     ENDDO
     C*
     C  N99 98             MOVE ERR,6     S#ERR2
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR3310    BEGSR
     C*****************************************************************
     C*檢核SFL
     C           S#USTP    IFNE 'A1'                       款項別
     C           S#USTP    ANDNE'A2'
     C           S#USTP    ANDNE'A3'
     C           S#USTP    ANDNE'A4'
     C           S#USTP    ANDNE'A5'
     C           S#USTP    ANDNE'Z5'
     C           S#USTP    ANDNE*BLANK
     C                     SETON                     4298
     C                     ENDIF
     C*
     C           S#NTTP    IFNE 'A'                        票款別
     C           S#NTTP    ANDNE'B'
     C           S#NTTP    ANDNE'C'
     C           S#NTTP    ANDNE'D'
     C           S#NTTP    ANDNE'E'
     C           S#NTTP    ANDNE'F'
     C           S#NTTP    ANDNE'G'
     C           S#NTTP    ANDNE'H'
     C           S#NTTP    ANDNE'I'
     C           S#NTTP    ANDNE'J'
     C           S#NTTP    ANDNE*BLANK
     C                     SETON                     4398
     C                     ENDIF
     C*
     C           S#ACNO    IFNE '1114'                     會計科目
     C           S#ACNO    ANDNE'7119'
     C           S#ACNO    ANDNE'8212'
     C           S#ACNO    ANDNE'8211'
     C           S#ACNO    ANDNE'8112'
     C           S#ACNO    ANDNE'8246'
     C                     SETON                     4498
     C                     ENDIF
     C*
     C           S#ACNO    IFEQ '8212'
     C           S#ACTP    ANDNE'D'
     C                     SETON                     414498
     C                     ENDIF
     C*
     C           S#ACNO    IFEQ '8112'
     C           S#ACTP    ANDNE'C'
     C                     SETON                     414498
     C                     ENDIF
     C*
     C           S#NTNO    IFNE *BLANK                     票據號碼
     C           S#DUDT    ANDEQ0                          到期日
     C                     SETON                     4498
     C                     ENDIF
     C*
     C           S#DUDT    IFNE 0                          到期日
     C                     MOVE S#DUDT    P#DATE           繳款日期
     C                     CALL 'UTS102R'
     C                     PARM           P#DATE  8
     C                     PARM '1'       P#MODE  1
     C                     PARM           P#MTL  24
     C                     PARM           P#LY    1
     C                     PARM           P#ERR   1
     C           P#ERR     IFNE '0'
     C                     SETON                     4798
     C                     ENDIF
     C                     ENDIF
     C*
     C           S#CURY    IFNE *BLANK                     幣別
     C           S#CURY    ANDNE'NTD'
     C           S#CURY    ANDNE'USD'
     C                     SETON                     5098
     C                     ENDIF
     C*
     C           S#ACNO    IFEQ '1114'                     幣別
     C           S#CURY    ANDNE'USD'
     C                     SETON                     5098
     C                     ENDIF
     C*
     C           S#RAMT    IFEQ 0                          金額
     C                     SETON                     5198
     C                     ENDIF
     C*
     C           S#EXC2    IFEQ 0                          銀行匯率
     C                     SETON                     5298
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR3320    BEGSR
     C*****************************************************************
     C                     Z-ADD0         W#TMNA 120
     C*
     C           S#CURY    IFEQ 'NTD'                      台幣繳款
     C           S#CURY    OREQ *BLANK                     台幣繳款
     C*
     C           S#ACTP    IFEQ 'D'                        借方
     C                     Z-ADDS#RAMT    W#TMNA
     C                     ELSE                            貸方
     C                     Z-SUBS#RAMT    W#TMNA
     C                     ENDIF
     C*
     C                     ELSE                            外幣繳款
     C*
     C           S#RAMT    MULT S#EXC1    W#TMNB 120H      幣值轉換
     C           S#ACTP    IFEQ 'D'                        借方
     C                     Z-ADDW#TMNB    W#TMNA
     C                     ELSE                            貸方
     C                     Z-SUBW#TMNB    W#TMNA
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     C                     ADD  W#TMNA    S#NAMT           合計繳款額
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR3400    BEGSR
     C*****************************************************************
     C*如為新增時,必須取得新的繳款單號
     C           W#OPT     IFEQ 1                          新增
     C                     MOVEL'04'      GEKIND
     C*          W#HEAD    IFEQ '*'
     C*                    MOVELU#AREA    GEPRIN
     C*                    ELSE
     C                     MOVEL'H'       GEPRIN
     C*                    ENDIF
     C*
     C           K#NSEQ    CHAINGEREC                90
     C                     Z-ADDGECUNO    W#GENO  50
     C           W#GENO    IFEQ 99999
     C                     Z-ADD1         W#GENO
     C                     ELSE
     C                     ADD  1         W#GENO
     C                     ENDIF
     C*
     C                     MOVELGEPRIN    D#RVN1           設繳款單號
     C                     MOVE W#GENO    D#RVN2
     C                     MOVE D#RVNX    S#RVNO
     C*
     C                     Z-ADDW#GENO    GECUNO
TEST C                     UPDATGEREC
     C                     ENDIF
     C*先將原有之所有資訊刪除
     C                     MOVE S#RVNO    SRRVNO           繳款單號
     C                     Z-ADD0         SRITEM           繳款項次
     C           K#SARC    SETLLSRREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SRREC                    90
     C   90                LEAVE
     C*
     C           SRRVNO    IFNE S#RVNO
     C                     LEAVE
     C                     ENDIF
     C*
     C                     DELETSRREC
     C                     ENDDO
     C*計算明細檔之沖銷總額
     C                     Z-ADD0         S#XAMT
     C*
     C                     MOVE S#RVNO    SIRVNO
     C                     Z-ADD0         SIITEM
     C           K#RVIN    SETLLSIREC
     C                     MOVE *OFF      *IN95
     C           *IN95     DOWEQ*OFF                       有資料
     C                     READ SIREC                    95
     C*
     C           *IN95     IFEQ *ON
     C           S#RVNO    ORNE SIRVNO
     C                     LEAVE
     C                     ENDIF
     C*
     C                     ADD  SIINXA    S#XAMT           沖銷金額
     C                     ENDDO
     C*存檔
     C                     Z-ADD0         W#CONT
     C                     Z-ADD0         SRITEM           繳款項次
     C           W#CONT    DOWLE19
     C                     ADD  1         W#CONT
     C           W#CONT    CHAINAR037F2              90
     C   90                LEAVE
     C*
     C           S#ACTP    IFEQ *BLANK                     借貸別
     C                     ITER
     C                     ENDIF
     C*
     C                     EXSR SR3410                      MOVE DATA
     C*
     C           K#SARC    CHAINSRREC                90
     C   90                WRITESRREC
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR3410    BEGSR
     C*****************************************************************
     C* MOVE DATA
     C                     MOVELS#RVNO    SRRVNO           繳款編號
     C                     Z-ADDS#NAMT    SRNAMT           合計金額
     C                     Z-ADDS#RVDT    SRRVDT           繳款日
     C                     MOVELS#CUNO    SRCUNO           客戶編號
     C                     MOVELS#DPNO    SRDPNO           部門別
     C                     MOVELS#RVID    SRRVID           收款業務
     C                     Z-ADDS#XAMT    SRXAMT           沖銷總額
     C                     MOVE *BLANK    SRFL01
     C                     MOVE *BLANK    SRFL02
     C                     MOVE *BLANK    SRFL03
     C*
     C                     ADD  1         SRITEM           項次
     C                     MOVELS#ACTP    SRACTP           借貸別
     C                     MOVELS#ACNO    SRACNO           會計科目
     C                     MOVELS#USTP    SRUSTP           款項別
     C                     MOVELS#NTTP    SRNTTP           票據別
     C                     MOVELS#PBID    SRPBID           付款銀行
     C                     MOVE *BLANK    SRPANO           付款帳號
     C                     MOVE *BLANK    SRPLAC           付款地
     C                     MOVELS#NTNO    SRNTNO           票據號碼
     C                     Z-ADDS#RAMT    SRRAMT           收款金額
     C                     MOVEL*BLANK    SRSBID           存入銀行
     C                     MOVELS#SANO    SRSANO           存入帳號
     C                     Z-ADDS#DUDT    SRDUDT           到期日
     C                     MOVELS#RLNO    SRRLNO           相關號碼
     C                     MOVEL*BLANK    SRRESV           保留碼
     C                     MOVE '1'       SRMKTP           外銷
     C                     MOVELS#CURY    SRCURY           幣別
     C                     Z-ADDS#EXC1    SREXC1           財會匯率
     C                     Z-ADDS#EXC2    SREXC2           銀行匯率
     C                     Z-ADDS#EXC3    SREXC3           報關匯率
     C*
     C                     Z-ADDUDATE     SRTXDT           登錄日期
     C                     MOVELU#USID    SRTXUS           登錄人員
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4000    BEGSR
     C*****************************************************************
     C*W#PRID='01'--->ARE037SC-3畫面
     C                     Z-ADD0         RRN3    40
     C                     Z-ADD1         S#NBR3            SFLRCDNBR
     C*
     C                     MOVE *ON       *IN73             SFLCLR
     C                     WRITEAR037F3C
     C                     MOVE *OFF      *IN73
     C*
     C           W#OPT     IFEQ 5                          查詢
     C                     MOVE *ON       *IN33
     C                     MOVEL'查詢'  S#SF2T
     C                     ELSE
     C                     MOVE *OFF      *IN33
     C                     MOVEL'沖銷'  S#SF2T
     C                     ENDIF
     C*
     C                     MOVE *ON       *IN61             PROTECT
     C*
     C                     EXSR SR3100                     讀入檔頭
     C*
     C                     EXSR SR4100                     初始SFL
     C*迴圈開始
     C           W#PRID    DOWEQ'04'
     C*
     C                     WRITEAR037F3M
     C                     EXFMTAR037F3C
     C*
     C                     SETOF                     3499
     C                     MOVE *BLANK    S#ERR3
     C*IN03
     C           *IN03     IFEQ *ON
     C                     MOVE '00'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*IN12
     C           *IN12     IFEQ *ON
     C                     MOVE '03'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*載入
     C  N33      *IN09     IFEQ *ON
     C                     EXSR SR4200
     C                     ITER
     C                     ENDIF
     C*檢核
     C  N33                EXSR SR4300
     C*存檔
     C  N33N99   *IN10     IFEQ *ON
     C                     EXSR SR4400
     C                     MOVE '01'      W#PRID
     C                     LEAVE
     C                     ENDIF
     C*
     C   33                MOVE '03'      W#PRID
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4100    BEGSR
     C*****************************************************************
     C*讀入明細
     C                     Z-ADD0         W#RRN3
     C*
     C           RRN3      DOWLE69
     C                     ADD  1         RRN3
     C                     MOVE S#RVNO    SIRVNO
     C                     Z-ADDRRN3      SIITEM
     C           K#RVIN    CHAINSIREC                95
     C                     Z-ADDRRN3      S#SFI3
     C           *IN95     IFEQ *OFF                       有資料
     C                     MOVELSIINNO    S#INNO           發票號碼
     C                     Z-ADDSIINXA    S#INXA           沖銷金額
     C                     Z-ADDSIINAM    S#INAM           發票金額
     C                     Z-ADDSIINBA    S#INBA           發票餘額
     C*
     C           S#INNO    CHAININREC                90
     C  N90                MOVELINORNO    S#INOR           訂單號碼
     C  N90                MOVELINAPNO    S#INAP           請款單號
     C*
     C           S#INNO    CHAINSIRECL               90
     C           *IN90     DOWEQ'0'
     C           SIRVNO    IFNE S#RVNO
     C                     ADD  SIINXA    S#INRA           未過已沖
     C                     ENDIF
     C           S#INNO    READESIRECL                   90
     C                     ENDDO
     C*
     C                     MOVELINORNO    S#ORNO
     C                     Z-ADDRRN3      W#RRN3  40       最後一筆
     C                     ELSE
     C                     MOVEL*BLANK    S#INNO           發票號碼
     C                     Z-ADD0         S#INXA           沖銷金額
     C                     Z-ADD0         S#INAM           發票金額
     C                     Z-ADD0         S#INBA           發票餘額
     C                     MOVEL*BLANK    S#INOR           訂單號碼
     C                     MOVEL*BLANK    S#INAP           請款單號
     C                     Z-ADD0         S#INRA           未過已沖
     C                     ENDIF
     C*
     C  N33
     CORN95                WRITEAR037F3
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4200    BEGSR
     C*****************************************************************
     C*載入明細
     C                     MOVE 'Y'       W#LOOP  1
     C                     CLEARAR037F3W
     C*
     C           W#LOOP    DOWEQ'Y'
     C                     EXFMTAR037F3W
     C                     MOVE *BLANK    S#ERRW
     C                     MOVE *OFF      *IN60
     C*
     C           *IN03     IFEQ *ON
     C           *IN12     OREQ *ON
     C                     LEAVE
     C                     ENDIF
     C*
     C                     EXSR SR4210                     檢核
     C*
     C  N60                EXSR SR4220                     載入
     C  N60                MOVE 'N'       W#LOOP
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4210    BEGSR
     C*****************************************************************
     C*檢核
     C                     MOVE S#ORNO    INORNO
     C                     MOVE *BLANK    INNO
     C           K#INL2    SETLLINRECL2
     C                     READ INRECL2                  90
     C*訂單號碼
     C           *IN90     IFEQ *ON
     C           INORNO    ORNE S#ORNO
     C                     MOVELERR,7     S#ERRW
     C                     MOVE *ON       *IN60
     C                     ENDIF
     C*客戶代號
     C  N60      INCUNO    IFNE S#CUNO
     C                     MOVELERR,8     S#ERRW
     C                     MOVE *ON       *IN60
     C                     ENDIF
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4220    BEGSR
     C*****************************************************************
     C*載入
     C                     Z-ADD0         RRN3
     C*
     C                     MOVE S#ORNO    INORNO
     C                     MOVE *BLANK    INNO
     C           K#INL2    SETLLINRECL2
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ INRECL2                  90
     C*
     C           *IN90     IFEQ *ON
     C           INORNO    ORNE S#ORNO
     C                     LEAVE
     C                     ENDIF
     C*
     C           INDECD    IFNE *BLANK
     C                     ITER
     C                     ENDIF
     C*
     C           RRN3      IFEQ 70                         最多70筆
     C                     LEAVE
     C                     ENDIF
     C                     ADD  1         RRN3
     C           RRN3      CHAINAR037F3              77
     C*
     C                     MOVELINNO      S#INNO           發票號碼
     C           INAAMT    ADD  INATAX    S#INAM           發票金額
     C                     Z-ADDINNBAL    S#INBA           發票餘額
     C                     MOVELINORNO    S#INOR           訂單號碼
     C                     MOVELINAPNO    S#INAP           請款單號
     C*
     C           S#INNO    CHAINSIRECL               91
     C           *IN91     DOWEQ'0'
     C           SIRVNO    IFNE S#RVNO
     C                     ADD  SIINXA    S#INRA           未過已沖
     C                     ENDIF
     C           S#INNO    READESIRECL                   91
     C                     ENDDO
     C*
     C           S#INBA    SUB  S#INRA    S#INXA           沖銷金額
     C                     ADD  S#INXA    S#XAMT           沖銷金額
     C*
     C  N77                UPDATAR037F3
     C                     Z-ADDRRN3      W#RRN3
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4300    BEGSR
     C*****************************************************************
     C*先計算沖銷總額
     C                     Z-ADD0         S#XAMT           沖銷總額
     C                     Z-ADD0         W#CONT
     C           W#CONT    DOWLE69
     C                     ADD  1         W#CONT
     C*
     C           W#CONT    CHAINAR037F3              90
     C   90                LEAVE
     C*
     C           S#INNO    IFEQ *BLANK                     發票號碼
     C                     ITER
     C                     ENDIF
     C*
     C                     ADD  S#INXA    S#XAMT
     C                     ENDDO
     C*AR037F3的檢核
     C                     MOVE *BLANK    W#ORNO  6
     C                     MOVE *OFF      *IN98
     C*
     C                     Z-ADD0         W#CONT
     C           W#CONT    DOWLE69
     C                     ADD  1         W#CONT
     C*
     C           W#CONT    CHAINAR037F3              90
     C   90                LEAVE
     C*
     C           S#INNO    IFEQ *BLANK                     發票號碼
     C                     Z-ADD0         S#INXA           沖銷金額
     C                     Z-ADD0         S#INAM           發票金額
     C                     Z-ADD0         S#INBA           發票餘額
     C                     MOVEL*BLANK    S#INOR           訂單號碼
     C                     MOVEL*BLANK    S#INAP           請款單號
     C                     Z-ADD0         S#INRA           未過已沖
     C                     ITER
     C                     ENDIF
     C*僅紀錄一次訂單號碼
     C  N98                MOVELS#ORNO    W#ORNO
     C  N98                MOVE *ON       *IN98
     C*以此訂單號碼檢核報關匯率
     C                     MOVE W#ORNO    C1ORNO
     C                     Z-ADD0         C1OSEQ
     C                     Z-ADD0         C1CITM
     C           K#CINV    SETLLRHSCINV
     C                     READ RHSCINV                  90
     C           *IN90     IFNE *OFF
     C           C1TRAT    ORNE S#EXC3
     C                     MOVELERR,15    S#ERR3
     C                     SETON                     5899
     C                     ENDIF
     C*發票錯誤
     C  N99      S#INNO    CHAININREC                90
     C           *IN90     IFEQ *ON
     C                     MOVELERR,9     S#ERR3
     C                     SETON                     5899
     C                     ENDIF
     C*客戶不同
     C  N99      INCUNO    IFNE S#CUNO
     C                     MOVELERR,10    S#ERR3
     C                     SETON                     5899
     C                     ENDIF
     C*訂單不同
     C                     MOVELINORNO    W#ORN1  6
     C  N99      W#ORNO    IFNE W#ORN1
     C                     MOVELERR,11    S#ERR3
     C                     SETON                     5899
     C                     ENDIF
     C*沖銷金額大於發票餘額
     C  N99      S#INXA    IFGT S#INBA
     C                     MOVELERR,12    S#ERR3
     C                     SETON                     5999
     C                     ENDIF
     C*
     C  N99                SETOF                     5859  無錯誤
     C*
     C                     UPDATAR037F3
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4400    BEGSR
     C*****************************************************************
     C*先將原有之所有資訊刪除
     C                     MOVE S#RVNO    SIRVNO           繳款單號
     C                     Z-ADD0         SIITEM           繳款項次
     C           K#RVIN    SETLLSIREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SIREC                    90
     C   90                LEAVE
     C*
     C           SIRVNO    IFNE S#RVNO
     C                     LEAVE
     C                     ENDIF
     C*
     C                     DELETSIREC
     C                     ENDDO
     C*存檔
     C                     Z-ADD0         W#CONT
     C                     Z-ADD0         SIITEM           繳款項次
     C           W#CONT    DOWLE69
     C                     ADD  1         W#CONT
     C           W#CONT    CHAINAR037F3              90
     C   90                LEAVE
     C*
     C           S#INNO    IFEQ *BLANK                     借貸別
     C           S#INXA    OREQ 0
     C                     ITER
     C                     ENDIF
     C*
     C                     EXSR SR4410                      MOVE DATA
     C*
     C           K#RVIN    CHAINSIREC                90
     C   90                WRITESIREC
     C*
     C                     ENDDO
     C*更新SARCVF沖銷金額合計
     C                     MOVE S#RVNO    SRRVNO           繳款單號
     C                     Z-ADD0         SRITEM           繳款項次
     C           K#SARC    SETLLSRREC
     C                     MOVE *OFF      *IN90
     C           *IN90     DOWEQ*OFF
     C                     READ SRREC                    90
     C   90                LEAVE
     C*
     C           SRRVNO    IFNE S#RVNO
     C                     LEAVE
     C                     ENDIF
     C*
     C                     Z-ADDS#XAMT    SRXAMT
     C                     UPDATSRREC
     C*
     C                     ENDDO
     C*
     C                     ENDSR
     C*****************************************************************
     C           SR4410    BEGSR
     C*****************************************************************
     C                     MOVE S#RVNO    SIRVNO
     C                     ADD  1         SIITEM
     C                     MOVELS#INNO    SIINNO           發票號碼
     C                     Z-ADDS#INXA    SIINXA           沖銷金額
     C                     Z-ADDS#INAM    SIINAM           發票金額
     C                     Z-ADDS#INBA    SIINBA           發票餘額
     C                     MOVE *BLANK    SIFL02           過入碼
     C*
     C                     ENDSR
**  ERR
01-無任何資料！
02-該繳款書已過入財會,如需異動請通知財會還原之！
03-該繳款書已確認,如需異動請還原之！
04-該繳款書尚未確認,無法還原！
05-資料存檔完畢！
06-欄位輸入錯誤！
07-無此訂單！
08-客戶不同！
09-發票號碼不存在！
10-客戶代號不同！
11-訂單號碼不同！
12-沖銷金額不得大於發票餘額！
13-繳款總額與沖銷總額不平,請修改或試算後再行確認！
14-試算完成！
15-報關匯率錯誤!
16-試算完畢,若結果無誤請逕行確認！
