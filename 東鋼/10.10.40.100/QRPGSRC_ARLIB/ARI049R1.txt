     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARI049R1
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02YSH
     H*            4.FUNCTION      客戶訂單暫收款查詢 (財會)
     H*            5.DATE-WRITTEN  91/04/01
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FACDYBL  IP  E           K        DISK
     FSAMAST  IF  E           K        DISK
     FHSMAST  IF  E           K        DISK
     FAR2159X O   E                    DISK
     IRACDYBL     01
9105 I                                              ACRLNOL1
     IRHSMAST
     I              S1SALE                          F#SALE
     I              S1SND                           F#SND
     I              S1RECV                          F#RECV
     I            DS
     I                                        1   6 D#ORNO
     I                                        1   1 D#ORN1
     I                                        2   60D#ORN2
     I            DS
     I                                        1   6 D#CUNO
     I                                        1   1 D#CUN1
     I                                        2   2 D#CUN2
     I                                        3   5 D#CUN3
     I                                        6   6 D#CUN4
     C*****************************************************************
     C*        主程式開始
     C*****************************************************************
9105 C   L1                EXSR L1CLR
     C   01                EXSR DTLSR
9105 CL1                   EXSR L1SR
     CLR                   RETRN
9105 C*****************************************************************
9105 C           L1CLR     BEGSR
9105 C*****************************************************************
9105 C                     Z-ADD0         W#AMT  120
9105 C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
9105 C           ACDC      IFEQ 'C'
9105 C                     ADD  ACAMT     W#AMT            金額
9105 C                     ELSE
9105 C                     SUB  ACAMT     W#AMT
9105 C                     ENDIF
9105 C*
     C                     ENDSR
9105 C*****************************************************************
9105 C           L1SR      BEGSR
9105 C*****************************************************************
     C                     CLEARRAR2159X
     C*
     C           K#MAST    KLIST
     C                     KFLD           S1OREA
     C                     KFLD           S1ORNO
     C                     KFLD           S1ORTM
     C*相關號碼第一碼非為英文即不判別訂單
     C                     MOVELACRLNO    W#RLNO  1
     C           W#RLNO    IFLT 'A'
     C           W#RLNO    ORGT 'Z'
 9204C           ACRLNO    OREQ 'MVP'
     C                     MOVELACRLNO    AXCUNM           客戶簡稱
     C                     MOVELACRLNO    AXORNO           訂單號碼
     C                     ELSE
     C*
     C                     MOVELACRLNO    AXORNO           訂單號碼
     C*
     C                     MOVELAXORNO    D#ORNO
     C                     MOVE D#ORN1    S1OREA
     C                     MOVE D#ORN2    S1ORNO
     C                     MOVE *LOVAL    S1ORTM
     C           K#MAST    SETLLRSAMAST
     C                     READ RSAMAST                  90
     C           *IN90     IFEQ *OFF
     C           S1OREA    ANDEQD#ORN1
     C           S1ORNO    ANDEQD#ORN2
     C                     MOVE S1KIND    D#CUN1
     C                     MOVE S1CUN1    D#CUN2
     C                     MOVE S1CUN2    D#CUN3
     C                     MOVE S1CD01    D#CUN4
     C                     MOVE D#CUNO    AXCUNO           客戶代號
     C                     MOVELS1CUNO    AXCUNM           客戶簡稱
     C*
     C                     ELSE
     C                     MOVE D#ORN1    S1OREA
     C                     MOVE D#ORN2    S1ORNO
     C                     MOVE *LOVAL    S1ORTM
     C           K#MAST    SETLLRHSMAST
     C                     READ RHSMAST                  90
     C           *IN90     IFEQ *OFF
     C           S1OREA    ANDEQD#ORN1
     C           S1ORNO    ANDEQD#ORN2
     C                     MOVE S1KIND    D#CUN1
     C                     MOVE S1CUN1    D#CUN2
     C                     MOVE S1CUN2    D#CUN3
     C                     MOVE S1CUN3    D#CUN4
     C                     MOVE D#CUNO    AXCUNO           客戶代號
     C                     MOVELS1CUNO    AXCUNM           客戶簡稱
     C                     ENDIF
     C*
     C                     ENDIF
     C*
     C                     ENDIF
     C*
9105 C                     Z-ADDW#AMT     AXAMT            金額
     C*
     C                     MOVELACVRNO    W#VRNO  1
     C           W#VRNO    IFEQ 'H'
     C                     MOVE '1'       AXMODE           來源
     C                     ELSE
     C                     MOVE '2'       AXMODE
     C                     ENDIF
     C*
     C                     WRITERAR2159X
     C                     ENDSR
