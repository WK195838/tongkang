     H*****************************************************************
     H*
     H*               >>   PROGRAM INTRODUCTION   <<
     H*
     H*            1.PROGRAM-ID    ARR083R
     H*            2.PROGRAM-TYPE  RPG/400
     H*            3.AUTHOR        S02YSH
     H*            4.FUNCTION     預收貨款增減明細表
     H*            5.DATE-WRITTEN  89/02/19
     H*            6.UPDATE  DATE 104/10/05  2015AR953  S00WCJ (0409A)
     H*                            產品代碼3碼擴5碼
     H*
     H*  說明：將本期預收退回寫入ARBBALT (單據代號AD01)
     H*
     H*****************************************************************
     H        1   Y                                     1                 BR003
     FTRNDTL  IP  E           K        DISK
     FARBBALT UF  E           K        DISK                      A
     ITXREC       01
     I                                              TXPCNOL2
     I                                              TXTXARL1
     I            DS
0409AI                                        1  30 D#RESV
     I                                        1   60D#YYMM
     I           UDS
     I                                      501 5060U#YYMM
     I*****************************************************************
     C           K#BALT    KLIST
     C                     KFLD           ATORNO
     C                     KFLD           ATCODE
     C                     KFLD           ATNO
     C*****************************************************************
     C  N99                MOVE *ON       *IN99
     C   L2                EXSR L2CLR
     C   L1                EXSR L1CLR
     C   01                EXSR DTLSR
     CL1 99                EXSR L1SR
     CLR                   RETRN
     C*****************************************************************
     C           L2CLR     BEGSR
     C*****************************************************************
     C                     Z-ADD1         W#AD01  40
     C                     ENDSR
     C*****************************************************************
     C           L1CLR     BEGSR
     C*****************************************************************
     C                     Z-ADD0         W#AMT   90
     C                     ENDSR
     C*****************************************************************
     C           DTLSR     BEGSR
     C*****************************************************************
     C                     MOVE TXRESV    D#RESV
     C           D#YYMM    IFEQ U#YYMM                     *入帳年月
     C                     ADD  TXAMT     W#AMT
     C                     ENDIF
     C                     ENDSR
     C*****************************************************************
     C           L1SR      BEGSR
     C*****************************************************************
     C           D#YYMM    IFEQ U#YYMM                     *入帳年月
     C                     MOVELTXORNO    ATORNO           *訂單編號
     C                     MOVE 'AD01'    ATCODE           *單據代號
     C                     Z-ADDW#AD01    ATNO             *單據編號
     C           K#BALT    CHAINRARBBALT             40
     C                     MOVELTXTXAR    ATAREA           *銷貨廠區
     C                     Z-ADDW#AMT     ATAMT     H      *預收金額
     C                     MOVELTXNO      ATINNO           *發票號碼
     C                     WRITERARBBALT
     C                     ADD  1         W#AD01
     C                     ENDIF
     C                     ENDSR
