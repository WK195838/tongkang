    /****************************************************************/
    /*                                                              */
    /*             UPDATE  DATE  98/12/31 2009AR406 S00WCJ (9812A)  */
    /*                                                              */
    /****************************************************************/

        /*  ＝＝＝＜台北　預收貨款CPRBAL傳輸程式＞＝＝＝   */
             PGM
             DCL        VAR(&INT)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&ARA2) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LOC) TYPE(*CHAR) LEN(1)
             DCLF       FILE(ARP027S) RCDFMT(TITLE)
             RTVDTAARA  DTAARA(*LDA (1021 1)) RTNVAR(&LOC)
             RTVJOBA    TYPE(&INT)
             IF         COND(&INT *EQ '0') THEN(GOTO CMDLBL(BATCH))
             CHGVAR     VAR(&AREA1) VALUE(&LOC)
 START:      SNDRCVF    RCDFMT(TITLE)
             CHGVAR     VAR(&ERRMSG) VALUE('                      ')
             IF         COND(&IN03 *EQ '1') THEN(RETURN)
             IF         COND(&ORNO *EQ '     ') THEN(DO)
             CHGVAR     VAR(&ERRMSG) VALUE('訂單號碼不可空白')
                        GOTO START
                        ENDDO
             IF         COND(&AREA2 *EQ ' ') THEN(DO)
             CHGVAR     VAR(&ERRMSG) VALUE('欲送廠區不可空白')
                        GOTO START
                        ENDDO
             ELSE IF    COND(&AREA2 *EQ &LOC) THEN(DO)
             CHGVAR     VAR(&ERRMSG) VALUE('欲送廠區不可為原廠區')
                        GOTO START
                        ENDDO
             CHGDTAARA  DTAARA(*LDA (1 1)) VALUE(&AREA1)
             CHGDTAARA  DTAARA(*LDA (2 1)) VALUE(&AREA2)
             CHGDTAARA  DTAARA(*LDA (3 6)) VALUE(&ORNO)
             SBMJOB     JOB(ARP027P) JOBD(ARJOBD) JOBQ(QSYS/QCTL) +
                          RQSDTA('CALL ARLIB/ARP027P')
      /*     SNDPGMMSG  +
                          MSG('發票主檔傳輸已送入批次作業執行中..+
                          .') TOPGMQ(*EXT)      */
             GOTO       CMDLBL(ENDCLPGM)
 BATCH:      RTVDTAARA  DTAARA(*LDA (2 1)) RTNVAR(&ARA2)
        /*  　　  ORNO --> ARLIB/BALF   (TEMP-FILE) */
        /*  建立　DDMF & DTAARA ............................ */
             DLTF       FILE(QTEMP/DDMP)
             MONMSG     MSGID(CPF0000)
             IF         COND(&ARA2 *EQ 'T') THEN(DO)
             CRTDUPOBJ  OBJ(TYDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(DDMP)
             ENDDO
    /*9812A START*/
             ELSE IF         COND(&ARA2 *EQ 'U') THEN(DO)
             CRTDUPOBJ  OBJ(U01DDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(DDMP)
             ENDDO
    /*9812A END */
             ELSE IF    COND(&ARA2 *EQ 'K') THEN(DO)
             CRTDUPOBJ  OBJ(KSDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(DDMP)
             ENDDO
             ELSE IF    COND(&ARA2 *EQ 'H') THEN(DO)
             CRTDUPOBJ  OBJ(MLDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(DDMP)
             ENDDO
             ELSE IF    COND(&ARA2 *EQ 'P') THEN(DO)
             CRTDUPOBJ  OBJ(TPDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(DDMP)
             ENDDO
 STEP1:
        /*  各廠區抓  ARLIB/CPRBAL    ................ */
             CHGDDMF    FILE(QTEMP/DDMP) RMTFILE(ARLIB/BALFT) +
                          LVLCHK(*NO)
             OVRDBF     FILE(BALFT) TOFILE(QTEMP/DDMP)
             MONMSG     MSGID(CPF0000)
             CALL       PGM(ARLIB/ARP027RA)
             SBMRMTCMD  CMD('call arlib/arp027PA') +
                          DDMFILE(QTEMP/DDMP)
             DLTOVR     FILE(*ALL)
 ENDCLPGM:   ENDPGM
