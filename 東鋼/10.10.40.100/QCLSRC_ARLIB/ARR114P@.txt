        /*********************************************************************/
        /*                                                                   */
        /*        >>   PROGRAM INTRODUCTION  <<                              */
        /*        1.PROGRAM-ID     ARR114P                                   */
        /*        2.PROGRAM-TYPE   CLP                                       */
        /*        3.AUTHOR         S02CSF                                    */
        /*        4.FUNCTION      折讓單統計表（含銷折／銷退／預收）　     */
        /*        5.DATE-WRITTEN   93/01/08                                  */
        /*        6.DATE-MODIFY                                              */
        /*                                                                   */
        /*********************************************************************/
          PGM
          DCLF     FILE(ARR114S)
          DCL      VAR(&TYPE)   TYPE(*CHAR) LEN(1)
          DCL      VAR(&C#YYMM) TYPE(*CHAR) LEN(6)
          DCL      VAR(&C#YM)   TYPE(*CHAR) LEN(4)
          DCL      VAR(&C#YY)   TYPE(*CHAR) LEN(4)
          DCL      VAR(&C#MM)   TYPE(*CHAR) LEN(2)
          DCL      VAR(&W#PGM)  TYPE(*CHAR) LEN(8)
          DCL      VAR(&ARE1)   TYPE(*CHAR) LEN(1)
          DCL      VAR(&ARE2)   TYPE(*CHAR) LEN(1)

          RTVJOBA  JOB(&S#DEVN) USER(&U#USID) TYPE(&TYPE)
          IF       COND(&TYPE *EQ '0') THEN(GOTO BATCH)

  START:  SNDRCVF

          CHGVAR     VAR(&IN30)      VALUE('0')
          CHGVAR     VAR(&IN31)      VALUE('0')
          IF         COND(&IN03 *EQ '1') THEN(RETURN)
          IF         COND(&IN12 *EQ '1') THEN(RETURN)

  /*  CHECK 折讓發票廠區*/
          IF         COND(&S#AREA *EQ  ' '  ) THEN(DO)
          CHGVAR     VAR(&S#ERR)     VALUE('請輸入折讓發票廠區')
          CHGVAR     VAR(&IN30)      VALUE('1')
          GOTO       CMDLBL(START)
          ENDDO

          IF         COND(&S#YYMM *EQ  0  ) THEN(DO)
          CHGVAR     VAR(&S#ERR)     VALUE('請輸入折讓確認年月')
          CHGVAR     VAR(&IN31)      VALUE('1')
          GOTO       CMDLBL(START)
          ENDDO

          CHGVAR     VAR(&C#YYMM)   VALUE(&S#YYMM)
          CHGDTAARA  DTAARA(*LDA (300  6))  VALUE(&C#YYMM)
          RTVDTAARA  DTAARA(*LDA (300  4))  RTNVAR(&C#YY)
          RTVDTAARA  DTAARA(*LDA (304  2))  RTNVAR(&C#MM)
          IF         COND(&C#MM  *GT '12')  THEN(DO)
          CHGVAR     VAR(&S#ERR)     VALUE('折讓確認年月輸入錯誤')
          CHGVAR     VAR(&IN31)      VALUE('1')
          GOTO       CMDLBL(START)
          ENDDO

          CHGDTAARA  DTAARA(*LDA (300 1)) VALUE(&S#AREA)
          CHGDTAARA  DTAARA(*LDA (301 6)) VALUE(&C#YYMM)
          CHGDTAARA  DTAARA(*LDA (307 1)) VALUE(&S#KIND)

          SBMJOB     CMD(CALL PGM(ARR114P)) JOB(ARR114P) +
                     JOBD(ARJOBD)
          SNDBRKMSG  MSG('<<折讓單統計表已開始處理，請稍待....>>') +
                     TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)
          RETURN
  BATCH:
          RTVDTAARA  DTAARA(*LDA (300 1)) RTNVAR(&S#AREA)
          RTVDTAARA  DTAARA(*LDA (301 6)) RTNVAR(&C#YYMM) /*年月六碼*/
          RTVDTAARA  DTAARA(*LDA (303 4)) RTNVAR(&C#YM)   /*年月四碼*/
          RTVDTAARA  DTAARA(*LDA (307 1)) RTNVAR(&S#KIND)

          IF         COND(&S#AREA *EQ 'P') THEN(DO)
          CHGVAR     VAR(&ARE1)     VALUE('P')
          CHGVAR     VAR(&ARE2)     VALUE('T')
          ENDDO
          ELSE (DO)
          CHGVAR     VAR(&ARE1)     VALUE(&S#AREA)
          CHGVAR     VAR(&ARE2)     VALUE(&S#AREA)
          ENDDO

          /*   先前處理　　*/
          CLRPFM     ARR114F
          OVRDBF     FILE(TRNDTLL3) TOFILE(DALIB/TRNDTLL3) SHARE(*YES)
          OPNQRYF    FILE((TRNDTLL3)) OPTION(*ALL) QRYSLT('     +
                     ((ACDT     *EQ "' || &C#YYMM || '") *OR    +
                      ((ACDX    *EQ "999999" )           *AND   +
                       (RESV    *EQ "' || &C#YM   || '"))) *AND +
                     (TXFL01    *EQ "Y" )               *AND +
                     (TXFLAG    *NE "D" )')           +
                     KEYFLD((TXNO) (TXACDT))         +
                     MAPFLD((ACD1   TXACDT  *CHAR 8) +
                            (ACDT   '%SST(ACD1   1 6)' *CHAR 6) +
                            (ACDX   '%SST(ACD1   3 6)' *CHAR 6) +
                            (RESV   '%SST(TXRESV 1 4)' *CHAR 4))
          CALL       PGM(ARR114R)
          CLOF       OPNID(TRNDTLL3)

          OVRPRTF    FILE(ARR114TA) TOFILE(ARR114TA) PAGESIZE(*N 132) +
                     CPI(10) HOLD(*YES) USRDTA('折讓明細')
          OVRPRTF    FILE(ARR114TB) TOFILE(ARR114TB) PAGESIZE(*N  98) +
                     CPI(10) HOLD(*YES) USRDTA('折讓彙總')
          OVRDBF     FILE(ARR114F)  TOFILE(ARR114F) SHARE(*YES)
          OPNQRYF    FILE((ARR114F)) OPTION(*ALL) QRYSLT('     +
                     (A4AREA   *GE "' || &ARE1 || '") *AND   +
                     (A4AREA   *LE "' || &ARE2 || '")')      +
                     KEYFLD((A4ACNT) (A4NO) (A4ACDT) (A4CUNM) (A4ORNO))
          CHGVAR     VAR(&W#PGM)    VALUE('ARR114R' || &S#KIND)
          CALL       PGM(&W#PGM)
          CLOF       OPNID(ARR114F)
          DLTOVR     *ALL

          SNDBRKMSG  MSG('<<折讓單統計表已處理完成，請列印....>>') +
                     TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)

          ENDPGM
