     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARR072P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02CLJ                                  */
     /*     4.FUNCTION     型鋼廠別出貨數量表                     */
     /*     5.DATE-WRITTEN  87/06/28                                */
     /*                                                             */
     /*  說明：*LDA  1 -->  成品大類(2, 3, 4, (3+4))            */
     /*               11 -->  起始年月                            */
     /*               21 -->  終止年月                            */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&INT)     TYPE(*CHAR)   LEN(1)
             DCL        VAR(&OUTQ)    TYPE(*CHAR)   LEN(10)
             DCL        VAR(&IN03)    TYPE(*CHAR)   LEN(1)
             DCL        VAR(&W#TYPE)  TYPE(*CHAR)   LEN(1)
             DCL        VAR(&W#BYMD)  TYPE(*CHAR)   LEN(8)
             DCL        VAR(&W#EYMD)  TYPE(*CHAR)   LEN(8)
             DCL        VAR(&W#YYMM)  TYPE(*CHAR)   LEN(6)


             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT)
             IF         COND(&INT *EQ '0') THEN(GOTO BATCH)

 INPUT:
             CALL       PGM(ARR072RS) PARM(&IN03)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)

             SBMJOB     JOB(ARR072P) JOBD(ARJOBD) OUTQ(&OUTQ) +
                        RQSDTA('CALL ARR072P')
             RETURN


 BATCH:
             RTVDTAARA  DTAARA(*LDA (1  1)) RTNVAR(&W#TYPE)
             RTVDTAARA  DTAARA(*LDA (11 6)) RTNVAR(&W#YYMM)
             CHGVAR     VAR(&W#BYMD) VALUE(&W#YYMM || '01')
             RTVDTAARA  DTAARA(*LDA (21 6)) RTNVAR(&W#YYMM)
             CHGVAR     VAR(&W#EYMD) VALUE(&W#YYMM || '31')

             OVRPRTF    FILE(ARR072T) TOFILE(ARLIB/ARR072T) +
                        PAGESIZE(66 132) LPI(6) CPI(10) +
                        OVRFLW(57) HOLD(*YES) USRDTA('客廠出貨') +
                        COPIES(1)
             OVRDBF     FILE(TRNDTL) TOFILE(DALIB/TRNDTL) SHARE(*YES)

             IF         COND(&W#TYPE *EQ '7') THEN(DO)   /*型板*/
             OPNQRYF    FILE(TRNDTL) +
                        QRYSLT( +
                            'TXCODE *EQ "SA04" *AND +
                             TXACNT *NE "3"    *AND +
                        %SST(TXPDNM 1 1) *EQ %VALUES("3" "4") *AND +
                             TXDATE *GE ' || &W#BYMD || ' *AND +
                             TXDATE *LE ' || &W#EYMD ) +
                        MAPFLD((TXTXDT 'TXDATE / 100' )) +
                        KEYFLD((TXTXDT) (TXCUNO) (TXNO))
             ENDDO

             ELSE       CMD(DO)
             OPNQRYF    FILE(TRNDTL) +
                        QRYSLT( +
                            'TXCODE *EQ "SA04" *AND +
                             TXACNT *NE "3"    *AND +
                        %SST(TXPDNM 1 1) *EQ "' || &W#TYPE || '" *AND +
                             TXDATE *GE ' || &W#BYMD || ' *AND +
                             TXDATE *LE ' || &W#EYMD ) +
                        MAPFLD((TXTXDT 'TXDATE / 100' )) +
                        KEYFLD((TXTXDT) (TXCUNO) (TXNO))
             ENDDO

             CALL       PGM(ARR072R)
             CLOF       OPNID(TRNDTL)
             DLTOVR     FILE(*ALL)

             ENDPGM
