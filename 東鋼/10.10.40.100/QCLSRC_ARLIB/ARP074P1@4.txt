     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP074P1                                */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02YSH                                  */
     /*     4.FUNCTION     折讓代開傳票(FOR H ONLY)               */
     /*     5.DATE-WRITTEN  89/12/29                                */
     /*     6.UPDATE DATE  100/11/28  2011AR634 S00WCJ (0011A)      */
     /*                     配合IFRS修改                        */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&AREA)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&YYMM)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&W#YM)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&YY)    TYPE(*CHAR) LEN(4)
             DCL        VAR(&MM)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&FNAME) TYPE(*CHAR) LEN(10)
 /*0011A*/   DCL        VAR(&D#VNO1) TYPE(*CHAR) LEN(6)
 /*0011A*/   DCL        VAR(&D#VNO2) TYPE(*CHAR) LEN(6)

             RTVDTAARA  DTAARA(*LDA (1 6)) RTNVAR(&YYMM)
             RTVDTAARA  DTAARA(*LDA (1 4)) RTNVAR(&YY)
             RTVDTAARA  DTAARA(*LDA (1 6)) RTNVAR(&W#YM)
             RTVDTAARA  DTAARA(*LDA (5 2)) RTNVAR(&MM)
             RTVDTAARA  DTAARA(*LDA (7 1)) RTNVAR(&AREA)
             CHGVAR     VAR(&FNAME) +
                        VALUE('AC' || &AREA || 'R')


STEP1:       /* 刪除已代開過的資料 */

             OVRDBF     FILE(ACTRAN) TOFILE(&FNAME) SHARE(*YES)
             OPNQRYF    FILE(ACTRAN) OPTION(*ALL) +
                        QRYSLT('YM     *EQ "' || &YYMM || '" *AND +
                                ACVRNO *GE "P00601"          *AND +
                                ACVRNO *LE "P00650"')             +
                        MAPFLD((YM  ACYM  *CHAR 6))

             CALL       PGM(ARP074R1)  /*刪除輸入日期後的資料*/
             CLOF       OPNID(ACTRAN)
             DLTOVR     *ALL
 /*0011A STRAT */

             CHGVAR     VAR(&D#VNO1)  VALUE('P00601')
             CHGVAR     VAR(&D#VNO2)  VALUE('P00650')

             CALL       PGM(ARS888P) PARM(&AREA &W#YM &D#VNO1 &D#VNO2)
 /*0011A  END */


STEP2:       /* 代開開始*/   /*------------------*/

             OVRDBF     FILE(ACTRAN) TOFILE(&FNAME) SHARE(*YES)
             OVRPRTF    FILE(ARP074T) TOFILE(ARP074T) PAGESIZE(*N +
                          158) CPI(12) HOLD(*YES) USRDTA('折讓退回')

             CHGDDMF    FILE(ARLIB/ARDDM) RMTFILE(ARLIB/QCLSRC)
             SBMRMTCMD  CMD('CALL ARLIB/ARP074P2') DDMFILE(ARLIB/ARDDM)

             DLTF       FILE(QTEMP/TRNDTLX)
             MONMSG     MSGID(CPF0000)

             CRTDUPOBJ  OBJ(TRNDTL) FROMLIB(DALIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(TRNDTLX)
             CHGDDMF    FILE(ARLIB/ARDDM) RMTFILE(QTEMP/TRNDTLX)
             CPYF       FROMFILE(ARLIB/ARDDM) TOFILE(QTEMP/TRNDTLX) +
                          TOMBR(*FROMMBR) MBROPT(*REPLACE) +
                          INCREL((*IF TXCODE *EQ 'AR06') (*AND +
                          TXDATE *GE 900101)) FMTOPT(*MAP *DROP)
             OVRDBF     FILE(TRNDTL) TOFILE(QTEMP/TRNDTLX) SHARE(*YES)
          /* 苗栗 DOWNLOAD使用START*/
             CLRPFM     FILE(ARP074F)
             MONMSG     MSGID(CPF0000)
          /* 苗栗 DOWNLOAD使用END*/

             OPNQRYF    FILE(TRNDTL) OPTION(*ALL) +
                        QRYSLT('+
                              (TXCODE *EQ "AR06") *AND +
                              (DATE *EQ "99999999") *AND +
                              (RESV *EQ "' || &W#YM || '") *AND +
                              (TXIVNO *NE "*") *AND +
                              (TXIVNO *NE "#") *AND +
                              ((TXACNT *EQ "2") *OR +
                               (TXACNT *EQ "3")) *AND +
                              (TXFLAG *NE "D")') +
                        MAPFLD((RESV '%SST(TXRESV 1 6)' *CHAR 6) +
                               (DATE TXACDT *CHAR 8)) +
                        KEYFLD((TXORNO) (TXNO) (TXPDNM))

             CALL       PGM(ARP074R)  /*傳票代開及列表*/
             CLOF       OPNID(TRNDTL)
             DLTOVR     FILE(*ALL)

 /*0011A STRAT */

             RTVDTAARA  DTAARA(*LDA (201 6)) RTNVAR(&D#VNO1)
             RTVDTAARA  DTAARA(*LDA (211 6)) RTNVAR(&D#VNO2)

             CALL       PGM(ARS888P) PARM(&AREA &W#YM &D#VNO1 &D#VNO2)
 /*0011A  END */
             ENDPGM
