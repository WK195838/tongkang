     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP112PF                                */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02CSF                                  */
     /*     4.FUNCTION     營業退款檔製票資料傳輸作業             */
     /*     5.DATE-WRITTEN  93/11/01                                */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&INT)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&USER)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&FLAG1) TYPE(*CHAR) LEN(3) VALUE('X  ')


             RTVJOBA    TYPE(&INT) USER(&USER)
             IF         COND(&INT *EQ '0') THEN(GOTO CMDLBL(BATCH))
             SBMJOB     JOB(ARP112PF) JOBD(ARJOBD) JOBQ(QSYS/QCTL) +
                        RQSDTA('CALL ARLIB/ARP112PF') LOGCLPGM(*YES)
             SNDBRKMSG  MSG('<<營業退款檔製票資料傳輸作業已開始處理+
                         ,請稍待.........>>') +
                        TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)
             RETURN


 BATCH:

 STEP0:        /*  建立　DDMF  */
             DLTF       FILE(QTEMP/DDMT)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(TYDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                        TOLIB(QTEMP) NEWOBJ(DDMT)

             DLTF       FILE(QTEMP/DDMK)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(KSDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                        TOLIB(QTEMP) NEWOBJ(DDMK)

             DLTF       FILE(QTEMP/DDMH)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(MLDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                        TOLIB(QTEMP) NEWOBJ(DDMH)


 STEP1:        /*  台北製票資料寫入傳輸檔ARRETNX   */
             CLRPFM     ARRETNX
             MONMSG     MSGID(CPF0000)
             OVRDBF     FILE(ARRETNX) TOFILE(ARLIB/ARRETNX)
             OVRDBF     FILE(ARRETN)  TOFILE(DALIB/ARRETN) SHARE(*YES)
             OPNQRYF    FILE((ARRETN)) OPTION(*ALL) QRYSLT('    +
                        (ANFLAG *EQ "C")                   *AND +
                        (ANFL03 *NE " ")                   *AND +
                        (ANTRUS *NE "          ")          *AND +
                        (ANTRDT *NE 0  )                   *AND +
                        (ANGLNO *NE "          ")')             +
                        KEYFLD((ANRTNO) (ANITEM))
             CALL       PGM(ARLIB/ARP112RF)
             CLOF       OPNID(ARRETN)

 STEP2:        /*  台北ARLIB/ARRETNX 送　桃園ARLIB/ARRETN   */
             CHGDDMF    FILE(QTEMP/DDMT) RMTFILE(ARLIB/ARRETNX) +
                        LVLCHK(*NO)
             CPYF       FROMFILE(ARLIB/ARRETNX) TOFILE(QTEMP/DDMT) +
                        MBROPT(*REPLACE)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO STEP3)
             SBMRMTCMD  CMD('CALL PGM(ARLIB/ARP112PFA) PARM(''T'')') +
                        DDMFILE(QTEMP/DDMT)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO STEP3)

             CLRPFM     FILE(QTEMP/DDMT)
             MONMSG     MSGID(CPF0000)
             CHGVAR     VAR(%SST(&FLAG1 1 1)) VALUE(' ')

 STEP3:        /*  台北　抓　苗栗  ARLIB/ARRETNX  */

             GOTO       STEP4 /*不傳輸苗栗資料，因苗栗資料用代開或是業務在Ｐ*/


 STEP4:        /*  台北　抓　高雄  ARLIB/ARRETNX  */
             GOTO       STEP5 /*不傳輸高雄資料，因高雄資料自己做*/

 /* -------------------------------------------------------------- */

 STEP5:       /* 未成功廠區之訊息 */

             DLTOVR     FILE(*ALL)
             IF         COND(&FLAG1 *NE '   ') THEN(DO)
             SNDMSG     TOUSR(&USER) MSG(+
                        '營業退款檔製票資料下傳失敗"' || &FLAG1 || '"')
             ENDDO
             ELSE (DO)
             SNDMSG     TOUSR(&USER) MSG(+
                        '營業退款檔製票資料下傳成功')
             ENDDO


             ENDPGM
