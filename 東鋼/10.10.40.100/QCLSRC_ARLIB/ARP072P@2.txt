     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP072P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02YSH                                  */
     /*     4.FUNCTION     銷貨收入每月差異代開傳票               */
     /*     5.DATE-WRITTEN  89/05/20                                */
     /*                                                             */
     /*  <說明:> 由於銷貨收入由銷貨明細代開而非發票明細代開     */
     /*            因此當隨訂發票愈多時，便會有差異出現。         */
     /*            本作業每月進行一次，先刪除該月份之傳票資料     */
     /*            再進行代開整月的動作。（可重複代開）           */
     /*            傳票編號：P001 -- P499                         */
     /*                                                             */
     /***************************************************************/
             PGM
             DCLF       FILE(ARP072S)
             DCL        VAR(&P#YYMM) TYPE(*CHAR) LEN(4)
             DCL        VAR(&P#CODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&INT)   TYPE(*CHAR) LEN(1)   /*執行狀態*/
             DCL        VAR(&OUTQ)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03)  TYPE(*LGL)  LEN(1)
             DCL        VAR(&AREA)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&YYMM)  TYPE(*CHAR) LEN(4)
             DCL        VAR(&YY)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&MM)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&FNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C#SDAT) TYPE(*CHAR) LEN(6)
             DCL        VAR(&C#EDAT) TYPE(*CHAR) LEN(6)

             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT) USER(&USER)
             IF         COND(&INT *EQ '0') THEN(GOTO BATCH)

             RTVDTAARA  DTAARA(*LDA (1021 1)) RTNVAR(&AREA)
             RTVDTAARA  DTAARA(*LDA (951 35)) RTNVAR(&S#COMP)
             RTVDTAARA  DTAARA(*LDA (1011 10)) RTNVAR(&S#DEVI)
             IF         COND(&AREA *EQ 'P')  THEN(DO)
             CHGVAR     VAR(&AREA) VALUE('M')
             ENDDO
             CHGVAR     VAR(&S#AREA) VALUE(&AREA)
 DISPLAY:
             SNDRCVF    RCDFMT(AR072F1)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)

             IF         COND(&S#AREA *EQ 'P') THEN(DO)
             CHGVAR     VAR(&S#ERR) +
                        VALUE('銷貨差異代開傳票作業，不得在台北進行')
                        GOTO DISPLAY
                        ENDDO

             CHGVAR     VAR(&YYMM) VALUE(&S#YYMM)
             CHGVAR     VAR(&P#YYMM) VALUE(&S#YYMM)
             CHGVAR     VAR(&YY) VALUE(%SST(&YYMM 1 2))
             CHGVAR     VAR(&MM) VALUE(%SST(&YYMM 3 2))
             CHGVAR     VAR(&FNAME) +
                        VALUE('AC' || &AREA || 'R' || &YY )
             CHGVAR     VAR(&MNAME) VALUE('VR' || &MM)
             CHKOBJ     OBJ(&FNAME) OBJTYPE(*FILE) MBR(&MNAME)
             MONMSG     CPF0000     EXEC(DO)
             CHGVAR     VAR(&S#ERR) +
                        VALUE('銷貨差異代開傳票作業，無法找到指定+
                          的傳票檔：' || &FNAME || ', ' || &MNAME)
                        GOTO DISPLAY
             ENDDO

             CALL    PGM(ACP101R) PARM(&P#YYMM &P#CODE)
             IF         COND(&P#CODE *EQ '1') THEN(DO)
             CHGVAR     VAR(&S#ERR) VALUE('關帳日期已過')
             GOTO       CMDLBL(DISPLAY)
             ENDDO

             CHGDTAARA  DTAARA(*LDA (1 4)) VALUE(&YYMM)
             CHGDTAARA  DTAARA(*LDA (7 1)) VALUE(&AREA)
             SBMJOB     JOB(ARP072P) JOBD(ARJOBD) OUTQ(&OUTQ)  +
                        RQSDTA('CALL ARP072P')
             RETURN

BATCH:
             RTVDTAARA  DTAARA(*LDA (1 4)) RTNVAR(&YYMM)
             RTVDTAARA  DTAARA(*LDA (1 2)) RTNVAR(&YY)
             RTVDTAARA  DTAARA(*LDA (3 2)) RTNVAR(&MM)
             RTVDTAARA  DTAARA(*LDA (7 1)) RTNVAR(&AREA)
             CHGVAR     VAR(&FNAME) +
                        VALUE('AC' || &AREA || 'R' || &YY )
             CHGVAR     VAR(&MNAME) VALUE('VR' || &MM)


STEP1:       /* 刪除已代開過的資料 */

             OVRDBF     FILE(ACTRAN) TOFILE(&FNAME) MBR(&MNAME) +
                        SHARE(*YES)
             OPNQRYF    FILE(ACTRAN) OPTION(*ALL) +
                        QRYSLT('ACVRNO *GE "P001" *AND +
                                ACVRNO *LE "P499"')

             CALL       PGM(ARP072R1) /*刪除輸入日期後的資料*/
             CLOF       OPNID(ACTRAN)
             DLTOVR     *ALL


STEP2:       /* 先前處理 */
             CLRPFM     ARP072F
             OVRDBF     FILE(INVMST) TOFILE(INVMST) SHARE(*YES)

             CHGVAR     VAR(&C#SDAT) VALUE(&YYMM || '01')
             CHGVAR     VAR(&C#EDAT) VALUE(&YYMM || '31')

             OPNQRYF    FILE(INVMST) OPTION(*ALL) +
                        QRYSLT('+
                              (INDT *GE "' || &C#SDAT || '") *AND +
                              (INDT *LE "' || &C#EDAT || '") *AND +
                              (INNO *NE "JW32774325")  *AND +
                              (INNO *NE "JW32774497")  *AND +
                              (INORNO *NE "M01744")    *AND +
                              (INORNO *NE "M01762")    *AND +
                              (INORNO *NE "M01763")    *AND +
                              (INORNO *NE "M01764")    *AND +
                              (INORNO *NE "M01765")    *AND +
                              (INORNO *NE "M01766")    *AND +
                              (INORNO *NE "M01767")    *AND +
                              (INORNO *NE "M01781")    *AND +
                              (INDECD *NE "D")') +
                        MAPFLD((DATE ININDT *CHAR 8) +
                               (INDT '%SST(DATE 3 6)' *CHAR 6))  +

             CALL       PGM(ARP072R2) /*先前處理*/
             CLOF       OPNID(INVMST)
             DLTOVR     *ALL

             IF         COND((&AREA *EQ 'H')  *OR +
                             (&AREA *EQ 'K')) THEN(DO)
             CLRPFM     ARP072F1
             OVRDBF     FILE(HSCINV) TOFILE(HSCINV) SHARE(*YES)
             OPNQRYF    FILE(HSCINV) OPTION(*ALL) +
                        QRYSLT('+
                              (CDTE *GE "' || &C#SDAT || '") *AND +
                              (CDTE *LE "' || &C#EDAT || '") *AND +
                              (C1FLAG *NE "D")') +
                        MAPFLD((DATE C1CDTE *CHAR 8) +
                               (CDTE '%SST(DATE 3 6)' *CHAR 6))
             CALL       PGM(ARP072R3) /*先前處理*/
             CLOF       OPNID(HSCINV)
             DLTOVR     FILE(*ALL)
             ENDDO


STEP3:       /* 代開開始*/   /*------------------*/

             OVRDBF     FILE(ACTRAN) TOFILE(&FNAME) MBR(&MNAME)        +
                        SHARE(*YES)
             OVRPRTF    FILE(ARP072T) TOFILE(ARP072T) PAGESIZE(*N +
                          132) CPI(10) HOLD(*YES) USRDTA('發銷差異')

             OVRDBF     FILE(ARP072F) TOFILE(ARP072F) SHARE(*YES)
             OPNQRYF    FILE(ARP072F) +
                        KEYFLD((A1ORNO) (A1IVNO) (A1PDNM))

             CALL       PGM(ARP072R)  /*傳票代開及列表*/
             CLOF       OPNID(ARP072F)

             IF         COND((&AREA *EQ 'H')  *OR +
                             (&AREA *EQ 'K')) THEN(DO)
             OVRPRTF    FILE(ARP072T) TOFILE(ARP072TA) PAGESIZE(*N +
                          158) CPI(12) HOLD(*YES) USRDTA('外銷差異')

             OVRDBF     FILE(ARP072F1) TOFILE(ARP072F1) SHARE(*YES)
             OPNQRYF    FILE(ARP072F1) +
                        KEYFLD((A1ORNO) (A1IVNO) (A1PDNM))

             CALL       PGM(ARP072RA)  /*傳票代開及列表*/
             CLOF       OPNID(ARP072F1)
             DLTOVR     FILE(*ALL)
             ENDDO

             ENDPGM
