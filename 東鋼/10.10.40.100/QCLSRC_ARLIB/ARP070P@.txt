     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP070P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02CLJ                                  */
     /*     4.FUNCTION     客戶暫收統計檔彙總後上傳台北           */
     /*     5.DATE-WRITTEN  88/11/30                                */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&INT)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&USER)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&FLAG1) TYPE(*CHAR) LEN(3) VALUE('X X')


             RTVJOBA    TYPE(&INT) USER(&USER)
             IF         COND(&INT *EQ '0') THEN(GOTO CMDLBL(BATCH))
             SBMJOB     JOB(ARP070P) JOBD(ARJOBD) JOBQ(QSYS/QCTL) +
                        RQSDTA('CALL ARLIB/ARP070P') LOGCLPGM(*YES)
             RETURN


 BATCH:

        /* ------------------------------------------------- */
        /*     彙總處理：兌現金額及繳貨款金額沖銷          */

             OVRDBF     FILE(AR2159F) TOFILE(DALIB/AR2159F)
             CALL       PGM(ARLIB/ARP070RC)
             DLTOVR     FILE(*ALL)


 /* -------------------------------------------------------------- */

 STEP0:        /*  建立　DDMF  */
             DLTF       FILE(QTEMP/DDMT)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(TYDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                        TOLIB(QTEMP) NEWOBJ(DDMT)

             DLTF       FILE(QTEMP/DDMK)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(KSDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                        TOLIB(QTEMP) NEWOBJ(DDMK)

             DLTF       FILE(QTEMP/DDMH)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(MLDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                        TOLIB(QTEMP) NEWOBJ(DDMH)


 STEP1:        /*  台北　抓　桃園  ARLIB/AR2159FT  */
             SBMRMTCMD  CMD('CALL PGM(ARLIB/ARP070PA) PARM(''T'')') +
                        DDMFILE(QTEMP/DDMT)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO STEP2)

             CHGDDMF    FILE(QTEMP/DDMT) RMTFILE(ARLIB/AR2159FT)  +
                        LVLCHK(*NO)
             OVRDBF     FILE(AR2159FP) TOFILE(QTEMP/DDMT)
             OVRDBF     FILE(AR2159F)  TOFILE(DALIB/AR2159F)
             CALL       PGM(ARLIB/ARP070RB)
             MONMSG     MSGID(CPF0000)  EXEC(DO)
             DLTOVR     FILE(*ALL)
             GOTO       CMDLBL(STEP2)
             ENDDO

             DLTOVR     FILE(*ALL)
             CLRPFM     FILE(QTEMP/DDMT)
             MONMSG     MSGID(CPF0000)
             CHGVAR     VAR(%SST(&FLAG1 1 1)) VALUE(' ')


 STEP2:        /*  台北　抓　苗栗  ARLIB/AR2159FH  */
             GOTO       STEP3           /* 略過苗栗  */

             SBMRMTCMD  CMD('CALL PGM(ARLIB/ARP070PA) PARM(''H'')') +
                        DDMFILE(QTEMP/DDMH)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO STEP3)

             CHGDDMF    FILE(QTEMP/DDMH) RMTFILE(ARLIB/AR2159FH)  +
                        LVLCHK(*NO)
             OVRDBF     FILE(AR2159FP) TOFILE(QTEMP/DDMH)
             OVRDBF     FILE(AR2159F)  TOFILE(DALIB/AR2159F)
             CALL       PGM(ARLIB/ARP070RB)
             MONMSG     MSGID(CPF0000)  EXEC(DO)
             DLTOVR     FILE(*ALL)
             GOTO       CMDLBL(STEP3)
             ENDDO

             DLTOVR     FILE(*ALL)
             CLRPFM     FILE(QTEMP/DDMH)
             MONMSG     MSGID(CPF0000)
             CHGVAR     VAR(%SST(&FLAG1 2 1)) VALUE(' ')


 STEP3:        /*  台北　抓　高雄  ARLIB/AR2159FK  */
             SBMRMTCMD  CMD('CALL PGM(ARLIB/ARP070PA) PARM(''K'')') +
                        DDMFILE(QTEMP/DDMK)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO STEP4)

             CHGDDMF    FILE(QTEMP/DDMK) RMTFILE(ARLIB/AR2159FK)  +
                        LVLCHK(*NO)
             OVRDBF     FILE(AR2159FP) TOFILE(QTEMP/DDMK)
             OVRDBF     FILE(AR2159F)  TOFILE(DALIB/AR2159F)
             CALL       PGM(ARLIB/ARP070RB)
             MONMSG     MSGID(CPF0000)  EXEC(DO)
             DLTOVR     FILE(*ALL)
             GOTO       CMDLBL(STEP4)
             ENDDO

             DLTOVR     FILE(*ALL)
             CLRPFM     FILE(QTEMP/DDMK)
             MONMSG     MSGID(CPF0000)
             CHGVAR     VAR(%SST(&FLAG1 3 1)) VALUE(' ')

 /* -------------------------------------------------------------- */

 STEP4:       /* 未成功廠區之訊息 */

             IF         COND(&FLAG1 *NE '   ') THEN(DO)
             SNDMSG     TOUSR(&USER) MSG(+
                        '客戶暫收統計檔上傳失敗"' || &FLAG1 || '"')
             ENDDO


             ENDPGM
