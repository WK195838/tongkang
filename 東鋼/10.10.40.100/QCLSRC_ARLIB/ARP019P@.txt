             PGM
             DCL        VAR(&P#YYMM) TYPE(*CHAR) LEN(4)
             DCL        VAR(&P#DATE) TYPE(*CHAR) LEN(6)
             DCL        VAR(&P#CODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&INT)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03) TYPE(*LGL)  LEN(1)
             DCL        VAR(&YMD)  TYPE(*CHAR) LEN(8)
             DCL        VAR(&AREA) TYPE(*CHAR) LEN(1)
             DCL        VAR(&VRNOF) TYPE(*CHAR) LEN(4)
             DCL        VAR(&Y)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&M)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&WSID)  TYPE(*CHAR) LEN(10)
             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT)
             RTVDTAARA  DTAARA(*LDA (1011 10)) RTNVAR(&WSID)
 DISPLY:     IF         COND(&INT *EQ '1') THEN(DO)
             CALL       PGM(ARP019RS) PARM(&IN03 &YMD)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)
             CHGDTAARA  DTAARA(*LDA (1 8)) VALUE(&YMD)
             RTVDTAARA  DTAARA(*LDA (3 4)) RTNVAR(&P#YYMM)
             RTVDTAARA  DTAARA(*LDA (3 6)) RTNVAR(&P#DATE)

             CALL    PGM(ACP101R) PARM(&P#YYMM &P#CODE)
             IF         COND(&P#CODE *EQ '1') THEN(DO)
             SNDBRKMSG  MSG('關帳日期已過，請與相關人員聯絡！') +
                          TOMSGQ(&WSID)
             GOTO       CMDLBL(DISPLY)
             ENDDO

             SBMJOB     JOB(ARP019P) JOBD(ARJOBD) OUTQ(&OUTQ) +
                          RQSDTA('CALL ARP019P')
             RETURN
             ENDDO
             RTVDTAARA  DTAARA(*LDA (1    8)) RTNVAR(&YMD)
             RTVDTAARA  DTAARA(*LDA (3    2)) RTNVAR(&Y)
             RTVDTAARA  DTAARA(*LDA (5    2)) RTNVAR(&M)
             RTVDTAARA  DTAARA(*LDA (1021 1)) RTNVAR(&AREA)
             OVRDBF     FILE(RCVDTLL1) TOFILE(RCVDTLL1) SHARE(*YES)
             OPNQRYF    FILE((RCVDTLL1))     QRYSLT('   +
                          RVDATE *EQ   ' || &YMD || '          +
                    *AND  RVTYPE *EQ   "3"                     +
                    *AND  %SST(RVNO 1 1) *EQ  "' || &AREA || '" +
                    *AND  RVDECD *NE  "D"')                    +
                          KEYFLD((RVNO)(RVITEM))

             OVRDBF     FILE(ACTRAN) TOFILE(('AC' || &AREA || 'R' || +
                          &Y)) MBR('VR' || &M) LVLCHK(*NO)
             OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 158) +
                          CPI(12)
             CALL       PGM(ARLIB/ARP019R)
             CLOF       OPNID(RCVDTLL1)
             DLTOVR     FILE(*ALL)
             ENDPGM
