        /*********************************************************************/
        /*                                                                   */
        /*        >>   PROGRAM INTRODUCTION  <<                              */
        /*        1.PROGRAM-ID     ARR123P                                   */
        /*        2.PROGRAM-TYPE   CLP                                       */
        /*        3.AUTHOR         S02CSF                                    */
        /*        4.FUNCTION      客戶銷售排行榜（財會用）                 */
        /*        5.DATE-WRITTEN   95/01/03                                  */
        /*        6.DATE-MODIFY                                              */
        /*                                                                   */
        /*********************************************************************/
             PGM
             DCL      VAR(&TYPE)   TYPE(*CHAR) LEN(1)
             DCL      VAR(&P#IN03) TYPE(*CHAR) LEN(1)
             DCL      VAR(&P#YM1)  TYPE(*CHAR) LEN(6)  /*銷貨年月起*/
             DCL      VAR(&P#YM2)  TYPE(*CHAR) LEN(6)  /*銷貨年月止*/
             DCL      VAR(&P#RAK1) TYPE(*CHAR) LEN(3)  /*排行名次起*/
             DCL      VAR(&P#RAK2) TYPE(*CHAR) LEN(3)  /*排行名次止*/
             DCL      VAR(&P#AMT)  TYPE(*CHAR) LEN(11) /*銷貨淨額*/
             DCL      VAR(&W#YM1)  TYPE(*CHAR) LEN(4)
             DCL      VAR(&W#YM2)  TYPE(*CHAR) LEN(4)
             DCL      VAR(&S#DEVN) TYPE(*CHAR) LEN(10)
             DCL      VAR(&S#USER) TYPE(*CHAR) LEN(10)

             RTVJOBA  JOB(&S#DEVN) USER(&S#USER) TYPE(&TYPE)
             IF       COND(&TYPE *EQ '0') THEN(GOTO BATCH)

             CALL     PGM(ARR123RS) PARM(&P#IN03)
             IF       COND(&P#IN03 *EQ '1') THEN(RETURN)

             SBMJOB     CMD(CALL PGM(ARR123P)) JOB(ARR123P) +
                        JOBD(ARJOBD)
             SNDBRKMSG  MSG('<<客戶銷售排行榜已開始處理+
                         ,請稍待.........>>') +
                       TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)
             RETURN
 BATCH:
             RTVDTAARA  DTAARA(*LDA (300  6)) RTNVAR(&P#YM1)
             RTVDTAARA  DTAARA(*LDA (306  6)) RTNVAR(&P#YM2)
             RTVDTAARA  DTAARA(*LDA (312  3)) RTNVAR(&P#RAK1)
             RTVDTAARA  DTAARA(*LDA (315  3)) RTNVAR(&P#RAK2)
             RTVDTAARA  DTAARA(*LDA (318 11)) RTNVAR(&P#AMT)

             CLRPFM     ARLIB/ARR123F

    /* STEP 1:計算銷貨收入*/
             OVRDBF     FILE(TRNDTL) TOFILE(DALIB/TRNDTL) SHARE(*YES)
             OPNQRYF    FILE((TRNDTL)) QRYSLT('                     +
                        (TXCODE *EQ "SA04")                    *AND +
                        (TXFLAG *NE "D")                       *AND +
                        (((DATE   *GE "' || &P#YM1  || '")    *AND  +
                          (DATE   *LE "' || &P#YM2  || '"))  *OR    +
                         ((ACDT   *GE "' || &P#YM1  || '")    *AND  +
                          (ACDT   *LE "' || &P#YM2  || '")))')      +
                        MAPFLD((DAT1   TXDATE             *CHAR 8)  +
                               (DATE   '%SST(DAT1  1 6)'  *CHAR 6)  +
                               (ACD1   TXACDT             *CHAR 8)  +
                               (ACDT   '%SST(ACD1  1 6)'  *CHAR 6)) +
                        KEYFLD((TXCUNM) (TXCUNO) (TXIVNO))
             CALL       PGM(ARR123R1)
             CLOF       OPNID(TRNDTL)
             DLTOVR     FILE(*ALL)

    /* STEP 2:計算銷貨調整*/
             OVRDBF     FILE(TRNDTL) TOFILE(DALIB/TRNDTL) SHARE(*YES)
             OPNQRYF    FILE((TRNDTL)) QRYSLT('                    +
                        (TXCODE *EQ "AR05")                   *AND +
                        (TXFLAG *NE "D")                      *AND +
                        (((DATE   *GE "' || &P#YM1  || '")   *AND  +
                          (DATE   *LE "' || &P#YM2  || '")) *OR    +
                         ((ACDT   *GE "' || &P#YM1  || '")   *AND  +
                          (ACDT   *LE "' || &P#YM2  || '")))')     +
                        MAPFLD((DAT1   TXDATE            *CHAR 8)  +
                               (DATE   '%SST(DAT1  1 6)' *CHAR 6)  +
                               (ACD1   TXACDT            *CHAR 8)  +
                               (ACDT   '%SST(ACD1  1 6)' *CHAR 6)) +
                        KEYFLD((TXCUNM))
             CALL       PGM(ARR123R2)
             CLOF       OPNID(TRNDTL)
             DLTOVR     FILE(*ALL)

             CHGVAR     VAR(&W#YM1)    VALUE(%SST(&P#YM1 3 4))
             CHGVAR     VAR(&W#YM2)    VALUE(%SST(&P#YM2 3 4))
    /* STEP 3:計算銷貨折讓（包含銷折與銷退）*/
             OVRDBF     FILE(TRNDTL) TOFILE(DALIB/TRNDTL) SHARE(*YES)
             OPNQRYF    FILE((TRNDTL)) QRYSLT('                    +
                        (TXCODE *EQ "AR06")                   *AND +
                        (TXFLAG *NE "D")                      *AND +
                        (TXACNT *NE "4")                      *AND +
                        (TXACDT *NE  0 )                      *AND +
                        (RESV   *GE "' || &P#YM1  || '")      *AND +
                        (RESV   *LE "' || &P#YM2  || '")')         +
                        MAPFLD((RESV   '%SST(TXRESV 1 6)' *CHAR 6)) +
                        KEYFLD((TXCUNM))
             CALL       PGM(ARR123R3)
             CLOF       OPNID(TRNDTL)
             DLTOVR     FILE(*ALL)

    /* STEP 4:計算銷貨折扣*/
             OVRDBF     FILE(INVMST) TOFILE(DALIB/INVMST) SHARE(*YES)
             OPNQRYF    FILE((INVMST)) QRYSLT('                    +
                        (INTYPE *EQ "1")                      *AND +
                        (INDECD *EQ " ")                      *AND +
                        (INDT   *GE "' || &P#YM1  || '")      *AND +
                        (INDT   *LE "' || &P#YM2  || '")')         +
                        MAPFLD((IND1   ININDT             *CHAR 8) +
                               (INDT   '%SST(IND1  1 6)'  *CHAR 6)) +
                        KEYFLD((INCUNM))
             CALL       PGM(ARR123R4)
             CLOF       OPNID(INVMST)
             DLTOVR     FILE(*ALL)

    /* STEP 5:計算東鋼鋼結構其他營業收入*/
             OVRDBF     FILE(INVMST) TOFILE(DALIB/INVMST) SHARE(*YES)
             OPNQRYF    FILE((INVMST)) QRYSLT('                    +
                        (INTYPE *EQ "9")                      *AND +
                        (INDECD *EQ " ")                      *AND +
                        (INCUNO *EQ "DH043")                  *AND +
                        (INDT   *GE "' || &P#YM1  || '")      *AND +
                        (INDT   *LE "' || &P#YM2  || '")')         +
                        MAPFLD((IND1   ININDT             *CHAR 8) +
                               (INDT   '%SST(IND1  1 6)'  *CHAR 6)) +
                        KEYFLD((INCUNM))
             CALL       PGM(ARR123R5)
             CLOF       OPNID(INVMST)
             DLTOVR     FILE(*ALL)

    /* STEP 5:合計總銷貨金額*/
             OVRDBF     FILE(ARR123F)   TOFILE(ARR123F) SHARE(*YES)
             OPNQRYF    FILE((ARR123F)) OPTION(*ALL)
             CALL       PGM(ARR123R6)
             CLOF       OPNID(ARR123F)
             DLTOVR     FILE(*ALL)

    /* STEP 6:出表*/

             OVRPRTF    FILE(ARR123T) TOFILE(ARR123T) PAGESIZE(*N 198)  +
                        CPI(15) HOLD(*YES)  USRDTA('銷售排行')
             OVRDBF     FILE(ARR123F) TOFILE(ARR123F) SHARE(*YES)
             OPNQRYF    FILE((ARR123F))                         +
                        KEYFLD((A1AMT5 *DESCEND))
             CALL       PGM(ARR123R)
             CLOF       OPNID(ARR123F)
             DLTOVR     FILE(*ALL)

             SNDBRKMSG  MSG('<<客戶銷售排行榜已完成+
                        ,請列印.........>>') +
                      TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)
             ENDPGM
