             PGM
             DCL        VAR(&YYA) TYPE(*CHAR) LEN(2)
             DCL        VAR(&MMA) TYPE(*CHAR) LEN(2)
             DCL        VAR(&YY)  TYPE(*DEC)  LEN(2 0)
             DCL        VAR(&MM)  TYPE(*DEC)  LEN(2 0)

             DLTF       FILE(QTEMP/TYDDM)
             MONMSG     CPF0000
             DLTF       FILE(QTEMP/KSDDM)
             MONMSG     CPF0000

             CRTDUPOBJ  OBJ(TYDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                        TOLIB(QTEMP)
             CRTDUPOBJ  OBJ(KSDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                        TOLIB(QTEMP)

             OVRDBF     FILE(SAMAST) TOFILE(DALIB/SAMAST)
             OVRDBF     FILE(SACUST) TOFILE(DALIB/SACUST)
             CLRPFM     FILE(CCLIB/ARSTAT)
             CPYF       FROMFILE(JEFF/ARSTAT) TOFILE(CCLIB/ARSTAT) +
                          MBROPT(*ADD)
             OVRDBF     FILE(ARSTAT) TOFILE(CCLIB/ARSTAT) SHARE(*YES)
             OVRDBF     FILE(INVDTL) TOFILE(DALIB/INVDTL) SHARE(*YES)
             OPNQRYF    FILE((INVDTL))      +
                          KEYFLD((IVRESV) (IVNO)) MAPFLD((IVRESV +
                          '%SST(IVORNO 1 5)'))
             CALL       PGM(CNV013R)
             CLOF       OPNID(INVDTL)
             DLTOVR     FILE(*ALL)
          /*    AFTER COMPLETE   */
             CPYF       FROMFILE(CCLIB/ARSTAT) TOFILE(DALIB/ARSTAT) +
                          MBROPT(*REPLACE)
          /*    TRANSFER TO TOUYAN  */
             CHGDDMF    FILE(QTEMP/TYDDM) RMTFILE(DALIB/ARSTATTM)
             CPYF       FROMFILE(CCLIB/ARSTAT) TOFILE(QTEMP/TYDDM) +
                          MBROPT(*REPLACE)
             SBMRMTCMD  CMD('cpyf fromfile(dalib/arstattm) +
                          tofile(dalib/arstat) mbropt(*replace)') +
                          DDMFILE(QTEMP/TYDDM)
          /*    TRANSFER TO KOUSHUN */
             CHGDDMF    FILE(QTEMP/KSDDM) RMTFILE(DALIB/ARSTATTM)
             CPYF       FROMFILE(CCLIB/ARSTAT) TOFILE(QTEMP/KSDDM) +
                          MBROPT(*REPLACE)
             SBMRMTCMD  CMD('cpyf fromfile(dalib/arstattm) +
                          tofile(dalib/arstat) mbropt(*replace)') +
                          DDMFILE(QTEMP/KSDDM)
             SBMJOB     CMD(CALL PGM(CCLIB/CCP000P)) JOB(CCP000P) +
                          JOBD(CCJOBD) JOBQ(QCTL) LOG(4 0 *SECLVL) +
                          LOGCLPGM(*YES)
             MONMSG     MSGID(CPF0000)
             RTVSYSVAL  SYSVAL(QYEAR) RTNVAR(&YYA)
             RTVSYSVAL  SYSVAL(QMONTH) RTNVAR(&MMA)
             CHGVAR     VAR(&YY) VALUE(&YYA)
             CHGVAR     VAR(&MM) VALUE(&MMA)
             CHGDTAARA  DTAARA(*LDA (5 2)) VALUE('01')
             CHGDTAARA  DTAARA(*LDA (11 2)) VALUE('31')
             CHGDTAARA  DTAARA(*LDA (7 2)) VALUE(&YYA)
             CHGDTAARA  DTAARA(*LDA (9 2)) VALUE(&MMA)
             IF         COND(&MM *EQ 1) THEN(DO)
                        CHGVAR  VAR(&MM)  VALUE(12)
                        CHGVAR  VAR(&YY)  VALUE(&YY - 1)
                        ENDDO
             ELSE  DO
                        CHGVAR  VAR(&MM)  VALUE(&MM - 1)
                   ENDDO
             CHGVAR     VAR(&YYA) VALUE(&YY)
             CHGVAR     VAR(&MMA) VALUE(&MM)
             CHGDTAARA  DTAARA(*LDA (1 2)) VALUE(&YYA)
             CHGDTAARA  DTAARA(*LDA (3 2)) VALUE(&MMA)
             SBMJOB     CMD(CALL PGM(CCLIB/CCP001P)) JOB(CCP001P) +
                          JOBD(CCJOBD) JOBQ(QCTL) LOG(4 0 *SECLVL) +
                          LOGCLPGM(*YES)
             MONMSG     MSGID(CPF0000)

             ENDPGM
