     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARR111P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02CSF                                  */
     /*     4.FUNCTION     應收帳款帳齡分析表（不含票據繳款）     */
     /*     5.DATE-WRITTEN  92/04/29                                */
     /*     6.DATE-MODIFY                                           */
     /*                                                             */
     /*                                                             */
     /***************************************************************/
            PGM
            DCLF       FILE(ARR111S)
            DCL        VAR(&TYPE)   TYPE(*CHAR) LEN(1)
            DCL        VAR(&W#FLAG) TYPE(*CHAR) LEN(1)
            DCL        VAR(&P#PDAT) TYPE(*CHAR) LEN(8)  /*輸入日期*/
            DCL        VAR(&P#MODE) TYPE(*CHAR) LEN(1)  /*年份別*/
            DCL        VAR(&P#MTL)  TYPE(*CHAR) LEN(24) /*月日數*/
            DCL        VAR(&P#LEAP) TYPE(*CHAR) LEN(1)  /*閏年碼*/
            DCL        VAR(&CUN1)   TYPE(*CHAR) LEN(6)  /*客戶代號暫存*/
            DCL        VAR(&CUN2)   TYPE(*CHAR) LEN(6)  /*客戶代號暫存*/
            DCL        VAR(&DAT1)   TYPE(*CHAR) LEN(8)  /*繳款日期暫存*/
            DCL        VAR(&DAT2)   TYPE(*CHAR) LEN(8)  /*繳款日期暫存*/

            RTVJOBA    JOB(&S#DEVN) USER(&S#USER) TYPE(&TYPE)
            IF         COND(&TYPE *EQ '0') THEN(GOTO CMDLBL(BATCH))

 START:     SNDRCVF

            CHGVAR     VAR(&IN30)     VALUE('0')
            CHGVAR     VAR(&IN31)     VALUE('0')

            IF         COND(&IN03 *EQ '1') THEN(RETURN)
            IF         COND(&IN12 *EQ '1') THEN(RETURN)

  /*  CHECK 繳款日期*/
            IF         COND(&S#DAT1 *GT &S#DAT2 ) THEN(DO)
            CHGVAR     VAR(&S#ERR)    VALUE('繳款起日不可大於繳款止日')
            CHGVAR     VAR(&IN30)     VALUE('1')
            CHGVAR     VAR(&IN31)     VALUE('1')
            GOTO       CMDLBL(START)
            ENDDO

            CHGVAR     VAR(&DAT1)      VALUE(&S#DAT1)
            CHGVAR     VAR(&P#PDAT)    VALUE(&DAT1)
            CHGVAR     VAR(&P#MODE)    VALUE('1')
            CALL       PGM(UTS102R)    PARM(&P#PDAT &P#MODE +
                                            &P#MTL  &P#LEAP &W#FLAG)
            IF         COND(&W#FLAG *NE '0') THEN(DO)
            CHGVAR     VAR(&IN30)      VALUE('1')
            CHGVAR     VAR(&S#ERR)     VALUE('繳款起日輸入錯誤！')
            GOTO       CMDLBL(START)
            ENDDO

            CHGVAR     VAR(&DAT2)      VALUE(&S#DAT2)
            CHGVAR     VAR(&P#PDAT)    VALUE(&DAT2)
            CHGVAR     VAR(&P#MODE)    VALUE('1')
            CALL       PGM(UTS102R)    PARM(&P#PDAT &P#MODE +
                                            &P#MTL  &P#LEAP &W#FLAG)
            IF         COND(&W#FLAG *NE '0') THEN(DO)
            CHGVAR     VAR(&IN31)      VALUE('1')
            CHGVAR     VAR(&S#ERR)     VALUE('繳款止日輸入錯誤！')
            GOTO       CMDLBL(START)
            ENDDO

  /*  CHECK 客戶代號*/
            IF         COND((&S#CUN1 *EQ ' ') *AND +
                            (&S#CUN2 *EQ ' ')) THEN(DO)
            CHGVAR     VAR(&CUN1)     VALUE('      ')
            CHGVAR     VAR(&CUN2)     VALUE('999999')
            ENDDO

            IF         COND((&S#CUN1 *EQ ' ') *AND +
                            (&S#CUN2 *NE ' ')) THEN(DO)
            CHGVAR     VAR(&CUN1)     VALUE('      ')
            CHGVAR     VAR(&CUN2)     VALUE(&S#CUN2)
            ENDDO

            IF         COND((&S#CUN1 *NE ' ') *AND +
                            (&S#CUN2 *EQ ' ')) THEN(DO)
            CHGVAR     VAR(&CUN1)     VALUE(&S#CUN1)
            CHGVAR     VAR(&CUN2)     VALUE('999999')
            ENDDO

            IF         COND((&S#CUN1 *NE ' ') *AND +
                            (&S#CUN2 *NE ' ')) THEN(DO)
            CHGVAR     VAR(&CUN1)     VALUE(&S#CUN1)
            CHGVAR     VAR(&CUN2)     VALUE(&S#CUN2)
            ENDDO

  /* 傳參數 */
            CHGVAR     VAR(&DAT1)     VALUE(&S#DAT1)
            CHGVAR     VAR(&DAT2)     VALUE(&S#DAT2)
            CHGDTAARA  DTAARA(*LDA (300 8))   VALUE(&DAT1)
            CHGDTAARA  DTAARA(*LDA (308 8))   VALUE(&DAT2)
            CHGDTAARA  DTAARA(*LDA (316 6))   VALUE(&CUN1)
            CHGDTAARA  DTAARA(*LDA (322 6))   VALUE(&CUN2)

            SBMJOB     CMD(CALL PGM(ARR111P)) JOB(ARR111P) +
                       JOBD(ARJOBD)
            SNDBRKMSG  MSG('<<應收帳款帳齡分析表（不含票據繳款）+
                         已開始處理,請稍待.........>>') +
                       TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)
            RETURN

 BATCH:
             RTVDTAARA  DTAARA(*LDA (300 8)) RTNVAR(&DAT1)
             RTVDTAARA  DTAARA(*LDA (308 8)) RTNVAR(&DAT2)
             RTVDTAARA  DTAARA(*LDA (316 6)) RTNVAR(&CUN1)
             RTVDTAARA  DTAARA(*LDA (322 6)) RTNVAR(&CUN2)

             OVRPRTF    FILE(ARR111T) TOFILE(ARR111T) +
                        PAGESIZE(*N 198) +
                        CPI(15) HOLD(*YES)  USRDTA('帳齡分析')
             OVRDBF     FILE(TRNDTLL2) TOFILE(TRNDTLL2) SHARE(*YES)
             OPNQRYF    FILE(TRNDTLL2) QRYSLT(' +
                            (DATE    *GE "' || &DAT1 || '") *AND +
                            (DATE    *LE "' || &DAT2 || '") *AND +
                            (TXCUNO  *GE "' || &CUN1 || '") *AND +
                            (TXCUNO  *LE "' || &CUN2 || '") *AND +
                            (TXCODE  *EQ "SA04"           )') +
                        KEYFLD((TXCUNO) (TXVUNO) (TXIVNO)) +
                        MAPFLD((DATE  TXDATE  *CHAR 8) +
                               (TXVUNO  '%SST(DATE 1 6)'))
             CALL       PGM(ARR111R)
             CLOF       OPNID(TRNDTLL2)
             DLTOVR     FILE(*ALL)

             SNDBRKMSG  MSG('<<應收帳款帳齡分析表（不含票據繳款）+
                         已處理完成,請列印.........>>') +
                       TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)
             ENDPGM
