           /******************************************************/
           /*                                                    */
           /*   PROGRAM   ID        :   ARR032R                  */
           /*   PROGRAM   FUNCTION  :  銷項統一發票明細表列印  */
           /*   AUTHER              :   ANNCY                    */
           /*   CREATE    DATE      :   81/09/24                 */
           /*   MAINTAIN  DATE      :                            */
           /*   RELATION  PROGRAM   :   ARR032P,ARR032R,ARR032S  */
           /*   NOTE                :                            */
           /*   UPDATE  DATE  99/12/01  2010AR555 S00WCJ (9912A) */
           /*                112/09/07  S00WCJ (1209A)           */
           /*                 增加廠區I                        */
           /*                                                    */
           /******************************************************/
             PGM
             DCL        VAR(&INT)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&OUTQ)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03)   TYPE(*LGL)  LEN(1)
             DCL        VAR(&KIND)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&YMA)    TYPE(*CHAR) LEN(6)
             DCL        VAR(&AREA)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&AREA1)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&AREA2)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&AREA3)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&AREA4)  TYPE(*CHAR) LEN(1)

             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT)
             IF         COND(&INT *EQ '1') THEN(DO)
             CALL  PGM(ARR032RS) PARM(&IN03 &KIND &YMA &AREA)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)
             CHGDTAARA  DTAARA(*LDA (1 1)) VALUE(&KIND)
             CHGDTAARA  DTAARA(*LDA (2 6)) VALUE(&YMA)
             CHGDTAARA  DTAARA(*LDA (8 1)) VALUE(&AREA)

             SBMJOB     JOB(ARR032P) JOBD(ARJOBD) OUTQ(&OUTQ) +
                          RQSDTA('CALL ARR032P')
             RETURN
             ENDDO

 BATCH:
             CLRPFM     FILE(ARLIB/AR1WF1)
             RTVDTAARA  DTAARA(*LDA (1 1)) RTNVAR(&KIND)
             RTVDTAARA  DTAARA(*LDA (2 6)) RTNVAR(&YMA)
             RTVDTAARA  DTAARA(*LDA (8 1)) RTNVAR(&AREA)

             IF         COND(&AREA *EQ 'P') THEN(DO)
                        CHGVAR   VAR(&AREA1)  VALUE('P')
                        CHGVAR   VAR(&AREA2)  VALUE('T')
 /*1209A*/              CHGVAR   VAR(&AREA3)  VALUE('I')
    /*9912A*/           CHGVAR   VAR(&AREA4)  VALUE('U')
             ENDDO

             IF         COND(&AREA *EQ 'K') THEN(DO)
                        CHGVAR   VAR(&AREA1)  VALUE('K')
                        CHGVAR   VAR(&AREA2)  VALUE('K')
                        CHGVAR   VAR(&AREA3)  VALUE('K')
                        CHGVAR   VAR(&AREA4)  VALUE('K')
             ENDDO

             IF         COND(&AREA *EQ 'M') THEN(DO)
                        CHGVAR   VAR(&AREA1)  VALUE('M')
                        CHGVAR   VAR(&AREA2)  VALUE('M')
                        CHGVAR   VAR(&AREA3)  VALUE('M')
                        CHGVAR   VAR(&AREA4)  VALUE('M')
             ENDDO

             IF         COND(&AREA *EQ 'H') THEN(DO)
                        CHGVAR   VAR(&AREA1)  VALUE('H')
                        CHGVAR   VAR(&AREA2)  VALUE('H')
                        CHGVAR   VAR(&AREA3)  VALUE('H')
                        CHGVAR   VAR(&AREA4)  VALUE('H')
             ENDDO

             OVRDBF     FILE(AR1WF1) TOFILE(ARLIB/AR1WF1) SHARE(*YES)
             OVRDBF     FILE(INVMST) TOFILE(INVMST) SHARE(*YES)
             OVRDBF     FILE(INVDTL) TOFILE(INVDTL) SHARE(*YES)
             OPNQRYF    FILE((INVMST))   QRYSLT('                +
                             INYM   *EQ "' || &YMA || '"         +
                        *AND INKIND *EQ "' || &KIND || '"        +
                        *AND INAREA = %VALUES("' || &AREA1 || '"  +
                                             "' || &AREA2 || '"  +
                                             "' || &AREA3 || '"  +
                                             "' || &AREA4 || '")') +
                          KEYFLD((INNO) (ININDT))               +
                          MAPFLD((CHAR6 ININDT *CHAR 8) +
                                 (INYM '%SST(CHAR6 1 6)' *CHAR 6))
             CALL       PGM(ARP032R)
             CALL       PGM(ARR032R)
             CLOF       OPNID(INVMST)
             DLTOVR     FILE(*ALL)
     /*      CLRPFM     FILE(ARLIB/AR1WF1)  */

ENDCLPGM:    ENDPGM
