     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARR069P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02CLJ                                  */
     /*     4.FUNCTION     訂單別出貨及發票明細表                 */
     /*     5.DATE-WRITTEN  86/10/09                                */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&INT)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)

             DCL        VAR(&IN03) TYPE(*LGL)  LEN(1)
             DCL        VAR(&ORNO) TYPE(*CHAR) LEN(5)
             DCL        VAR(&YMDS) TYPE(*CHAR) LEN(6)
             DCL        VAR(&YMDE) TYPE(*CHAR) LEN(6)

             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT)
             IF         COND(&INT *EQ '0') THEN(GOTO BATCH)

             CALL       PGM(ARR069RS) PARM(&IN03)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)

             SBMJOB     JOB(ARR069P) JOBD(ARJOBD) OUTQ(&OUTQ) +
                          RQSDTA('CALL ARR069P')
             RETURN

 BATCH:      RTVDTAARA  DTAARA(*LDA ( 1 5))      RTNVAR(&ORNO)
             RTVDTAARA  DTAARA(*LDA ( 6 6))      RTNVAR(&YMDS)
             RTVDTAARA  DTAARA(*LDA (12 6))      RTNVAR(&YMDE)

             CRTDUPOBJ  OBJ(ARR069F) FROMLIB(ARLIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             OVRDBF   FILE(ARR069F) TOFILE(QTEMP/ARR069F) SHARE(*YES)

 STEP1:                 /* 找出地磅磅單 */
             OVRDBF   FILE(WTMASPJ1) TOFILE(DALIB/WTMASPJ1) SHARE(*YES)
             OPNQRYF  FILE(WTMASPJ1) +
                        QRYSLT( +
                           'W6FLAG *NE "D" *AND +
                            W6ENTD *GE ' || &YMDS || ' *AND +
                            W6ENTD *LE ' || &YMDE || ' *AND +
                            W6ORNO *EQ "' || &ORNO || '" *AND +
                            W6TRCD *EQ %VALUES("11" "1E" "1F" "1G" +
                                               "0A") *AND +
                            W7SPLT *EQ %VALUES("W" "1" "2" "3" "4" +
                                    "5" "6" "7" "8" "9" " ") ') +
                        KEYFLD((W6ENTD) (W7AREA) (W7VNO2) +
                               (W7NAME) (W7QULT) (W7SPE1))
             CALL       PGM(ARR069R1)
             CLOF       OPNID(WTMASPJ1)

 STEP2:                 /* 從銷貨找發票資料 */
             OVRDBF     FILE(INVMST) TOFILE(DALIB/INVMST) SHARE(*YES)
             OVRDBF     FILE(INVDTL) TOFILE(DALIB/INVDTL) SHARE(*YES)
             OVRDBF     FILE(TRNDTL) TOFILE(DALIB/TRNDTL) SHARE(*YES)
             OPNQRYF    FILE(TRNDTL) +
                        QRYSLT(   +
                         '(TXDATE *GE ' || &YMDS || '  *AND    +
                           TXDATE *LE ' || &YMDE || ') *AND    +
                           TXRESV *EQ "' || &ORNO ||'" *AND   +
                           TXIVNO *NE "          " *AND       +
                           TXCODE *EQ "SA04"') +
                        MAPFLD((TXRESV '%SST(TXORNO 1 5)')) +
                        KEYFLD((TXNO) (TXIVNO))
             CALL       PGM(ARR069R2)
             CLOF       OPNID(TRNDTL)

 STEP3:                 /* 列印報表 */
             OVRPRTF    FILE(ARR069T) TOFILE(ARLIB/ARR069T) +
                        HOLD(*YES) USRDTA('訂出發明')
             CALL       PGM(ARR069R3)

             DLTOVR     FILE(*ALL)
             ENDPGM
