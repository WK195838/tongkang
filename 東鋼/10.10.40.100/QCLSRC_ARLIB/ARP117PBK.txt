     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP117P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02LBU                                  */
     /*     4.FUNCTION     新桃工程自用發票開立作業               */
     /*     5.DATE-WRITTEN  98/02/24                                */
     /*                                                             */
     /*  <說明:>                                                  */
     /*            本作業每日由業務助理進行。開立的銷貨明細為：   */
     /*                                                             */
     /*             1.不在訂單控制檔內的訂單項次（隨車）          */
     /*             2.開立方式為隨車(00)                          */
     /*             3.開立方式為一訂一張(01)                      */
     /*                                                             */
     /*            輸入之發票日期可為上月之最後一日               */
     /*                                                           */
     /*            若有加工費或廠組費發生時，自動產生調整單       */
     /*                                                             */
     /*      *LDA   1 --> 開立廠區及年月(GEPRIN)                  */
     /*            11 --> 發票日期                                */
     /*            31 --> 隨車發票張數                            */
     /*            41 --> 隨訂發票張數                            */
     /*            51 --> 調整發票張數                            */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&INT)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&OUTQ)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03)   TYPE(*LGL)  LEN(1)

             DCL        VAR(&W#DATE) TYPE(*CHAR) LEN(8)
             DCL        VAR(&W#YM)   TYPE(*CHAR) LEN(6)
             DCL        VAR(&W#AREA) TYPE(*CHAR) LEN(1)
             DCL        VAR(&W#RTNV) TYPE(*CHAR) LEN(1)
             DCL        VAR(&W#IVNO) TYPE(*CHAR) LEN(8)
             DCL        VAR(&W#MSGT) TYPE(*CHAR) LEN(200)


             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT) USER(&USER)
             IF         COND(&INT *EQ '0') THEN(GOTO BATCH)


INPUT:
             CALL       PGM(ARP117RS) PARM(&IN03)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)

             SBMJOB     CMD(CALL PGM(ARP117P)) JOB(ARP117P) +
                          JOBD(ARJOBD) OUTQ(&OUTQ) INLLIBL(*JOBD)
             RETURN


BATCH:
             RTVDTAARA  DTAARA(*LDA (1  1)) RTNVAR(&W#AREA)
             RTVDTAARA  DTAARA(*LDA (11 8)) RTNVAR(&W#DATE)
             RTVDTAARA  DTAARA(*LDA (11 6)) RTNVAR(&W#YM)


STEP1:       /* 複製暫存資料 */

             DLTF       FILE(QTEMP/TRNDTLN)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(UENDTLN) FROMLIB(ARLIB) OBJTYPE(*FILE) +
                        TOLIB(QTEMP)
             OVRDBF     FILE(UENDTLN) TOFILE(QTEMP/UENDTLN)
             OVRDBF     FILE(UENDTL) TOFILE(UENDTL) SHARE(*YES)
             OPNQRYF    FILE(UENDTL) +
                        QRYSLT('YYMM   *LE ' || &W#YM   || ' *AND +
                                UVFLAG *NE "D" *AND +
                                UVNO   *EQ " "')                  +
                        MAPFLD((YYMM   UVYM     *CHAR 6))
             CALL       ARP117R1
             CLOF       UENDTL
             DLTOVR     *ALL


STEP2:       /* 開立新桃工程自用發票 */

             OVRDBF     FILE(UENDTL) TOFILE(UENDTL)
             OVRDBF     FILE(UENDTLN) TOFILE(QTEMP/UENDTLN) SHARE(*YES)
             OPNQRYF    FILE(UENDTLN) +
                        KEYFLD((TNNO) (TNPDNM) (TNUPRC))
             OVRPRTF    FILE(ARP117T) TOFILE(ARP117T) PAGESIZE(*N +
                          198) CPI(15) HOLD(*YES) USRDTA('工程自用')
             CALL       ARP117R2
             CLOF       UENDTLN
             DLTOVR     *ALL


STEP3:       /* 送出作業結束訊息 */

             RTVDTAARA  DTAARA(*LDA (31 8)) RTNVAR(&W#IVNO)
             CHGVAR     VAR(&W#MSGT) +
                        VALUE('發票開立作業處理完畢！新桃工程發票開+
                              立' || &W#IVNO || '張，隨訂開立')
             RTVDTAARA  DTAARA(*LDA (41 8)) RTNVAR(&W#IVNO)
             CHGVAR     VAR(&W#MSGT) +
                        VALUE(&W#MSGT *TCAT &W#IVNO || '張，調整發+
                              票開立')
             RTVDTAARA  DTAARA(*LDA (51 8)) RTNVAR(&W#IVNO)
             CHGVAR     VAR(&W#MSGT) +
                        VALUE(&W#MSGT *TCAT &W#IVNO || '張。')

             SNDMSG     MSG(&W#MSGT) TOUSR(&USER)


STEP6:       /* 檢核發票字軌可用號數 */

             CALL       PGM(ARP002RV)    /* 票軌剩十分之一警告 */

             RTVDTAARA  DTAARA(*LDA (1 1)) RTNVAR(&W#RTNV)
             IF         COND(&W#RTNV *EQ 'T') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (3 8)) RTNVAR(&W#IVNO)
             SNDMSG     MSG('請注意！二聯式發票可用票軌數目只剩+
                             十分之一，約' || &W#IVNO || '張；+
                             必要時請通知財會單位申請。') +
                        TOUSR(&USER)
             ENDDO

             RTVDTAARA  DTAARA(*LDA (2 1))  RTNVAR(&W#RTNV)
             IF         COND(&W#RTNV *EQ 'T') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (11 8)) RTNVAR(&W#IVNO)
             SNDMSG     MSG('請注意！三聯式發票可用票軌數目只剩+
                             十分之一，約' || &W#IVNO || '張；+
                             必要時請通知財會單位申請。') +
                        TOUSR(&USER)
             ENDDO


             ENDPGM
