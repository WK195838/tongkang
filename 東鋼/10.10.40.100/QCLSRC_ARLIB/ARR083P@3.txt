     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARR083P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02YSH                                  */
     /*     4.FUNCTION     預收貨款增減明細表                     */
     /*     5.DATE-WRITTEN  89/02/18                                */
     /*                                                             */
     /***************************************************************/
             PGM
             DCLF       FILE(ARLIB/ARR083S)

             DCL        VAR(&DSPID)   TYPE(*CHAR)  LEN(10)
             DCL        VAR(&OUTQ)    TYPE(*CHAR)  LEN(10)
             DCL        VAR(&INT)     TYPE(*CHAR)  LEN(1)
             DCL        VAR(&QDATE)   TYPE(*CHAR)  LEN(6)
             DCL        VAR(&QDATE8)  TYPE(*CHAR)  LEN(8)
             DCL        VAR(&SDATE6)  TYPE(*DEC)   LEN(6 0)
             DCL        VAR(&W#DAY)   TYPE(*DEC)   LEN(8 0)
             DCL        VAR(&W#LDAY)  TYPE(*CHAR)  LEN(8)
             DCL        VAR(&W#CDAY)  TYPE(*CHAR)  LEN(8)
             DCL        VAR(&W#ERR)   TYPE(*CHAR)  LEN(1)
             DCL        VAR(&W#MOD)   TYPE(*CHAR)  LEN(1)
             DCL        VAR(&W#CNT)   TYPE(*CHAR)  LEN(4)
             DCL        VAR(&W#FUN)   TYPE(*CHAR)  LEN(1)
             DCL        VAR(&YYMM)    TYPE(*CHAR)  LEN(6)
             DCL        VAR(&SYMD)    TYPE(*CHAR)  LEN(8)
             DCL        VAR(&SYMD1)   TYPE(*CHAR)  LEN(6)
             DCL        VAR(&EYMD)    TYPE(*CHAR)  LEN(8)
             DCL        VAR(&SAREA)   TYPE(*CHAR)  LEN(1)
             DCL        VAR(&EAREA)   TYPE(*CHAR)  LEN(1)

             RTVJOBA    JOB(&DSPID) OUTQ(&OUTQ) TYPE(&INT)
             IF         COND(&INT *EQ '0') THEN(GOTO BATCH)


             RTVDTAARA  DTAARA(*LDA ( 951 35)) RTNVAR(&S#COMP)
             RTVDTAARA  DTAARA(*LDA (1011 10)) RTNVAR(&S#DEVI)
             RTVDTAARA  DTAARA(*LDA (1021  1)) RTNVAR(&S#AREA)
             RTVSYSVAL  SYSVAL(QDATE) RTNVAR(&QDATE)
             CHGVAR     VAR(&S#YYMM)  VALUE('00' || %SST(&QDATE 1 4))
             CHGVAR     VAR(&S#CODE)  VALUE('Y')
 INPUT:
             SNDRCVF    RCDFMT(AR083F1)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)
             IF         COND(&IN12 *EQ '1') THEN(RETURN)
             CHGVAR     VAR(&IN30)    VALUE('0')
             CHGVAR     VAR(&IN31)    VALUE('0')

         /*  檢核輸入條件   */
             CHGVAR     VAR(&QDATE8)  VALUE(&S#YYMM || '01')
             CHGVAR     VAR(&W#DAY)   VALUE(&QDATE8)
             CHGVAR     VAR(&W#LDAY)  VALUE(&W#DAY)
             CHGVAR     VAR(&W#MOD)   VALUE('1') /*中國年*/
             CHGVAR     VAR(&W#CNT)   VALUE('0000') /*天數*/
             CHGVAR     VAR(&W#FUN)   VALUE('2') /* 1:往前, 2:往後 */
             CALL       PGM(UTS104R) PARM(&W#LDAY  &W#MOD  &W#FUN &W#CNT +
                                          &W#CDAY &W#ERR)
             IF         COND(&W#ERR *NE '0')  THEN(DO)
                        CHGVAR   VAR(&S#ERR)  VALUE('異動年月輸入錯誤!')
                        CHGVAR   VAR(&IN30)   VALUE('1')
                        GOTO     CMDLBL(INPUT)
             ENDDO

             IF         COND((&S#AREA *NE 'P') *AND +
                             (&S#AREA *NE 'T') *AND +
                             (&S#AREA *NE 'H') *AND +
                             (&S#AREA *NE 'M') *AND +
                             (&S#AREA *NE 'K') *AND +
                             (&S#AREA *NE ' ')) THEN(DO)
                        CHGVAR   VAR(&S#ERR)  VALUE('歸屬廠區輸入錯誤!')
                        CHGVAR   VAR(&IN31)   VALUE('1')
                        GOTO     CMDLBL(INPUT)
             ENDDO

             CHGDTAARA  DTAARA(*LDA (501 6))  VALUE(&S#YYMM)
             CHGDTAARA  DTAARA(*LDA (507 1))  VALUE(&S#AREA)
             CHGDTAARA  DTAARA(*LDA (508 10)) VALUE(&DSPID)
             CHGDTAARA  DTAARA(*LDA (518 1))  VALUE(&S#CODE)

      /*     SBMJOB     CMD(CALL PGM(ARR083P)) JOB(ARR083P) +
                        JOBD(ARJOBD)              */
             SNDBRKMSG  MSG('<<預收貨款明細表開始處理，請稍待...>>'  ) +
                          TOMSGQ(&DSPID) MSGTYPE(*INFO)
      /*     RETURN     /* INTERACTIVE JOB */
 BATCH:
             RTVDTAARA  DTAARA(*LDA (501  6)) RTNVAR(&S#YYMM)
             RTVDTAARA  DTAARA(*LDA (507  1)) RTNVAR(&S#AREA)
             RTVDTAARA  DTAARA(*LDA (508 10)) RTNVAR(&DSPID)
             RTVDTAARA  DTAARA(*LDA (518  1)) RTNVAR(&S#CODE)
        /* 重新產生資料*/
             IF         COND(&S#CODE *EQ 'Y')  THEN(DO)
             /*  取得期初月份   */
             CHGVAR     VAR(&QDATE8)  VALUE(&S#YYMM || '01')
             CHGVAR     VAR(&W#DAY)   VALUE(&QDATE8)
             CHGVAR     VAR(&W#LDAY)  VALUE(&W#DAY)
             CHGVAR     VAR(&W#MOD)   VALUE('1') /*中國年*/
             CHGVAR     VAR(&W#CNT)   VALUE('0001') /*天數*/
             CHGVAR     VAR(&W#FUN)   VALUE('1') /* 1:往前, 2:往後 */
             CALL       PGM(UTS104R) PARM(&W#LDAY  &W#MOD  &W#FUN &W#CNT +
                                          &W#CDAY &W#ERR)
             CHGVAR     VAR(&W#DAY)   VALUE(&W#CDAY)
             CHGVAR     VAR(&QDATE8)  VALUE(&W#DAY)
             CHGVAR     VAR(&YYMM)    VALUE(%SST(&QDATE8 1 6))

             DLTF       FILE(QTEMP/ARBBALT)
             MONMSG     MSGID(CPF2105)

             CRTDUPOBJ  OBJ(ARBBALT) FROMLIB(DALIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(ARBBALT)

             /*  取得期初 */
             CLRPFM     (DALIB/ARBBALT)
             OVRDBF     FILE(ARBBALT) TOFILE(DALIB/ARBBALT)
             OVRDBF     FILE(ARBBAL)  TOFILE(ARBBAL) SHARE(*YES)
             OPNQRYF    FILE(ARBBAL) +
                        QRYSLT('(DATE *EQ "' || &YYMM || '")') +
                        MAPFLD((DATE  ALYYMM *CHAR 6))
             CALL       PGM(ARR083R1)
             CLOF       OPNID(ARBBAL)
             DLTOVR     FILE(*ALL)

             /*  取得異動情形*/
                 /*  PART 1 : 取得預收發票*/
             CHGVAR     VAR(&SYMD)    VALUE(&S#YYMM || '01')
             CHGVAR     VAR(&EYMD)    VALUE(&S#YYMM || '31')
             OVRDBF     FILE(ARBBALT) TOFILE(DALIB/ARBBALT)
             OVRDBF     FILE(INVMST)  TOFILE(INVMST) SHARE(*YES)
             OPNQRYF    FILE(INVMST) +
                        QRYSLT('(YMD *GE "' || &SYMD || '") *AND +
                                (YMD *LE "' || &EYMD || '") *AND +
                                (INDECD *NE "D") *AND +
                                (INTYPE *EQ "2")') +
                        MAPFLD((YMD   ININDT *CHAR 8)) +
                        KEYFLD((INORNO) (INNO))
             CALL       PGM(ARR083R2)
             CLOF       OPNID(INVMST)
             DLTOVR     FILE(*ALL)

                 /*  PART 2 .1 : 取得銷貨預收 (本期) */
             OVRDBF     FILE(ARBBALT) TOFILE(DALIB/ARBBALT)
             OVRDBF     FILE(TRNDTL)  TOFILE(TRNDTL) SHARE(*YES)
             OVRDBF     FILE(INVMST)  TOFILE(INVMST)
             OPNQRYF    FILE(TRNDTL) +
                        QRYSLT('(DATE1 *GE "' || &SYMD || '") *AND +
                                (DATE1 *LE "' || &EYMD || '") *AND +
                                (TXCODE *EQ "SA04") *AND +
                                (TXACNT *EQ "1")') +
                        MAPFLD((DATE1 TXACDT *CHAR 8) +
                               (TXPCNO '%SST(TXORNO 1 6) || "   "' *CHAR 8) +
                               (TXTXAR '%SST(TXNO 1 1)' *CHAR 1)) +
                        KEYFLD((TXPCNO) (TXTXAR) (TXIVNO))
             CALL       PGM(ARR083R3)
             CLOF       OPNID(TRNDTL)
             DLTOVR     FILE(*ALL)

                 /*  PART 2 .2 : 取得銷貨預收 (前出本開) */
             OVRDBF     FILE(ARBBALT) TOFILE(DALIB/ARBBALT)
             OVRDBF     FILE(TRNDTLL2) TOFILE(DALIB/TRNDTLL2)
             OVRDBF     FILE(INVMST)   TOFILE(DALIB/INVMST) SHARE(*YES)
             OPNQRYF    FILE(INVMST) QRYSLT(' +
                        ((INTYPE *EQ "1") *OR +
                         (INTYPE *EQ "3")) *AND +
                        (INDECD *NE "D") *AND +
                        (CHAR2  *EQ "' || &S#YYMM || '")') +
                        KEYFLD((INORNO) (INNO)) +
                        MAPFLD((CHAR1 ININDT *CHAR 8) +
                               (CHAR2 '%SST(CHAR1 1 6)' *CHAR 6))
             CALL       PGM(ARR083RA)
             CLOF       OPNID(INVMST)
             DLTOVR     FILE(*ALL)

                 /*  PART 3 : 取得預收折讓 */
             OVRDBF     FILE(ARBBALT) TOFILE(DALIB/ARBBALT)
             OVRDBF     FILE(TRNDTL)  TOFILE(TRNDTL) SHARE(*YES)
             OVRDBF     FILE(INVMST)  TOFILE(INVMST)
             OPNQRYF    FILE(TRNDTL) +
                        QRYSLT('(DATE1 *GE "' || &SYMD || '") *AND +
                                (DATE1 *LE "' || &EYMD || '") *AND +
                                (TXCODE *EQ "AR06") *AND +
                                (TXACNT *EQ "4")') +
                        MAPFLD((DATE1 TXACDT *CHAR 8) +
                               (TXPCNO '%SST(TXORNO 1 6) || "   "' *CHAR 8) +
                               (TXTXAR '%SST(TXNO 1 1)' *CHAR 1)) +
                        KEYFLD((TXPCNO) (TXTXAR))
             CALL       PGM(ARR083R4)
             CLOF       OPNID(TRNDTL)
             DLTOVR     FILE(*ALL)

                 /*  PART 4 : 取得預收退回 */
             OVRDBF     FILE(ARBBALT) TOFILE(DALIB/ARBBALT)
             OVRDBF     FILE(TRNDTL)  TOFILE(TRNDTL) SHARE(*YES)
             OPNQRYF    FILE(TRNDTL) +
                        QRYSLT('(CHAR1 *EQ "' || &S#YYMM || '") *AND +
                                (DATE2 *EQ "99999999") *AND +
                                (TXCODE *EQ "AR06") *AND +
                                (TXACNT *EQ "4")') +
                        MAPFLD((CHAR1 '%SST(TXRESV 1 6)' *CHAR 6) +
                               (DATE2 TXACDT *CHAR 8) +
                               (TXPCNO '%SST(TXORNO 1 6) || "   "' *CHAR 8) +
                               (TXTXAR '%SST(TXNO 1 1)' *CHAR 1)) +
                        KEYFLD((TXPCNO) (TXTXAR))
             CALL       PGM(ARR083R5)
             CLOF       OPNID(TRNDTL)
             DLTOVR     FILE(*ALL)

                 /*  PART 5 : 取得專案退稅 */
             OVRDBF     FILE(ARBBALT) TOFILE(DALIB/ARBBALT)
             OVRDBF     FILE(ARBTAX)  TOFILE(DALIB/ARBTAX) SHARE(*YES)
             OVRDBF     FILE(INVMST) TOFILE(DALIB/INVMST)
             OPNQRYF    FILE(ARBTAX) +
                        KEYFLD((AXCUNO) (AXORNO))
             CALL       PGM(ARR083R9)
             CLOF       OPNID(ARBTAX)
             DLTOVR     FILE(*ALL)

             /*  清除本期資料*/
             OVRDBF     FILE(ARBBAL)  TOFILE(ARBBAL) SHARE(*YES)
             OPNQRYF    FILE(ARBBAL) OPTION(*ALL) +
                        QRYSLT('(DATE1 *EQ "' || &S#YYMM || '")') +
                        MAPFLD((CHAR1 ALYYMM *CHAR 6) +
                               (DATE1 '%SST(CHAR1 3 4)' *CHAR 4))
             CALL       PGM(ARR083R6)
             CLOF       OPNID(ARBBAL)
             DLTOVR     FILE(*ALL)

             /*  開始處理*/
             OVRDBF     FILE(ARBBALT) TOFILE(DALIB/ARBBALT) SHARE(*YES)
             OVRDBF     FILE(ARBBAL)  TOFILE(ARBBAL)
             OPNQRYF    FILE(ARBBALT) +
                        KEYFLD((ATORNO) (ATCODE) (ATAREA) (ATINNO))
             CALL       PGM(ARR083R7)
             CLOF       OPNID(ARBBALT)
             DLTOVR     FILE(*ALL)

             ENDDO

             /*  出表*/
             IF         COND(&S#AREA *NE ' ') THEN(DO)
             CHGVAR     VAR(&SAREA)   VALUE(&S#AREA)
             CHGVAR     VAR(&EAREA)   VALUE(&S#AREA)
             ENDDO
             ELSE (DO)
             CHGVAR     VAR(&SAREA)   VALUE(' ')
             CHGVAR     VAR(&EAREA)   VALUE('9')
             ENDDO
             OVRDBF     FILE(ARBBAL)  TOFILE(ARBBAL) SHARE(*YES)
             OVRPRTF    FILE(ARR083T) TOFILE(ARR083T) +
                        USRDTA('預收增減') HOLD(*YES) +
                        PAGESIZE(*N 198) CPI(15)
             OPNQRYF    FILE(ARBBAL ) +
                        QRYSLT('+
                              (DATE1 *EQ "' || &S#YYMM || '") *AND +
                              (ALAREA *GE "' || &SAREA || '") *AND +
                              (ALAREA *LE "' || &EAREA || '")') +
                        MAPFLD((DATE1 ALYYMM *CHAR 6)) +
                        KEYFLD((ALORNO))
             CALL       PGM(ARR083R8)
             CLOF       OPNID(ARBBAL)
             DLTOVR     FILE(*ALL)

             SNDBRKMSG  MSG('<<預收貨款明細表處理完畢,請列印...>>'  ) +
                          TOMSGQ(&DSPID) MSGTYPE(*INFO)
             RETURN     /* BATCH JOB */
             ENDPGM
