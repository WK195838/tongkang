     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARR078P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02CLJ                                  */
     /*     4.FUNCTION     銷貨帳款每日應收明細表                 */
     /*     5.DATE-WRITTEN  88/06/10                                */
     /*                                                             */
     /*  <說明:>                                                  */
     /*            由使用者挑選要列印的人員，印出其應收明細       */
     /*                                                             */
     /*  *LDA       1 --> 挑選的人員(200個字元)                 */
     /*           201 --> 領款日數                                */
     /*           205 --> 逾期日數                                */
     /*           209 --> 催收日數                                */
     /*                   指定之區間216,217,218,219               */
     /*                   指定之狀態221,222,223,224               */
     /*           231 --> 統計截止日                              */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&INT)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&OUTQ)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03)   TYPE(*LGL)  LEN(1)

             DCL        VAR(&W#MBRS) TYPE(*CHAR) LEN(200)
             DCL        VAR(&W#ETXD) TYPE(*CHAR) LEN(8)


             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT) USER(&USER)
             IF         COND(&INT *EQ '0') THEN(GOTO BATCH)

INPUT:
             CALL       PGM(ARR078RS) PARM(&IN03)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)

             SBMJOB     JOB(ARR078P) JOBD(ARJOBD) OUTQ(&OUTQ)  +
                        RQSDTA('CALL ARR078P')
             CHGDTAARA  DTAARA(*LDA (801 70)) +
             VALUE('銷貨帳款應收明細表已匯入排程中。')
             RETURN

BATCH:

STEP1:       /* 找出要列印的資料  */

             RTVDTAARA  DTAARA(*LDA (1 200)) RTNVAR(&W#MBRS)
             RTVDTAARA  DTAARA(*LDA (231 8)) RTNVAR(&W#ETXD)
             CRTDUPOBJ  OBJ(ARSTTX) FROMLIB(DALIB) OBJTYPE(*FILE) +
                        TOLIB(QTEMP) NEWOBJ(ARSTTX@)

             OVRDBF     FILE(ARSTTX@) TOFILE(QTEMP/ARSTTX@)
             OVRDBF     FILE(ARSTTX) TOFILE(DALIB/ARSTTX) SHARE(*YES)
             OPNQRYF    FILE(ARSTTX) +
                        QRYSLT('AXTXDT *LE ' || &W#ETXD || ' *AND +
                                AXTXCD *NE "    " *AND +
                                AXRVID *EQ %VALUES(' *TCAT +
                                &W#MBRS *TCAT ') ')
             CALL       ARR078R1
             CLOF       OPNID(ARSTTX)
             DLTOVR     *ALL

             CLRPFM     FILE(DALIB/ARR078F)
             MONMSG     MSGID(CPF0000)

STEP2:       /* 列印報表  */

             OVRPRTF    FILE(ARR078T) USRDTA('應收明細') HOLD(*YES)
             OVRDBF     FILE(ARSTTX) TOFILE(QTEMP/ARSTTX@) SHARE(*YES)
             OPNQRYF    FILE(ARSTTX) +
                        KEYFLD((AXDEPT) (AXRVID) (AXRESV) (AXORNO) +
                               (AXINDU))
             CALL       ARR078R2
             CLOF       OPNID(ARSTTX)
             DLTOVR     *ALL

             ENDPGM
