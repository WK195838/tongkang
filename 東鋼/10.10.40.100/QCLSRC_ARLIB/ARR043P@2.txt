             PGM
             DCL        VAR(&INT)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03) TYPE(*LGL)  LEN(1)
             DCL        VAR(&AREA) TYPE(*CHAR) LEN(1)
             DCL        VAR(&S)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&E)    TYPE(*CHAR) LEN(1)
             DCL        VAR(&ST)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&EN)   TYPE(*CHAR) LEN(2)
             DCL        VAR(&SYSN) TYPE(*CHAR) LEN(8)
             DCL        VAR(&CUNO) TYPE(*CHAR) LEN(6)
             DCL        VAR(&CUNS) TYPE(*CHAR) LEN(6)
             DCL        VAR(&CUNE) TYPE(*CHAR) LEN(6)

             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT)
             RTVNETA    SYSNAME(&SYSN)

             IF         COND(&INT *EQ '1') THEN(DO)
             CALL       PGM(ARR043RS) PARM(&IN03 &S &E &AREA &CUNO)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)

             CHGDTAARA  DTAARA(*LDA (1 1)) VALUE(&S)
             CHGDTAARA  DTAARA(*LDA (2 1)) VALUE(' ')
             CHGDTAARA  DTAARA(*LDA (3 1)) VALUE(&E)
             CHGDTAARA  DTAARA(*LDA (4 1)) VALUE(' ')
             CHGDTAARA  DTAARA(*LDA (5 1)) VALUE(&AREA)
             CHGDTAARA  DTAARA(*LDA (6 6)) VALUE(&CUNO)

             SBMJOB     JOB(ARR043P) JOBD(CCJOBD)            +
                          OUTQ(&OUTQ) RQSDTA('CALL ARR043P')
             RETURN
             ENDDO
BATCH:
             RTVDTAARA  DTAARA(*LDA (1 2))   RTNVAR(&ST)
             RTVDTAARA  DTAARA(*LDA (3 2))   RTNVAR(&EN)
             RTVDTAARA  DTAARA(*LDA (5 1))   RTNVAR(&AREA)
             RTVDTAARA  DTAARA(*LDA (6 6))   RTNVAR(&CUNO)

 /*客戶代號如為空白，則顯示全部*/
             IF         COND(&CUNO *EQ '      ') THEN(DO)
             CHGVAR     VAR(&CUNS) VALUE('      ')
             CHGVAR     VAR(&CUNE) VALUE('999999')
             ENDDO
             ELSE (DO)
             CHGVAR     VAR(&CUNS) VALUE(&CUNO)
             CHGVAR     VAR(&CUNE) VALUE(&CUNO)
             ENDDO

             OVRPRTF    FILE(QPRINT) PAGESIZE(*N 176) CPI(13.3)
             OVRDBF     FILE(INVMST) TOFILE(INVMST) SHARE(*YES)

             /*排除發票MW38881554 (因畸零尾差造成負1 ) */
             /*排除發票BW49428622 (因畸零尾差造成負1 ) */

                     /* 不依請款單分類（台北） */
             IF         COND(&SYSN *EQ 'P02') THEN(DO)
             OPNQRYF    FILE(INVMST)                           +
                        QRYSLT('INRVID *GE "' || &ST   || '"   +
                           *AND INRVID *LE "' || &EN   || '"   +
                           *AND INCUNO *GE "' || &CUNS || '"   +
                           *AND INCUNO *LE "' || &CUNE || '"   +
                           *AND INTYPE *NE "9"                 +
                           *AND INNO   *NE "MW38881554"        +
                           *AND INNO   *NE "BW49428622"        +
                           *AND INDECD *EQ " "')               +
                        KEYFLD((INRVID) (INCUNM) (INCUNO)      +
                                        (ININDT) (INNO))
             CALL       PGM(ARR043R1)
             ENDDO

                     /* 依請款單分類（高雄） */
             ELSE       DO
             OPNQRYF    FILE(INVMST)                           +
                        QRYSLT('INRVID *GE "' || &ST || '"     +
                           *AND INRVID *LE "' || &EN || '"     +
                           *AND INCUNO *GE "' || &CUNS || '"   +
                           *AND INCUNO *LE "' || &CUNE || '"   +
                           *AND INTYPE *NE "9"                 +
                           *AND INDECD *EQ " "')               +
                        KEYFLD((INRVID) (INCUNM) (INCUNO)      +
                               (INAPNO) (ININDT) (INNO))
             CALL       PGM(ARR043R2)
             ENDDO

             CLOF       OPNID(INVMST)
             DLTOVR     FILE(*ALL)
             ENDPGM
