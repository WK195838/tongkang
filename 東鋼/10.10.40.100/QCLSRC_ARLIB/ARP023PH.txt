     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP023PH                                */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02CLJ                                  */
     /*     4.FUNCTION     銷貨明細檔即時上傳　（苗栗）           */
     /*     5.DATE-WRITTEN  87/02/24                                */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INT)  TYPE(*CHAR) LEN(1)

             RTVJOBA    USER(&USER) TYPE(&INT)
             IF         COND(&INT *EQ '1') THEN(DO)
             SBMJOB     JOB(ARP023PH) JOBD(ARJOBD) JOBQ(QCTL) +
                        RQSDTA('CALL ARLIB/ARP023PH')
             RETURN
             ENDDO

 BATCH:
 STEP1:
             DLTF       FILE(QTEMP/DDMH)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(MLDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                        TOLIB(QTEMP) NEWOBJ(DDMH)

 STEP2:       /*  台北　抓　苗栗  ARLIB/TRNDTLH ............ */
             SBMRMTCMD  CMD('CALL PGM(ARLIB/ARP023PA) PARM(''H'')') +
                        DDMFILE(QTEMP/DDMH)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO STEP3)

             CHGDDMF    FILE(QTEMP/DDMH) RMTFILE(*NONSTD +
                        'ARLIB/TRNDTLH') LVLCHK(*NO)
             CPYF       FROMFILE(QTEMP/DDMH) TOFILE(QTEMP/TRNDTLH) +
                        TOMBR(*FROMMBR) MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO STEP3)

             OVRDBF     FILE(TRNDTLT) TOFILE(QTEMP/TRNDTLH)
             OVRDBF     FILE(TRNDTL)  TOFILE(TRNDTL)
             OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 198) +
                        CPI(15)
             CHGDTAARA  DTAARA(*LDA (21 1)) VALUE('H')
             CALL       PGM(ARLIB/ARP023RB)
             DLTOVR     FILE(*ALL)

             CLRPFM     FILE(QTEMP/DDMH)  /* 清除廠區的異動資料 */
             MONMSG     MSGID(CPF0000) EXEC(GOTO STEP3)

             GOTO       ENDCLPGM

 STEP3:
             SNDMSG     TOUSR(&USER) MSG(+
                        '苗栗廠銷貨明細檔即時傳輸失敗')

 ENDCLPGM:   ENDPGM
