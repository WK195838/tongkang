     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP022PN                                */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S00WCJ                                  */
     /*     4.FUNCTION     發票明細檔即時上傳　（大業)          */
     /*     5.DATE-WRITTEN  112/02/17 2023AR00006                   */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INT)  TYPE(*CHAR) LEN(1)

             RTVJOBA    USER(&USER) TYPE(&INT)
             IF         COND(&INT *EQ '1') THEN(DO)
             SBMJOB     JOB(ARP022PN) JOBD(ARJOBD) JOBQ(QCTL) +
                        RQSDTA('CALL ARLIB/ARP022PN')
             RETURN
             ENDDO

 BATCH:

 STEP1:
             DLTF       FILE(QTEMP/DDMN)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(N01DDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                        TOLIB(QTEMP) NEWOBJ(DDMN)

 STEP2:       /*  台北　抓　大業ARLIB/INVDTLI ............ */
             SBMRMTCMD  CMD('CALL PGM(ARLIB/ARP022PA) PARM(''N'')') +
                        DDMFILE(QTEMP/DDMN)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO STEP3)

             CHGDDMF    FILE(QTEMP/DDMN) RMTFILE(*NONSTD +
                        'ARLIB/INVDTLN') LVLCHK(*NO)
             CPYF       FROMFILE(QTEMP/DDMN) TOFILE(QTEMP/INVDTLN) +
                        TOMBR(*FROMMBR) MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF0000) EXEC(GO STEP3)

             OVRDBF     FILE(INVDTLP) TOFILE(QTEMP/INVDTLN)
             OVRDBF     FILE(INVDTL)  TOFILE(INVDTL)
             OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 198) +
                        CPI(15)
             CHGDTAARA  DTAARA(*LDA (21 1)) VALUE('N')
             CALL       PGM(ARLIB/ARP022RJ)
             DLTOVR     FILE(*ALL)

             CLRPFM     FILE(QTEMP/DDMN)     /* 清除廠區之異動資料*/
             MONMSG     MSGID(CPF0000) EXEC(GOTO STEP3)

             GOTO       ENDCLPGM

 STEP3:     /* 失敗通知 */
             SNDMSG     TOUSR(&USER) MSG(+
                        '大業廠發票明細檔即時傳輸失敗')

 ENDCLPGM:   ENDPGM
