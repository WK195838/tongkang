        /*********************************************************************/
        /*                                                                   */
        /*        >>   PROGRAM INTRODUCTION  <<                              */
        /*        1.PROGRAM-ID     ARR126P                                   */
        /*        2.PROGRAM-TYPE   CLP                                       */
        /*        3.AUTHOR         S02CSF                                    */
        /*        4.FUNCTION      期後收回應收帳款明細表                   */
        /*        5.DATE-WRITTEN   95/06/28                                  */
        /*        6.DATE-MODIFY                                              */
        /*                                                                   */
        /*********************************************************************/
             PGM
             DCLF     FILE(ARLIB/ARR126S)
             DCL      VAR(&TYPE)   TYPE(*CHAR) LEN(1)
             DCL      VAR(&S#DEVN) TYPE(*CHAR) LEN(10)
             DCL      VAR(&S#USER) TYPE(*CHAR) LEN(10)
             DCL      VAR(&W#BLK)  TYPE(*CHAR) LEN(70)
             DCL      VAR(&C#DATS) TYPE(*CHAR) LEN(8)
             DCL      VAR(&C#DATE) TYPE(*CHAR) LEN(8)
             DCL      VAR(&C#YEAR) TYPE(*CHAR) LEN(4)
             DCL      VAR(&C#YMDS) TYPE(*CHAR) LEN(8)
             DCL      VAR(&C#YMDE) TYPE(*CHAR) LEN(8)
             DCL      VAR(&C#CUNS) TYPE(*CHAR) LEN(6)
             DCL      VAR(&C#CUNE) TYPE(*CHAR) LEN(6)
             DCL      VAR(&W#FLAG) TYPE(*CHAR) LEN(1)
             DCL      VAR(&P#PDAT) TYPE(*CHAR) LEN(8)  /*輸入日期*/
             DCL      VAR(&P#MODE) TYPE(*CHAR) LEN(1)  /*年份別*/
             DCL      VAR(&P#MTL)  TYPE(*CHAR) LEN(24) /*月日數*/
             DCL      VAR(&P#LEAP) TYPE(*CHAR) LEN(1)  /*閏年碼*/

             RTVJOBA  JOB(&S#DEVN) USER(&S#USER) TYPE(&TYPE)
             IF       COND(&TYPE *EQ '0') THEN(GOTO BATCH)

    START:   SNDRCVF
             IF         COND(&IN03 *EQ '1') THEN(RETURN)
             IF         COND(&IN12 *EQ '1') THEN(RETURN)

             CHGVAR     VAR(&IN30)    VALUE('0')
             CHGVAR     VAR(&IN31)    VALUE('0')
             CHGVAR     VAR(&S#ERR)   VALUE(&W#BLK)

 /* CHECK繳款起日*/
            CHGVAR     VAR(&P#PDAT)    VALUE(&S#DATS)
            CHGVAR     VAR(&P#MODE)    VALUE('1')
            CALL       PGM(UTS102R)    PARM(&P#PDAT &P#MODE +
                                            &P#MTL  &P#LEAP &W#FLAG)
            IF         COND(&W#FLAG *NE '0') THEN(DO)
            CHGVAR     VAR(&IN30)      VALUE('1')
            CHGVAR     VAR(&S#ERR)     VALUE('繳款起日錯誤！')
            GOTO       CMDLBL(START)
            ENDDO

 /* CHECK繳款止日*/
            CHGVAR     VAR(&P#PDAT)    VALUE(&S#DATE)
            CHGVAR     VAR(&P#MODE)    VALUE('1')
            CALL       PGM(UTS102R)    PARM(&P#PDAT &P#MODE +
                                            &P#MTL  &P#LEAP &W#FLAG)
            IF         COND(&W#FLAG *NE '0') THEN(DO)
            CHGVAR     VAR(&IN31)      VALUE('1')
            CHGVAR     VAR(&S#ERR)     VALUE('繳款止日錯誤！')
            GOTO       CMDLBL(START)
            ENDDO

 /* CHECK繳款起、止日*/
            IF         COND(&S#DATS *GT &S#DATE) THEN(DO)
            CHGVAR     VAR(&IN30)      VALUE('1')
            CHGVAR     VAR(&IN31)      VALUE('1')
            CHGVAR     VAR(&S#ERR)     VALUE('繳款日期錯誤！')
            GOTO       CMDLBL(START)
            ENDDO

 /*給予輸入年度起、訖日期*/
            CHGVAR     VAR(&C#YEAR)    VALUE(&S#YEAR)

            IF         COND(&S#YEAR *NE 0) THEN(DO)
            CHGVAR     VAR(&C#YMDS)    VALUE(&C#YEAR || '0101')
            CHGVAR     VAR(&C#YMDE)    VALUE(&C#YEAR || '1231')
            ENDDO

            IF         COND(&S#YEAR *EQ 0) THEN(DO)
            CHGVAR     VAR(&C#YMDS)    VALUE('00000000')
            CHGVAR     VAR(&C#YMDE)    VALUE('99999999')
            ENDDO

 /*給予客戶代號起、訖值*/
            IF         COND(&S#CUNO *EQ '      ') THEN(DO)
            CHGVAR     VAR(&C#CUNS)    VALUE('A     ')
            CHGVAR     VAR(&C#CUNE)    VALUE('999999')
            ENDDO

            IF         COND(&S#CUNO *NE '      ') THEN(DO)
            CHGVAR     VAR(&C#CUNS)    VALUE(&S#CUNO)
            CHGVAR     VAR(&C#CUNE)    VALUE(&S#CUNO)
            ENDDO

            CHGVAR     VAR(&C#DATS)    VALUE(&S#DATS)
            CHGVAR     VAR(&C#DATE)    VALUE(&S#DATE)

            CHGDTAARA  DTAARA(*LDA (301 8))   VALUE(&C#DATS)
            CHGDTAARA  DTAARA(*LDA (309 8))   VALUE(&C#DATE)
            CHGDTAARA  DTAARA(*LDA (317 8))   VALUE(&C#YMDS)
            CHGDTAARA  DTAARA(*LDA (325 8))   VALUE(&C#YMDE)
            CHGDTAARA  DTAARA(*LDA (333 4))   VALUE(&C#YEAR)
            CHGDTAARA  DTAARA(*LDA (337 6))   VALUE(&S#CUNO)
            CHGDTAARA  DTAARA(*LDA (343 6))   VALUE(&C#CUNS)
            CHGDTAARA  DTAARA(*LDA (349 6))   VALUE(&C#CUNE)

            SBMJOB     CMD(CALL PGM(ARR126P)) JOB(ARR126P) +
                       JOBD(ARJOBD)
            SNDBRKMSG  MSG('<<期後收回應收帳款明細表+
                        已開始處理,請稍待.........>>') +
                      TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)
            RETURN

 BATCH:
             RTVDTAARA  DTAARA(*LDA (301  8)) RTNVAR(&C#DATS)
             RTVDTAARA  DTAARA(*LDA (309  8)) RTNVAR(&C#DATE)
             RTVDTAARA  DTAARA(*LDA (317  8)) RTNVAR(&C#YMDS)
             RTVDTAARA  DTAARA(*LDA (325  8)) RTNVAR(&C#YMDE)
             RTVDTAARA  DTAARA(*LDA (333  4)) RTNVAR(&C#YEAR)
             RTVDTAARA  DTAARA(*LDA (337  6)) RTNVAR(&S#CUNO)
             RTVDTAARA  DTAARA(*LDA (343  6)) RTNVAR(&C#CUNS)
             RTVDTAARA  DTAARA(*LDA (349  6)) RTNVAR(&C#CUNE)

             CLRPFM     FILE(ARLIB/ARR126F)

             OVRDBF     FILE(RCVDTL) TOFILE(RCVDTL) SHARE(*YES)
             OPNQRYF    FILE((RCVDTL)) QRYSLT(' +
                        (DATE   *GE "' ||  &C#DATS || '")     *AND +
                        (DATE   *LE "' ||  &C#DATE || '")     *AND +
                        (RVCUNO *GE "' ||  &C#CUNS || '")     *AND +
                        (RVCUNO *LE "' ||  &C#CUNE || '")     *AND +
                        (RVTYPE *EQ "2")                      *AND +
                        (RVDECD *NE "D")')                         +
                        MAPFLD((DATE   RVDATE   *CHAR 8))          +
                        KEYFLD((RVNO)(RVITEM))
             CALL       PGM(ARR126R1)
             CLOF       OPNID(RCVDTL)
             DLTOVR     FILE(*ALL)

             OVRPRTF    FILE(ARR126T) TOFILE(ARR126T) PAGESIZE(*N 198)  +
                        CPI(15) HOLD(*YES)  USRDTA('期後收回')
             OVRDBF     FILE(ARR126F) TOFILE(ARR126F) SHARE(*YES)
             OPNQRYF    FILE((ARR126F))                     +
                        KEYFLD((A1RVNO)(A1ITEM))
             CALL       PGM(ARR126R)
             CLOF       OPNID(ARR126F)
             DLTOVR     FILE(*ALL)

             SNDBRKMSG  MSG('<<期後收回應收帳款明細表+
                        已處理完成,請列印.........>>') +
                      TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)

             ENDPGM
