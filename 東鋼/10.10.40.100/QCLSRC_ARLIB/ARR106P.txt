        /**********************************************************/
        /*                                                        */
        /*        >>   PROGRAM INTRODUCTION  <<                   */
        /*        1.PROGRAM-ID     ARR106P                        */
        /*        2.PROGRAM-TYPE   CLP                            */
        /*        3.AUTHOR         S02CSF                         */
        /*        4.FUNCTION      應收帳款承購客戶其他應收明細表*/
        /*        5.DATE-WRITTEN   92/02/06                       */
        /*        6.DATE-MODIFY                                   */
        /*                                                        */
        /**********************************************************/
              PGM
              DCLF       FILE(ARR106S)
              DCL        VAR(&TYPE)    TYPE(*CHAR)  LEN(1)
              DCL        VAR(&S#DEVN)  TYPE(*CHAR)  LEN(10)
              DCL        VAR(&S#USER)  TYPE(*CHAR)  LEN(10)
              DCL        VAR(&C_CUN1)  TYPE(*CHAR)  LEN(5)  /*客戶代號暫存*/
              DCL        VAR(&C_CUN2)  TYPE(*CHAR)  LEN(5)  /*客戶代號暫存*/
              DCL        VAR(&W#INDT)  TYPE(*CHAR)  LEN(8)  /*發票日期暫存*/
              DCL        VAR(&W#FLAG)  TYPE(*CHAR)  LEN(1)
              DCL        VAR(&P#PDAT)  TYPE(*CHAR)  LEN(8)  /*輸入日期*/
              DCL        VAR(&P#MODE)  TYPE(*CHAR)  LEN(1)  /*年份別*/
              DCL        VAR(&P#MTL)   TYPE(*CHAR)  LEN(24) /*月日數*/
              DCL        VAR(&P#LEAP)  TYPE(*CHAR)  LEN(1)  /*閏年碼*/

              RTVJOBA    JOB(&S#DEVN) USER(&S#USER) +
                         TYPE(&TYPE)

              IF         COND(&TYPE *EQ '0') THEN(GOTO CMDLBL(BATCH))

              RTVDTAARA  DTAARA(*LDA (1011 10))   RTNVAR(&S#DEVN)
              RTVDTAARA  DTAARA(*LDA (1001 10))   RTNVAR(&S#USER)

   START:   SNDRCVF

            CHGVAR     VAR(&IN30)      VALUE('0')
            CHGVAR     VAR(&S#ERR)     VALUE(*BLANK)

            IF         COND(&IN03 *EQ '1') THEN(RETURN)
            IF         COND(&IN12 *EQ '1') THEN(RETURN)

        /*    CHECK發票日期    */
            IF         COND(&S#INDT *EQ '      ') THEN(DO)
            CHGVAR     VAR(&S#ERR)     VALUE('請輸入發票日期')
            CHGVAR     VAR(&IN30)      VALUE('1')
            GOTO       CMDLBL(START)
            ENDDO

            CHGDTAARA  DTAARA(*LDA (400 2))   VALUE('  ')
            CHGDTAARA  DTAARA(*LDA (402 6))   VALUE(&S#INDT)
            RTVDTAARA  DTAARA(*LDA (400 8))   RTNVAR(&W#INDT)
            CHGVAR     VAR(&P#PDAT)    VALUE(&W#INDT)
            CHGVAR     VAR(&P#MODE)    VALUE('1')
            CALL       PGM(UTS102R)    PARM(&P#PDAT &P#MODE +
                                            &P#MTL  &P#LEAP &W#FLAG)
            IF         COND(&W#FLAG *NE '0') THEN(DO)
            CHGVAR     VAR(&IN30)      VALUE('1')
            CHGVAR     VAR(&S#ERR)     VALUE('發票日期錯誤！')
            GOTO       CMDLBL(START)
            ENDDO

        /*    CHECK客戶代號    */
            IF         COND(&S#CUNO *EQ '     ') THEN(DO)
            CHGVAR     VAR(&C_CUN1)    VALUE('     ')
            CHGVAR     VAR(&C_CUN2)    VALUE('99999')
            ENDDO
            ELSE (DO)
            CHGVAR     VAR(&C_CUN1)    VALUE(&S#CUNO)
            CHGVAR     VAR(&C_CUN2)    VALUE(&S#CUNO)
            ENDDO

            CHGDTAARA  DTAARA(*LDA  (300  5))  VALUE(&C_CUN1)
            CHGDTAARA  DTAARA(*LDA  (305  5))  VALUE(&C_CUN2)
            CHGDTAARA  DTAARA(*LDA  (310  5))  VALUE(&S#CUNO)
            CHGDTAARA  DTAARA(*LDA  (315  6))  VALUE(&S#INDT)

            SBMJOB     JOB(ARR106P) JOBD(ARJOBD) +
                       RQSDTA('CALL ARR106P')
            SNDBRKMSG  MSG('<<應收帳款承購客戶其他應收明細表+
                           ,已開始處理,請稍待...>>')    +
                           TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)
            RETURN
   BATCH:

            RTVDTAARA  DTAARA(*LDA (300 5))    RTNVAR(&C_CUN1)
            RTVDTAARA  DTAARA(*LDA (305 5))    RTNVAR(&C_CUN2)
            RTVDTAARA  DTAARA(*LDA (310 5))    RTNVAR(&S#CUNO)
            RTVDTAARA  DTAARA(*LDA (315 6))    RTNVAR(&S#INDT)

            OVRPRTF    FILE(ARR106T) TOFILE(ARR106T) PAGESIZE(*N 198)   +
                       CPI(15) HOLD(*YES) USRDTA('承購明細')
            OVRDBF     FILE(ARINVM) TOFILE(ARINVM) SHARE(*YES)
            OPNQRYF    FILE(ARINVM)  OPTION(*ALL) QRYSLT(' +
                       (CUNO    *GE "' || &C_CUN1 || '") *AND +
                       (CUNO    *LE "' || &C_CUN2 || '") *AND +
                       (AMCLOC  *EQ " ") *AND +
                       (INDT    *LE "' || &S#INDT || '")')    +
                       KEYFLD((AMCUNO) (AMORNO))  +
                       MAPFLD((CUNO  '%SST(AMCUNO 1 5)' *CHAR 5) +
                              (IND1  AMINDT  *CHAR 8) +
                              (INDT  '%SST(IND1   3 6)' *CHAR 6))
            CALL       PGM(ARR106R)
            CLOF       OPNID(ARINVM)
            DLTOVR     FILE(*ALL)
            SNDBRKMSG  MSG('<<應收帳款承購客戶承購明細表+
                          ,已處理完成,請列印...>>')    +
                          TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)
   END:     ENDPGM
