    /****************************************************************/
    /*                                                              */
    /*             UPDATE  DATE  100/11/28 2011AR634 S00WCJ (0011A) */
    /*                            配合IFRS修改                  */
    /*                           101/12/13 2012AR704 S00WCJ (0112A) */
    /*                            配合IFRS正式轉換修改          */
    /*                                                              */
    /****************************************************************/

             PGM
             DCL        VAR(&P#YYMM) TYPE(*CHAR) LEN(6)
             DCL        VAR(&P#DATE) TYPE(*CHAR) LEN(8)
             DCL        VAR(&P#CODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&INT)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03) TYPE(*LGL)  LEN(1)
             DCL        VAR(&YMD)  TYPE(*CHAR) LEN(8)
             DCL        VAR(&ACYMD)  TYPE(*CHAR) LEN(8)
             DCL        VAR(&AREA) TYPE(*CHAR) LEN(1)
             DCL        VAR(&Y)    TYPE(*CHAR) LEN(4)
             DCL        VAR(&M)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&DTARA) TYPE(*CHAR) LEN(11)
             DCL        VAR(&DTAR)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&WSID)  TYPE(*CHAR) LEN(10)
 /*0011A*/   DCL        VAR(&D#VNO1) TYPE(*CHAR) LEN(6)
 /*0011A*/   DCL        VAR(&D#VNO2) TYPE(*CHAR) LEN(6)
 /*0011A*/   DCL        VAR(&YM)     TYPE(*CHAR) LEN(6)
 /*0112A*/   DCL        VAR(&IFRSD)  TYPE(*CHAR) LEN(8)

             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT)
             RTVDTAARA  DTAARA(*LDA (1021 1)) RTNVAR(&AREA)
             RTVDTAARA  DTAARA(*LDA (1011 10)) RTNVAR(&WSID)
             CHGVAR     VAR(&DTARA) VALUE('ACCTLDTA' *CAT &AREA)
             RTVDTAARA  DTAARA(DALIB/&DTARA (1 8)) RTNVAR(&ACYMD)

 DISPLY:     IF         COND(&INT *EQ '1') THEN(DO)
             CALL       PGM(ARP017RS) PARM(&IN03 &YMD &ACYMD)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)
             CHGDTAARA  DTAARA(*LDA (1 8)) VALUE(&YMD)
             RTVDTAARA  DTAARA(*LDA (1 6)) RTNVAR(&P#YYMM)
             RTVDTAARA  DTAARA(*LDA (1 8)) RTNVAR(&P#DATE)

             CALL    PGM(ACP101R) PARM(&P#YYMM &P#CODE)
             IF         COND(&P#CODE *EQ '1') THEN(DO)
             SNDBRKMSG  MSG('關帳日期已過，請與相關人員聯絡！') +
                          TOMSGQ(&WSID)
             GOTO       CMDLBL(DISPLY)
             ENDDO

             SBMJOB     JOB(ARP017P) JOBD(ARJOBD) OUTQ(&OUTQ) +
                          RQSDTA('CALL ARP017P')
             RETURN
             ENDDO

 BATCH:
             RTVDTAARA  DTAARA(*LDA (1    8)) RTNVAR(&YMD)
             RTVDTAARA  DTAARA(*LDA (1    4)) RTNVAR(&Y)
             RTVDTAARA  DTAARA(*LDA (5    2)) RTNVAR(&M)
 /*0011A*/   RTVDTAARA  DTAARA(*LDA (1    6)) RTNVAR(&YM)
             RTVDTAARA  DTAARA(*LDA (1021 1)) RTNVAR(&AREA)
 /*0112A*/   RTVDTAARA  DTAARA(ARIFRSCTL (1  8)) RTNVAR(&IFRSD)

             OVRDBF     FILE(RCVDTLL1) TOFILE(RCVDTLL1) SHARE(*YES)
             OPNQRYF    FILE((RCVDTLL1))     QRYSLT('   +
                          RVNTDT *EQ   ' || &YMD || '          +
                    *AND  RVNTCD *EQ   "2"                     +
                    *AND  RVVNO2 *EQ   " "                     +
                    *AND  %SST(RVWRNO 1 1) *EQ  "' || &AREA || '" +
                    *AND  RVDECD *NE  "D"')                    +
                          KEYFLD((RVWRNO)(RVITEM))

             OVRDBF     FILE(ACTRAN) TOFILE(DALIB/('AC' || &AREA || +
                          'R')) LVLCHK(*NO)
             OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 176) +
                          CPI(13.3)
             CALL       PGM(ARP017R)
             CLOF       OPNID(RCVDTLL1)
             DLTOVR     FILE(*ALL)
 /*0112A*/  IF    COND(&YMD  *LT  &IFRSD) THEN(DO)
 /*0011A STRAT */

             RTVDTAARA  DTAARA(*LDA (201 6)) RTNVAR(&D#VNO1)
             RTVDTAARA  DTAARA(*LDA (211 6)) RTNVAR(&D#VNO2)

             CALL       PGM(ARS888P) PARM(&AREA &YM &D#VNO1 &D#VNO2)
 /*0011A  END */
 /*0112A*/  ENDDO
             ENDPGM
