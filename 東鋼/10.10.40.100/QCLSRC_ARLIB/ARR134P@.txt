        /*********************************************************************/
        /*                                                                   */
        /*        >>   PROGRAM INTRODUCTION  <<                              */
        /*        1.PROGRAM-ID     ARR134P                                   */
        /*        2.PROGRAM-TYPE   CLP                                       */
        /*        3.AUTHOR         S02LBU                                    */
        /*        4.FUNCTION      折讓簽回彙總表（含銷折／銷退／預收）　   */
        /*        5.DATE-WRITTEN   98/01/10                                  */
        /*        6.DATE-MODIFY                                              */
        /*                                                                   */
        /*********************************************************************/
          PGM
          DCLF     FILE(ARR134S)
          DCL      VAR(&TYPE)   TYPE(*CHAR) LEN(1)
          DCL      VAR(&C#DATS) TYPE(*CHAR) LEN(8)
          DCL      VAR(&C#DATE) TYPE(*CHAR) LEN(8)
          DCL      VAR(&C#YMS)  TYPE(*CHAR) LEN(6)
          DCL      VAR(&C#YME)  TYPE(*CHAR) LEN(6)
          DCL      VAR(&ARE1)   TYPE(*CHAR) LEN(1)
          DCL      VAR(&ARE2)   TYPE(*CHAR) LEN(1)
          DCL      VAR(&SALE1)  TYPE(*CHAR) LEN(2)
          DCL      VAR(&SALE2)  TYPE(*CHAR) LEN(2)
          DCL      VAR(&W#BLK)  TYPE(*CHAR) LEN(70)
          DCL      VAR(&P#DATE) TYPE(*CHAR) LEN(8)  /*輸入日期*/
          DCL      VAR(&W#FLAG) TYPE(*CHAR) LEN(1)
          DCL      VAR(&P#MODE) TYPE(*CHAR) LEN(1)  /*年份別*/
          DCL      VAR(&P#MTL)  TYPE(*CHAR) LEN(24) /*月日數*/
          DCL      VAR(&P#LEAP) TYPE(*CHAR) LEN(1)  /*閏年碼*/

          RTVJOBA  JOB(&S#DEVN) USER(&U#USID) TYPE(&TYPE)
          IF       COND(&TYPE *EQ '0') THEN(GOTO BATCH)
          CHGVAR   VAR(&S#CODE)    VALUE('1')

  START:  SNDRCVF

          CHGVAR     VAR(&IN30)      VALUE('0')
          CHGVAR     VAR(&IN31)      VALUE('0')
          CHGVAR     VAR(&IN32)      VALUE('0')
          CHGVAR     VAR(&IN33)      VALUE('0')
          CHGVAR     VAR(&IN34)      VALUE('0')
          CHGVAR     VAR(&S#ERR)     VALUE(&W#BLK)
          CHGVAR     VAR(&S#CODE)    VALUE('1')
          IF         COND(&IN03 *EQ '1') THEN(RETURN)
          IF         COND(&IN12 *EQ '1') THEN(RETURN)

  /*  CHECK 折讓開立廠區*/
          IF         COND((&S#AREA *NE  ' '  )  *AND +
                          (&S#AREA *NE  'P'  )  *AND +
                          (&S#AREA *NE  'T'  )  *AND +
                          (&S#AREA *NE  'M'  )  *AND +
                          (&S#AREA *NE  'K'  )  *AND +
                          (&S#AREA *NE  'H'  )) THEN(DO)
          CHGVAR     VAR(&S#ERR)     VALUE('折讓開立廠區輸入錯誤')
          CHGVAR     VAR(&IN30)      VALUE('1')
          GOTO       CMDLBL(START)
          ENDDO

      /*給予折讓開立廠區*/
          IF         COND(&S#AREA *EQ  ' ') THEN(DO)
          CHGVAR     VAR(&ARE1)      VALUE('A')
          CHGVAR     VAR(&ARE2)      VALUE('Z')
          ENDDO
          ELSE (DO)
          CHGVAR     VAR(&ARE1)      VALUE(&S#AREA)
          CHGVAR     VAR(&ARE2)      VALUE(&S#AREA)
          ENDDO

  /*  CHECK 折讓簽回日期*/
          CHGVAR     VAR(&P#DATE)    VALUE(&S#DATS)
          CHGVAR     VAR(&P#MODE)    VALUE('1')
          CALL       PGM(UTS102R)    PARM(&P#DATE &P#MODE +
                                          &P#MTL  &P#LEAP &W#FLAG)
          IF         COND(&W#FLAG *NE '0') THEN(DO)
          CHGVAR     VAR(&IN31)      VALUE('1')
          CHGVAR     VAR(&S#ERR)     VALUE('起始日期錯誤！')
          GOTO       CMDLBL(START)
          ENDDO

          CHGVAR     VAR(&P#DATE)    VALUE(&S#DATE)
          CHGVAR     VAR(&P#MODE)    VALUE('1')
          CALL       PGM(UTS102R)    PARM(&P#DATE &P#MODE +
                                          &P#MTL  &P#LEAP &W#FLAG)
          IF         COND(&W#FLAG *NE '0') THEN(DO)
          CHGVAR     VAR(&IN32)      VALUE('1')
          CHGVAR     VAR(&S#ERR)     VALUE('終止日期錯誤！')
          GOTO       CMDLBL(START)
          ENDDO

          IF         COND(&S#DATS *GT  &S#DATE) THEN(DO)
          CHGVAR     VAR(&S#ERR)     VALUE('起始日期不可大於終止日期')
          CHGVAR     VAR(&IN31)      VALUE('1')
          CHGVAR     VAR(&IN32)      VALUE('1')
          GOTO       CMDLBL(START)
          ENDDO

  /*  CHECK 部門*/
          IF         COND((&S#CODE *NE  '1'  )  *AND +
                          (&S#CODE *NE  '2'  )) THEN(DO)
          CHGVAR     VAR(&S#ERR)     VALUE('部門輸入錯誤')
          CHGVAR     VAR(&IN33)      VALUE('1')
          GOTO       CMDLBL(START)
          ENDDO

  /*  CHECK 業務*/
          IF         COND(((&S#SALE *LT  'A'  )  *OR   +
                           (&S#SALE *GT  'Z'  )) *AND  +
                          (&S#SALE *NE  ' '  )) THEN(DO)
          CHGVAR     VAR(&S#ERR)     VALUE('業務員輸入錯誤')
          CHGVAR     VAR(&IN34)      VALUE('1')
          GOTO       CMDLBL(START)
          ENDDO

          IF         COND(&S#SALE *EQ  ' ') THEN(DO)
          CHGVAR     VAR(&SALE1)     VALUE('A')
          CHGVAR     VAR(&SALE2)     VALUE('Z')
          ENDDO
          ELSE (DO)
          CHGVAR     VAR(&SALE1)     VALUE(&S#SALE)
          CHGVAR     VAR(&SALE2)     VALUE(&S#SALE)
          ENDDO

  /*記錄輸入值*/
          CHGVAR     VAR(&C#DATS)   VALUE(&S#DATS)
          CHGVAR     VAR(&C#DATE)   VALUE(&S#DATE)
          CHGDTAARA  DTAARA(*LDA (301  8))  VALUE(&C#DATS)
          CHGDTAARA  DTAARA(*LDA (309  8))  VALUE(&C#DATE)
          CHGDTAARA  DTAARA(*LDA (317  1))  VALUE(&ARE1)
          CHGDTAARA  DTAARA(*LDA (318  1))  VALUE(&ARE2)
          CHGDTAARA  DTAARA(*LDA (319  1))  VALUE(&S#CODE)
          CHGDTAARA  DTAARA(*LDA (320  2))  VALUE(&SALE1)
          CHGDTAARA  DTAARA(*LDA (322  2))  VALUE(&SALE2)

     /*   SBMJOB     CMD(CALL PGM(ARR134P)) JOB(ARR134P) +
                     JOBD(ARJOBD)        */
          SNDBRKMSG  MSG('<<折讓簽回彙總表已開始處理，請稍待....>>') +
                     TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)
     /*   RETURN                         */

  BATCH:
          RTVDTAARA  DTAARA(*LDA (301  8)) RTNVAR(&C#DATS)
          RTVDTAARA  DTAARA(*LDA (301  6)) RTNVAR(&C#YMS)
          RTVDTAARA  DTAARA(*LDA (309  8)) RTNVAR(&C#DATE)
          RTVDTAARA  DTAARA(*LDA (309  6)) RTNVAR(&C#YME)
          RTVDTAARA  DTAARA(*LDA (317  1)) RTNVAR(&ARE1)
          RTVDTAARA  DTAARA(*LDA (318  1)) RTNVAR(&ARE2)
          RTVDTAARA  DTAARA(*LDA (319  1)) RTNVAR(&S#CODE)
          RTVDTAARA  DTAARA(*LDA (320  2)) RTNVAR(&SALE1)
          RTVDTAARA  DTAARA(*LDA (322  2)) RTNVAR(&SALE2)

          /*   先前處理　　*/
          CLRPFM     FILE(ARLIB/ARR134F)
          OVRDBF     FILE(TRNDTLL3) TOFILE(DALIB/TRNDTLL3) SHARE(*YES)
          OPNQRYF    FILE((TRNDTLL3)) OPTION(*ALL) QRYSLT('     +
                     ((ACDT     *GE "' || &C#DATS || '")   *AND +
                      (ACDT     *LE "' || &C#DATE || '")   *OR  +
                      ((ACDT    *EQ "99999999")            *AND +
                       (RESV    *GE "' || &C#YMS  || '")   *AND +
                       (RESV    *LE "' || &C#YME  || '"))) *AND +
                     (TXTXAR    *GE "' || &ARE1   || '")   *AND +
                     (TXTXAR    *LE "' || &ARE2   || '")   *AND +
                     (TXRVID    *GE "' || &SALE1  || '")   *AND +
                     (TXRVID    *LE "' || &SALE2  || '")   *AND +
                     (TXFL01    *EQ "Y" )                  *AND +
                     (TXFLAG    *NE "D" )')           +
                     KEYFLD((TXNO) (TXACDT))         +
                     MAPFLD((ACDT   TXACDT  *CHAR 8) +
                            (RESV   '%SST(TXRESV 1 6)' *CHAR 6))
          CALL       PGM(ARR134R)
          CLOF       OPNID(TRNDTLL3)

          OVRPRTF    FILE(ARR134T) TOFILE(ARR134T) PAGESIZE(*N  98) +
                     CPI(10) HOLD(*YES) USRDTA('折讓彙總')
          OVRDBF     FILE(ARR134F)  TOFILE(ARR134F) SHARE(*YES)
          OPNQRYF    FILE((ARR134F)) OPTION(*ALL) QRYSLT('     +
                     (A4AREA   *GE "' || &ARE1 || '") *AND   +
                     (A4AREA   *LE "' || &ARE2 || '")')      +
                     KEYFLD((A4ACNT) (A4NO) (A4ACDT) (A4CUNM) (A4ORNO))
          CALL       PGM(ARR134R1)
          CLOF       OPNID(ARR134F)
          DLTOVR     *ALL

          SNDBRKMSG  MSG('<<折讓單統計表已處理完成，請列印....>>') +
                     TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)

          ENDPGM
