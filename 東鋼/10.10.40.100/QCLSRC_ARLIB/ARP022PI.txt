     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP022PI                                */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S00WCJ                                  */
     /*     4.FUNCTION     發票明細檔即時上傳　（加工中心）       */
     /*     5.DATE-WRITTEN  111/07/07 2022AR00028                   */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INT)  TYPE(*CHAR) LEN(1)

             RTVJOBA    USER(&USER) TYPE(&INT)
             IF         COND(&INT *EQ '1') THEN(DO)
             SBMJOB     JOB(ARP022PI) JOBD(ARJOBD) JOBQ(QCTL) +
                        RQSDTA('CALL ARLIB/ARP022PI')
             RETURN
             ENDDO

 BATCH:

 STEP1:
             DLTF       FILE(QTEMP/DDMI)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(I01DDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                        TOLIB(QTEMP) NEWOBJ(DDMI)

 STEP2:       /*  台北　抓　加工中心ARLIB/INVDTLI ............ */
             SBMRMTCMD  CMD('CALL PGM(ARLIB/ARP022PA) PARM(''I'')') +
                        DDMFILE(QTEMP/DDMI)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO STEP3)

             CHGDDMF    FILE(QTEMP/DDMI) RMTFILE(*NONSTD +
                        'ARLIB/INVDTLI') LVLCHK(*NO)
             CPYF       FROMFILE(QTEMP/DDMI) TOFILE(QTEMP/INVDTLI) +
                        TOMBR(*FROMMBR) MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF0000) EXEC(GO STEP3)

             OVRDBF     FILE(INVDTLP) TOFILE(QTEMP/INVDTLI)
             OVRDBF     FILE(INVDTL)  TOFILE(INVDTL)
             OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 198) +
                        CPI(15)
             CHGDTAARA  DTAARA(*LDA (21 1)) VALUE('I')
             CALL       PGM(ARLIB/ARP022RJ)
             DLTOVR     FILE(*ALL)

             CLRPFM     FILE(QTEMP/DDMI)     /* 清除廠區之異動資料*/
             MONMSG     MSGID(CPF0000) EXEC(GOTO STEP3)

             GOTO       ENDCLPGM

 STEP3:     /* 失敗通知 */
             SNDMSG     TOUSR(&USER) MSG(+
                        '加工中心發票明細檔即時傳輸失敗')

 ENDCLPGM:   ENDPGM
