     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP023PN                                */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S00WCJ                                  */
     /*     4.FUNCTION     銷貨明細檔即時上傳　（大業）           */
     /*     5.DATE-WRITTEN  112/02/17 2023AR00006 S00WCJ            */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INT)  TYPE(*CHAR) LEN(1)

             RTVJOBA    USER(&USER) TYPE(&INT)
             IF         COND(&INT *EQ '1') THEN(DO)
             SBMJOB     JOB(ARP023PN) JOBD(ARJOBD) JOBQ(QCTL) +
                        RQSDTA('CALL ARLIB/ARP023PN')
             RETURN
             ENDDO

 BATCH:
 STEP1:
             DLTF       FILE(QTEMP/DDMN)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(N01DDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                        TOLIB(QTEMP) NEWOBJ(DDMN)

 STEP2:       /*  台北　抓　大業  ARLIB/TRNDTLN ............ */
             SBMRMTCMD  CMD('CALL PGM(ARLIB/ARP023PA) PARM(''N'')') +
                        DDMFILE(QTEMP/DDMN)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO STEP3)

             CHGDDMF    FILE(QTEMP/DDMN) RMTFILE(*NONSTD +
                        'ARLIB/TRNDTLY') LVLCHK(*NO)
             CPYF       FROMFILE(QTEMP/DDMN) TOFILE(QTEMP/TRNDTLY) +
                        TOMBR(*FROMMBR) MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO STEP3)

             OVRDBF     FILE(TRNDTLT) TOFILE(QTEMP/TRNDTLY)
             OVRDBF     FILE(TRNDTL)  TOFILE(TRNDTL)
             OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 198) +
                        CPI(15)
             CHGDTAARA  DTAARA(*LDA (21 1)) VALUE('N')
             CALL       PGM(ARLIB/ARP023RB)
             DLTOVR     FILE(*ALL)

             CLRPFM     FILE(QTEMP/DDMN)   /* 清除廠區的異動資料 */
             MONMSG     MSGID(CPF0000) EXEC(GOTO STEP3)

             GOTO       ENDCLPGM

 STEP3:
             SNDMSG     TOUSR(&USER) MSG(+
                        '大業廠銷貨明細檔即時傳輸失敗')

 ENDCLPGM:   ENDPGM
