     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP074P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02YSH                                  */
     /*     4.FUNCTION     折讓退回代開傳票                       */
     /*     5.DATE-WRITTEN  89/12/29                                */
     /*                                                             */
     /***************************************************************/
             PGM
             DCLF       FILE(ARP103S)
             DCL        VAR(&P#YYMM) TYPE(*CHAR) LEN(4)
             DCL        VAR(&P#CODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&INT)   TYPE(*CHAR) LEN(1)   /*執行狀態*/
             DCL        VAR(&OUTQ)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&AREA)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&YYMM)  TYPE(*CHAR) LEN(4)
             DCL        VAR(&YY)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&MM)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&FNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C#SDAT) TYPE(*CHAR) LEN(6)
             DCL        VAR(&C#EDAT) TYPE(*CHAR) LEN(6)

             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT) USER(&USER)
             IF         COND(&INT *EQ '0') THEN(GOTO BATCH)

             RTVDTAARA  DTAARA(*LDA (1021 1)) RTNVAR(&AREA)
             RTVDTAARA  DTAARA(*LDA (951 35)) RTNVAR(&S#COMP)
             RTVDTAARA  DTAARA(*LDA (1011 10)) RTNVAR(&S#DEVI)
             CHGVAR     VAR(&S#AREA) VALUE(&AREA)
 DISPLAY:
             SNDRCVF    RCDFMT(AR103F1)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)
             IF         COND(&IN12 *EQ '1') THEN(RETURN)

             CHGVAR     VAR(&YYMM) VALUE(&S#YYMM)
             CHGVAR     VAR(&P#YYMM) VALUE(&S#YYMM)
             CHGVAR     VAR(&YY) VALUE(%SST(&YYMM 1 2))
             CHGVAR     VAR(&MM) VALUE(%SST(&YYMM 3 2))
             CHGVAR     VAR(&FNAME) +
                        VALUE('AC' || &AREA || 'R' || &YY )
             CHGVAR     VAR(&MNAME) VALUE('VR' || &MM)
             CHKOBJ     OBJ(&FNAME) OBJTYPE(*FILE) MBR(&MNAME)
             MONMSG     CPF0000     EXEC(DO)
             CHGVAR     VAR(&S#ERR) +
                        VALUE('折扣代開傳票作業，無法找到指定+
                          的傳票檔：' || &FNAME || ', ' || &MNAME)
                        GOTO DISPLAY
             ENDDO

             ADDLIBLE   ACLIB
             MONMSG     CPF0000

             CALL    PGM(ACP101R) PARM(&P#YYMM &P#CODE)
             IF         COND(&P#CODE *EQ '1') THEN(DO)
             CHGVAR     VAR(&S#ERR) VALUE('關帳日期已過')
             GOTO       CMDLBL(DISPLAY)
             ENDDO

             CHGDTAARA  DTAARA(*LDA (301 4)) VALUE(&YYMM)
             CHGDTAARA  DTAARA(*LDA (307 1)) VALUE(&S#AREA)

             SBMJOB     JOB(ARP103P) JOBD(ARJOBD) OUTQ(&OUTQ)  +
                        RQSDTA('CALL ARP103P')
             RETURN

BATCH:
             RTVDTAARA  DTAARA(*LDA (1011 10)) RTNVAR(&S#DEVI)
             RTVDTAARA  DTAARA(*LDA (301 4)) RTNVAR(&YYMM)
             RTVDTAARA  DTAARA(*LDA (301 2)) RTNVAR(&YY)
             RTVDTAARA  DTAARA(*LDA (303 2)) RTNVAR(&MM)
             RTVDTAARA  DTAARA(*LDA (307 1)) RTNVAR(&AREA)

             CHGVAR     VAR(&FNAME) +
                        VALUE('AC' || &AREA || 'R' || &YY)
             CHGVAR     VAR(&MNAME) VALUE('VR' || &MM)


STEP1:       /* 刪除已代開過的資料 */

             OVRDBF     FILE(ACTRAN) TOFILE(&FNAME) MBR(&MNAME)        +
                        SHARE(*YES)
             OPNQRYF    FILE(ACTRAN) OPTION(*ALL) +
                        QRYSLT('ACVRNO *GE "C800" *AND +
                                ACVRNO *LE "C999"')

             CALL       PGM(ARP103R1)  /*刪除輸入日期後的資料*/
             CLOF       OPNID(ACTRAN)
             DLTOVR     *ALL

STEP2:       /* 代開開始*/   /*------------------*/

             OVRDBF     FILE(ACTRAN) TOFILE(&FNAME) MBR(&MNAME)        +
                        SHARE(*YES)
             OVRDBF     FILE(INVDTL) TOFILE(INVDTL) SHARE(*YES)
             OVRPRTF    FILE(ARP103T) TOFILE(ARP103T) PAGESIZE(*N +
                          132) CPI(10) HOLD(*YES) USRDTA('折扣開１')
             OVRPRTF    FILE(ARP103TA) TOFILE(ARP103TA) PAGESIZE(*N +
                          132) CPI(10) HOLD(*YES) USRDTA('折扣開２')

             OPNQRYF    FILE(INVDTL) OPTION(*ALL) +
                        QRYSLT('+
                              (IVACNT *EQ "3") *AND +
                              (IVFL03 *EQ "K") *AND +
                              (ACD1   *EQ "' || &YYMM || '") *AND +
                              (IVTXAR *EQ "' || &AREA || '") *AND +
                              (IVDECD *NE "D")') +
                        MAPFLD((ACDT IVACDT  *CHAR 8)  +
                               (ACD1 '%SST(ACDT   3 4)' *CHAR 4)) +
                        KEYFLD((IVNO) (IVPDCD))

             CALL       PGM(ARP103R)  /*傳票代開及列表*/
             CLOF       OPNID(INVDTL)
             DLTOVR     FILE(*ALL)

             SNDBRKMSG  MSG('<<折扣代開傳票處理完畢，請列印           +
                        ....>>') TOMSGQ(&S#DEVI) +
                        MSGTYPE(*INFO)

             ENDPGM
