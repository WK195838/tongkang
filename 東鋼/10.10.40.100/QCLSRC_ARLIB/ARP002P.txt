             PGM
             DCL        VAR(&INT)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&USER)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&YMDA)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&YMD)   TYPE(*DEC)  LEN(6 0)
             DCL        VAR(&AREA)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&AREAX) TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN03)  TYPE(*LGL)  LEN(1)
             DCL        VAR(&RTNV)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&IVNO)  TYPE(*CHAR) LEN(8)

             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT) USER(&USER)
             IF         COND(&INT *EQ '1') THEN(DO)
             CALL       PGM(ARP002RS) PARM(&IN03 &YMD &AREA)
             IF         COND(&IN03 *EQ '1') THEN(GOTO ENDCLPGM)
             CHGVAR     VAR(&YMDA)    VALUE(&YMD)
             CHGDTAARA  DTAARA(*LDA (1 6)) VALUE(&YMDA)
             CHGDTAARA  DTAARA(*LDA (7 1)) VALUE(&AREA)
             SBMJOB     CMD(CALL PGM(ARP002P)) JOB(ARP002P) +
                          JOBD(ARJOBD) OUTQ(&OUTQ)
             RETURN
             ENDDO
     /*                                                       */
             RTVDTAARA  DTAARA(*LDA (1 6)) RTNVAR(&YMDA)
             RTVDTAARA  DTAARA(*LDA (7 1)) RTNVAR(&AREA)
             CHGVAR     VAR(&AREAX) VALUE(&AREA)
             IF         COND(&AREA = 'L') THEN(CHGVAR VAR(&AREAX) +
                          VALUE('P'))
             DLTF       FILE(QTEMP/TRNDTLW)
             MONMSG     MSGID(CPF0000)
             CRTPF      FILE(QTEMP/TRNDTLW) SRCFILE(ARLIB/QDDSSRC) +
                          OPTION(*NOSRC *NOLIST) SIZE(*NOMAX)
             OVRDBF     FILE(TRNDTLW) TOFILE(QTEMP/TRNDTLW)
             OVRDBF     FILE(TRNDTL) TOFILE(TRNDTL) SHARE(*YES)
             OPNQRYF    FILE((TRNDTL)) OPTION(*INP) QRYSLT('      +
                        TXCODE *NE "AR06"                          +
                        *AND TXDATE *LE ' || &YMDA || '            +
                        *AND TXTXAR *EQ "'|| &AREA ||'"            +
                        *AND %SST(TXCUNO 1 1) *NE "E"              +
                        *AND TXFLAG *NE "D"                        +
                        *AND TXFL02 *EQ " "                        +
                        *AND TXIVNO *EQ " "')                      +
                      KEYFLD((TXCODE)(TXNO)(TXRESV)(TXPDNM)(TXUPRC)) +
                      MAPFLD((TXRESV '%SST(TXORNO 1 5)'))
             CALL       PGM(ARP002RA)
             CLOF       OPNID(TRNDTL)
             DLTOVR     FILE(*ALL)
         /*                                   */
             OVRDBF     FILE(INVMST) TOFILE(INVMST)
             OVRDBF     FILE(INVDTL) TOFILE(INVDTL)
             OVRDBF     FILE(TRNDTL) TOFILE(TRNDTL)
             OVRDBF     FILE(TRNDTLW) TOFILE(QTEMP/TRNDTLW) SHARE(*YES)
             OPNQRYF    FILE((TRNDTLW)) OPTION(*ALL) QRYSLT('TXHOLD +
                          *NE "Y"') KEYFLD((TXCODE) (TXNO) (TXRESV) +
                          (TXPDNM) (TXUPRC)) MAPFLD((TXRESV +
                          '%SST(TXORNO 1 5)'))
             OVRDBF     FILE(HIPROD) TOFILE(HIPROD)
             OVRDBF     FILE(CBCUST) TOFILE(CBCUST)
             OVRDBF     FILE(SACUST) TOFILE(SACUST)
             OVRDBF     FILE(SAMAST) TOFILE(SAMAST)
             OVRDBF     FILE(BRIV) TOFILE(BRIV)
             OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 160) +
                          CPI(12) OUTQ(&OUTQ)
             CALL       PGM(ARP002R) PARM(&AREAX)
             CLOF       OPNID(TRNDTLW)
             DLTOVR     FILE(*ALL)

             /* ---------------------------------------- */

             CALL       PGM(ARP002RV)    /* 票軌剩十分之一警告 */

             RTVDTAARA  DTAARA(*LDA (1 1)) RTNVAR(&RTNV)
             IF         COND(&RTNV *EQ 'T') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (3 8)) RTNVAR(&IVNO)
             SNDMSG     MSG('請注意！二聯式發票可用票軌數目只剩+
                             十分之一，約' || &IVNO || '張；+
                             必要時請通知財會單位申請。') +
                        TOUSR(&USER)
             ENDDO

             RTVDTAARA  DTAARA(*LDA (2 1)) RTNVAR(&RTNV)
             IF         COND(&RTNV *EQ 'T') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (11 8)) RTNVAR(&IVNO)
             SNDMSG     MSG('請注意！三聯式發票可用票軌數目只剩+
                             十分之一，約' || &IVNO || '張；+
                             必要時請通知財會單位申請。') +
                        TOUSR(&USER)
             ENDDO
ENDCLPGM:    ENDPGM
