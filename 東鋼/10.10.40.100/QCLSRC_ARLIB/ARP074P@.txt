     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP074P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02YSH                                  */
     /*     4.FUNCTION     折讓退回代開傳票                       */
     /*     5.DATE-WRITTEN  89/12/29                                */
     /*                                                             */
     /*  <說明:> 本作業每月進行一次，先刪除該月份之傳票資料     */
     /*            再進行代開整月的動作。（可重複代開）           */
     /*            傳票編號：P601 -- P699                         */
     /*                  P.S.其中P601--P650為代開之傳票         */
     /*                        而P651--P699為異常處理回沖所使用 */
     /*                        (跨月更改確認日期)                 */
     /***************************************************************/
             PGM
             DCLF       FILE(ARP074S)
             DCL        VAR(&P#YYMM) TYPE(*CHAR) LEN(4)
             DCL        VAR(&P#CODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&INT)   TYPE(*CHAR) LEN(1)   /*執行狀態*/
             DCL        VAR(&OUTQ)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&AREA)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&YYMM)  TYPE(*CHAR) LEN(4)
             DCL        VAR(&YY)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&MM)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&FNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C#SDAT) TYPE(*CHAR) LEN(6)
             DCL        VAR(&C#EDAT) TYPE(*CHAR) LEN(6)

             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT) USER(&USER)
             IF         COND(&INT *EQ '0') THEN(GOTO BATCH)

             RTVDTAARA  DTAARA(*LDA (1021 1)) RTNVAR(&AREA)
             RTVDTAARA  DTAARA(*LDA (951 35)) RTNVAR(&S#COMP)
             RTVDTAARA  DTAARA(*LDA (1011 10)) RTNVAR(&S#DEVI)
             CHGVAR     VAR(&S#AREA) VALUE(&AREA)
 DISPLAY:
             SNDRCVF    RCDFMT(AR074F1)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)
             IF         COND(&IN12 *EQ '1') THEN(RETURN)

             IF         COND(&S#AREA *EQ 'P') THEN(DO)
             CHGVAR     VAR(&S#ERR) +
                        VALUE('銷貨差異代開傳票作業，不得在台北進行')
                        GOTO DISPLAY
                        ENDDO

             IF         COND(&S#AREA *EQ 'T') THEN(DO)
             CHGVAR     VAR(&S#ERR) +
                        VALUE('銷貨差異代開傳票作業，不得在桃園進行')
                        GOTO DISPLAY
                        ENDDO

             IF         COND(&S#AREA *EQ 'M') THEN(DO)
             CHGVAR     VAR(&S#ERR) +
                        VALUE('銷貨差異代開傳票作業，不得在台中進行')
                        GOTO DISPLAY
                        ENDDO

             CHGVAR     VAR(&AREA) VALUE(&S#AREA)
             CHGVAR     VAR(&YYMM) VALUE(&S#YYMM)
             CHGVAR     VAR(&P#YYMM) VALUE(&S#YYMM)
             CHGVAR     VAR(&YY) VALUE(%SST(&YYMM 1 2))
             CHGVAR     VAR(&MM) VALUE(%SST(&YYMM 3 2))
             CHGVAR     VAR(&FNAME) +
                        VALUE('AC' || &AREA || 'R' || &YY )
             CHGVAR     VAR(&MNAME) VALUE('VR' || &MM)
             CHKOBJ     OBJ(&FNAME) OBJTYPE(*FILE) MBR(&MNAME)
             MONMSG     CPF0000     EXEC(DO)
             CHGVAR     VAR(&S#ERR) +
                        VALUE('銷貨差異代開傳票作業，無法找到指定+
                          的傳票檔：' || &FNAME || ', ' || &MNAME)
                        GOTO DISPLAY
             ENDDO

   /*        ADDLIBLE   ACLIB        */
   /*        MONMSG     CPF0000      */

   /*        CALL    PGM(ACP101R) PARM(&P#YYMM &P#CODE)        */
   /*        IF         COND(&P#CODE *EQ '1') THEN(DO)         */
   /*        CHGVAR     VAR(&S#ERR) VALUE('關帳日期已過')    */
   /*        GOTO       CMDLBL(DISPLAY)                        */
   /*        ENDDO                                             */

             CHGDTAARA  DTAARA(*LDA (1 4)) VALUE(&YYMM)
             CHGDTAARA  DTAARA(*LDA (7 1)) VALUE(&AREA)
   /*        SBMJOB     JOB(ARP074P) JOBD(ARJOBD) OUTQ(&OUTQ)  +
                        RQSDTA('CALL ARP074P')   */
   /*        RETURN   */

BATCH:
             RTVDTAARA  DTAARA(*LDA (1 4)) RTNVAR(&YYMM)
             RTVDTAARA  DTAARA(*LDA (1 2)) RTNVAR(&YY)
             RTVDTAARA  DTAARA(*LDA (3 2)) RTNVAR(&MM)
             RTVDTAARA  DTAARA(*LDA (7 1)) RTNVAR(&AREA)
             CHGVAR     VAR(&FNAME) +
                        VALUE('AC' || &AREA || 'R' || &YY || 'T')
             CHGVAR     VAR(&MNAME) VALUE('VR' || &MM)


STEP1:       /* 刪除已代開過的資料 */

             OVRDBF     FILE(ACTRAN) TOFILE(S02YSH/&FNAME) MBR(&MNAME) +
                        SHARE(*YES)
             OPNQRYF    FILE(ACTRAN) OPTION(*ALL) +
                        QRYSLT('ACVRNO *GE "P601" *AND +
                                ACVRNO *LE "P650"')

             CALL       PGM(ARP074R1)  /*刪除輸入日期後的資料*/
             CLOF       OPNID(ACTRAN)
             DLTOVR     *ALL


STEP2:       /* 代開開始*/   /*------------------*/

             OVRDBF     FILE(ACTRAN) TOFILE(S02YSH/&FNAME) MBR(&MNAME) +
                        SHARE(*YES)
             OVRPRTF    FILE(ARP074T) TOFILE(ARP074T) PAGESIZE(*N +
                          132) CPI(10) HOLD(*YES) USRDTA('折讓退回')

             OVRDBF     FILE(TRNDTL) TOFILE(DALIB/TRNDTL) SHARE(*YES)
             OPNQRYF    FILE(TRNDTL) OPTION(*ALL) +
                        QRYSLT('+
                              (TXCODE *EQ "AR06") *AND +
                              (ACDT *EQ "999999") *AND +
                              (RESV *EQ "' || &YYMM || '") *AND +
                              (TXIVNO *NE "*") *AND +
                              (TXIVNO *NE "#") *AND +
                              ((TXACNT *EQ "2") *OR +
                               (TXACNT *EQ "3")) *AND +
                              (TXFLAG *NE "D")') +
                        MAPFLD((RESV '%SST(TXRESV 1 4)' *CHAR 4) +
                               (ACDT TXACDT *CHAR 6)) +
                        KEYFLD((TXORNO) (TXNO) (TXPDNM))

             CALL       PGM(ARP074R)  /*傳票代開及列表*/
             CLOF       OPNID(TRNDTL)
             DLTOVR     FILE(*ALL)

             ENDPGM
