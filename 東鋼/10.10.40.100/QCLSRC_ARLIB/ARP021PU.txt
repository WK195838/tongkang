     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP021PU                                */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S00WCJ                                  */
     /*     4.FUNCTION     發票主檔即時上傳　（新桃）             */
     /*     5.DATE-WRITTEN  98/12/30 2009AR406                      */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INT)  TYPE(*CHAR) LEN(1)

             RTVJOBA    USER(&USER) TYPE(&INT)

             IF         COND(&INT *EQ '1') THEN(DO)
             SBMJOB     JOB(ARP021PU) JOBD(ARJOBD) JOBQ(QSYS/QCTL) +
                        RQSDTA('CALL ARLIB/ARP021PU')
             RETURN
             ENDDO

 BATCH:

 STEP1:
             DLTF       FILE(QTEMP/DDMU)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(U01DDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                        TOLIB(QTEMP) NEWOBJ(DDMU)

 STEP2:       /*  台北　抓　新桃  ARLIB/INVMSTT ............ */
             SBMRMTCMD  CMD('CALL PGM(ARLIB/ARP021PA) PARM(''U'')') +
                        DDMFILE(QTEMP/DDMU)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO STEP3)

             CHGDDMF    FILE(QTEMP/DDMU) RMTFILE(*NONSTD +
                        'ARLIB/INVMSTU') LVLCHK(*NO)
             CPYF       FROMFILE(QTEMP/DDMU) TOFILE(QTEMP/INVMSTU) +
                        TOMBR(*FROMMBR) MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO STEP3)

             OVRDBF     FILE(INVMSTP) TOFILE(QTEMP/INVMSTU)
             OVRDBF     FILE(INVMST)  TOFILE(INVMST)
             OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 198) +
                        CPI(15)
             OVRPRTF    FILE(PRINT1) TOFILE(QPRINT) PAGESIZE(*N 198) +
                        CPI(15) USRDTA('桃園重複')
             CHGDTAARA  DTAARA(*LDA (21 1)) VALUE('U')
             CALL       PGM(ARLIB/ARP021RJ)
             DLTOVR     FILE(*ALL)

             CLRPFM     FILE(QTEMP/DDMU)     /* 清除廠區之異動檔 */
             MONMSG     MSGID(CPF0000) EXEC(GOTO STEP3)

             GOTO       ENDCLPGM

 STEP3:     /* 失敗通知 */
             SNDMSG     TOUSR(&USER) MSG(+
                        '桃園廠發票主檔即時傳輸失敗')

 ENDCLPGM:   ENDPGM
