             PGM
             DCL        VAR(&P#YYMM) TYPE(*CHAR) LEN(4)
             DCL        VAR(&P#DATE) TYPE(*CHAR) LEN(6)
             DCL        VAR(&P#CODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&INT)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&OUTQ)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03)  TYPE(*LGL)  LEN(1)
             DCL        VAR(&DATE)  TYPE(*DEC)  LEN(6)
             DCL        VAR(&DATEA) TYPE(*CHAR) LEN(8)
             DCL        VAR(&DATEB) TYPE(*CHAR) LEN(6)
             DCL        VAR(&AREA)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&Y)     TYPE(*CHAR) LEN(2)
             DCL        VAR(&M)     TYPE(*CHAR) LEN(2)
             DCL        VAR(&DTARA) TYPE(*CHAR) LEN(11)
             DCL        VAR(&WSID)  TYPE(*CHAR) LEN(10)
             RTVDTAARA  DTAARA(*LDA (1021 1)) RTNVAR(&AREA)
             CHGVAR     VAR(&DTARA) VALUE('ACCTLDTA' *CAT &AREA)
             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT)
             IF         COND(&INT *EQ '1') THEN(DO)
             RTVDTAARA  DTAARA(&DTARA (7 6)) RTNVAR(&DATEB)
             RTVDTAARA  DTAARA(*LDA (1011 10)) RTNVAR(&WSID)
 DISPLY:     CALL       PGM(ARP012RS) PARM(&IN03 &DATE &AREA &DATEB)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)
             CHGVAR     VAR(&DATEA) VALUE(&DATE)
             CHGDTAARA  DTAARA(*LDA (1 8)) VALUE(&DATEA)
             CHGDTAARA  DTAARA(*LDA (9 1)) VALUE(&AREA)
             RTVDTAARA  DTAARA(*LDA (1 4)) RTNVAR(&P#YYMM)
             RTVDTAARA  DTAARA(*LDA (3 6)) RTNVAR(&P#DATE)

             CALL    PGM(ACP101R) PARM(&P#YYMM  &P#CODE)
             IF         COND(&P#CODE *EQ '1') THEN(DO)
             SNDBRKMSG  MSG('關帳日期已過，請與相關人員聯絡！') +
                          TOMSGQ(&WSID)
             GOTO       CMDLBL(DISPLY)
             ENDDO

             SBMJOB     JOB(ARP012P) JOBD(ARJOBD) OUTQ(&OUTQ) +
                          RQSDTA('CALL ARP012P')
             RETURN
             ENDDO
             RTVDTAARA  DTAARA(*LDA (1 8)) RTNVAR(&DATEA)
             RTVDTAARA  DTAARA(*LDA (3 2)) RTNVAR(&Y)
             RTVDTAARA  DTAARA(*LDA (5 2)) RTNVAR(&M)
             RTVDTAARA  DTAARA(*LDA (9 1)) RTNVAR(&AREA)
             CPYF       FROMFILE(TRNDTL) TOFILE(QTEMP/TXTEMP) +
                          MBROPT(*ADD) CRTFILE(*YES) INCCHAR(*RCD 6 +
                          *EQ &AREA) INCREL((*IF TXVUNO *EQ '       +
                          ') (*AND TXCODE *EQ 'AR05') (*AND TXFLAG +
                          *NE 'D') (*AND TXACDT *EQ &DATEA))
             OVRDBF     FILE(TRNDTLUP) TOFILE(TRNDTL)
             OVRDBF     FILE(TRNDTL) TOFILE(QTEMP/TXTEMP) SHARE(*YES)
             OPNQRYF    FILE((TRNDTL)) KEYFLD((TXFL02) (TXIVNO) +
                          (TXCUNO) (TXUPRC)) MAPFLD((TXIVNO +
                          '%SST(TXORNO 1 6)') (TXFL02 '%SST(TXPCNO +
                          1 1)'))

             OVRDBF     FILE(ACTRAN) TOFILE('AC' || &AREA || +
                          'R' || &Y) MBR('VR' || &M) LVLCHK(*NO)
             OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 156) +
                          CPI(12)
             CALL       PGM(ARP012R)
             CLOF       OPNID(TRNDTL)
             DLTOVR     FILE(*ALL)
             DLTF       FILE(QTEMP/TXTEMP)

             ENDPGM
