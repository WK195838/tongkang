     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP023PK                                */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02CLJ                                  */
     /*     4.FUNCTION     銷貨明細檔即時上傳　（高雄）           */
     /*     5.DATE-WRITTEN  87/02/24                                */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INT)  TYPE(*CHAR) LEN(1)

             RTVJOBA    USER(&USER) TYPE(&INT)
             IF         COND(&INT *EQ '1') THEN(DO)
             SBMJOB     JOB(ARP023PK) JOBD(ARJOBD) JOBQ(QCTL) +
                        RQSDTA('CALL ARLIB/ARP023PK')
             RETURN
             ENDDO

 BATCH:
 STEP1:
             DLTF       FILE(QTEMP/DDMK)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(KSDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                        TOLIB(QTEMP) NEWOBJ(DDMK)

 STEP2:       /*  台北　抓　高雄  ARLIB/TRNDTLK ............ */
             SBMRMTCMD  CMD('CALL PGM(ARLIB/ARP023PA) PARM(''K'')') +
                        DDMFILE(QTEMP/DDMK)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO STEP3)

             CHGDDMF    FILE(QTEMP/DDMK) RMTFILE(*NONSTD +
                        'ARLIB/TRNDTLK') LVLCHK(*NO)
             CPYF       FROMFILE(QTEMP/DDMK) TOFILE(QTEMP/TRNDTLK) +
                        TOMBR(*FROMMBR) MBROPT(*REPLACE) CRTFILE(*YES)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO STEP3)

             OVRDBF     FILE(TRNDTLT) TOFILE(QTEMP/TRNDTLK)
             OVRDBF     FILE(TRNDTL)  TOFILE(TRNDTL)
             OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 198) +
                        CPI(15)
             CHGDTAARA  DTAARA(*LDA (21 1)) VALUE('K')
             CALL       PGM(ARLIB/ARP023RB)
             DLTOVR     FILE(*ALL)

             CLRPFM     FILE(QTEMP/DDMK)   /* 清除廠區的異動資料 */
             MONMSG     MSGID(CPF0000) EXEC(GOTO STEP3)

             GOTO       ENDCLPGM

 STEP3:
             SNDMSG     TOUSR(&USER) MSG(+
                        '高雄廠銷貨明細檔即時傳輸失敗')

 ENDCLPGM:   ENDPGM
