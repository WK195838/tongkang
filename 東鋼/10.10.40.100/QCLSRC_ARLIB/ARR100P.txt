    /*****************************************************************/
    /*                                                               */
    /*        >>  PROGRAM INTRODUCTION  <<                           */
    /*                                                               */
    /*        1.PROGRAM-ID      ARR100P                              */
    /*        2.PROGRAM-TYPE    CLP                                  */
    /*        3.AUTHOR          S02LYW                               */
    /*        4.FUNCTION       應收帳款承購已承購未結案發票明細    */
    /*        5.DATE-WRITTEN    91/10/17                             */
    /*                                                               */
    /*****************************************************************/
        PGM

        DCLF      FILE(ARLIB/ARR100S)
        /*宣告變數*/
        DCL       VAR(&TYPE)         TYPE(*CHAR) LEN(1)
        DCL       VAR(&ORNS)         TYPE(*CHAR) LEN(6)
        DCL       VAR(&ORNE)         TYPE(*CHAR) LEN(6)

        RTVJOBA   TYPE(&TYPE)
        IF        COND(&TYPE *EQ '0')   THEN(GOTO CMDLBL(BATCH))

        RTVDTAARA DTAARA(*LDA  (1001 10)) RTNVAR(&S#USER)
        RTVDTAARA DTAARA(*LDA  (1011 10)) RTNVAR(&S#DEVN)

   START: SNDRCVF

        CHGVAR    VAR(&S#ERR)   VALUE(' ')
        CHGVAR    VAR(&IN51)    VALUE('0')
        CHGDTAARA DTAARA(*LDA  (301   6)) VALUE(&S#CUNO)
        CHGDTAARA DTAARA(*LDA  (307   6)) VALUE(&S#ORNS)
        CHGDTAARA DTAARA(*LDA  (313   6)) VALUE(&S#ORNE)

        IF        COND((&IN03   *EQ '1') *OR +
                       (&IN12   *EQ '1'))    +
                  THEN(RETURN)

        IF        COND(&S#CUNO *EQ '     ') THEN(DO)
                  CHGVAR   VAR(&IN51)  VALUE('1')
                  CHGVAR   VAR(&S#ERR) VALUE(+
                  '客戶編號不得空白！')
                  GOTO CMDLBL(START)
        ENDDO

        SBMJOB    CMD(CALL PGM(ARR100P))  JOB(ARR100P) +
                  JOBD(ARJOBD)
        SNDBRKMSG  +
                  MSG('<<應收帳款承購已承購未結案發票明細表+
                          開始處理，請稍候......>>') +
                  TOMSGQ(&S#DEVN) MSGTYPE(*INFO)
        RETURN    /*INTERACTIVE JOB */

   BATCH:
        RTVDTAARA DTAARA(*LDA (301  6)) RTNVAR(&S#CUNO)
        RTVDTAARA DTAARA(*LDA (307  6)) RTNVAR(&S#ORNS)
        RTVDTAARA DTAARA(*LDA (313  6)) RTNVAR(&S#ORNE)

        IF        COND((&S#ORNS *EQ '      ')) THEN(DO)
        CHGVAR    VAR(&ORNS)    VALUE('      ')
        CHGVAR    VAR(&ORNE)    VALUE('999999')
        ENDDO
        ELSE (DO)
        CHGVAR    VAR(&ORNS)    VALUE(&S#ORNS)
        CHGVAR    VAR(&ORNE)    VALUE(&S#ORNE)
        ENDDO

        OVRDBF    FILE(ARINVM)  TOFILE(DALIB/ARINVM)  SHARE(*YES)
        OVRPRTF   FILE(ARR100T) TOFILE(ARR100T) PAGESIZE(*N 198)   +
                  CPI(15) HOLD(*YES) USRDTA('承購明細')
        OPNQRYF   FILE(ARINVM)  QRYSLT(' +
                  (AMCLOC *EQ " ") *AND +
                  (AMCUNO *EQ "' || &S#CUNO ||'") *AND +
                  (AMORNO *GE "' || &ORNS || '") *AND +
                  (AMORNO *LE "' || &ORNE || '")')    +
                  KEYFLD((AMORNO) (AMINNO))
        CALL      PGM(ARR100R)
        CLOF      OPNID(ARINVM)
        DLTOVR    FILE(*ALL)

        RTVDTAARA DTAARA(*LDA (1011 10))  RTNVAR(&S#DEVN)
        SNDBRKMSG  +
                  MSG('<<應收帳款承購已承購未結案發票明細表+
                          處理完畢，請列印...>>') TOMSGQ(&S#DEVN) +
                  MSGTYPE(*INFO)

 ENDCLPGM:
        ENDPGM
