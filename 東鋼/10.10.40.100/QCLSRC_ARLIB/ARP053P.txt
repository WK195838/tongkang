             PGM
             DCL        VAR(&INT)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&WHOAMI) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DATEX) TYPE(*CHAR) LEN(6)   /*輸入日期*/
             DCL        VAR(&DATEE) TYPE(*CHAR) LEN(6)
             DCL        VAR(&ENDD)  TYPE(*CHAR) LEN(2)
             DCL        VAR(&AREA)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&DTARA) TYPE(*CHAR) LEN(11)
             DCL        VAR(&Y)     TYPE(*CHAR) LEN(2)
             DCL        VAR(&M)     TYPE(*CHAR) LEN(2)
             DCL        VAR(&IN03)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&CNTT)  TYPE(*CHAR) LEN(6) /* 改明細數 */
             DCL        VAR(&CNTA)  TYPE(*CHAR) LEN(6) /* 刪傳票數 */

             RTVDTAARA  DTAARA(*LDA (1021 1)) RTNVAR(&AREA)
             CHGVAR     VAR(&DTARA) VALUE('ACCTLDTA2' *CAT &AREA)

             RTVJOBA    TYPE(&INT) USER(&WHOAMI)
             IF         COND(&INT *EQ '0')  THEN(GOTO BATCH)

             RTVDTAARA  DTAARA(DALIB/&DTARA (1 6)) RTNVAR(&DATEX)

                        /* 傳進已代開日期回還原起日 止日     */
             CALL       PGM(ARP053RS) PARM(&IN03 &DATEX &ENDD)

             IF         COND(&IN03 *EQ '0') THEN(DO)
             CHGVAR     VAR(&DATEE)  VALUE(&DATEX)
             CHGVAR     VAR(%SST(&DATEE 5 2))  VALUE(&ENDD)
             CHGDTAARA  DTAARA(*LDA (1 6)) VALUE(&DATEX)
             CHGDTAARA  DTAARA(*LDA (7 6)) VALUE(&DATEE)
             SBMJOB     JOB(ARP053P) JOBD(ARJOBD)  +
                        RQSDTA('CALL ARP053P')
             ENDDO
             RETURN

 BATCH:
             RTVDTAARA  DTAARA(*LDA (1 6)) RTNVAR(&DATEX)
             RTVDTAARA  DTAARA(*LDA (7 6)) RTNVAR(&DATEE)
             CHGVAR     VAR(&Y)   VALUE(%SST(&DATEX 1 2))
             CHGVAR     VAR(&M)   VALUE(%SST(&DATEX 3 2))

             CRTDUPOBJ  OBJ(INVDTL) FROMLIB(DALIB) OBJTYPE(*FILE) +
                        TOLIB(QTEMP) NEWOBJ(INVDTLW) DATA(*NO)

             OVRDBF     FILE(INVDTLW) TOFILE(QTEMP/INVDTLW)
             OVRDBF     FILE(INVDTL) TOFILE(DALIB/INVDTL) SHARE(*YES)

             OPNQRYF    FILE((INVDTL)) +
                        QRYSLT('IVPDCD *EQ "A10" *AND +
                                IVDECD *NE "D" *AND +
                                IVACNO *NE " " *AND +
                               (IVACDT *LE ' || &DATEE || ' *AND +
                                IVACDT *GE ' || &DATEX || ')') +
                        KEYFLD((IVACDT) (IVNO))

             CALL       PGM(ARP043R1)  /*複製要處理的資料至INVDTLW */
             CLOF       OPNID(INVDTL)

             OVRDBF     FILE(ACTRAN) TOFILE('AC' || &AREA || +
                          'R' || &Y) MBR('VR' || &M) LVLCHK(*NO)
             CPYF       FROMFILE(ACTRAN) TOFILE(QTEMP/ACTRANW) +
                        MBROPT(*ADD) CRTFILE(*YES) +
                        INCREL((*IF ACVRNO *GE 'O000' ) +
                               (*AND ACVRNO *LE 'O999' ) +
                               (*AND ACYMD *LE &DATEE) +
                               (*AND ACYMD *GE &DATEX) )
             OVRDBF     FILE(ACTRANW) TOFILE(QTEMP/ACTRANW) LVLCHK(*NO)

                        /* 還原發票明細並刪除已代開傳票 */
             CALL       PGM(ARP053R) PARM(&CNTT &CNTA)

             DLTOVR     FILE(*ALL)
             CHGDTAARA  DTAARA(DALIB/&DTARA (1 6)) VALUE(&DATEX)
             SNDMSG     MSG('勞務代開傳票還原作業已完成，合計改+
                             發票明細' || &CNTT || '筆，刪傳票' +
                              || &CNTA || '筆。' || &DATEX) +
                        TOUSR(&WHOAMI)
             ENDPGM
