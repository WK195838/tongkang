        /*********************************************************************/
        /*                                                                   */
        /*        >>   PROGRAM INTRODUCTION  <<                              */
        /*        1.PROGRAM-ID     ARR107P                                   */
        /*        2.PROGRAM-TYPE   CLP                                       */
        /*        3.AUTHOR         S02CSF                                    */
        /*        4.FUNCTION      應收帳款承購(E-FACTOTING)託收票據明細表*/
        /*        5.DATE-WRITTEN   92/02/16                                  */
        /*        6.DATE-MODIFY                                              */
        /*                                                                   */
        /*********************************************************************/
            PGM
            DCLF       FILE(ARR107S)
            DCL        VAR(&TYPE)   TYPE(*CHAR) LEN(1)
            DCL        VAR(&CUN1)   TYPE(*CHAR) LEN(5)  /*客戶代號*/
            DCL        VAR(&CUN2)   TYPE(*CHAR) LEN(5)  /*客戶代號*/
            DCL        VAR(&BKI1)   TYPE(*CHAR) LEN(3)  /*銀行代號*/
            DCL        VAR(&BKI2)   TYPE(*CHAR) LEN(3)  /*銀行代號*/

            RTVJOBA    JOB(&S#DEVN) USER(&S#USER) +
                       TYPE(&TYPE)

            IF         COND(&TYPE *EQ '0') THEN(GOTO CMDLBL(BATCH))
            CHGVAR     VAR(&S#CODE)    VALUE('N')

 START:     SNDRCVF

            CHGVAR     VAR(&IN30)      VALUE('0')
            CHGVAR     VAR(&S#ERR)     VALUE(*BLANK)

            IF         COND(&IN03 *EQ '1') THEN(RETURN)
            IF         COND(&IN12 *EQ '1') THEN(RETURN)

  /*  CHECK 列印時是否包含已兌現票據*/
            IF         COND(&S#CODE *EQ ' ' ) THEN(DO)
            CHGVAR     VAR(&S#ERR)     VALUE('請輸入列印時是否包含已兌現票據')
            CHGVAR     VAR(&IN30)      VALUE('1')
            GOTO       CMDLBL(START)
            ENDDO

  /*  CHECK 銀行代號*/
            IF         COND(&S#BKID *EQ ' ' ) THEN(DO)
            CHGVAR     VAR(&BKI1)      VALUE('   ')
            CHGVAR     VAR(&BKI2)      VALUE('999')
            ENDDO
            ELSE (DO)
            CHGVAR     VAR(&BKI1)      VALUE(&S#BKID)
            CHGVAR     VAR(&BKI2)      VALUE(&S#BKID)
            ENDDO

  /*  CHECK 客戶代號*/
            IF         COND(&S#CUNO *EQ ' ' ) THEN(DO)
            CHGVAR     VAR(&CUN1)      VALUE('     ')
            CHGVAR     VAR(&CUN2)      VALUE('99999')
            ENDDO
            ELSE (DO)
            CHGVAR     VAR(&CUN1)      VALUE(&S#CUNO)
            CHGVAR     VAR(&CUN2)      VALUE(&S#CUNO)
            ENDDO

            CHGDTAARA   DTAARA(*LDA  (300  3))  VALUE(&BKI1)
            CHGDTAARA   DTAARA(*LDA  (303  3))  VALUE(&BKI2)
            CHGDTAARA   DTAARA(*LDA  (306  5))  VALUE(&CUN1)
            CHGDTAARA   DTAARA(*LDA  (311  5))  VALUE(&CUN2)
            CHGDTAARA   DTAARA(*LDA  (316  1))  VALUE(&S#CODE)
            CHGDTAARA   DTAARA(*LDA  (317  3))  VALUE(&S#BKID)
            CHGDTAARA   DTAARA(*LDA  (320  5))  VALUE(&S#CUNO)

            SBMJOB     CMD(CALL PGM(ARR107P)) JOB(ARR107P) +
                          JOBD(ARJOBD)
    /*      SNDBRKMSG  MSG('<<應收帳款承購(E-FACTOTING)託收票據明細表+
                             ,已開始處理，請稍待　....>>') +
                       TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)      */
            RETURN
 BATCH:
          RTVDTAARA   DTAARA(*LDA (300 3))  RTNVAR(&BKI1)
          RTVDTAARA   DTAARA(*LDA (303 3))  RTNVAR(&BKI2)
          RTVDTAARA   DTAARA(*LDA (306 5))  RTNVAR(&CUN1)
          RTVDTAARA   DTAARA(*LDA (311 5))  RTNVAR(&CUN2)
          RTVDTAARA   DTAARA(*LDA (316 1))  RTNVAR(&S#CODE)
          RTVDTAARA   DTAARA(*LDA (317 1))  RTNVAR(&S#BKID)
          RTVDTAARA   DTAARA(*LDA (320 5))  RTNVAR(&S#CUNO)

 /*   開始處理   */
         /* STEP1:先前處理*/

          CLRPFM     ARR107F
          OVRDBF     FILE(ARCUDT) TOFILE(ARCUDT) SHARE(*YES)
          OPNQRYF    FILE(ARCUDT) OPTION(*ALL) QRYSLT(' +
                         (ACBKID  *GE  "' || &BKI1 || '") *AND +
                         (ACBKID  *LE  "' || &BKI2 || '") *AND +
                         (CUNO    *GE  "' || &CUN1 || '") *AND +
                         (CUNO    *LE  "' || &CUN2 || '")') +
                     KEYFLD((ACBKID) (ACCUNO))  +
                     MAPFLD((CUNO  '%SST(ACCUNO 1 5)' *CHAR 5))
          CALL       ARR107R1
          CLOF       OPNID(ARCUDT)
          DLTOVR     FILE(*ALL)

         /* STEP1:出  表*/

          OVRPRTF    FILE(ARR107T) TOFILE(ARLIB/ARR107T) +
                     PAGESIZE(*N 198) CPI(15) HOLD(*YES) +
                     USRDTA('票據明細')
          OVRDBF     FILE(ARR107F) TOFILE(ARR107F) SHARE(*YES)

          IF         COND(&S#CODE *EQ 'N') THEN( +
          OPNQRYF    FILE(ARR107F) OPTION(*ALL) QRYSLT(' +
                         (AFNTCD    *EQ " ")') +
                     KEYFLD((AFBKID) (AFDUDT) (AFCUNO)))
          ELSE       CMD( +
          OPNQRYF    FILE(ARR107F) +
                     KEYFLD((AFBKID) (AFDUDT) (AFCUNO)))
          CALL       ARR107R
          CLOF       OPNID(ARR107F)
          DLTOVR     FILE(*ALL)
      /*  SNDBRKMSG  MSG('<< 應收帳款承購(E-FACTOTING)託收票據明細表+
                            已處理完成，請列印......>>') +
                     TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)   */
         ENDPGM
