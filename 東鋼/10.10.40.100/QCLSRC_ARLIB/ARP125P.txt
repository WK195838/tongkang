     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP125P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S00WCJ                                  */
     /*     4.FUNCTION     折讓單沖銷檔傳輸                       */
     /*     5.DATE-WRITTEN  103/07/18 2014AR845                     */
     /*     6.UPDATE  DATE                                          */
     /*                                                             */
     /*                                                             */
     /***************************************************************/

             PGM
             DCL        VAR(&INT)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&ARA)  TYPE(*CHAR) LEN(4)
             DCL        VAR(&ARA1) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CNT1) TYPE(*DEC) LEN(1 0)


             RTVJOBA    TYPE(&INT)
             IF         COND(&INT *EQ '0') THEN(GOTO CMDLBL(BATCH))

 START:
             SBMJOB     JOB(ARP125P) JOBD(ARJOBD) JOBQ(QCTL) +
                          RQSDTA('CALL ARLIB/ARP125P')
             SNDPGMMSG  +
                          MSG('折讓單沖銷檔傳輸作業執行中..+
                          .') TOPGMQ(*EXT)
             GOTO       CMDLBL(ENDCLPGM)

 BATCH:
        /*  建立　DDMF & DTAARA ............................ */

             DLTF       FILE(QTEMP/DDMK)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(KSDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(DDMK)

             DLTF       FILE(QTEMP/DDMH)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(MLDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(DDMH)

             DLTDTAARA  DTAARA(QTEMP/ARA)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/ARA) TYPE(*CHAR) LEN(4) +
                          VALUE('0  0')


 STEP2: /*  台北　抓　苗栗  ARLIB/ARSRAE   ................ */

             RTVDTAARA  DTAARA(QTEMP/ARA (2 1)) RTNVAR(&ARA1)
             IF         COND(&ARA1 *EQ '0') THEN(GOTO CMDLBL(STEP3))
             SBMRMTCMD  CMD('CALL ARLIB/ARP125PA') +
                        DDMFILE(QTEMP/DDMH)
             MONMSG     MSGID(CPF0000)  EXEC(DO)
             CHGDTAARA  DTAARA(QTEMP/ARA (2 1)) VALUE('1')
             GOTO       CMDLBL(STEP3)
                        ENDDO

             CHGDDMF    FILE(QTEMP/DDMH) RMTFILE(*NONSTD +
                          'ARLIB/ARSRAEW') LVLCHK(*NO)
             CPYF       FROMFILE(QTEMP/DDMH) TOFILE(ARLIB/ARSRAEW) +
                          MBROPT(*REPLACE) FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO CMDLBL(STEP3))

             OVRDBF     FILE(ARSRAEW) TOFILE(ARLIB/ARSRAEW)
             OVRDBF     FILE(ARSRAE)  TOFILE(DALIB/ARSRAE)
             CALL       PGM(ARLIB/ARP125R)
             MONMSG     MSGID(CPF0000)  EXEC(DO)
             CHGDTAARA  DTAARA(QTEMP/ARA (2 1)) VALUE('1')
             GOTO       CMDLBL(STEP3)
                        ENDDO
             DLTOVR     FILE(*ALL)

             CHGDTAARA  DTAARA(QTEMP/ARA (2 1)) VALUE('0')
             SBMRMTCMD  CMD('CLRPFM ARLIB/ARSRAEW') +
                        DDMFILE(QTEMP/DDMH)

 STEP3: /*  台北　抓　高雄  ARLIB/ARDSLG   ................ */

             RTVDTAARA  DTAARA(QTEMP/ARA (3 1)) RTNVAR(&ARA1)
             IF         COND(&ARA1 *EQ '0') THEN(GOTO CMDLBL(LOOP))
             SBMRMTCMD  CMD('CALL ARLIB/ARP125PA') +
                        DDMFILE(QTEMP/DDMK)
             MONMSG     MSGID(CPF0000)  EXEC(DO)
             CHGDTAARA  DTAARA(QTEMP/ARA (3 1)) VALUE('1')
             GOTO       CMDLBL(LOOP)
                        ENDDO

             CHGDDMF    FILE(QTEMP/DDMK) RMTFILE(*NONSTD +
                          'ARLIB/ARSRAEW') LVLCHK(*NO)
             CPYF       FROMFILE(QTEMP/DDMK) TOFILE(ARLIB/ARSRAEW) +
                          MBROPT(*REPLACE) FMTOPT(*MAP *DROP)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO CMDLBL(LOOP))

             OVRDBF     FILE(ARSRAEW) TOFILE(ARLIB/ARSRAEW)
             OVRDBF     FILE(ARSRAE)  TOFILE(DALIB/ARSRAE)
             CALL       PGM(ARLIB/ARP125R)
             MONMSG     MSGID(CPF0000)  EXEC(DO)
             CHGDTAARA  DTAARA(QTEMP/ARA (3 1)) VALUE('1')
             GOTO       CMDLBL(LOOP)
                        ENDDO
             DLTOVR     FILE(*ALL)

             CHGDTAARA  DTAARA(QTEMP/ARA (3 1)) VALUE('0')
             SBMRMTCMD  CMD('CLRPFM ARLIB/ARSRAEW') +
                        DDMFILE(QTEMP/DDMK)


 LOOP:  /*   CLEAR .... TEMP-FILE  TAIPEI .......... */
             RTVDTAARA  DTAARA(QTEMP/ARA (1 4)) RTNVAR(&ARA)
             IF         COND(&ARA *NE '0000') THEN(CHGVAR VAR(&CNT1) +
                          VALUE(&CNT1 + 1))
             IF         COND(&CNT1 *GT 2) THEN(GOTO CMDLBL(ENDCLPGM))
             ELSE       DO
                        IF         COND(&ARA *NE '0000') THEN(DO)
                                   DLYJOB   DLY(60)
                                   GOTO     CMDLBL(STEP2)
                                   ENDDO
                        ENDDO
 ENDCLPGM:
          ENDPGM
