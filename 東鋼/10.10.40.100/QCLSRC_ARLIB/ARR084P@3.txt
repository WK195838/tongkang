    /****************************************************************/
    /*                                                              */
    /*             UPDATE  DATE  99/08/17  2010AR517 S00WCJ (9908A) */
    /*                           99/12/06  2010ARXXX S00WCJ (9912A) */
    /*                           增加U廠選項                    */
    /*                                                              */
    /****************************************************************/

             PGM
             DCLF       FILE(ARLIB/ARR084S)

             DCL        VAR(&DSPID)   TYPE(*CHAR)  LEN(10)
             DCL        VAR(&OUTQ)    TYPE(*CHAR)  LEN(10)
             DCL        VAR(&INT)     TYPE(*CHAR)  LEN(1)
    /*9908A  DCL        VAR(&QDATE)   TYPE(*CHAR)  LEN(6)  */
    /*9908A*/DCL        VAR(&QDATE)   TYPE(*CHAR)  LEN(8)
             DCL        VAR(&QDATE8)  TYPE(*CHAR)  LEN(8)
             DCL        VAR(&C#DATE)  TYPE(*CHAR)  LEN(6)
             DCL        VAR(&D#DATE)  TYPE(*DEC)   LEN(8 0)
             DCL        VAR(&W#DAY)   TYPE(*DEC)   LEN(8 0)
             DCL        VAR(&W#LDAY)  TYPE(*CHAR)  LEN(8)
             DCL        VAR(&W#CDAY)  TYPE(*CHAR)  LEN(8)
             DCL        VAR(&W#ERR)   TYPE(*CHAR)  LEN(1)
             DCL        VAR(&W#MOD)   TYPE(*CHAR)  LEN(1)
             DCL        VAR(&W#CNT)   TYPE(*CHAR)  LEN(4)
             DCL        VAR(&W#FUN)   TYPE(*CHAR)  LEN(1)
             DCL        VAR(&DATES)   TYPE(*CHAR)  LEN(8)
             DCL        VAR(&DATEE)   TYPE(*CHAR)  LEN(8)
             DCL        VAR(&AREAS)   TYPE(*CHAR)  LEN(1)
             DCL        VAR(&AREAE)   TYPE(*CHAR)  LEN(1)

             RTVJOBA    JOB(&DSPID) OUTQ(&OUTQ) TYPE(&INT)
             IF         COND(&INT *EQ '0') THEN(GOTO BATCH)
       /*           */
             RTVDTAARA  DTAARA(*LDA ( 951 35)) RTNVAR(&S#COMP)
             RTVDTAARA  DTAARA(*LDA (1011 10)) RTNVAR(&S#DEVI)
             RTVDTAARA  DTAARA(*LDA (1021  1)) RTNVAR(&S#AREA)
       /*  取得發票年月之預設值   */
    /*9908A  START */
        /*   RTVSYSVAL  SYSVAL(QDATE) RTNVAR(&QDATE)  */
        /*   CHGVAR     VAR(&C#DATE)  VALUE('  ' || (%SST(&QDATE 1 4))) */

             RTVJOBA    CYMDDATE(&QDATE)
             CHGVAR     VAR(&QDATE)   VALUE('0' *CAT &QDATE)
             CHGVAR     VAR(&C#DATE)  VALUE(%SST(&QDATE 1 6))
    /*9908A  END */
             CHGVAR     VAR(&QDATE8)  VALUE(&C#DATE || '01')
             CHGVAR     VAR(&D#DATE)  VALUE(&QDATE8)
             CHGVAR     VAR(&W#DAY)   VALUE(&D#DATE)
             CHGVAR     VAR(&W#LDAY)  VALUE(&W#DAY)
             CHGVAR     VAR(&W#MOD)   VALUE('1') /*中國年*/
             CHGVAR     VAR(&W#CNT)   VALUE('0001') /*天數*/
             CHGVAR     VAR(&W#FUN)   VALUE('1') /* 1:往前, 2:往後 */
             CALL       PGM(UTS104R) PARM(&W#LDAY  &W#MOD  &W#FUN &W#CNT +
                                          &W#CDAY &W#ERR)
             CHGVAR     VAR(&D#DATE)  VALUE(&W#CDAY)
             CHGVAR     VAR(&QDATE8)  VALUE(&D#DATE)
             CHGVAR     VAR(&C#DATE)  VALUE(%SST(&QDATE8 1 6))
             CHGVAR     VAR(&S#YYMM)  VALUE(&C#DATE)

  START:
             SNDRCVF
             IF         COND(&IN03 *EQ '1')   THEN(RETURN)
             IF         COND(&IN12 *EQ '1')   THEN(RETURN)

             CHGVAR     VAR(&IN30)    VALUE('0')
             CHGVAR     VAR(&IN31)    VALUE('0')
             CHGVAR     VAR(&S#ERR)   VALUE(' ')

       /* 檢核發票年月 */
             IF         COND(&S#YYMM *EQ 0)   THEN(DO)
             CHGVAR     (&IN30)       VALUE('1')
             CHGVAR     (&S#ERR)      VALUE('發票年月不得為0 !')
             GOTO       CMDLBL(START)
             ENDDO

             CHGVAR     VAR(&C#DATE)  VALUE(&S#YYMM)
             CHGVAR     VAR(&QDATE8)  VALUE(&C#DATE || '01')
             CHGVAR     VAR(&D#DATE)  VALUE(&QDATE8)
             CHGVAR     VAR(&W#DAY)   VALUE(&D#DATE)
             CHGVAR     VAR(&W#LDAY)  VALUE(&W#DAY)
             CHGVAR     VAR(&W#MOD)   VALUE('1') /*中國年*/
             CHGVAR     VAR(&W#CNT)   VALUE('0000') /*天數*/
             CHGVAR     VAR(&W#FUN)   VALUE('1') /* 1:往前, 2:往後 */
             CALL       PGM(UTS104R) PARM(&W#LDAY  &W#MOD  &W#FUN &W#CNT +
                                          &W#CDAY &W#ERR)
             IF         COND(&W#ERR *EQ '1')   THEN(DO)
             CHGVAR     (&IN30)       VALUE('1')
             CHGVAR     (&S#ERR)      VALUE('發票年月輸入錯誤!')
             GOTO       CMDLBL(START)
             ENDDO

       /* 檢核開立廠區 */
             IF         COND((&S#AREA *NE ' ')  *AND +
                             (&S#AREA *NE 'P')  *AND +
                             (&S#AREA *NE 'T')  *AND +
                             (&S#AREA *NE 'H')  *AND +
                             (&S#AREA *NE 'M')  *AND +
                             (&S#AREA *NE 'U')  *AND +
                             (&S#AREA *NE 'K')) THEN(DO)
             CHGVAR     (&IN31)       VALUE('1')
             CHGVAR     (&S#ERR)      VALUE('開立廠區輸入錯誤!')
             GOTO       CMDLBL(START)
             ENDDO

       /* 紀錄輸入值 */
             CHGVAR     VAR(&C#DATE)  VALUE(&S#YYMM)
             CHGVAR     VAR(&DATES)   VALUE(&C#DATE || '01')
             CHGDTAARA  DTAARA(*LDA (500 8))  VALUE(&DATES)  /*起始*/
             CHGVAR     VAR(&DATEE)   VALUE(&C#DATE || '31')
             CHGDTAARA  DTAARA(*LDA (508 8))  VALUE(&DATEE)  /*終止*/
             CHGDTAARA  DTAARA(*LDA (516 1))  VALUE(&S#AREA)
             CHGDTAARA  DTAARA(*LDA (517 10)) VALUE(&DSPID)

             SBMJOB     CMD(CALL PGM(ARR084P)) JOB(ARR084P) +
                        JOBD(ARJOBD)
             SNDBRKMSG  MSG('<<前期出貨本期開立發票明細表開始處理+
                             ，請稍待...>>') +
                          TOMSGQ(&DSPID) MSGTYPE(*INFO)
             RETURN     /* INTERACTIVE JOB */
 BATCH:
             RTVDTAARA  DTAARA(*LDA (500 8))  RTNVAR(&DATES)
             RTVDTAARA  DTAARA(*LDA (508 8))  RTNVAR(&DATEE)
             RTVDTAARA  DTAARA(*LDA (516 1))  RTNVAR(&S#AREA)
             RTVDTAARA  DTAARA(*LDA (517 10)) RTNVAR(&DSPID)

             IF         COND(&S#AREA *EQ ' ') THEN(DO)
             CHGVAR     VAR(&AREAS)   VALUE(' ')
             CHGVAR     VAR(&AREAE)   VALUE('9')
             ENDDO
             ELSE (DO)
             CHGVAR     VAR(&AREAS)   VALUE(&S#AREA)
             CHGVAR     VAR(&AREAE)   VALUE(&S#AREA)
             ENDDO


        /* 處理開始 */
           OVRDBF     FILE(TRNDTLL2) TOFILE(DALIB/TRNDTLL2) SHARE(*YES)
        /* OVRDBF     FILE(TRNDTLL2) TOFILE(DALIB/TRNDTLYY) SHARE(*YES) */
           OVRDBF     FILE(TRNDTL)   TOFILE(DALIB/TRNDTL)
           OVRDBF     FILE(INVMST)   TOFILE(DALIB/INVMST) SHARE(*YES)
             OVRPRTF    FILE(ARR084T)  TOFILE(ARR084T) +
                        USRDTA('前出本開') HOLD(*YES) +
                        PAGESIZE(*N 198) CPI(15)
             OPNQRYF    FILE(TRNDTLL2)  KEYFLD((TXIVNO) (TXNO) (TXITEM))
             OPNQRYF    FILE(INVMST) QRYSLT(' +
                        ((INTYPE *EQ "1") *OR +
                         (INTYPE *EQ "3")) *AND +
                        (INDECD *NE "D") *AND +
                        (DATE1  *GE "' || &DATES || '") *AND +
                        (DATE1  *LE "' || &DATEE || '") *AND +
                        (INAREA *GE "' || &AREAS || '") *AND +
                        (INAREA *LE "' || &AREAE || '")') +
                        KEYFLD((INAREA) (INCUNO) (INORNO) (INNO)) +
                        MAPFLD((DATE1 ININDT *CHAR 8))
             CALL       PGM(ARR084R)
             CLOF       OPNID(INVMST)
             CLOF       OPNID(TRNDTLL2)
             DLTOVR     FILE(*ALL)

             SNDBRKMSG  MSG('<<前期出貨本期開立發票明細表處理完畢+
                             ，請列印...>>') +
                          TOMSGQ(&DSPID) MSGTYPE(*INFO)
             ENDPGM
