        /*  ＝＝＝＜台北　發票明細檔INVDTL傳輸程式＞＝＝＝   */
             PGM
             DCL        VAR(&INT)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&ARA)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&ARA1) TYPE(*CHAR) LEN(1)
             DCL        VAR(&ARA2) TYPE(*CHAR) LEN(1)
             DCL        VAR(&ARA3) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CNT1) TYPE(*DEC) LEN(1 0)
             DCL        VAR(&CNT2) TYPE(*DEC) LEN(1 0)
             RTVJOBA    TYPE(&INT)
             IF         COND(&INT *EQ '0') THEN(GOTO CMDLBL(BATCH))
 START:
             SBMJOB     JOB(ARP022P) JOBD(ARJOBD) JOBQ(QCTL) +
                          RQSDTA('CALL ARLIB/ARP022P')
         /*  SNDPGMMSG  +
                          MSG('發票明細檔傳輸已送入批次作業執行中..+
                          .') TOPGMQ(*EXT)      */
             GOTO       CMDLBL(ENDCLPGM)
 BATCH:
             CHGVAR     VAR(&CNT1) VALUE(0)
             CHGVAR     VAR(&CNT2) VALUE(0)


        /*  台北  DALIB/INVDTL--> ARLIB/INVDTLP  (TEMP-FILE) */
  PASS:      CALL       PGM(ARLIB/ARP022PA) PARM('P')
             MONMSG     MSGID(CPF0000)
        /*  建立　DDMF & DTAARA ............................ */
             DLTF       FILE(QTEMP/DDMT)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(TYDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(DDMT)
             DLTF       FILE(QTEMP/DDMK)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(KSDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(DDMK)
             DLTF       FILE(QTEMP/DDMH)
             MONMSG     MSGID(CPF0000)
             CRTDUPOBJ  OBJ(MLDDM) FROMLIB(QGPL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(DDMH)
             DLTDTAARA  DTAARA(QTEMP/ARA)
             MONMSG     MSGID(CPF0000)
             CRTDTAARA  DTAARA(QTEMP/ARA) TYPE(*CHAR) LEN(6) +
                          VALUE('      ')
 STEP1: /*  台北　抓　桃園  ARLIB/INVDTLT   ................ */
             RTVDTAARA  DTAARA(QTEMP/ARA (1 1)) RTNVAR(&ARA1)
             IF         COND(&ARA1 *EQ '0') THEN(GOTO CMDLBL(STEP2))
             SBMRMTCMD  CMD('CALL PGM(ARLIB/ARP022PA) PARM(''T'')') +
                        DDMFILE(QTEMP/DDMT)
             MONMSG     MSGID(CPF0000)  EXEC(DO)
             CHGDTAARA  DTAARA(QTEMP/ARA (1 1)) VALUE('1')
             GOTO       CMDLBL(STEP2)
                        ENDDO
             CHGDDMF    FILE(QTEMP/DDMT) RMTFILE(*NONSTD +
                          'ARLIB/INVDTLT') LVLCHK(*NO)
             OVRDBF     FILE(INVDTLT) TOFILE(QTEMP/DDMT)
             OVRDBF     FILE(INVDTLP) TOFILE(ARLIB/INVDTLP)
             OVRDBF     FILE(INVDTL)  TOFILE(INVDTL)
             OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 198) +
                          CPI(15)
             CHGDTAARA  DTAARA(*LDA (21 1)) VALUE('T')
             CALL       PGM(ARLIB/ARP022RB)
             MONMSG     MSGID(CPF0000)  EXEC(DO)
             CHGDTAARA  DTAARA(QTEMP/ARA (1 1)) VALUE('1')
             GOTO       CMDLBL(STEP2)
                        ENDDO
             DLTOVR     FILE(*ALL)
             CHGDTAARA  DTAARA(QTEMP/ARA (1 1)) VALUE('0')
             CLRPFM     FILE(QTEMP/DDMT)
             MONMSG     MSGID(CPF0000)
 STEP2:       /*  台北　抓　高雄  ARLIB/INVDTLK ............ */
             RTVDTAARA  DTAARA(QTEMP/ARA (2 1)) RTNVAR(&ARA2)
             IF         COND(&ARA2 *EQ '0') THEN(GOTO CMDLBL(STEP21))
             SBMRMTCMD  CMD('CALL PGM(ARLIB/ARP022PA) PARM(''K'')') +
                        DDMFILE(QTEMP/DDMK)
             MONMSG     MSGID(CPF0000)  EXEC(DO)
             CHGDTAARA  DTAARA(QTEMP/ARA (2 1)) VALUE('1')
             GOTO       CMDLBL(STEP21)
                        ENDDO
             CHGDDMF    FILE(QTEMP/DDMK) RMTFILE(*NONSTD +
                          'ARLIB/INVDTLK') LVLCHK(*NO)
             OVRDBF     FILE(INVDTLT) TOFILE(QTEMP/DDMK)
             OVRDBF     FILE(INVDTLP) TOFILE(ARLIB/INVDTLP)
             OVRDBF     FILE(INVDTL)  TOFILE(INVDTL)
             OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 198) +
                          CPI(15)
             CHGDTAARA  DTAARA(*LDA (21 1)) VALUE('K')
             CALL       PGM(ARLIB/ARP022RB)
             MONMSG     MSGID(CPF0000)  EXEC(DO)
             CHGDTAARA  DTAARA(QTEMP/ARA (2 1)) VALUE('1')
             GOTO       CMDLBL(STEP21)
                        ENDDO
             DLTOVR     FILE(*ALL)
             CHGDTAARA  DTAARA(QTEMP/ARA (2 1)) VALUE('0')
             CLRPFM     FILE(QTEMP/DDMK)
             MONMSG     MSGID(CPF0000)
 STEP21:      /*  台北　抓　苗栗  ARLIB/INVDTLH ............ */
             RTVDTAARA  DTAARA(QTEMP/ARA (3 1)) RTNVAR(&ARA3)
             IF         COND(&ARA3 *EQ '0') THEN(GOTO CMDLBL(STEP3))
             SBMRMTCMD  CMD('CALL PGM(ARLIB/ARP022PA) PARM(''H'')') +
                        DDMFILE(QTEMP/DDMH)
             MONMSG     MSGID(CPF0000)  EXEC(DO)
             CHGDTAARA  DTAARA(QTEMP/ARA (3 1)) VALUE('1')
             GOTO       CMDLBL(STEP3)
                        ENDDO
             CHGDDMF    FILE(QTEMP/DDMH) RMTFILE(*NONSTD +
                          'ARLIB/INVDTLH') LVLCHK(*NO)
             OVRDBF     FILE(INVDTLT) TOFILE(QTEMP/DDMH)
             OVRDBF     FILE(INVDTLP) TOFILE(ARLIB/INVDTLP)
             OVRDBF     FILE(INVDTL)  TOFILE(INVDTL)
             OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 198) +
                          CPI(15)
             CHGDTAARA  DTAARA(*LDA (21 1)) VALUE('H')
             CALL       PGM(ARLIB/ARP022RB)
             MONMSG     MSGID(CPF0000)  EXEC(DO)
             CHGDTAARA  DTAARA(QTEMP/ARA (3 1)) VALUE('1')
             GOTO       CMDLBL(STEP3)
                        ENDDO
             DLTOVR     FILE(*ALL)
             CHGDTAARA  DTAARA(QTEMP/ARA (3 1)) VALUE('0')
             CLRPFM     FILE(QTEMP/DDMH)
             MONMSG     MSGID(CPF0000)

   /* ----------------新版台北送廠區開始------------------- */

 STEP3: /*  台北 ARLIB/INVDTLP 送　桃園 DALIB/INVDTL      */
             RTVDTAARA  DTAARA(QTEMP/ARA (4 1)) RTNVAR(&ARA2)
             IF         COND(&ARA2 *EQ '0') THEN(GOTO CMDLBL(STEP4))
             CHGDDMF    FILE(QTEMP/DDMT) RMTFILE(ARLIB/INVDTLP) +
                        LVLCHK(*NO)
             CLRPFM     FILE(QTEMP/DDMT)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO ERROR3)
             CPYF       FROMFILE(ARLIB/INVDTLP) TOFILE(QTEMP/DDMT) +
                        MBROPT(*REPLACE)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO ERROR3)
             SBMRMTCMD  CMD('CALL ARLIB/ARP022PCT') +
                        DDMFILE(QTEMP/DDMT)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO ERROR3)

             GOTO       OK3
    ERROR3:
             CHGDTAARA  DTAARA(QTEMP/ARA (4 1)) VALUE('1')
             GOTO       CMDLBL(STEP4)
    OK3:
             CHGDTAARA  DTAARA(QTEMP/ARA (4 1)) VALUE('0')

 STEP4: /*  台北 ARLIB/INVDTLP 送　高雄 DALIB/INVDTL      */
             RTVDTAARA  DTAARA(QTEMP/ARA (5 1)) RTNVAR(&ARA2)
             IF         COND(&ARA2 *EQ '0') THEN(GOTO CMDLBL(STEP5))
             CHGDDMF    FILE(QTEMP/DDMK) RMTFILE(ARLIB/INVDTLP) +
                        LVLCHK(*NO)
             CLRPFM     FILE(QTEMP/DDMK)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO ERROR4)
             CPYF       FROMFILE(ARLIB/INVDTLP) TOFILE(QTEMP/DDMK) +
                        MBROPT(*REPLACE)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO ERROR4)
             SBMRMTCMD  CMD('CALL ARLIB/ARP022PCK') +
                        DDMFILE(QTEMP/DDMK)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO ERROR4)

             GOTO       OK4
    ERROR4:
             CHGDTAARA  DTAARA(QTEMP/ARA (5 1)) VALUE('1')
             GOTO       CMDLBL(STEP5)
    OK4:
             CHGDTAARA  DTAARA(QTEMP/ARA (5 1)) VALUE('0')

 STEP5: /*  台北 ARLIB/INVDTLP 送　苗栗 DALIB/INVDTL      */
             RTVDTAARA  DTAARA(QTEMP/ARA (6 1)) RTNVAR(&ARA2)
             IF         COND(&ARA2 *EQ '0') THEN(GOTO CMDLBL(LOOP2))
             CHGDDMF    FILE(QTEMP/DDMH) RMTFILE(ARLIB/INVDTLP) +
                        LVLCHK(*NO)
             CLRPFM     FILE(QTEMP/DDMH)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO ERROR5)
             CPYF       FROMFILE(ARLIB/INVDTLP) TOFILE(QTEMP/DDMH) +
                        MBROPT(*REPLACE) FMTOPT(*NOCHK)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO ERROR5)
             SBMRMTCMD  CMD('CALL ARLIB/ARP022PDH') +
                        DDMFILE(QTEMP/DDMH)
             MONMSG     MSGID(CPF0000)  EXEC(GOTO ERROR5)

             GOTO       OK5
    ERROR5:
             CHGDTAARA  DTAARA(QTEMP/ARA (6 1)) VALUE('1')
             GOTO       CMDLBL(LOOP2)
    OK5:
             CHGDTAARA  DTAARA(QTEMP/ARA (6 1)) VALUE('0')

     /* ------------------新版台北送廠區結束 ------------------- */

 LOOP2: /*   CLEAR .... TEMP-FILE  TAIPEI  & DLCOBJ .......... */
             RTVDTAARA  DTAARA(QTEMP/ARA (1 6)) RTNVAR(&ARA)
             IF         COND(&ARA *NE '000000') THEN(CHGVAR VAR(&CNT2) +
                          VALUE(&CNT2 + 1))
             IF         COND(&CNT2 *GT 2) THEN(GOTO CMDLBL(ENDCLPGM))
             ELSE       DO
                        IF         COND(&ARA *NE '000000') THEN(DO)
                                   DLYJOB   DLY(60)
                                   GOTO     CMDLBL(STEP1)
                                   ENDDO
                        ENDDO
       /*    DLCOBJ     OBJ((INVDTL *FILE *EXCL))      */
             CLRPFM     FILE(ARLIB/INVDTLP)
             MONMSG     MSGID(CPF0000)
 ENDCLPGM:   ENDPGM
