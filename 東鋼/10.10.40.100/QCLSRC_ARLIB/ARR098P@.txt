        /******************************************************/
        /*                                                    */
        /*        >>   PROGRAM INTRODUCTION  <<               */
        /*        1.PROGRAM-ID     ARR098P                    */
        /*        2.PROGRAM-TYPE   CLP                        */
        /*        3.AUTHOR         S02CSF                     */
        /*        4.FUNCTION      自製出貨明細表            */
        /*        5.DATE-WRITTEN   91/09/16                   */
        /*        6.DATE-MODIFY                               */
        /*                                                    */
        /******************************************************/
            PGM
            DCLF       FILE(ARR098S)
            DCL        VAR(&YYMM)   TYPE(*CHAR) LEN(6)
            DCL        VAR(&YYM1)   TYPE(*CHAR) LEN(6)
            DCL        VAR(&MMF)    TYPE(*CHAR) LEN(2)
            DCL        VAR(&MMT)    TYPE(*CHAR) LEN(2)
            DCL        VAR(&MM1)    TYPE(*DEC)  LEN(2 0)
            DCL        VAR(&MM2)    TYPE(*DEC)  LEN(2 0)
            DCL        VAR(&M1)     TYPE(*CHAR) LEN(2)
            DCL        VAR(&Y)      TYPE(*CHAR) LEN(2)
            DCL        VAR(&AREA)   TYPE(*CHAR) LEN(1)
            DCL        VAR(&OUTQ)   TYPE(*CHAR) LEN(10)
            DCL        VAR(&TYPE)   TYPE(*CHAR) LEN(1)
            DCL        VAR(&DSPID)  TYPE(*CHAR) LEN(10)
            DCL        VAR(&C_PDNS) TYPE(*CHAR) LEN(3)
            DCL        VAR(&C_PDNE) TYPE(*CHAR) LEN(3)
            DCL        VAR(&C_DATS) TYPE(*CHAR) LEN(4)
            DCL        VAR(&C_DATE) TYPE(*CHAR) LEN(4)

            RTVJOBA    JOB(&DSPID) USER(&S#USER) OUTQ(&OUTQ) +
                       TYPE(&TYPE)
            IF         COND(&TYPE *EQ '0') THEN(GOTO CMDLBL(BATCH))

            RTVDTAARA  DTAARA(*LDA (1011 10))   RTNVAR(&S#DEVN)
            RTVDTAARA  DTAARA(*LDA (1021 1))    RTNVAR(&AREA)
            CHGDTAARA  DTAARA(*LDA (500 10))    VALUE(&DSPID)

            CHGVAR     VAR(&S#AREA) VALUE('T')
            CHGVAR     VAR(&S#PDNM) VALUE('2')

 START:     SNDRCVF

            IF         COND(&IN03 *EQ '1') THEN(RETURN)

            CHGVAR     VAR(&IN25)   VALUE('0')
            CHGVAR     VAR(&IN26)   VALUE('0')
            CHGVAR     VAR(&IN27)   VALUE('0')
            CHGVAR     VAR(&IN28)   VALUE('0')
            CHGVAR     VAR(&S#ERR)  VALUE(' ')

/*CKECK 廠區代號*/

            IF         COND((&S#AREA  *NE 'K')  *AND     +
                            (&S#AREA  *NE 'T')  *AND     +
                            (&S#AREA  *NE 'M')  *AND     +
                            (&S#AREA  *NE 'L')  *AND     +
                            (&S#AREA  *NE 'H')  *AND     +
                            (&S#AREA  *NE 'P')) THEN(DO)
            CHGVAR     VAR(&IN25)   VALUE('1')
            CHGVAR     VAR(&S#ERR)  VALUE('廠區別錯誤！')
            GOTO       CMDLBL(START)
            ENDDO

  /*    日期檢核  */
            IF         COND(&S#YYMM *EQ 0) THEN(DO)
            CHGVAR     VAR(&IN26)   VALUE('1')
            CHGVAR     VAR(&S#ERR)  VALUE('年月錯誤')
            GOTO       CMDLBL(START)
            ENDDO

            IF         COND(&S#YYM1 *EQ 0) THEN(DO)
            CHGVAR     VAR(&IN27)   VALUE('1')
            CHGVAR     VAR(&S#ERR)  VALUE('年月錯誤')
            GOTO       CMDLBL(START)
            ENDDO

  /*    CHECK 品名　*/
            IF         COND((&S#PDNM *NE ' '  )  *AND +
                            (&S#PDNM *NE '2'  )  *AND +
                            (&S#PDNM *NE '3'  )  *AND +
                            (&S#PDNM *NE '4'  )) THEN(DO)
            CHGVAR     VAR(&IN28)    VALUE('1')
            CHGVAR     VAR(&S#ERR)   VALUE('類別輸入錯誤!!!')
            GOTO       CMDLBL(START)
            ENDDO

            IF         COND(&S#PDNM *EQ '2'    ) THEN(DO)
            CHGVAR     VAR(&C_PDNS)  VALUE('100')
            CHGVAR     VAR(&C_PDNE)  VALUE('299')
            ENDDO

            IF         COND(&S#PDNM *EQ '3'    ) THEN(DO)
            CHGVAR     VAR(&C_PDNS)  VALUE('300')
            CHGVAR     VAR(&C_PDNE)  VALUE('399')
            ENDDO

            IF         COND(&S#PDNM *EQ '4'    ) THEN(DO)
            CHGVAR     VAR(&C_PDNS)  VALUE('400')
            CHGVAR     VAR(&C_PDNE)  VALUE('499')
            ENDDO

            IF         COND(&S#PDNM *EQ ' '    ) THEN(DO)
            CHGVAR     VAR(&C_PDNS)  VALUE('200')
            CHGVAR     VAR(&C_PDNE)  VALUE('499')
            ENDDO

             /* MOVE Screen Field */
             CHGDTAARA  DTAARA(*LDA (410 1))   VALUE(&S#AREA)
             CHGVAR     VAR(&YYMM)  VALUE(&S#YYMM)
             CHGVAR     VAR(&YYM1)  VALUE(&S#YYM1)
             CHGDTAARA  DTAARA(*LDA (411 6))   VALUE(&YYMM)
             CHGDTAARA  DTAARA(*LDA (417 6))   VALUE(&YYM1)
             CHGDTAARA  DTAARA(*LDA (423 3))   VALUE(&C_PDNS)
             CHGDTAARA  DTAARA(*LDA (426 3))   VALUE(&C_PDNE)
             CHGDTAARA  DTAARA(*LDA (429 1))   VALUE(&AREA)

            SBMJOB     CMD(CALL PGM(ARR098P)) JOB(ARR098P) +
                       JOBD(ARJOBD)
            SNDBRKMSG  MSG('<<自製出貨明細表已開始處理 +
                           ,請稍待.........>>') +
                       TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)
            RETURN

 BATCH:      RTVDTAARA  DTAARA(*LDA (410 1)) RTNVAR(&S#AREA)
             RTVDTAARA  DTAARA(*LDA (411 6)) RTNVAR(&YYMM)
             RTVDTAARA  DTAARA(*LDA (417 6)) RTNVAR(&YYM1)
             RTVDTAARA  DTAARA(*LDA (415 2)) RTNVAR(&MMF)
             RTVDTAARA  DTAARA(*LDA (421 2)) RTNVAR(&MMT)
             RTVDTAARA  DTAARA(*LDA (413 2)) RTNVAR(&Y)
             RTVDTAARA  DTAARA(*LDA (413 4)) RTNVAR(&C_DATS)
             RTVDTAARA  DTAARA(*LDA (419 4)) RTNVAR(&C_DATE)
             RTVDTAARA  DTAARA(*LDA (423 3)) RTNVAR(&C_PDNS)
             RTVDTAARA  DTAARA(*LDA (426 3)) RTNVAR(&C_PDNE)
             RTVDTAARA  DTAARA(*LDA (429 1)) RTNVAR(&AREA)
             CHGVAR     VAR(&MM1) VALUE(&MMF)
             CHGVAR     VAR(&MM2) VALUE(&MMT)
             CHGVAR     VAR(&M1) VALUE(&MM1)

             ADDLIBLE ACLIB
             MONMSG   MSGID(CPF0000)

             IF         COND(&AREA *EQ 'P') THEN(DO)
             CPYF       FROMFILE(DALIB/('ACVR' || &Y)) +
                        TOFILE(QTEMP/TRANBU) FROMMBR('VR' || &M1) +
                        MBROPT(*REPLACE) CRTFILE(*YES)
             ENDDO
             ELSE (DO)
             CPYF       FROMFILE(DTALIB/('ACVR' || &Y)) +
                        TOFILE(QTEMP/TRANBU) FROMMBR('VR' || &M1) +
                        MBROPT(*REPLACE) CRTFILE(*YES)
             ENDDO
 AGAIN:
             CHGVAR     VAR(&MM1) VALUE(&MM1 + 1)
             CHGVAR     VAR(&M1) VALUE(&MM1)
             IF         COND(&MM1 *LE &MM2) THEN(DO)
             IF         COND(&AREA *EQ 'P') THEN(DO)
             CPYF       FROMFILE(DALIB/('ACVR' || &Y)) +
                        TOFILE(QTEMP/TRANBU) FROMMBR('VR' || &M1) +
                        MBROPT(*ADD) CRTFILE(*NO)
             ENDDO
             ELSE (DO)
             CPYF       FROMFILE(DTALIB/('ACVR' || &Y)) +
                        TOFILE(QTEMP/TRANBU) FROMMBR('VR' || &M1) +
                        MBROPT(*ADD) CRTFILE(*NO)
             ENDDO
             GOTO       CMDLBL(AGAIN)
             ENDDO

            /*      開始處理       */
            /*    　寫入AC255PF    */
            CLRPFM     AC255PF
            OVRDBF     FILE(ACTRAN) TOFILE(QTEMP/TRANBU) SHARE(*YES)
            IF         COND(&S#AREA *NE ' ') THEN(OPNQRYF +
                         FILE((ACTRAN)) QRYSLT('(ACNO *EQ "4111" +
                         *OR ACNO *EQ "4114" *or acno *eq "4112" +
                         *or acno *eq "4115" *or acno *eq "4113" +
                         *or acno *eq "4116") *AND ACCARD *EQ "' +
                         || &S#AREA || '" *AND %SST(ACVRNO 1 1) +
                         *NE "X"') KEYFLD((ACCARD) (ACDSC1)))
            ELSE       CMD(OPNQRYF FILE((ACTRAN)) QRYSLT('(ACNO *EQ +
                         "4111" *OR ACNO *EQ "4114" *or acno *eq +
                         "4112" *or acno *eq "4115" *or acno *eq +
                         "4113" *or acno *eq "4116") *AND +
                         %SST(ACVRNO 1 1) *NE "X"') +
                        KEYFLD((ACCARD) (ACDSC1)))
            CALL       ACP255R
            CLOF       OPNID(ACTRAN)
            DLTOVR     *ALL

            /* 依據ACR255PF,寫入W1ARR098    */
            CLRPFM     W1ARR098

            OVRDBF     FILE(AC255PF) TOFILE(DALIB/AC255PF) SHARE(*YES)
            OVRDBF     FILE(ACPROD) TOFILE(ACPROD)
            OPNQRYF    FILE((AC255PF)) +
                       KEYFLD((A1PRNO))  +
                       MAPFLD((YM A1YYMM *CHAR 6))
            CALL       PGM(ARR098R1)
            CLOF       OPNID(AC255PF)
            DLTOVR     FILE(*ALL)

            DLTF       FILE(QTEMP/TRANBU)

            /* 依據ARR089P,CHAIN W1ARR098    */
            OVRDBF     FILE(TRNDTL) TOFILE(DALIB/TRNDTL) SHARE(*YES)
            OVRDBF     FILE(WTMASP) TOFILE(DALIB/WTMASP)
            OPNQRYF    FILE(TRNDTL) QRYSLT('+
                       (TXCODE *EQ "SA04") *AND +
                       (AREA *EQ "' || &S#AREA || '") *AND +
                       (ENTD *GE "' || &C_DATS || '") *AND +
                       (ENTD *LE "' || &C_DATE || '") *AND +
                       (TXPDNM *GE "' || &C_PDNS || '") *AND +
                       (TXPDNM *LE "' || &C_PDNE || '")') +
                       KEYFLD((TXPDNM) (TXDATE) (TXNO)) +
                       MAPFLD((CHAR TXACDT *CHAR 8) +
                              (ENTD '%SST(CHAR 3 4)' *CHAR 4) +
                              (AREA '%SST(TXNO 1 1)' *CHAR 1))
            CALL       PGM(ARR098R2)
            CLOF       OPNID(TRNDTL)
            DLTOVR     FILE(*ALL)

            OVRPRTF    FILE(ARR098T) TOFILE(ARR098T) +
                       PAGESIZE(*N 198)  CPI(15) HOLD(*YES) +
                       USRDTA('自製出貨')
            CALL       PGM(ARR098R3)

            SNDBRKMSG  MSG('<< 自製出貨明細表已處理完成, +
                               請列印.......>>') +
                       TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)
            ENDPGM
