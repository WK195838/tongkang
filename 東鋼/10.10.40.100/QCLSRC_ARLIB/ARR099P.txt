     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARR099P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02LBU                                  */
     /*     4.FUNCTION     應收帳款承購(E-FACTORING)繳款呈核表  */
     /*     5.DATE-WRITTEN  91/10/07                                */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&INT)     TYPE(*CHAR)   LEN(1)
             DCL        VAR(&IN03)    TYPE(*LGL)    LEN(1)
             DCL        VAR(&W#AREA)  TYPE(*CHAR)   LEN(1)
             DCL        VAR(&W#DATE)  TYPE(*DEC)    LEN(8 0)
             DCL        VAR(&W#RVDT)  TYPE(*CHAR)   LEN(8)
             DCL        VAR(&OUTQ)    TYPE(*CHAR)   LEN(10)

             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT)

             IF         COND(&INT *EQ '0') THEN(GOTO CMDLBL(BATCH))

             IF         COND(&INT *EQ '1') THEN(DO)
             CALL       PGM(ARR099RS) PARM(&IN03 &W#DATE &W#AREA)
             ENDDO
             IF         COND(&IN03 *EQ '1') THEN(RETURN)

    /*記錄輸入值*/
             CHGDTAARA  DTAARA(*LDA (1 1)) VALUE(&W#AREA)
             CHGVAR     VAR(&W#RVDT)       VALUE(&W#DATE)
             CHGDTAARA  DTAARA(*LDA (2 8)) VALUE(&W#RVDT)

             SBMJOB     CMD(CALL PGM(ARR099P)) JOB(ARR099P) +
                        JOBD(ARJOBD)
             RETURN

 BATCH:
             RTVDTAARA  DTAARA(*LDA (1 1)) RTNVAR(&W#AREA)
             RTVDTAARA  DTAARA(*LDA (2 8)) RTNVAR(&W#RVDT)
             CHGVAR     VAR(&W#DATE)       VALUE(&W#RVDT)

             OVRPRTF    FILE(ARR099T) TOFILE(ARLIB/ARR099T) +
                        PAGESIZE(*N 198) CPI(15) +
                        OVRFLW(57) HOLD(*YES) USRDTA('繳款呈核') +
                        COPIES(1)
             OVRDBF     FILE(ARCVDT) TOFILE(DALIB/ARCVDT)
             OVRDBF     FILE(ARCVMS) TOFILE(DALIB/ARCVMS) SHARE(*YES)
             OPNQRYF    FILE(ARCVMS) OPTION(*ALL) QRYSLT( +
                    '%SST(ASRCNO 2 1) *EQ "' || &W#AREA || '" *AND +
                          ASENDT *EQ ' || &W#RVDT) +
                        KEYFLD((ASRCNO)(ASENDT))
             CALL       PGM(ARR099R)
             CLOF       OPNID(ARCVMS)
             DLTOVR     FILE(*ALL)
             SNDBRKMSG  MSG('<<應收帳款承購繳款呈核表+
                            ,已處理完成，請列印...>>') +
                            TOMSGQ(%SST(*LDA 1011 10)) MSGTYPE(*INFO)

             ENDPGM
