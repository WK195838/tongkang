     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP002PX                                */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02CLJ                                  */
     /*     4.FUNCTION     隨車發票開立異常版                     */
     /*     5.DATE-WRITTEN  87/04/01                                */
     /*                                                             */
     /*   <說明>原本隨車開立是一車一發票，遇有特殊情形時，如    */
     /*           多車一發票時，本程式專用於開立此類發票。        */
     /*           需視開立時之需求來修改本程式，請參考ARP002P     */
     /*                                                             */
     /***************************************************************/
             PGM
             DCL        VAR(&INT)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&OUTQ)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&YMDA)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&YMD)   TYPE(*DEC)  LEN(6 0)
             DCL        VAR(&AREA)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&AREAX) TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN03)  TYPE(*LGL)  LEN(1)
             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT)
             IF         COND(&INT *EQ '1') THEN(DO)
             CALL       PGM(ARP002RS) PARM(&IN03 &YMD &AREA)
             IF         COND(&IN03 *EQ '1') THEN(GOTO ENDCLPGM)
             CHGVAR     VAR(&YMDA)    VALUE(&YMD)
             CHGDTAARA  DTAARA(*LDA (1 6)) VALUE(&YMDA)
             CHGDTAARA  DTAARA(*LDA (7 1)) VALUE(&AREA)
             ENDDO

             RTVDTAARA  DTAARA(*LDA (1 6)) RTNVAR(&YMDA)
             RTVDTAARA  DTAARA(*LDA (7 1)) RTNVAR(&AREA)
             CHGVAR     VAR(&AREAX) VALUE(&AREA)
             IF         COND(&AREA = 'L') THEN(CHGVAR VAR(&AREAX) +
                          VALUE('P'))
             DLTF       FILE(QTEMP/TRNDTLW)
             MONMSG     MSGID(CPF0000)
             CRTPF      FILE(QTEMP/TRNDTLW) SRCFILE(ARLIB/QDDSSRC) +
                          OPTION(*NOSRC *NOLIST) SIZE(*NOMAX)
             OVRDBF     FILE(TRNDTLW) TOFILE(QTEMP/TRNDTLW)
             OVRDBF     FILE(TRNDTL) TOFILE(TRNDTL) SHARE(*YES)
             OPNQRYF    FILE((TRNDTL)) OPTION(*INP) QRYSLT('      +
                        TXCODE *NE "AR06"                          +
                        *AND TXDATE *LE ' || &YMDA || '            +
                        *AND TXTXAR *EQ "'|| &AREA ||'"            +
                        *AND %SST(TXCUNO 1 1) *NE "E"              +
                        *AND TXFLAG *NE "D"                        +
                        *AND TXFL02 *EQ " "                        +
                        *AND TXIVNO *EQ " "')                      +
                      KEYFLD((TXCODE)(TXNO)(TXRESV)(TXPDNM)(TXUPRC)) +
                      MAPFLD((TXRESV '%SST(TXORNO 1 5)'))
             CALL       PGM(ARP002RA)
             CLOF       OPNID(TRNDTL)
             DLTOVR     FILE(*ALL)

             OVRDBF     FILE(INVMST) TOFILE(INVMST)
             OVRDBF     FILE(INVDTL) TOFILE(INVDTL)
             OVRDBF     FILE(TRNDTL) TOFILE(TRNDTL)
             OVRDBF     FILE(TRNDTLW) TOFILE(QTEMP/TRNDTLW) SHARE(*YES)
 /*          OPNQRYF    FILE((TRNDTLW)) OPTION(*ALL) QRYSLT('TXHOLD +
                          *NE "Y"') KEYFLD((TXCODE) (TXNO) (TXRESV) +
                          (TXPDNM) (TXUPRC)) MAPFLD((TXRESV +
                          '%SST(TXORNO 1 5)'))                   */

               /* 數張磅單開成一張發票 */

             OPNQRYF    FILE((TRNDTLW)) OPTION(*ALL) QRYSLT('TXHOLD +
                          *NE "Y"') KEYFLD((TXCODE) (TXRESV) +
                          (TXPDNM) (TXUPRC)) MAPFLD((TXRESV +
                          '%SST(TXORNO 1 5)'))
             OVRDBF     FILE(HIPROD) TOFILE(HIPROD)
             OVRDBF     FILE(CBCUST) TOFILE(CBCUST)
             OVRDBF     FILE(SACUST) TOFILE(SACUST)
             OVRDBF     FILE(SAMAST) TOFILE(SAMAST)
             OVRDBF     FILE(BRIV) TOFILE(BRIV)
             OVRPRTF    FILE(QPRINT) TOFILE(QPRINT) PAGESIZE(*N 160) +
                          CPI(12) OUTQ(&OUTQ)
             CALL       PGM(ARP002RX) PARM(&AREAX)
             CLOF       OPNID(TRNDTLW)
             DLTOVR     FILE(*ALL)
ENDCLPGM:    ENDPGM
