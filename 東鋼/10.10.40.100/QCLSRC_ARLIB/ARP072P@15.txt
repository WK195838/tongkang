     /***************************************************************/
     /*                                                             */
     /*               >>   PROGRAM INTRODUCTION   <<                */
     /*                                                             */
     /*     1.PROGRAM-ID    ARP072P                                 */
     /*     2.PROGRAM-TYPE  CLP/400                                 */
     /*     3.AUTHOR        S02YSH                                  */
     /*     4.FUNCTION     銷貨收入每月差異代開傳票               */
     /*     5.DATE-WRITTEN  89/05/20                                */
     /*     6.UPDATE  DATE  99/08/03  2010AR511  S00WCJ (9908A)     */
     /*                     將發票NW67395440排除                */
     /*                     99/09/02  2010AR522  S00WCJ (9909A)     */
     /*                     將發票PW67395222排除                */
     /*                     99/09/10  2010AR524  S00WCJ (9909B)     */
     /*                     將發票PW67395321排除                */
     /*                     99/10/06  2010AR529  S00WCJ (9910A)     */
     /*                     將發票PW67396239排除                */
     /*                     99/11/04  2010AR541  S00WCJ (9911A)     */
     /*                     將發票QW67395259排除                */
     /*                     99/12/03  2010AR558  S00WCJ (9912A)     */
     /*                     將發票QW67396231排除                */
     /*                     99/12/22  2010AR568  S00WCJ (9912B)     */
     /*                     將發票QW67396500排除                */
     /*                    100/01/03  2011AR578  S00WCJ (0001A)     */
     /*                     將發票QW67396639.QW67396640排除     */
     /*                    100/01/13  2011AR584  S00WCJ (0001B)     */
     /*                     將發票RX70904815排除                */
     /*                    100/02/22  S00WCJ (0002A)                */
     /*                     將發票RX70905787排除                */
     /*                    100/03/07  S00WCJ (0003A)                */
     /*                     將發票TB70904663排除                */
     /*                    100/03/10  S00WCJ (0003B)                */
     /*                     將發票RX11138909排除                */
     /*                    100/03/21  S00WCJ (0003B)                */
     /*                     將發票TB70904894排除                */
     /*                                                             */
     /*  <說明:> 由於銷貨收入由銷貨明細代開而非發票明細代開     */
     /*            因此當隨訂發票愈多時，便會有差異出現。         */
     /*            本作業每月進行一次，先刪除該月份之傳票資料     */
     /*            再進行代開整月的動作。（可重複代開）           */
     /*            傳票編號：P00001 -- P00499                     */
     /*                                                             */
     /***************************************************************/
             PGM
             DCLF       FILE(ARP072S)
             DCL        VAR(&P#YYMM) TYPE(*CHAR) LEN(6)
             DCL        VAR(&P#CODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&INT)   TYPE(*CHAR) LEN(1)   /*執行狀態*/
             DCL        VAR(&OUTQ)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&USER)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03)  TYPE(*LGL)  LEN(1)
             DCL        VAR(&AREA)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&YYMM)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&YY)    TYPE(*CHAR) LEN(4)
             DCL        VAR(&MM)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&FNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C#SDAT) TYPE(*CHAR) LEN(8)
             DCL        VAR(&C#EDAT) TYPE(*CHAR) LEN(8)

             RTVJOBA    OUTQ(&OUTQ) TYPE(&INT) USER(&USER)
             IF         COND(&INT *EQ '0') THEN(GOTO BATCH)

             RTVDTAARA  DTAARA(*LDA (1021 1)) RTNVAR(&AREA)
             RTVDTAARA  DTAARA(*LDA (951 35)) RTNVAR(&S#COMP)
             RTVDTAARA  DTAARA(*LDA (1011 10)) RTNVAR(&S#DEVI)
             IF         COND(&AREA *EQ 'P')  THEN(DO)
             CHGVAR     VAR(&AREA) VALUE('M')
             ENDDO
             CHGVAR     VAR(&S#AREA) VALUE(&AREA)
 DISPLAY:
             SNDRCVF    RCDFMT(AR072F1)
             IF         COND(&IN03 *EQ '1') THEN(RETURN)

             IF         COND(&S#AREA *EQ 'P') THEN(DO)
             CHGVAR     VAR(&S#ERR) +
                        VALUE('銷貨差異代開傳票作業，不得在台北進行')
                        GOTO DISPLAY
                        ENDDO

             CHGVAR     VAR(&YYMM) VALUE(&S#YYMM)
             CHGVAR     VAR(&P#YYMM) VALUE(&S#YYMM)
             CHGVAR     VAR(&YY) VALUE(%SST(&YYMM 1 4))
             CHGVAR     VAR(&MM) VALUE(%SST(&YYMM 5 2))
             CHGVAR     VAR(&FNAME) +
                        VALUE('AC' || &AREA || 'R')
             CHKOBJ     OBJ(&FNAME) OBJTYPE(*FILE)
             MONMSG     CPF0000     EXEC(DO)
             CHGVAR     VAR(&S#ERR) +
                        VALUE('銷貨差異代開傳票作業，無法找到指定+
                          的傳票檔：' || &FNAME )
                        GOTO DISPLAY
             ENDDO

             CALL    PGM(ACP101R) PARM(&P#YYMM &P#CODE)
             IF         COND(&P#CODE *EQ '1') THEN(DO)
             CHGVAR     VAR(&S#ERR) VALUE('關帳日期已過')
             GOTO       CMDLBL(DISPLAY)
             ENDDO

             CHGDTAARA  DTAARA(*LDA (1 6)) VALUE(&YYMM)
             CHGDTAARA  DTAARA(*LDA (7 1)) VALUE(&AREA)
             SBMJOB     JOB(ARP072P) JOBD(ARJOBD) OUTQ(&OUTQ)  +
                        RQSDTA('CALL ARP072P')
             RETURN

BATCH:
             RTVDTAARA  DTAARA(*LDA (1 6)) RTNVAR(&YYMM)
             RTVDTAARA  DTAARA(*LDA (1 4)) RTNVAR(&YY)
             RTVDTAARA  DTAARA(*LDA (5 2)) RTNVAR(&MM)
             RTVDTAARA  DTAARA(*LDA (7 1)) RTNVAR(&AREA)

             CHGVAR     VAR(&FNAME) +
                        VALUE('AC' || &AREA || 'R')


STEP1:       /* 刪除已代開過的資料 */

             OVRDBF     FILE(ACTRAN) TOFILE(&FNAME) SHARE(*YES)
             OPNQRYF    FILE(ACTRAN) OPTION(*ALL) +
                        QRYSLT('ACVRNO *GE "P00001" *AND +
                                ACVRNO *LE "P00499" *AND +
                                ACAREA *EQ "' || &AREA || '" *AND +
                                YM     *EQ "' || &YYMM || '"')    +
                        MAPFLD((YM    ACYM  *CHAR 6))

             CALL       PGM(ARP072R1) /*刪除輸入日期後的資料*/
             CLOF       OPNID(ACTRAN)
             DLTOVR     *ALL


STEP2:       /* 先前處理 */
             CLRPFM     ARP072F
             OVRDBF     FILE(INVMST) TOFILE(INVMST) SHARE(*YES)

             CHGVAR     VAR(&C#SDAT) VALUE(&YYMM || '01')
             CHGVAR     VAR(&C#EDAT) VALUE(&YYMM || '31')

             OPNQRYF    FILE(INVMST) OPTION(*ALL) +
                        QRYSLT('+
                              (INDT *GE "' || &C#SDAT || '") *AND +
                              (INDT *LE "' || &C#EDAT || '") *AND +
                              (INNO *NE "JW32774325")  *AND +
                              (INNO *NE "JW32774497")  *AND +
                              (INNO *NE "NW67395440")  *AND +
                              (INNO *NE "PW67395222")  *AND +
                              (INNO *NE "PW67395321")  *AND +
                              (INNO *NE "PW67396239")  *AND +
                              (INNO *NE "QW67395259")  *AND +
                              (INNO *NE "QW67395259")  *AND +
                              (INNO *NE "QW67396500")  *AND +
                              (INNO *NE "QW67396639")  *AND +
                              (INNO *NE "QW67396640")  *AND +
                              (INNO *NE "RX70904815")  *AND +
                              (INNO *NE "RX70905787")  *AND +
                              (INNO *NE "TB70904663")  *AND +
                              (INNO *NE "RX11138909")  *AND +
                              (INNO *NE "TB70904894")  *AND +
                              (INORNO *NE "M01744")    *AND +
                              (INORNO *NE "M01762")    *AND +
                              (INORNO *NE "M01763")    *AND +
                              (INORNO *NE "M01764")    *AND +
                              (INORNO *NE "M01765")    *AND +
                              (INORNO *NE "M01766")    *AND +
                              (INORNO *NE "M01767")    *AND +
                              (INORNO *NE "M01781")    *AND +
                              (INDECD *NE "D")') +
                        MAPFLD((INDT ININDT *CHAR 8))

             CALL       PGM(ARP072R2) /*先前處理*/
             CLOF       OPNID(INVMST)
             DLTOVR     *ALL

             IF         COND((&AREA *EQ 'H')  *OR +
                             (&AREA *EQ 'K')) THEN(DO)
             CLRPFM     ARP072F1
             OVRDBF     FILE(HSCINV) TOFILE(HSCINV) SHARE(*YES)
             OPNQRYF    FILE(HSCINV) OPTION(*ALL) +
                        QRYSLT('+
                              (CDTE *GE "' || &C#SDAT || '") *AND +
                              (CDTE *LE "' || &C#EDAT || '") *AND +
                              (C1FLAG *NE "D")') +
                        MAPFLD((CDTE C1CDTE *CHAR 8))
             CALL       PGM(ARP072R3) /*先前處理*/
             CLOF       OPNID(HSCINV)
             DLTOVR     FILE(*ALL)
             ENDDO


STEP3:       /* 代開開始*/   /*------------------*/

             OVRDBF     FILE(ACTRAN) TOFILE(&FNAME) SHARE(*YES)
             OVRPRTF    FILE(ARP072T) TOFILE(ARP072T) PAGESIZE(*N +
                          132) CPI(10) HOLD(*YES) USRDTA('發銷差異')

             OVRDBF     FILE(ARP072F) TOFILE(ARP072F) SHARE(*YES)
             OPNQRYF    FILE(ARP072F) +
                        KEYFLD((A1ORNO) (A1IVNO) (A1PDNM))

             CALL       PGM(ARP072R)  /*傳票代開及列表*/
             CLOF       OPNID(ARP072F)

             IF         COND((&AREA *EQ 'H')  *OR +
                             (&AREA *EQ 'K')) THEN(DO)
             OVRPRTF    FILE(ARP072T) TOFILE(ARP072TA) PAGESIZE(*N +
                          158) CPI(12) HOLD(*YES) USRDTA('外銷差異')

             OVRDBF     FILE(ARP072F1) TOFILE(ARP072F1) SHARE(*YES)
             OPNQRYF    FILE(ARP072F1) +
                        KEYFLD((A1ORNO) (A1IVNO) (A1PDNM))

             CALL       PGM(ARP072RA)  /*傳票代開及列表*/
             CLOF       OPNID(ARP072F1)
             DLTOVR     FILE(*ALL)
             ENDDO

             ENDPGM
