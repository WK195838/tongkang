     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARP043T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     組立加工之勞務收入代開傳票
     A*           4.DATE-WRITTEN  85/12/26
     A*           5.WRITER        S02CLJ
     A*
     A*****************************************************************
     A          R AR043T1H
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      ' PRT-ID  : ARP043T'
     A                                    65
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   133
     A                                      '列表日期:'
     A                                   144
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      ' USR-ID  :'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                   +46
     A                                      '勞務收入入帳清單'
     A                                   133
     A                                      '列表時間:'
     A                                   144
     A                                      TIME
     A                                   133
     A                                      '頁　　次:'
     A                                      SPACEB(001)
     A                                   144
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '入帳日期'
     A                                      SPACEB(002)
     A                                    +0
     A                                      '訂單'
     A                                    +0
     A                                      '發票號碼'
     A                                    +1
     A                                      '客戶'
     A                                    +1
     A                                      '借貸'
     A                                    +3
     A                                      '會計科目'
     A                                   +17
     A                                      '品　　名'
     A                                   +24
     A                                      '借方金額'
     A                                    +9
     A                                      '貸方金額　部門'
     A                                    +2
     A                                      '傳票編號'
     A                                     3
     A                                      '--------'
     A                                      SPACEB(001)
     A                                    +1
     A                                      '-----'
     A                                    +1
     A                                      '----------'
     A                                    +1
     A                                      '----------'
     A                                    +1
     A                                      '-'
     A                                    +3
     A                                      '-----------------'
     A                                    +1
     A                                      '-----------------------------------
     A                                      -'
     A                                    +1
     A                                      '------------------'
     A                                    +1
     A                                      '------------------'
     A                                    +1
     A                                      '----'
     A                                    +3
     A                                      '---------'
     A                                      SPACEA(001)
     A**************************************************************************
     A          R AR043T1D
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A            R#DATE         6S  O     3
     A                                      EDTCDE(Y)
     A            R#ORNO         5A  O    12
     A            R#IVNO        10A  O    +1
     A            R#CUNO        10A  O    +1
     A            R#ACDC         1A  O    +1
     A            R#ACNO         4A  O    +3
     A            R#ACNM        10A  O    +1
     A            R#TYCO         1A  O    +1
     A            R#PDNO         3A  O    +1
     A            R#PDNM        32A  O    +0
     A            R#AMTD        13S 2O    +1
     A                                      EDTCDE(K)
     A            R#AMTC        13S 2O    +1
     A                                      EDTCDE(K)
     A            R#DPNO         4A  O    +1
     A            R#FRAR         1A  O    +3
     A            R#TOAR         1A  O    +0
     A            R#VRNO         4A  O    +0
     A                                    +0
     A                                      '-'
     A            R#VRIT         2S  O    +0
     A**************************************************************************
     A          R AR043T1T
     A                                      SPACEB(001)
     A*----------------------------------------------------------------*********
     A                                    38
     A                                      '入帳筆數:'
     A            R#RCNT         3S  O    +1
     A                                    97
     A                                      '勞務收入金額合計:'
     A            R#TAMT        13S 2O   117
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR043T1M
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A*----------------------------------------------------------------
     A                                    19
     A                                      '*****無條件符合之資料！！'
