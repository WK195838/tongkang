     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARR079R2
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     應收帳款帳齡分析表
     A*            5.DATE-WRITTEN  88/07/17
     A*
     A***************************************************************
     A          R AR079T1H
     A                                      SKIPB(003)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報表代號: ARR079T'
     A                                    66
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   138
     A                                      '列表日期:'
     A                                   149
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                    67
     A                                      '經管會應收帳款帳齡分析'
     A                                   138
     A                                      '列表時間:'
     A                                   149
     A                                      TIME
     A                                   138
     A                                      '頁　　次:'
     A                                      SPACEB(001)
     A                                   149
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '統計型態: '
     A                                      SPACEB(001)
     A            R#TYPE        16O  O    +2
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '項'
     A                                      SPACEB(003)
     A                                     6
     A                                      '部門'
     A                                    12
     A                                      '客戶名稱'
     A                                    23
     A                                      '訂單'
     A                                    31
     A                                      '業務員'
     A            R#TA1B         4S  O    43
     A                                      EDTCDE(4)
     A                                    48
     A                                      '--'
     A            R#TA1E         4S  O    51
     A                                      EDTCDE(4)
     A            R#TA2B         4S  O    59
     A                                      EDTCDE(4)
     A                                    64
     A                                      '--'
     A            R#TA2E         4S  O    67
     A                                      EDTCDE(4)
     A            R#TA3B         4S  O    75
     A                                      EDTCDE(4)
     A                                    80
     A                                      '--'
     A            R#TA3E         4S  O    83
     A                                      EDTCDE(4)
     A            R#TA4B         4S  O    91
     A                                      EDTCDE(4)
     A                                    96
     A                                      '以上'
     A                                   109
     A                                      '合計'
     A                                   136
     A                                      '說　明'
     A*----------------------------------------------------------------*********
     A                                     3
     A                                      '---'
     A                                      SPACEB(001)
     A                                     7
     A                                      '----'
     A                                    12
     A                                      '----------'
     A                                    23
     A                                      '-----'
     A                                    29
     A                                      '--'
     A                                    31
     A                                      '----------'
     A                                    42
     A                                      '---------------'
     A                                    +1
     A                                      '---------------'
     A                                    +1
     A                                      '---------------'
     A                                    +1
     A                                      '---------------'
     A                                    +1
     A                                      '---------------'
     A                                    +1
     A                                      '-----------------------------------
     A                                      ---'
     A***************************************************************
     A          R AR079T1D
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A            R#ITEM         3S 0O     3
     A                                      EDTCDE(4)
     A            R#DEPT         4A  O     7
     A            R#CUNM        10O       12
     A            R#ORNO         5A       23
     A            R#RVID         2A       29
     A            R#RVNM        10O       31
     A            R#AMTA        11S 0     42
     A                                      EDTCDE(J)
     A            R#AMTB        11S 0     +1
     A                                      EDTCDE(J)
     A            R#AMTC        11S 0     +1
     A                                      EDTCDE(J)
     A            R#AMTD        11S 0     +1
     A                                      EDTCDE(J)
     A            R#AMTE        11S 0     +1
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR079T1T
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A                                    24
     A                                      '合計：'
     A            R#TAMA        11S 0     42
     A                                      EDTCDE(J)
     A            R#TAMB        11S 0     +1
     A                                      EDTCDE(J)
     A            R#TAMC        11S 0     +1
     A                                      EDTCDE(J)
     A            R#TAMD        11S 0     +1
     A                                      EDTCDE(J)
     A            R#TAME        11S 0     +1
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR079T1X
     A                                      SKIPB(061)
     A*----------------------------------------------------------------
     A                                     2
     A                                      '<營業>'
     A                                    +0
     A                                      '副總:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '經理:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '製表:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    74
     A                                      '<財會>'
     A                                    +0
     A                                      '副總:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '經理:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '覆核:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '製表:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A**************************************************************************
     A          R AR079T1M
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A                                    19
     A                                      '*****無條件符合之資料！！'
     A**************************************************************************
