     A*%%TS  SD  19930818  152422  S02CSF      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARQ017S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02CSF
     A*            4.FUNCTION     發票別出貨明細查詢─ＦＯＲ士鼎
     A*            5.DATE-WRITTEN  93/08/16
     A*            6.DATE-MODIFY
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束作業')
     A                                      CA12(12 '回前畫面')
     A*================================================================
     A          R AR017T1H
     A*%%TS  SD  19930816  104727  S02CSF      REL-V4R4M0  5769-PW1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 29'東和鋼鐵股份有限公司'
     A                                  1 70'ARQ017S'
     A                                  2  2TIME
     A                                  2 28'★發票別出貨明細查詢★'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USID        10A  O  3  2
     A*================================================================
     A          R AR017F1                   SFL
     A*%%TS  SD  19930816  160155  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#ORNO         6A  O 11  2
     A                                 11  9'-'
     A            S#ORTM         3S 0O 11 11
     A            S#VRNO         8A  O 11 15
     A                                 11 24'-'
     A            S#VRTM         3A  O 11 26
     A            S#STND         1A  O 11 31
     A            S#QULT         8A  O 11 34
     A            S#PDNM         3A  O 11 44
     A            S#QTY          9Y 0O 11 48EDTCDE(1)
     A            S#UPRC         5Y 3O 11 60EDTCDE(2)
     A            S#AMT          9Y 0O 11 68EDTCDE(1)
     A            S#SPE1         5Y 0O 12  3EDTCDE(4)
     A                                 12  9'X'
     A            S#SPE2         5Y 0O 12 11EDTCDE(4)
     A                                 12 17'X'
     A            S#SPE3         5Y 0O 12 19EDTCDE(4)
     A                                 12 25'X'
     A            S#SPE4         5Y 0O 12 27EDTCDE(4)
     A            S#SPE5         5Y 0O 12 33EDTCDE(3)
     A            S#PNUM         5Y 0O 12 40EDTCDE(4)
     A            S#PLE1        34O  O 12 46
     A            S#NOTE        78O  O 13  2
     A            S#LINE        78A  O 14  2
     A*================================================================
     A          R AR017F1C                  SFLCTL(AR017F1)
     A*%%TS  SD  19930816  160155  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0003)
     A                                      OVERLAY
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A                                      CF05(05 '列印')
     A                                  5  2'發票號碼：'
     A            S#INNO        10A  O  5 15
     A                                  5 59'合計：'
     A            S#AMTT         9Y 0O  5 68EDTCDE(1)
     A            S#LIN1        78A  O  6  2
     A                                  7  3'訂單編號'
     A                                  7 17'磅單編號'
     A                                  7 28'規範'
     A                                  7 35'鋼種'
     A                                  7 42'品名'
     A                                  7 52'重量'
     A                                  7 60'單價'
     A                                  7 70'金額'
     A                                  8 11'規　　　　　格'
     A                                  8 33'長度'
     A                                  8 40'支數'
     A                                  8 48'交貨地點'
     A                                  9  4'備　　　　　　　　　註'
     A            S#LIN2        78A  O 10  2
     A            S#CRN1         5S 0H
     A            S#NBR          4S 0H
     A*================================================================
     A          R AR017F1M
     A*%%TS  SD  19930818  152422  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A*           S#LIN3        78A  O 22  2
     A                                 23  2'F3=結束作業'
     A                                 23 16'F12=回前畫面'
     A                                 23 31'F5=列印'
     A            S#ERR2        70A  O 24  2DSPATR(HI)
