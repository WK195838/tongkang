     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR051T#
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     折讓開立簽回彙總表（整月）
     A*           4.DATE-WRITTEN  95/08/17
     A*           5.WRITER        S02LBU
     A*           6.UPDATE  DATE  102/12/20  2013AR790  S00WCJ (0212A)
     A*
     A*****************************************************************
     A          R AR051T1H
     A**************************************************************************
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報表編號: ARR051T#'
     A                                    38
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    81
     A                                      '列表日期:'
     A                                    92
     A                                      DATE
     A                                      EDTCDE(Y)
     A*
     A                                     2
     A                                      '地　　區:'
     A                                      SPACEB(001)
     A            T#AREA         1   O    14
     A            T#ARNM         6   O    15
     A                                    36
     A                                      ' 折讓開立簽回彙總表（整月）'
     A                                    81
     A                                      '列表時間:'
     A                                    92
     A                                      TIME
     A*
     A                                     2
     A                                      '簽回年月:'
     A                                      SPACEB(001)
     A            T#YM           6  0O    +1EDTWRD('    /  ')
0212AA                                    +2'客戶編號:'
0212AA            T1CUNO         6   O    +1
     A                                    81
     A                                      '頁　　次:'
     A                                    92
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*----------------------------------------------------------------
     A            T#LIN1        98A  O     2
     A                                      SPACEB(001)
     A*
     A                                     2
     A                                      '客戶編號'
     A                                      SPACEB(001)
     A                                    13
     A                                      '客戶名稱'
     A                                    24
     A                                      '折讓單號'
     A                                    36
     A                                      '開立日期'
     A                                    51
     A                                      '金　　額'
     A                                    66
     A                                      '稅　　額'
     A                                    78
     A                                      '簽回日期'
     A                                    90
     A                                      '折讓種類'
     A*
     A            T#LIN2        98A  O     2
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR051T1D
     A**************************************************************************
     A            T#CUNO         6A  O     4
     A                                      SPACEB(001)
     A            T#CUNM        10O  O    13
     A            T#NO           8A  O    26
     A            T#DATE         8  0O    35EDTWRD('    /  /  ')
     A            T#AMT         10  0O    47EDTCDE(J)
     A            T#TAX         10  0O    62EDTCDE(J)
     A            T#ACDT         8  0O    77EDTWRD('    /  /  ')
     A            T#TYPM        10O  O    90
     A**************************************************************************
     A          R AR051T1T
     A**************************************************************************
     A                                    26
     A                                      '客戶合計:'
     A                                      SPACEB(001)
     A            T#AMT1        10  0O    47
     A                                      EDTCDE(J)
     A            T#TAX1        10  0O    62
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR051T2T
     A**************************************************************************
     A                                    26
     A                                      '廠區合計:'
     A                                      SPACEB(002)
     A            T#AMT2        10  0O    47
     A                                      EDTCDE(J)
     A            T#TAX2        10  0O    62
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR051T3T
     A**************************************************************************
     A                                    26
     A                                      '總　　計:'
     A                                      SPACEB(002)
     A            T#AMT3        10  0O    47
     A                                      EDTCDE(J)
     A            T#TAX3        10  0O    62
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR051T1M
     A**************************************************************************
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A                                    19
     A                                      '*****無條件符合之資料！！'
     A**************************************************************************
