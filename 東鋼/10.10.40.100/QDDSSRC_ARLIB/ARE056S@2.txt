     A*%%TS  SD  19940216  133727  S02CSF      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*         >>   PROGRAM  INTRODUCTION  <<
     A*         1.PROGRAM-ID      ARE056S
     A*         2.PROGRAM-TYPE    DSPF
     A*         3.AUTHOR          S02CSF
     A*         4.FUNCTION       營業發票作廢申請作業
     A*         5.DATE-WRITTEN    93/12/22
     A*         6.DATE-MODIFY
     A*
     A*         *IN38 PROTECT燈號
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前畫面')
     A*****************************************************************
     A          R AR056H
     A*%%TS  SD  19931222  184505  S02CSF      REL-V4R4M0  5769-PW1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 26'東和鋼鐵企業股份有限公司'
     A                                  1 70'ARE056S'
     A                                  2  2TIME
     A                                  2 28'營業發票作廢申請作業'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            U#USID        10A  O  3  2
     A N39        S#MODE         6A  O  3 70DSPATR(RI)
     A*================================================================
     A          R AR056F1
     A*%%TS  SD  19940103  090908  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                  5 28'功　　能:'
     A            S#CODE         1A  B  5 40
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                  5 42'(1:新增 2:修改3:刪除'
     A                                  6 43'4:查詢 5:列印 )'
     A                                  8 28'客戶編號:'
     A            S#CUNO         6A  B  8 40
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                 10 28'訂單編號:'
     A            S#ORNO         6A  B 10 40
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                 12 24'發票開立廠區:'
     A            S#AREA         1A  B 12 40
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A                                 14 24'發票開立日期:'
     A            S#IND1         8Y 0B 14 40EDTWRD('    /  /  ')
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A                                 14 51'~~'
     A            S#IND2         8Y 0B 14 54EDTWRD('    /  /  ')
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A                                 16 24'作廢申請單號:'
     A            S#DLNO         6A  B 16 40
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A                                 18 18'注意：新增及修改才需要輸入發票開+
     A                                      立日期'
     A                                      DSPATR(HI)
     A            S#ERR1        70A  O 23  2DSPATR(HI)
     A                                 24  5'PF03:結束'
     A                                 24 20'PF12:回前'
     A***************************************************************
     A          R AR056F2                   SFL
     A*%%TS  SD  19940216  133551  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT          1A  B 12  5VALUES(' ' '1' '2' '3' '4' '5')
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A            S#INNO        10A  O 12  8
     A            S#INDT         8Y 0O 12 19EDTWRD('    /  /  ')
     A            S#AMT1         8Y 0O 12 30EDTCDE(1)
     A            S#TAX1         9Y 0O 12 41EDTCDE(1)
     A            S#BAM1         9Y 0O 12 53EDTCDE(J)
     A            S#AAMT         9Y 0O 12 66EDTCDE(1)
     A            S#CHOC         1A  O 12 78
     A*================================================================
     A          R AR056F2C                  SFLCTL(AR056F2)
     A*%%TS  SD  19940216  133551  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0010)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A                                      OVERLAY
     A                                      CF10(10 '存檔')
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A                                  5  2'原客戶代號:'
     A            S#CUNO         6A  O  5 16
     A            S#CUNM        10A  O  5 23
     A                                  5 35'原訂單編號:'
     A            S#ORNO         6A  O  5 49
     A                                  5 59'申請日期:'
     A            S#APDT         8Y 0O  5 71EDTWRD('    /  /  ')
     A                                  6  2'發票區間:'
     A            S#IND1         8Y 0O  6 14EDTWRD('    /  /  ')
     A                                  6 25'~'
     A            S#IND2         8Y 0O  6 27EDTWRD('    /  /  ')
     A                                  6 39'發票廠區:'
     A            S#AREA         1A  O  6 51
     A                                  6 59'申請單號:'
     A            S#DLNO         6A  O  6 73DSPATR(HI)
     A                                  8  2'選項-1:選取發票2:取消發票3:+
     A                                      編輯差異計算4:刪除差異計算5:+
     A                                      查詢差異計算'
     A                                      DSPATR(HI)
     A                                  9  2'異常原因說明:'
     A                                      COLOR(PNK)
     A            S#DESP        60O  B  9 18COLOR(PNK)
     A  38                                  DSPATR(PR)
     A                                 10  2'選項'
     A                                 10  9'發票號碼'
     A                                 10 20'發票日期'
     A                                 10 31'發票金額'
     A                                 10 42'發票稅額'
     A                                 10 53'扣預收金額'
     A                                 10 67'未收餘額挑'
     A            S#LIN1        78A  O 11  2
     A*================================================================
     A          R AR056F2M
     A*%%TS  SD  19940131  162633  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                 23  5'F3=結束'
     A                                 23 15'F12=回前畫面'
     A N76                             23 30'F10=存檔'
     A            S#LIN2        78A  O 22  2
     A            S#ERR2        70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR056F3                   SFL
     A*%%TS  SD  19931228  140332  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#WTNO         8A  O 11  3
     A            S#ITEM         2S 0O 11 14
     A            S#PDN1         3A  O 11 19
     A            S#QTY1         7Y 0O 11 23EDTCDE(1)
     A            S#PRC1         5Y 2O 11 33EDTCDE(1)
     A            S#AMT1         8Y 0O 11 40EDTCDE(1)
     A            S#QTY3         7Y 0B 11 51EDTCDE(J)
     A  38                                  DSPATR(PR)
     A  50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A                                      COLOR(RED)
     A            S#PRC3         5Y 2B 11 62EDTCDE(J)
     A  38                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A                                      COLOR(RED)
     A            S#AMT3         8Y 0B 11 70EDTCDE(J)
     A  38                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A                                      COLOR(RED)
     A            S#QTY2         7Y 0B 12 23EDTCDE(1)
     A  38                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                      COLOR(BLU)
     A            S#PRC2         5Y 2B 12 33EDTCDE(1)
     A  38                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A                                      COLOR(BLU)
     A            S#AMT2         8Y 0B 12 40EDTCDE(1)
     A  38                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A                                      COLOR(BLU)
     A            S#TAX2         6Y 0B 12 52EDTCDE(1)
     A  38                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A                                      COLOR(BLU)
     A            S#AAMT         9Y 0B 12 61EDTCDE(1)
     A  38                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A                                      COLOR(BLU)
     A***************************************************************
     A          R AR056F3C                  SFLCTL(AR056F3)
     A*%%TS  SD  19931229  175558  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A                                      CF01(01)
     A                                      CF10(10)
     A N81                                  SFLDSPCTL
     A N82                                  SFLDSP
     A  83                                  SFLCLR
     A N84                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A                                  4  2'新客戶代號：'
     A            S#CUNN         6A  B  4 17
     A  38                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A            S#CUMM        10A  O  4 24
     A                                  4 37'新訂單編號：'
     A            S#ORNN         6A  B  4 52
     A  38                                  DSPATR(PR)
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(RI)
     A            S#LIN3        78A  O  5  2
     A                                  6  2'|<-'
     A                                  6 10'交　　　　貨　　　　資　　　　料-
     A                                      '
     A                                  6 50'->|<-'
     A                                  6 59'差　異　資　料'
     A                                      COLOR(RED)
     A                                  6 78'->|'
     A                                  7  2'磅單編號項次品名'
     A                                  7 25'數　量'
     A                                  7 34'單價'
     A                                  7 42'金　額'
     A                                  7 53'數　量'
     A                                      COLOR(RED)
     A                                  7 63'單價'
     A                                      COLOR(RED)
     A                                  7 71'金　額'
     A                                      COLOR(RED)
     A                                  8 21'|<-'
     A                                      COLOR(BLU)
     A                                  8 25'　新　　開　　立　　發　　票　　+
     A                                      內　　容　'
     A                                      COLOR(BLU)
     A                                  8 70'->|'
     A                                      COLOR(BLU)
     A                                  9 25'數　量'
     A                                      COLOR(BLU)
     A                                  9 34'單價'
     A                                      COLOR(BLU)
     A                                  9 42'金　額'
     A                                      COLOR(BLU)
     A                                  9 53'稅額'
     A                                      COLOR(BLU)
     A                                  9 62'應收帳款'
     A                                      COLOR(BLU)
     A            S#LIN4        78A  O 10  2
     A***************************************************************
     A          R AR056F3M
     A*%%TS  SD  19940216  133727  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A            S#LIN5        78A  O 22  2
     A                                 23  2'F03:結束'
     A                                 23 13'F12:回前畫面'
     A N76                             23 28'ENTER:檢核'
     A N76                             23 41'F10:存檔'
     A            S#ERR3        70A  O 24  2DSPATR(HI)
