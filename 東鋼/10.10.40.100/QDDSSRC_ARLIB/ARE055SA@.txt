     A*%%TS  SD  19931109  165218  S02CSF      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE055S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02CSF
     A*            4.FUNCTION     營業退款過入財會處理作業
     A*            5.DATE-WRITTEN  93/08/05
     A*            6.DATE-MODIFY
     A*
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QPRINT)
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前畫面')
     A*================================================================
     A          R AR055H
     A*%%TS  SD  19931013  111129  S02CSF      REL-V4R4M0  5769-PW1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 26'東和鋼鐵企業股份有限公司'
     A                                  1 70'ARE055S'
     A                                  2  2TIME
     A                                  2 26'營業退款過入財會處理作業'
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3  2
     A*================================================================
     A          R AR055F1                   SFL
     A*%%TS  SD  19931013  142130  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT1         1A  B  9  5VALUES(' ' '1' '4' '5')
     A  31                                  DSPATR(RI)
     A            S#RTNO         6A  O  9 11
     A            S#RTDT         8Y 0O  9 19EDTWRD('    /  /  ')
     A            S#CUNO         6A  O  9 30
     A            S#CUNM        10O  O  9 37
     A            S#ORNO         6A  O  9 49
     A            S#AAMT         8Y 0O  9 58EDTCDE(2)
     A            S#TRNO        10A  O  9 69
     A            S#TRDT         8Y 0H
     A*================================================================
     A          R AR055F1C                  SFLCTL(AR055F1)
     A*%%TS  SD  19931013  142334  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CF05(05 '上傳資料')
     A                                      CF06(06 '下送資料')
     A                                      ROLLUP(91)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A            S#SFN1         4S 0H
     A                                  4  2'選項:'
     A                                  4 10'1=製票'
     A                                  4 19'4=取消製票'
     A                                  4 32'5=查詢'
     A            S#LIN1        78A  O  5  2
     A                                  6  2'選項'
     A                                  6  9'退款單號'
     A                                  6 20'退款日期'
     A                                  6 31'客　　　　戶'
     A                                  6 47'訂單編號'
     A                                  6 58'退款金額'
     A                                  6 69'製票號碼'
     A            S#LIN2        78A  O  7  2
     A            S#KEY1         6A  B  8 11
     A  30                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A*================================================================
     A          R AR055F1M
     A*%%TS  SD  19931109  165218  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      TEXT('按鍵及錯誤訊息')
     A            S#LIN3        78A  O 21  2
     A                                 22  4'F03:結束'
     A                                 22 15'F12:回前'
     A  60                             22 26'F05:抓取廠區退款資料'
     A  60                             22 49'F06:下送台北製票資料'
     A            S#ERR1        70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR055F2                   SFL
     A*%%TS  SD  19930803  142230  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#SFI2         2Y 0O 11  3
     A            S#DC           1A  O 11  9
     A                                      DSPATR(UL)
     A            S#ACNO         4A  O 11 15
     A                                      DSPATR(UL)
     A            S#RLNO        10A  O 11 23
     A                                      DSPATR(UL)
     A            S#DUDT         8Y 0O 11 34EDTWRD('    /  /  ')
     A                                      DSPATR(UL)
     A            S#CSNM        10O  O 11 45
     A                                      DSPATR(UL)
     A            S#DSC2        12O  O 11 56
     A                                      DSPATR(UL)
     A            S#AAMT         8Y 0O 11 69EDTCDE(1)
     A                                      DSPATR(UL)
     A            S#DSC1        60O  O 12 12
     A                                      DSPATR(UL)
     A***************************************************************
     A          R AR055F2C                  SFLCTL(AR055F2)
     A*%%TS  SD  19931013  144627  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A                                  4  7'退款編號:'
     A            S#RTNO         6A  O  4 19
     A                                  4 30'退款部門:'
     A            S#DPNO         4A  O  4 42
     A                                  4 49'退款日期:'
     A            S#RTDT         8Y 0O  4 61EDTWRD('    /  /  ')
     A                                  5  7'客戶編號:'
     A            S#CUNO         6A  O  5 19
     A                                  5 26'受款人:'
     A            S#FNAM        36O  O  5 36
     A                                  6 49'退款金額:'
     A            S#OAMT         8Y 0O  6 61EDTCDE(1)
     A                                      DSPATR(HI)
     A            S#LIN4        78A  O  7  2
     A                                  8  2'項'
     A                                  8  7'借'
     A                                  8 12'會計科目'
     A                                  8 23'相關號碼'
     A                                  8 35'到期日'
     A                                  8 46'對象別'
     A                                  8 57'輔助摘要'
     A                                  8 72'金額'
     A                                  9  2'次'
     A                                  9  7'貸'
     A                                  9 12'摘　　　　　要'
     A            S#LIN5        78A  O 10  2
     A***************************************************************
     A          R AR055F2M
     A*%%TS  SD  19931013  144627  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A            S#LIN6        78A  O 22  2
     A                                 23  2'F03:結束'
     A                                 23 13'F12:回前'
     A            S#ERR2        70A  O 24  2DSPATR(HI)
