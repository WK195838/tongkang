     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARP051T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     日期訂單別磅單重量之差異分析表
     A*           4.DATE-WRITTEN  86/04/26
     A*           5.WRITER        S02CLJ
     A*
     A*****************************************************************
     A          R AR051T1H
     A                                      SKIPB(003)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      ' PRT-ID  : ARP051T'
     A                                    45
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    94
     A                                      '列表日期:'
     A                                   105
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      ' USR-ID  :'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                   +20
     A                                      '日期客戶別磅單重量差異分析表'
     A                                   +21
     A                                      '列表時間:'
     A                                   105
     A                                      TIME
     A                                     2
     A                                      '統計期間:'
     A                                      SPACEB(001)
     A            R#YMS          6S  O    14
     A                                      EDTWRD('  /  /  ')
     A                                    22
     A                                      '-'
     A            R#YME          6S  O    23
     A                                      EDTWRD('  /  /  ')
     A            R#AREA         8A  O    33
     A                                    94
     A                                      '頁　　次:'
     A                                   105
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '客戶:'
     A                                      SPACEB(001)
     A            R#CUNO         5A  O    10
     A            R#CUNM        10A  O    16
     A*----------------------------------------------------------------*********
     A                                     4
     A                                      '訂單號碼'
     A                                      SPACEB(002)
     A                                    16
     A                                      '磅單號碼'
     A                                    26
     A                                      '進出大類'
     A                                    36
     A                                      '異動代號'
     A                                    49
     A                                      '地磅出貨重'
     A                                    69
     A                                      '營業銷貨重'
     A                                    88
     A                                      '地磅重減營業重'
     A                                     4
     A                                      '---------    --------  --------  --
     A                                      -------    ----------------    -----
     A                                      ------------    ----------------'
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR051T1D
     A                                      SPACEA(001)
     A            R#ORNO         6A  O     7
     A            R#WTNO         8A  O    17
     A            R#VNO1         2A  O    27
     A                                    30
     A                                      '-'
     A            R#GROP         1A  O    32
     A            R#TRCD         2A  O    38
     A            R#PDWT        12S 0O    49
     A                                      EDTCDE(K)
     A            R#SA04        12S 0O    69
     A                                      EDTCDE(K)
     A            R#PADF        12S 0O    89
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR051T1S
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A*----------------------------------------------------------------*********
     A                                    13
     A                                      '訂單小計:'
     A            R#STWT        12S 0O    49
     A                                      EDTCDE(K)
     A            R#ST04        12S 0O    69
     A                                      EDTCDE(K)
     A            R#STDF        12S 0O    89
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR051T1T
     A                                      SPACEB(002)
     A*----------------------------------------------------------------*********
     A                                    13
     A                                      '客戶合計:'
     A            R#TTWT        12S 0O    49
     A                                      EDTCDE(K)
     A            R#TT04        12S 0O    69
     A                                      EDTCDE(K)
     A            R#TTDF        12S 0O    89
     A                                      EDTCDE(K)
     A*****************************************************************
     A          R AR051T1M
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A*----------------------------------------------------------------
     A                                    19
     A                                      '*****無條件符合之資料！！'
