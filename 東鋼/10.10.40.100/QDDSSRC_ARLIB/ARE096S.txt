     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE096S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02LBU
     A*            4.FUNCTION     發票暫不開立維護作業
     A*            5.DATE-WRITTEN  98/01/16
     A*            6.DATE-UPDATED
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前畫面')
     A*================================================================
     A          R AR096F1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 28'東和鋼鐵企業股份有限公司'
     A                                  1 70'ARE096S-1'
     A                                  2  2TIME
     A                                  2 28'★發票暫不開立維護作業★'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3  2
     A*========
     A                                  9 27'訂單號碼:'
     A            S#ORNO         6   B  9 39
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A*
     A                                 11 27'出貨日期:'
     A            S#DATS         8  0B 11 39EDTWRD('    /  /  ')
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A*
     A                                 11 50'~'
     A            S#DATE         8  0B 11 52EDTWRD('    /  /  ')
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A*
     A                                 13 28'(此程式僅針對尚未開立之發票做維+
     A                                       護)'
     A                                      COLOR(RED)
     A*========
     A                                 23  3'PF3=結束作業'
     A                                 23 20'PF12=回前畫面'
     A            S#MSG1        70A  O 24  2DSPATR(HI)
     A*****************************************************************
     A          R AR096F2                   SFL
     A  88                                  SFLNXTCHG
     A            S#OPT          1A  B  8  4
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A            S#CUNO         6A  O  8  8
     A            S#CUNM        10A  O  8 16
     A            S#ORN1         9A  O  8 27
     A            S#NO           8A  O  8 37
     A            S#PDNO         3A  O  8 46
     A            S#UPRC         5  3O  8 51EDTWRD('  .   ')
     A            S#QTY          7Y 0O  8 59EDTWRD(' ,   ,   -')
     A            S#INNO        10A  O  8 70
     A            S#ITEM         2S 0H
     A***************************************************************
     A          R AR096F2C                  SFLCTL(AR096F2)
     A                                      SFLSIZ(0025)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CF05(05 'Ｙ全選')
     A                                      CF06(06 'Ｎ全選')
     A                                      CF10(10 '存檔')
     A  71                                  SFLCLR
     A N72                                  SFLDSP
     A N73                                  SFLDSPCTL
     A  74                                  SFLEND
     A            S#NBR          4S 0H      SFLRCDNBR(CURSOR)
     A*----------
     A                                  1 70'ARE096S-2'
     A                                  1 28'東和鋼鐵企業股份有限公司'
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'★發票暫不開立維護作業★'
     A*----------
     A                                  4  2'Ｙ：上暫不開立碼　Ｎ：清除暫不開+
     A                                      立碼'
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6  2'選　客戶編號　客戶名稱　訂單編號+
     A                                      　磅單編號　品　單　價'
     A                                  6 59'數　　量'
     A                                  6 70'發票號碼'
     A***************************************************************
     A          R AR096F2M
     A                                 21  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A            S#MSG2        70A  O 23  2DSPATR(HI)
     A                                 24  3'ENTER:檢核'
     A                                    +1'PF03:結束'
     A                                    +1'PF05:Ｙ全選'
     A                                    +1'PF06:Ｎ全選'
     A                                    +1'PF10:存檔'
     A                                    +1'PF12:前畫面'
