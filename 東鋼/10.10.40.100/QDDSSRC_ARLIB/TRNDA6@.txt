     H*****************************************************************
     H*
     H*            *****   PROGRAM INTRODUCTION   *****
     H*
     H*            1.PROGRAM-ID    TRNDA6
     H*            2.PROGRAM-TYPE  PF
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION     型鋼折讓發票上傳介面檔
     H*            5.DATE-WRITTEN  108/04/26 2019AR00025
     H*            6.UPDATE BY
     H*
     H*****************************************************************
     A*
     A                                      UNIQUE
     A          R RTRNDA6                   TEXT('銷貨明細檔')
     A            A6FLAG         1A         COLHDG('處理代碼')
     A            A6CODE         4A         COLHDG('單據代號')
     A            A6NO           8A         COLHDG('單據號碼')
     A            A6ITEM         2S 0       COLHDG('項次')
     A            A6ACNT         1A         COLHDG('原因別')
     A            A6DATE         8S 0       COLHDG('單據日期')
     A            A6ACDT         8S 0       COLHDG('入帳日期')
     A            A6CUNO         6A         COLHDG('客戶代號')
     A            A6CUNM        10O         COLHDG('客戶名稱')
     A            A6ORNO         9A         COLHDG('訂單編號')
     A            A6IVNO        10A         COLHDG('發票號碼')
     A            A6PCNO         8A         COLHDG('磅單號碼')
     A            A6VUNO         7A         COLHDG('傳票號碼')
     A            A6RVID         2A         COLHDG('收款業務員')
     A            A6SALE         2A         COLHDG('出貨業務員')
     A            A6SATP         1A         COLHDG('銷售別')
     A            A6IVTP         1A         COLHDG('發票別')
     A            A6PDNM         5A         COLHDG('品名代號')
     A            A6QTY          7P 0       COLHDG('數量')
     A            A6UPRC         5P 3       COLHDG('單價')
     A            A6AMT         11P 0       COLHDG('金額')
     A            A6TAX         11P 0       COLHDG('稅額')
     A            A6FL01         1A         COLHDG('折讓確認碼')
     A            A6FL02         1A         COLHDG('過發票碼')
     A            A6TXAR         1A         COLHDG('異動廠區')
     A            A6TXDT         8S 0       COLHDG('異動日期')
     A            A6RESV        30A         COLHDG('保留碼')
     A****
     A*           註:保留碼第十位元乃未傳台北旗標
     A*                保留碼第十一依及十二位為異動代碼
     A*                保留碼第十三及十四位為磅單項次
     A*                保留碼第十六及十七為東鋼營造用
     A*                保留碼第十六及十七放代加工異動代號1J(高雄)
     A*                保留碼第十八配合電子發票上送，放折讓種類
     A*                 1.客戶開立折讓單
     A*                 2.東和開立折讓單
     A****
     A            A6CHYN         1A         COLHDG('折讓分攤碼')
     A            A6METL         8A         COLHDG('鋼種')
     A            A6SPE1         5A         COLHDG(' H/T/D ')
     A            A6SPE2         5A         COLHDG(' B/W ')
     A            A6SPE5         7A         COLHDG('長度')
     A            A6SPE3         3A         COLHDG(' T1 ')
     A****
     A            A6CTNU        13A         COLHDG('序號')
     A            A6CTRL         1A         COLHDG('轉檔碼')
     A            A6TMDT         8S 0       COLHDG('轉檔日期')
     A            A6TMTM         6S 0       COLHDG('轉檔時間')
     A            A6TMUS        10A         COLHDG('轉檔人員')
     A            A6MARK        10A         COLHDG('備註')
     A****
     A          K A6CTNU
     A          K A6CUNO
     A          K A6ORNO
     A          K A6IVNO
     A          K A6PDNM
     A          K A6UPRC
     A          K A6METL
     A          K A6SPE1
     A          K A6SPE2
     A          K A6SPE3
     A          K A6SPE5
