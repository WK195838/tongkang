     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR150T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     勤美公司出貨補價差統計表
     A*           4.DATE-WRITTEN  107/01/26 2018AR1126
     A*           5.WRITER        S00WCJ
     A*           6.UPDATE DATE
     A*
     A*****************************************************************
     A          R AR150TH
     A**************************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A                                     2
     A                                      '報表代號: ARR150T'
     A                                    35
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    73
     A                                      '列表日期:'
     A                                    +1
     A                                      DATE
     A                                      EDTWRD('01  /  /  ')
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            T#USID        10        +1
     A                                    35
     A                                      '勤美公司出貨補價差統計表'
     A                                    73
     A                                      '列表時間:'
     A                                    +1
     A                                      TIME
     A                                    73
     A                                      '頁　　次:'
     A                                      SPACEB(001)
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '日期區間:'
     A                                      SPACEB(001)
     A            T#DATS         8  0     +1
     A                                      EDTWRD('    /  /  ')
     A                                    +1'-'
     A            T#DATE         8  0     +1
     A                                      EDTWRD('    /  /  ')
     A                                     3
     A                                      '客戶編號'
     A                                      SPACEB(001)
     A                                    +0
     A                                      '客戶名稱'
     A                                    24
     A                                      '訂單編號'
     A                                    35
     A                                      '庫別'
     A                                    44
     A                                      '品名'
     A                                    53
     A                                      ' 出貨量 '
     A                                    66
     A                                      '價差'
     A                                    77
     A                                      '金額'
     A            T#LIN2        96         1
     A                                      SPACEB(001)
     A*%%***********************************************************************
     A          R AR150T1
     A*%%***********************************************************************
     A                                     2
     A                                      ' '
     A                                      SPACEB(001)
     A N60        T#CUNO         6         3
     A N60        T#CUNM        10        13
     A N61        T#ORNO         6        25
     A            T#STNO         4        36
     A            T#PDNM         5        45
     A            T#QTY          8  0     52EDTCDE(N)
     A            T#SAMT         4  2     66EDTCDE(N)
     A            T#AAMT        11  0     74EDTCDE(N)
     A*****************************************************************
     A          R AR150T2
     A*%%***********************************************************************
     A            T#LIN1        96         1
     A                                      SPACEB(001)
     A                                    33
     A                                      '訂單小計:'
     A                                      SPACEB(001)
     A            T1QTY          9  0     51EDTCDE(N)
     A            T1AAMT        12  0     73EDTCDE(N)
     A                                    33
     A                                      '價差計算公式:'
     A                                      SPACEB(001)
     A            T1SAUE        20        +1
     A                                      SPACEA(001)
     A*****************************************************************
     A          R AR150T3
     A*%%***********************************************************************
     A            T#LIN1        96         1
     A                                      SPACEB(001)
     A                                    33
     A                                      '總  計:'
     A                                      SPACEB(001)
     A            T2QTY          9  0     51EDTCDE(N)
     A            T2AAMT        12  0     73EDTCDE(N)
     A*****************************************************************
     A          R AR150T4
     A*%%***********************************************************************
     A                                      SKIPB(063)
     A                                     3
     A                                      '核准:__________________'
     A                                    60'經辦:__________________'
     A*****************************************************************
     A          R AR150TR
     A*%%***********************************************************************
     A                                      SKIPB(010)
     A                                    40
     A                                      '查無區間內資料'
