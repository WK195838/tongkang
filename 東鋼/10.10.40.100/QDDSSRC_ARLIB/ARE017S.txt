     A                                      DSPSIZ(24 80 *DS3)
     A*                                     REF(*LIBL/RCVDTL)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARE017S-1'
     A            S#COMP        35A  O  1 21
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 18'*** 暫收未兌現票據退回申請－暫+
     A                                       收其它 ***'
     A            S#DEVN        10A  O  2 70
     A*=====
     A                                  5 30'功　　能:'
     A            S#ACT          1A  B  5 42
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  5 46'(1:新增 2:修改'
     A                                  7 46' 3:刪除 4:查詢)'
     A*
     A                                  9 30'客戶代號:'
     A            S#CUN          6A  B  9 42
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A*
     A                                 11 30'訂單編號:'
     A            S#ORNO         6A  B 11 42
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A*
     A                                 13 30'繳款單號:'
     A            S#RNO          6A  B 13 42
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A*
     A                                 15 30'退回單號:'
     A            S#WRNO         6A  B 15 42
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A*=====
     A            S#ERR         70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            S#SRL1         2S 0O  9  2
     A            S#NTNO        20A  B  9  5
     A  70                                  DSPATR(PR)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A            S#PBID        14O  O  9 26
     A            S#PANO        10A  O  9 41
     A            S#NTTP         1A  O  9 53
     A            S#USTP         2A  O  9 58
     A            S#PLAC         1A  O  9 64
     A            S#RAMT         9Y 0O  9 68EDTWRD('   ,   ,   -')
     A            S#ACD1         1A  B 10 11VALUES('A' 'C' 'D' ' ')
     A  70
     AO 71                                  DSPATR(PR)
     A            S#DPNO         4A  O 10 18
     A            S#SANO        10A  O 10 28
     A            S#RLNO        10A  O 10 41
     A            S#DUDT         8Y 0O 10 57
     A                                      EDTWRD('    /  /  ')
     A            S#AMCD         4A  O 10 72
     A            H#TAMT         9Y 0H
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A                                      SFLSIZ(0037)
     A                                      SFLPAG(0006)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARE017S-2'
     A            S#COMP        35A  O  1 21
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#MOD          6A  O  2  2DSPATR(RI)
     A                                  2 18'*** 暫收未兌現票據退回申請－暫+
     A                                       收其它 ***'
     A            S#DEVN        10A  O  2 70
     A                                  3 32'退回單號:'
     A            S#WRNO         6A  O  3 44
     A                                  3  1'客戶編號:'
     A            S#CUNO         6A  B  3 13
     A  70                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A            S#CUNM        10O  O  3 21
     A                                  4  1'申請日期:'
     A            S#DATE         8  0B  4 13
     A                                      EDTWRD('    /  /  ')
     A  70                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A                                  4 32'繳款單號:'
     A            S#RNO          6A  O  4 44
     A                                  4 60'合計:'
     A                                      DSPATR(RI)
     A            S#TAMT         9Y 0O  4 68EDTCDE(J)
     A                                      DSPATR(RI)
     A                                  6  1'項　票據號碼'
     A                                  6 27'付款銀行'
     A                                  6 40'付款帳戶　票款　款項　地點'
     A*                                 6  6'票據號碼'
     A                                  6 69'金　　額'
     A                                  7  1'次'
     A                                  7 17'繳款部門　存入帳號'
     A                                  7 40'相關號碼'
     A                                  7 58'到期日期'
     A                                  7 69'資金代號'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  9'標記'
     A***************************************************************
     A          R UND01
     A                                      OVERLAY
     A                                 21  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  5'PF10存檔'
     A                                 23 20'PF12回前畫面'
     A            S#ERR         70A  O 24  2DSPATR(HI)
     A***************************************************************
