     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR131T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     外銷出口報單核對明細表
     A*           4.DATE-WRITTEN  97/02/20
     A*           5.WRITER        S02LBU
     A*
     A*
     A*****************************************************************
     A          R AR131T1H
     A*****************************************************************
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報表代號: ARR131T'
     A                                    60
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   116
     A                                      '列表日期:'
     A                                   127
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                    61
     A                                      '外銷出口報單核對明細表'
     A                                   116
     A                                      '列表時間:'
     A                                   127
     A                                      TIME
     A                                     2
     A                                      '報單號碼:'
     A                                      SPACEB(001)
     A            T#APNO        20A  O    14
     A                                   116
     A                                      '頁　　次:'
     A                                   127
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*----------------------------------------------------------------
     A                                     2
     A                                      '客戶名稱'
     A                                      SPACEB(002)
     A                                    18
     A                                      '報關匯率'
     A                                    37
     A                                      '報關日期'
     A                                    50
     A                                      '訂單號碼'
     A                                    63
     A                                      '廠區'
     A                                    72
     A                                      '重　　　量'
     A                                    87
     A                                      '美　金　金　額'
     A                                   106
     A                                      '台　幣　金　額'
     A                                   125
     A                                      '外銷國別'
     A*----------------------------------------------------------------
     A                                     2
     A                                      '----------'
     A                                      SPACEB(001)
     A                                    18
     A                                      '----------'
     A                                    37
     A                                      '----------'
     A                                    50
     A                                      '----------'
     A                                    63
     A                                      '------'
     A                                    72
     A                                      '------------'
     A                                    87
     A                                      '----------------'
     A                                   106
     A                                      '----------------'
     A                                   125
     A                                      '----------'
     A**************************************************************************
     A          R AR131T1D
     A**************************************************************************
     A            T#BLK          1   O     1
     A                                      SPACEB(001)
     A            T#CUNM        10O  O     2
     A            T#TRAT         7  4O    19
     A                                      EDTCDE(1)
     A            T#DATE         8  0O    37
     A                                      EDTWRD('    /  /  ')
     A            T#ORNO         6A  O    52
     A            T#AREA         1A  O    65
     A            T#QTY          9  0O    72
     A                                      EDTCDE(1)
     A            T#UAMT        13  2O    85
     A                                      EDTCDE(1)
     A            T#NAMT        11  0O   107
     A                                      EDTCDE(1)
     A            T#WKAR        10   O   125
     A*****************************************************************
     A          R AR131T1S
     A*****************************************************************
     A            T#TXAR         1   O    57
     A                                      SPACEB(001)
     A                                    58
     A                                      '廠小計：'
     A            T#SQTY         9  0     72
     A                                      EDTCDE(1)
     A            T#SUAT        13  2     85
     A                                      EDTCDE(1)
     A            T#SNAT        11  0    107
     A                                      EDTCDE(1)
     A*****************************************************************
     A          R AR131T2S
     A*****************************************************************
     A                                    56
     A                                      '報單小計：'
     A                                      SPACEB(001)
     A            T#AQTY         9  0     72
     A                                      EDTCDE(1)
     A            T#AUAT        13  2     85
     A                                      EDTCDE(1)
     A            T#ANAT        11  0    107
     A                                      EDTCDE(1)
     A*****************************************************************
     A          R AR131T1T
     A*****************************************************************
     A            T#TXAR         1   O    57
     A                                      SPACEB(001)
     A                                    58
     A                                      '總合計：'
     A            T#TQTY         9  0     72
     A                                      EDTCDE(1)
     A            T#TUAT        13  2     85
     A                                      EDTCDE(1)
     A            T#TNAT        11  0    107
     A                                      EDTCDE(1)
     A*****************************************************************
     A          R AR131T1M
     A*****************************************************************
     A                                    19
     A                                      '*****無條件符合之資料！！'
     A                                      SPACEB(005)
     A*****************************************************************
