     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE097S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR
     A*            4.FUNCTION     新桃工程自用每月單價分攤維護作業
     A*            5.DATE-WRITTEN
     A*            6.DATE-MODIFY   98/02/19
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*================================================================
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 28'東和鋼鐵企業股份有限公司'
     A                                  1 70'ARE097S-1'
     A                                  2 26'**工程自用每月單價分攤維護** '
     A            S#DEVN        10A  O  2 70
     A*=========
     A                                  8 30'年　　月:'
     A            S#YM           6  0B  8 42EDTCDE(Z)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A*
     A                                 10 30'來源廠區:'
     A            S#AREA         1A  B 10 42
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A                                 12 30'異動代號:'
     A            S#TRCD         2A  B 12 42
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A                                 14 30'目標客戶:'
     A            S#CUNO         6A  B 14 42
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A*=========
     A            S#ERR         70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            S#ITEM         2Y 0O  9  4
     A*
     A            S#PDNM         3A  O  9 10
     A*
     A            S#QTY          9Y 0O  9 18EDTCDE(1)
     A*
     A            S#AMT         11Y 0B  9 35EDTCDE(1)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CF03(03 '離開')
     A                                      CF10(10 '存檔')
     A                                      CF12(12 '回前畫面')
     A  84                                  SFLCLR
     A N85                                  SFLDSP
     A N86                                  SFLDSPCTL
     A  75                                  SFLEND
     A            S#NBR          4S 0H      SFLRCDNBR(CURSOR)
     A*-----------
     A                                  1 70'ARE097S-2'
     A                                  1 28'東和鋼鐵企業股份有限公司'
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 26'**工程自用每月單價分攤維護** '
     A            S#DEVN        10A  O  2 70
     A*-----------
     A                                  5  4'年月:'
     A            S#YYMM         6Y 0O  5 12EDTWRD('    /  ')
     A*
     A                                  5 22'廠區:'
     A            S#TXAR         1A  O  5 30
     A*
     A                                  5 34'異動代號:'
     A            S#TRC1         2A  O  5 46
     A*
     A                                  5 51'目標客戶:'
     A            S#CUN1         6A  O  5 63
     A*
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  2'項次'
     A                                  7  9'品名'
     A                                  7 21'數　量'
     A                                  7 37'總　金　額'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A*****************************************************************
     A          R UND01
     A                                 21  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A                                      DSPATR(HI)
     A                                 22  8'合　計:'
     A            S#QTYS         9  0O 22 18EDTCDE(1)
     A            S#AMTS        11  0O 22 35EDTCDE(1)
     A            S#MSG         70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                    +1'PF03:結束'
     A                                    +1'PF10:存檔'
     A                                    +1'PF12:回前畫面'
     A*****************************************************************
