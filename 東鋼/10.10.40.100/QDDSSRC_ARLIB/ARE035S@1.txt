     A*%%TS  SD  19900919  161256  S02YSH      REL-V4R4M0  5769-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE035R
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     發票維護作業異常版
     A*            5.DATE-WRITTEN  86/03/29
     A*
     A***************************************************************
     A*  畫面一:發票選擇畫面
     A***************************************************************
     A          R AR035F1                   SFL
     A*%%TS  SD  19900919  161256  S02YSH      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT1         1A  B  9  5VALUES('1' '2' ' ')
     A  52                                  DSPATR(RI)
     A            S#INTP         1A  O  9  8
     A            S#INNO        10A  O  9 10
     A            S#INDT         8Y 0O  9 22EDTWRD('    /  /  ')
     A            S#INAR         1A  O  9 34
     A            S#INKD         1A  O  9 37
     A            S#INDE         1A  O  9 40
     A            S#INAM        11Y 0O  9 44EDTWRD('  ,   ,   ,   -')
     A            S#INTX        11Y 0O  9 63EDTWRD('  ,   ,   ,   -')
     A***************************************************************
     A          R AR035F1C                  SFLCTL(AR035F1)
     A*%%TS  SD  19900919  161256  S02YSH      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      CA05(05)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A            S#SFN1         4S 0H
     A                                  1 70'ARE035S-1'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 28'★發票維護作業異常版★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A                                  4  2'選項:１－＞選擇發票明細　　　+
     A                                      　　　２－＞編輯發票資料　'
     A                                      DSPATR(RI)
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6  2'選項'
     A                                  6  9'發票號碼'
     A                                  6 21'日期'
     A                                  6 30'廠區聯式作廢'
     A                                  6 51'金　額'
     A                                  6 70'稅　額'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#INK1        10A  B  8 10
     A  51                                  DSPATR(RI)
     A  81                                  DSPATR(PC)
     A            S#INK2         8Y 0B  8 22EDTWRD('    /  /  ')
     A  51                                  DSPATR(RI)
     A            S#INK3         1A  B  8 34
     A  51                                  DSPATR(RI)
     A***************************************************************
     A          R AR035F1M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  2'ENTER:執行'
     A                                      DSPATR(HI)
     A                                 23 21'F3:結束本作業'
     A                                      DSPATR(HI)
     A                                 23 41'F5:重新顯示'
     A                                      DSPATR(HI)
     A                                 23 61'F12:回前畫面'
     A                                      DSPATR(HI)
     A            S#MSG1        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面二:選擇發票明細畫面
     A***************************************************************
     A          R AR035F2                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT2         1A  B 12  4VALUES('2' ' ')
     A  52                                  DSPATR(RI)
     A            S#DECD         1A  O 12  8
     A            S#ACNT         2A  O 12 12
     A            S#ITEM         2Y 0O 12 15
     A            S#PDNM         3A  O 12 20
     A            S#QTY          7Y 0O 12 25EDTWRD(' ,   ,   -')
     A            S#UPRC         7Y 5O 12 37EDTWRD(' 0.     ')
     A            S#AMT         11Y 0O 12 49EDTWRD('  ,   ,   ,   -')
     A            S#APNO         8A  O 12 67
     A***************************************************************
     A          R AR035F2C                  SFLCTL(AR035F2)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE035S-2'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'★發票明細選擇作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  4  2'發票號碼:'
     A            S#INNO        10   O  4 14
     A                                  5  2'客戶編號:'
     A            S#CUNO         6A  O  5 14
     A                                  5 21'客戶簡稱:'
     A            S#CUNM        10O  O  5 33
     A                                  5 45'訂單號碼:'
     A            S#ORNO         6A  O  5 57
     A                                  6  2'發票類別:'
     A            S#TYPE         1A  O  6 14
     A                                  6 21'發票日期:'
     A            S#INDT         8Y 0O  6 33EDTCDE(4)
     A                                  6 45'課稅別:'
     A            S#TXTP         1A  O  6 55
     A                                  6 62'發票聯式:'
     A            S#KIND         1A  O  6 74
     A                                  7  2'銷售別:'
     A            S#SATP         1A  O  7 12
     A                                  7 21'出貨業務員:'
     A            S#SALE         2A  O  7 35
     A                                  7 45'收款業務員:'
     A            S#RVID         2A  O  7 59
     A                                  7 62'開立廠區:'
     A            S#AREA         1A  O  7 74
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9  2'選項:２－＞編輯發票明細　'
     A                                      DSPATR(RI)
     A                                 10  2'選項　種類　項次　品名'
     A                                 10 29'數量'
     A                                 10 40'單價'
     A                                 10 54'金　　額'
     A                                 10 66'憑証編號'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR035F2M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  2'ENTER:執行'
     A                                      DSPATR(HI)
     A                                 23 21'F3:結束本作業'
     A                                      DSPATR(HI)
     A                                 23 43'F12:回前畫面'
     A                                      DSPATR(HI)
     A            S#MSG2        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面三:編輯發票主檔畫面
     A***************************************************************
     A          R AR035F3H
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      CF10(10)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A*----------------------------------------------------------------
     A                                  1 70'ARE035S-3'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'★發票主檔編輯作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  4 24'發票號碼:'
     A                                      DSPATR(HI)
     A            INNO          10A  O    +1
     A                                      DSPATR(HI)
     A                                  5  4'處理代碼:　　'
     A            INFLAG         1A  B    +1
     A  62                                  DSPATR(PR)
     A                                  5 44'發票類別:　　'
     A            INTYPE         1A  B    +1
     A  62                                  DSPATR(PR)
     A                                  6  4'作廢碼:　　　'
     A            INDECD         1A  B    +1
     A  62                                  DSPATR(PR)
     A                                  6 44'作廢日期:　　'
     A            INDEDT         8Y 0B    +1
     A                                      EDTCDE(M)
     A  62                                  DSPATR(PR)
     A                                  7  4'客戶編號:　　'
     A            INCUNO         6A  B    +1
     A  62                                  DSPATR(PR)
     A                                  7 44'客戶簡稱:　　'
     A            INCUNM        10O  B    +1
     A  62                                  DSPATR(PR)
     A                                  8  4'訂單號碼:　　'
     A            INORNO         6A  B    +1
     A  62                                  DSPATR(PR)
     A                                  8 44'發票日期:　　'
     A            ININDT         8Y 0B    +1
9009 A                                      EDTWRD('    /  /  ')
9009 A*                                     EDTCDE(Y)
     A  62                                  DSPATR(PR)
     A                                  9  4'發票聯式:　　'
     A            INKIND         1A  B    +1
     A  62                                  DSPATR(PR)
     A                                  9 44'出貨業務員:　'
     A            INSALE         2A  B    +1
     A  62                                  DSPATR(PR)
     A                                 10  4'收款業務員:　'
     A            INRVID         2A  B    +1
     A  62                                  DSPATR(PR)
     A                                 10 44'銷售別:　　　'
     A            INSATP         1A  B    +1
     A  62                                  DSPATR(PR)
     A                                 11  4'開立廠區:　　'
     A            INAREA         1A  O    +1
     A                                 11 44'課稅別:　　　'
     A            INTXTP         1A  B    +1
     A                                      VALUES('1' '2' '3')
     A  62                                  DSPATR(PR)
     A                                 12  4'出貨金額:　　'
     A            INAAMT        11Y 0O    +1
     A                                      EDTCDE(L)
     A                                      DSPATR(UL)
     A                                 12 44'銷項稅額:　　'
     A            INATAX        11Y 0O    +1
     A                                      EDTCDE(L)  DSPATR(UL)
     A                                 13  4'扣預收貨款:　'
     A            INBAMT        11Y 0O    +1
     A                                      EDTCDE(L)
     A                                      DSPATR(UL)
     A                                 13 44'折讓銷退金額:'
     A            INCAMT        11Y 0O    +1
     A                                      EDTCDE(L)
     A                                      DSPATR(UL)
     A                                 14  4'折讓銷退稅額:'
     A            INCTAX        11Y 0O    +1
     A                                      EDTCDE(L)
     A                                      DSPATR(UL)
     A                                 14 44'扣預收金額沖回:'
     A            INDAMT        11Y 0O    +1
     A                                      EDTCDE(L)
     A                                      DSPATR(UL)
     A                                 15  4'扣預收稅額沖回:'
     A            INDTAX        11Y 0O    +1
     A                                      EDTCDE(L)
     A                                      DSPATR(UL)
     A                                 15 44'繳款金額:　　'
     A            INEAMT        11Y 0O    +1
     A                                      EDTCDE(L)
     A                                      DSPATR(UL)
     A                                 16  4'退票金額:　　'
     A            INFAMT        11Y 0O    +1
     A                                      EDTCDE(L)
     A                                      DSPATR(UL)
     A                                 16 44'未收餘額:　　'
     A            INNBAL        11Y 0O    +1
     A                                      EDTCDE(L)
     A                                      DSPATR(UL)
     A                                 17  4'運費金額:　　'
     A            INMAMT        11Y 0O    +1
     A                                      EDTCDE(L)
     A                                      DSPATR(UL)
     A                                 17 44'請款單號:　　'
     A            INAPNO         7A  B    +1
     A                                      DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 18  4'請款日期:　　'
     A            INAPDT         8Y 0B    +1
     A                                      EDTCDE(M)
     A                                      DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 18 44'繳款單號:　　'
9009 A            INRCNO         6A  B    +1
     A                                      DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 19  4'繳款日期:　　'
     A            INRCDT         8Y 0B    +1
     A                                      EDTCDE(M)
     A                                      DSPATR(UL)
     A  62                                  DSPATR(PR)
     A                                 19 44'結案碼:　　　'
     A            INFL01         1A  B    +1
     A  62                                  DSPATR(PR)
     A                                 20  4'異動廠區:　　'
     A            INTXAR         1A  O    +1
     A                                      DSPATR(UL)
     A                                 20 44'異動日期:　　'
     A            INTXDT         8Y 0B    +1
     A                                      EDTCDE(M)
     A  62                                  DSPATR(PR)
     A                                 21  4'印發票控制碼:'
     A            INPRTC         1A  B    +1
     A                                 21 44'保留碼:　　　'
     A            INRESV         6A  B    +1
     A  62                                  DSPATR(PR)
     A***************************************************************
     A          R AR035F3M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 23  2'ENTER:試算'
     A                                      DSPATR(HI)
     A                                 23 21'F3:結束本作業'
     A                                      DSPATR(HI)
     A                                 23 43'F12:回前畫面'
     A                                      DSPATR(HI)
     A                                 23 64'F10:存檔'
     A                                      DSPATR(HI)
     A            S#MSG3        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面四:編輯發票明細畫面
     A***************************************************************
     A          R AR035F4H
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10(10)
     A                                      CA12(12)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A*----------------------------------------------------------------
     A                                  1 70'ARE035S-4'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'★發票明細編輯作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  4  8'發票號碼:'
     A                                      DSPATR(HI)
     A            IVNO          10A  O    +1
     A                                      DSPATR(HI)
     A                                  5  8'類別:　'
     A                                      DSPATR(HI)
     A            IVACNT         1A  O    +1
     A                                      DSPATR(HI)
     A                                  6  8'項次:　'
     A                                      DSPATR(HI)
     A            IVITEM         2Y 0O    +1
     A                                      DSPATR(HI)
     A                                  8  8'處理代碼:'
     A            IVFLAG         1A  B    +1
     A  62                                  DSPATR(PR)
     A                                  8 44'入帳日期:'
     A            IVACDT         8Y 0B    +1
     A  62                                  DSPATR(PR)
     A                                      EDTCDE(M)
     A                                  9  8'訂單號碼:'
     A            IVORNO         9A  O    +1
     A                                  9 44'品名代號:'
     A            IVPDCD         3A  B    +1
     A  62                                  DSPATR(PR)
     A                                 10  8'數量:　'
     A            IVQTY          7Y 0B    +1
     A                                      EDTCDE(L)
     A  62                                  DSPATR(PR)
     A                                 10 44'單價:　'
     A            IVUPRC         5Y 3B    +1
     A                                      EDTCDE(L)
     A  62                                  DSPATR(PR)
     A                                 11  8'金額:　'
     A            IVAMT         11Y 0B    +1
     A                                      EDTCDE(L)
     A  62                                  DSPATR(PR)
     A                                 11 44'作廢碼:'
     A            IVDECD         1A  B    +1
     A  62                                  DSPATR(PR)
     A                                 12  8'作廢日期:'
     A            IVDEDT         8Y 0B    +1
     A                                      EDTCDE(M)
     A  62                                  DSPATR(PR)
     A                                 12 44'憑證編號:'
     A            IVAPNO         8A  B    +1
     A  62                                  DSPATR(PR)
     A                                 13  8'傳票編號:'
     A            IVACNO         7A  B    +1
     A  62                                  DSPATR(PR)
     A                                 13 44'過訂單碼:'
     A            IVFL01         1A  B    +1
     A  62                                  DSPATR(PR)
     A                                 14  8'過發票碼:'
     A            IVFL02         1A  B    +1
     A  62                                  DSPATR(PR)
     A                                 14 44'類別碼:'
     A            IVFL03         1A  B    +1
     A  62                                  DSPATR(PR)
     A                                 15  8'異動廠區:'
     A            IVTXAR         1A  O    +1
     A                                      DSPATR(UL)
     A                                 15 44'異動日期:'
     A            IVTXDT         8Y 0B    +1
     A                                      EDTCDE(M)
     A  62                                  DSPATR(PR)
     A                                 16  8'保留碼:'
     A            IVRESV         5A  B    +1
     A  62                                  DSPATR(PR)
     A***************************************************************
     A          R AR035F4M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 23  2'ENTER:試算'
     A                                      DSPATR(HI)
     A                                 23 21'F3:結束本作業'
     A                                      DSPATR(HI)
     A                                 23 43'F12:回前畫面'
     A                                      DSPATR(HI)
     A                                 23 64'F10:存檔'
     A                                      DSPATR(HI)
     A            S#MSG4        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面五:發票主檔存檔確認
     A***************************************************************
     A          R AR035F5H
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A*----------------------------------------------------------------
     A                                  1 70'ARE035S-5'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'★發票主檔編輯作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  6  8'請注意：你現在正要對發票主檔做+
     A                                      異動，請確定該發票主檔的資料是'
     A                                  8 16'正確無誤之後，輸入簡要的異動原+
     A                                      因，再按執行鍵以存檔。'
     A
     A                                 16 16'異動原因:'
     A            S#CGCA        40O  I 16 28DSPATR(PC)
     A                                 23  2'ENTER:執行'
     A                                      DSPATR(HI)
     A                                 23 21'F3:取消'
     A                                      DSPATR(HI)
     A                                 23 37'F12:取消'
     A                                      DSPATR(HI)
     A***************************************************************
     A*  畫面六:發票明細存檔確認
     A***************************************************************
     A          R AR035F6H
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A*----------------------------------------------------------------
     A                                  1 70'ARE035S-6'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'★發票明細編輯作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  6  8'請注意：你正要對本發票明細進行+
     A                                      存檔異動，請確定資料無誤後再輸入'
     A                                  8 16'簡要的說明，按執行以存檔。在處+
     A                                      理完該號碼的相關明細後，'
     A                                 10 16'記得對發票主檔進行試算的查核動+
     A                                      作。'
     A                                      DSPATR(HI)
     A                                 12 16'若資料有誤，請連絡系統工程部相+
     A                                      關負責人員。'
     A                                 16 16'異動原因:'
     A            S#CGCA        40O  I 16 28DSPATR(PC)
     A                                 23  2'ENTER:執行'
     A                                      DSPATR(HI)
     A                                 23 21'F3:取消'
     A                                      DSPATR(HI)
     A                                 23 37'F12:取消'
     A                                      DSPATR(HI)
