     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARE098T1
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     區間發票開立試算表
     A*           4.DATE-WRITTEN  101/12/25 2012AR708
     A*           5.WRITER        S00WCJ
     A*           6.UPDATE  DATE  102/07/09 2013AR754 S00WCJ (0207A)
     A*                            增加備註欄位
     A*
     A*****************************************************************
     A          R AR98T11H
     A*****************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      ' PRT-ID  : ARE098T'
     A                                    40
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   105
     A                                      '列表日期:'
     A                                    +1
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      ' USR-ID  :'
     A                                      SPACEB(001)
     A            U#USID        10        +1
     A                                    40
     A                                      '※區間發票差異試算表※  '
     A                                   105
     A                                      '列表時間:'
     A                                    +1
     A                                      TIME
     A                                   105
     A                                      '頁　　次:'
     A                                      SPACEB(001)
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '試算單號:'
     A                                      SPACEB(002)
     A            R#NO          10        +1
     A                                    +1
     A                                      '訂單編號:'
     A            R#ORNO         6        +1
     A                                    +1'客戶編號:'
     A            R#CUNO         6        +1
     A                                    +1'客戶名稱:'
     A            R#CUNM        10        +1
     A                                    +1'發票開立方式:'
     A            R#IVYN         1        +1
     A  45                               105'區間開立'
     A N45                               105'指定開立'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
     A                                      ===================+
     A                                      ================'
     A                                      SPACEB(001)
     A                                     1
     A                                      '日  期 '
     A                                      SPACEB(001)
     A                                    +1
     A                                      '磅單編號'
     A                                    +0
     A                                      '項次'
     A                                    +0
     A                                      '品名'
     A                                    +0
     A                                      '單價'
     A                                    42
     A                                      '數 量 '
     A                                    55
     A                                      '金     額'
     A                                    72
     A                                      '差異數量'
     A                                    82
     A                                      '差異單價'
     A                                    93
     A                                      '差異金額'
     A                                   103
     A                                      '調整後數量'
     A                                   118
     A                                      '調整後金額'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
     A                                      ===================+
     A                                      ================'
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR98T11D
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A  31        R#TXDE         8  0O     2EDTWRD('    /  /  ')
     A            R#TXNO         8   O    14
     A            R#ITEM         2  0O    24
     A            R#PDNM         3   O    30
     A            R#UPRC         5  3O    34EDTCDE(K)
     A            R#QTY          7  0O    42EDTCDE(K)
     A            R#TAM1        11  0O    55EDTCDE(K)
     A            R#SQTY         7  0O    72EDTCDE(K)
     A            R#SPRC         5  3O    84EDTCDE(K)
     A            R#AMT          7  0O    93EDTCDE(K)
     A            R#UQTY         7  0O   105EDTCDE(K)
     A            R#UAMT         7  0O   120EDTCDE(K)
     A**************************************************************************
     A          R AR98T11L
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                     1
     A                                      ' '
     A**************************************************************************
     A          R AR98T11M
     A**************************************************************************
     A N35                                 1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
     A                                      ===================+
     A                                      ================'
     A*                                     SPACEB(001)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A N35                                14 '品名小計:'
     A            RASPDM         3   O    30
     A            RATQTY         8  0     41EDTCDE(K)
     A            RATAM2        12  0     54EDTCDE(K)
     A            RATQY2         8  0     71EDTCDE(K)
     A            RATAMT        10  0     89EDTCDE(K)
     A            RAUQY2         8  0    104EDTCDE(K)
     A            RAUAM2        10  0    116EDTCDE(K)
     A
     A**************************************************************************
     A          R AR98T21M
     A**************************************************************************
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
     A                                      ===================+
     A                                      ================'
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                    14 '合  計:'
     A            RBTQTY         8  0     41EDTCDE(K)
     A            RBTAM2        12  0     54EDTCDE(K)
     A            RBTQY2         8  0     71EDTCDE(K)
     A            RBTAMT        10  0     89EDTCDE(K)
     A            RBUQY2         8  0    104EDTCDE(K)
     A            RBUAM2        10  0    116EDTCDE(K)
     A
     A**************************************************************************
     A          R AR98T11T
     A**************************************************************************
     A                                      SKIPB(059)
     A                                    14
0207AA                                      '備 註：'
0207AA            R#TEX1        72        +1UNDERLINE
0207AA                                      SPACEA(001)
0207AA            R#TEX2        72        26UNDERLINE
     A**************************************************************************
     A          R AR98T21T
     A**************************************************************************
     A                                      SKIPB(063)
     A                                     2
     A                                      '副總:______________'
     A                                    +5'經理:______________'
     A                                    +5'副理:______________'
     A                                    +5'複核:______________'
     A                                    +5'製表人:_____________'
     A**************************************************************************
     A          R AR98T11X
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     1
     A                                      '===============================-
     A                                      =======================+
     A                                      ==接　下　頁==============-
     A                                      =======================+
     A                                      ========================'
