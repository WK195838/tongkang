     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR122T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.WRITER        S02CSF
     A*           4.FUNCTION     未開立發票明細表－即時
     A*           5.DATE-WRITTEN  94/08/18
     A*           6.DATE-MODIFY
     A*
     A*
     A*****************************************************************
     A          R AR122T1H
     A*****************************************************************
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報　　表: ARR122T'
     A                                    58
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   126
     A                                      '列表日期:'
     A                                   137
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            T#USID        10A       14
     A                                    59
     A                                      '未開立發票明細表－即時'
     A                                   126
     A                                      '列表時間:'
     A                                   137
     A                                      TIME
     A                                     2
     A                                      '銷貨年月:'
     A                                      SPACEB(001)
     A            T#YMS          6S 0     13
     A                                      EDTWRD('    /  ')
     A                                    21
     A                                      '~~'
     A            T#YME          6S 0     24
     A                                      EDTWRD('    /  ')
     A                                   126
     A                                      '頁　　次:'
     A                                   137
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '銷貨廠區:'
     A                                      SPACEB(001)
     A            T#AREA         6        13
     A                                     2
     A                                      '營業部門:'
     A                                      SPACEB(001)
     A            T#DEPT         6        13
     A                                     2
     A                                      '業務代號:'
     A                                      SPACEB(001)
     A            T#SAIH         6        13
     A                                     2
     A                                      '發票開立方式:'
     A                                      SPACEB(001)
     A            T#KIND        10        17
     A            T#LIN1       146         2
     A                                      SPACEB(001)
     A                                     2
     A                                      '客戶編號'
     A                                      SPACEB(001)
     A                                    14
     A                                      '客戶名稱'
     A                                    26
     A                                      '訂單號碼'
     A                                    38
     A                                      '磅單號碼'
     A                                    50
     A                                      '業務代號'
     A                                    62
     A                                      '出貨日期'
     A                                    74
     A                                      '品　名'
     A                                    85
     A                                      '單　價'
     A                                    98
     A                                      '數　量'
     A                                   112
     A                                      '金　額'
     A                                   124
     A                                      '傳票號碼'
     A                                   136
     A                                      '備　　註'
     A            T#LIN2       146A        2
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR122T1D
     A*****************************************************************
     A                                     2
     A                                      ' '
     A                                      SPACEB(001)
     A N50        T#CUNO         6A        4
     A N50        T#CUNM        10O       14
     A N51        T#ORNO         6A       28
     A N52        T#TXNO         8A       39
     A            T#SAID         2A       54
     A            T#DATE         8S 0     62
     A                                      EDTWRD('    /  /  ')
     A            T#PDNM         3A       77
     A            T#UPRC         5S 2     85
     A                                      EDTCDE(J)
     A            T#QTY          7S 0     96
     A                                      EDTCDE(J)
     A            T#AMT         11S 0    109
     A                                      EDTCDE(J)
     A            T#VUNO         7A      125
     A            T#IVTP        10A      136
     A*****************************************************************
     A          R AR122T1S
     A*****************************************************************
     A                                    80
     A                                      '磅單小計：'
     A                                      SPACEB(001)
     A            T#QTY1         8S 0     95
     A                                      EDTCDE(J)
     A            T#AMT1        12S 0    108
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR122T2S
     A*****************************************************************
     A                                    80
     A                                      '廠區小計：'
     A                                      SPACEB(001)
     A            T#QTY2         8S 0     95
     A                                      EDTCDE(J)
     A            T#AMT2        12S 0    108
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR122T3S
     A*****************************************************************
     A                                    80
     A                                      '訂單小計：'
     A                                      SPACEB(001)
     A            T#QTY3         9S 0     94
     A                                      EDTCDE(J)
     A            T#AMT3        13S 0    106
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR122T4S
     A*****************************************************************
     A                                    80
     A                                      '客戶小計：'
     A                                      SPACEB(001)
     A            T#QTY4         9S 0     94
     A                                      EDTCDE(J)
     A            T#AMT4        13S 0    106
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR122T1T
     A*****************************************************************
     A                                    80
     A                                      '　總合計：'
     A                                      SPACEB(001)
     A            T#QTYT         9S 0     94
     A                                      EDTCDE(J)
     A            T#AMTT        13S 0    106
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR122T1M
     A*****************************************************************
     A                                      SPACEB(002)
     A                                    59
     A                                      '無相關資料!!!'
     A*****************************************************************
