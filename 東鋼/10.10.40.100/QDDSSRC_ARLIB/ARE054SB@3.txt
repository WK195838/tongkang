     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE054RA
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CSF
     A*            4.FUNCTION     營業退款輸入作業（依退款日期）
     A*            5.DATE-WRITTEN  93/07/21
     A*            6.DATE-UPDATED 100/02/14  2011AR594  S00WCJ (0002A)
     A*                           101/09/19  2012AR682  S00WCJ (0109A)
     A*
     A***************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 '結束')
     A                                      CA12(12 '回上頁')
     A*  *IN33 : ON 表示為查詢狀態
     A*================================================================
     A          R AR054H
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 28'東和鋼鐵企業股份有限公司'
     A                                  1 70'ARE054S'
     A                                  2  2TIME
     A                                  2 32'營業退款輸入程式'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USID        10A  O  3  2
     A  65        S#SF1T         6O  O  3 70DSPATR(RI)
     A*================================================================
     A          R AR054F1                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT1         1A  B  9  5VALUES(' ' '2' '4' '5' '8' '9')
     A  31                                  DSPATR(RI)
     A            S#CUNO         6A  O  9  9
     A            S#CUNM        10O  O  9 17
     A            S#ORNO         6A  O  9 29
     A            S#RTNO         6A  O  9 40
     A            S#RTDT         8Y 0O  9 49EDTWRD('    /  /  ')
     A            S#AAMT         8Y 0O  9 61EDTCDE(2)
     A            S#FL01         1A  O  9 74
     A            S#FL02         1A  O  9 76
     A            S#FL03         1A  O  9 78
     A*================================================================
     A          R AR054F1C                  SFLCTL(AR054F1)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CF05(05)
     A                                      CF06(06)
     A                                      CF17(17)
     A                                      CF18(18)
     A*                                     ROLLUP(91)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A            S#SFN1         4S 0H
     A                                  4  2'選項:'
     A                                  4 11'2:修改'
     A                                  4 21'4:刪除'
     A                                  4 31'5:查詢'
     A                                  4 41'8:確認'
     A                                  4 51'9:確認還原'
     A            S#LIN1        78A  O  5  2
     A                                  6  2'選項'
     A                                  6 10'客　　　　戶'
     A                                  6 27'訂單編號'
     A                                  6 38'退款編號'
     A                                  6 49'退款日期'
     A                                  6 61'退款金額'
     A                                  6 72'確印過'
     A            S#LIN2        78A  O  7  2
     A            S#KEY1         6A  B  8  9
     A  30                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A*================================================================
     A          R AR054F1M
     A                                      OVERLAY
     A                                      TEXT('按鍵及錯誤訊息')
     A            S#LIN3        78A  O 21  2
     A                                 22  4'F03:結束'
     A                                 22 15'F12:回前'
     A                                 22 26'F06:新增'
     A            S#ERR1        70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR054F2                   SFL
     A  70                                  SFLNXTCHG
     A            S#SFI2         2Y 0O 13  3
     A            S#DC           1A  B 13  9VALUES(' ' 'C' 'D')
     A  33                                  DSPATR(PR)
     A  50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A            S#USTP         2A  B 13 13
     A  33                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
0109AA            S#ACNO         6A  B 13 18
     A  33                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A            S#RLNO        10A  B 13 23
     A  33                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A            S#DUDT         8Y 0B 13 34EDTWRD('    /  /  ')
     A  33                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A            S#CSNM        10O  B 13 45
     A  33                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A            S#DSC2        12O  B 13 56
     A  33                                  DSPATR(PR)
     A            S#AAMT         8Y 0B 13 69EDTCDE(1)
     A  33                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A            S#DSC1        60O  B 14 19
     A  33                                  DSPATR(PR)
0002AA            S#USMT        11  2B 15 21EDTCDE(1)
0002AA  33                                  DSPATR(PR)
0002AA  60                                  DSPATR(PC)
0002AA  60                                  DSPATR(RI)
     A***************************************************************
     A          R AR054F2C                  SFLCTL(AR054F2)
     A                                      SFLSIZ(0006)
0002AA*                                     SFLPAG(0005)
0002AA                                      SFLPAG(0003)
     A                                      OVERLAY
     A                                      CF01(01)
     A N33                                  CF10(10)
     A N81                                  SFLDSPCTL
     A N82                                  SFLDSP
     A  83                                  SFLCLR
     A N84                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A                                  4  8'退款編號:'
     A            S#RTNO         6A  O  4 20
     A                                  4 31'退款部門:'
     A            S#DPNO         4A  B  4 43
     A  33                                  DSPATR(PR)
     A                                  4 51'退款日期:'
     A            S#RTDT         8Y 0B  4 63EDTWRD('    /  /  ')
     A  33                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A                                  5  8'客戶編號:'
     A            S#CUNO         6A  B  5 20
     A  33                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A                                  5 27'受款人:'
     A            S#FNAM        36O  O  5 37
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(RI)
     A                                  6  8'借方金額:'
     A            S#DAMT         8Y 0O  6 20EDTCDE(1)
     A                                  6 31'貸方金額:'
     A            S#CAMT         8Y 0O  6 43EDTCDE(1)
     A N33                              7  2'借貸方清成空白，該項明細視同刪除-
     A                                      '
     A                                      DSPATR(RI)
     A                                  7 37'領款人:'
     A            S#SAID         1A  B  7 47
     A                                  7 53'退款金額:'
     A            S#OAMT         8Y 0O  7 65EDTCDE(1)
     A                                      DSPATR(HI)
     A            S#LIN4        78A  O  8  2
     A                                  9  2'項'
     A                                  9  7'借'
0109AA                                  9 12'款會計'
     A                                  9 24'相關號碼'
     A                                  9 35'到期日'
     A                                  9 46'對象別'
     A                                  9 57'輔助摘要'
0002AA*                                 9 72'金額'
0002AA                                  9 70'台幣金額'
     A                                 10  2'次'
     A                                 10  7'貸'
0109AA                                 10 12'項科目'
     A                                 10 24'摘　　　　　要'
0002AA                                 11 24'美金金額'
0002AA*           S#LIN5        78A  O 11  2
0002AA            S#LIN5        78A  O 12  2
     A***************************************************************
     A          R AR054F2M
     A                                      OVERLAY
     A            S#LIN6        78A  O 22  2
     A                                 23  4'F01:說明'
     A                                 23 19'F03:結束'
     A                                 23 34'F12:回前'
     A  33                             23 49'ENTER:執行'
     A N33                             23 49'ENTER:檢核'
     A N33                             23 66'F10:存檔'
     A            S#ERR2        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A* 畫面二：輸入資料說明視窗
     A***************************************************************
     A          R AR054F2W
     A                                      WINDOW(10 14 10 56)
     A                                      TEXT('說明視窗')
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      OVERLAY
     A                                  2  4'款項別'
     A                                      DSPATR(HI)
     A                                  4  2'A1:預收貨款'
     A                                  5  2'A2:銷退貨款'
     A                                  6  2'A3:銷折貨款'
     A                                  7  2'A4:暫收貨款'
     A                                  2 20'常用會計科目'
     A                                      DSPATR(HI)
     A                                  4 20'1111庫存現金'
     A                                  4 36'4112內銷銷貨退回'
     A                                  5 20'1112銀行存款'
     A                                  5 36'4113應收帳款折讓'
     A                                  6 20'2121應付票據'
9801 A                                  6 36'1134應收帳款'
     A                                  7 20'2127銷項稅額'
     A                                  7 36'7142郵電費'
     A                                  8 20'2131預收貨款'
     A                                  9 20'2159暫收其他'
0109AA***************************************************************
0109AA* 畫面二：輸入資料說明視窗
0109AA***************************************************************
0109AA          R AR054F3W
0109AA                                      WINDOW(10 14 10 56)
0109AA                                      TEXT('說明視窗')
0109AA                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
0109AA                                       (*CHAR '        '))
0109AA                                      OVERLAY
0109AA                                  2  4'款項別'
0109AA                                      DSPATR(HI)
0109AA                                  4  2'A1:預收貨款'
0109AA                                  5  2'A2:銷退貨款'
0109AA                                  6  2'A3:銷折貨款'
0109AA                                  7  2'A4:暫收貨款'
0109AA*                                 8  2'A5:其他'
0109AA                                  2 20'常用會計科目'
0109AA                                      DSPATR(HI)
0109AA                                  4 16'110101庫存現金'
0109AA                                  4 34'410102內銷銷貨退回'
0109AA                                  5 16'110102銀行存款'
0109AA                                  5 34'410103應收帳款折讓'
0109AA                                  6 16'210701應付票據'
0109AA                                  6 34'111101應收帳款'
0109AA                                  7 16'210904銷項稅額'
0109AA                                  7 34'710402郵電費'
0109AA                                  8 16'211201預收貨款'
0109AA                                  9 16'211301暫收其他'
     A***************************************************************
