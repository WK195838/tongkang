     A*%%TS  SD  19930901  155551  S02CSF      REL-V4R4M0  5769-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE043R
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     客戶暫收統計檔編修程式
     A*            5.DATE-WRITTEN  88/10/28
     A*
     A***************************************************************
     A*  畫面一:輸入客戶
     A***************************************************************
     A          R AR043F1                   SFL
     A*%%TS  SD  19930901  132432  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT1         1A  B 11  4VALUES(' ' '2')
     A            S#CUNO         6A  O 11  7
     A            S#CUNM        10A  O 11 14
     A            S#AREA         1A  O 11 26
     A            S#ORNO         6A  O 11 33
     A            S#TAMT         9Y 0O 11 41EDTCDE(J)
     A            S#AMT2         9Y 0O 11 54EDTCDE(J)
     A            S#NBAL         9Y 0O 11 67EDTCDE(J)
     A***************************************************************
     A          R AR043F1C                  SFLCTL(AR043F1)
     A*%%TS  SD  19930901  155551  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      CF05(05)
     A                                      CF06(06)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A            S#SFN1         4S 0H
     A                                  1 70'ARE043S-1'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A            S#DEVI        10A  O  2 70
     A                                  2 27'★客戶暫收統計檔維護作業★'
     A                                      DSPATR(RI)
     A                                  5  2'選項: 2 --> 修改。'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  2'選項'
     A                                  8  9'客戶編號名稱'
     A                                  8 24'廠區'
     A                                  8 31'訂單編號'
     A                                  8 42'總金額'
     A                                  8 55'繳貨款'
     A                                  8 69'現金餘額'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#INK1         6A  B 10  7
     A  51                                  DSPATR(PC)
     A***************************************************************
     A          R AR043F1M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 21  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22  4'F03:結束'
     A                                 22 18'F12:回前'
     A                                 22 32'F05:重新顯示'
     A                                 22 50'F06:新增'
     A            S#MSG1        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面二:編輯畫面
     A***************************************************************
     A          R AR043F2
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      CF10(10)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A*---------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE043S-2'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 27'★客戶暫收統計檔維護作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A            S#EDTP         6O  O  4  4DSPATR(RI)
     A*---------------------------------------------------------------
     A                                  5 27'客戶編號:'
     A            S#CUNO         6A  B  5 40
     A  52                                  DSPATR(PR)
     A  53                                  DSPATR(PC RI)
     A            S#CUNM        10O  O  5 47
     A                                  7 27'繳款廠區:'
     A            S#AREA         1A  B  7 40
     A  52                                  DSPATR(PR)
     A  54                                  DSPATR(PC RI)
     A                                  9 27'訂單編號:'
     A            S#ORNO         6A  B  9 40
     A  52                                  DSPATR(PR)
     A  55                                  DSPATR(PC RI)
9010 A                                 12 21'暫收總金額:'
9010 A            S#TAMT         9Y 0O 12 40EDTCDE(J)
     A                                 13 21'暫收已兌現金額:'
     A            S#AMT1         9Y 0B 13 40EDTCDE(J)
     A                                 14 21'暫收繳貨款金額:'
     A            S#AMT2         9Y 0B 14 40EDTCDE(J)
     A  55                                  DSPATR(PC RI)
     A                                 15 21'暫收未兌現金額:'
     A            S#AMT3         9Y 0B 15 40EDTCDE(J)
     A                                 16 21'財會切傳票金額:'
     A            S#AMT4         9Y 0B 16 40EDTCDE(J)
     A                                 17 21'客戶現金餘額:'
     A            S#NBAL         9Y 0O 17 40EDTCDE(J)
     A                                 19 29'保留碼:'
     A            S#RESV        20O  B 19 40
     A*---------------------------------------------------------------
     A                                 22  6'F03:結束'
     A                                      DSPATR(HI)
     A                                 22 20'F12:回前'
     A                                      DSPATR(HI)
     A                                 22 34'PGUP:上筆'
     A                                      DSPATR(HI)
     A                                 22 49'PGDN:下筆'
     A                                      DSPATR(HI)
     A                                 22 64'F10:存檔'
     A                                      DSPATR(HI)
     A            S#MSG2        70A  O 24  2DSPATR(HI)
