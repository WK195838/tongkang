     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE098S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     區間發票開立試算維護作業
     A*            5.DATE-WRITTEN  99/05/29
     A*            6.UPDATE  DATE 100/01/11  2011ARXXX  S00WCJ (0001A)
     A*                           100/01/14  2011AR564  S00WCJ (0001B)
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      INDARA
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前螢幕')
     A*================================================================
     A          R TITLE
     A                                      OVERLAY
     A                                  1 70'ARE098S-1'
     A            COMP          35A  O  1 23
     A            S#DATE         8  0O  1  2EDTWRD('    /  /  ')
     A                                  2 26'*區間發票開立試算維護作業*'
     A            DEVNM         10A  O  2 70
     A*=========
     A                                  5 30'功　　能:'
     A            CODE           1A  B  5 42
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  5 44'(1:新增 2:修改'
     A                                  7 45'3:刪除 4:查詢5:列印)'
     A*
     A                                 11 30'訂單編號:'
     A            S#ORNO         6A  B 11 42
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A*
     A                                 13 30'日期區間:'
     A            S#DAT1         8  0B    +1
     A                                      EDTWRD('    /  /  ')
     A  43                                  DSPATR(PC RI)
     A                                    +1'∼'
     A            S#DAT2         8  0B    +1
     A                                      EDTWRD('    /  /  ')
     A  44                                  DSPATR(PC RI)
     A*
     A                                 15 30'試算單號:'
     A            S#NO          10A  B    +1
     A  45                                  DSPATR(PC RI)
     A*
0001BA                                 17 30'自動上Y'
0001BA            S#YN           1A  B    +1VALUES('Y' 'N')
0001BA                                    +1'(Y/N)'
     A            S#ERR         70A  O 23  2DSPATR(HI)
     A*=========
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A*****************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            S#OPT          1A  B 10  3
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A            S#DATT         8  0O    +1EDTWRD('    /  /  ')
     A            S#TXNO         8A  O    +1
     A            S#ITEM         2  0O    +1
     A            S#PDNM         3A  O    +3
0001AA*           S#UPRC         5  3O    +2EDTCDE(2)
0001AA            S#UPRC         5  3O    +2EDTCDE(K)
0001AA*           S#QTY          7  0O    +1EDTCDE(2)
0001AA            S#QTY          7  0O    +1EDTCDE(K)
     A            S#SQTY         7  0B    +1EDTCDE(K)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A            S#SPRC         5  3B    +1EDTCDE(K)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A            S#SAMT         6  0B    +1EDTCDE(K)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N73                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&S#CSR)
     A            S#CSR          5  0H
     A            S#NBR          4  0H      SFLRCDNBR(CURSOR)
     A*-----------
     A                                  1 70'ARE098S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD           10A  O  2  2DSPATR(RI)
     A                                  2 26'*發票區間開立試算維護作業*'
     A            DEVNM         10A  O  2 70
     A*-----------
     A                                  4  2'試算編號:'
     A            S#NO          10A  O    +1
     A                                  4 33'單據日期:'
     A            S#ACDS         8  0O  4 45EDTCDE(Z)
     A                                  5  2'訂單編號:'
     A            S#ORNO         6   O    +1
     A                                  5 33'客戶編號:'
     A            S#CUNO         5A  O    +1
     A            S#CUNM        10A  O    +1
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  2'挑  '
     A                                  7 16'磅單'
     A                                  7 53'調 整 調整  調整'
     A                                  8  2'Ｙ日 期 編號'
     A                                    +1'項次品名單價'
     A                                    +1'數 量  數 量'
     A                                    +1'單價  金額'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND01
     A                                      OVERLAY
     A                                 21  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A                                      DSPATR(HI)
     A                                 22 12'合　計:'
     A            S#TQTY         8  0O 22 41EDTCDE(2)
     A            S#TQY2         7  0O    +1EDTCDE(K)
     A            S#TAMT         7  0O    +7EDTCDE(K)
     A            S#ERR         70A  O 23 02DSPATR(HI)
     A*
     A                                 24  5'PF03:結束'
     A                                    +2'PF12:回前畫面'
     A***************************************************************
