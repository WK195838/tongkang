     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR073T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     營業繳款已過入財會明細表
     A*           4.DATE-WRITTEN  87/08/18
     A*           5.WRITER        S02CLJ
     A*           6.UPDATE  DATE  101/09/25  2012AR682  S00WCJ (0109A)
     A*
     A*       已確認及未確認之報表由傳入資料而定
     A*
     A*****************************************************************
     A          R AR073T1H
     A*****************************************************************
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報表代號: ARR073T'
     A                                    67
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   149
     A                                      '列表日期:'
     A                                   160
     A                                      DATE
     A                                      EDTCDE(Y)
     A*
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                    69
     A                                      '已過入營業繳款明細表'
     A                                   149
     A                                      '列表時間:'
     A                                   160
     A                                      TIME
     A*
     A                                     2
     A                                      '繳款日期:'
     A                                      SPACEB(001)
     A            R#RVDT         8S  O    14
     A                                      EDTWRD('    /  /  ')
     A                                   149
     A                                      '頁　　次:'
     A            R#PGNO         4S 0O   160
     A                                      EDTCDE(Z)
     A*
     A                                     2
     A                                      '繳款部門:'
     A                                      SPACEB(001)
     A            R#DPNO         4A  O    +1
     A                                    +4
     A                                      '收款業務:'
     A            R#RVID         2A  O    +1
     A            R#RVNM        10O  O    +0
     A*----------------------------------------------------------------
     A                                     2
     A                                      '財會手填'
     A                                      SPACEB(002)
     A                                    +0
     A                                      '繳款'
     A                                   +19
     A                                      '款'
     A                                    +0
     A                                      '票'
     A                                    +0
     A                                      '會計'
     A                                    +0
     A                                      '借'
     A                                   +57
     A                                      '付款'
     A                                   +19
     A                                      '存入'
     A                                   +20
     A                                      '資金'
     A*
     A                                     2
     A                                      '傳票編號'
     A                                      SPACEB(001)
     A                                    +0
     A                                      '單號'
     A                                    +6
     A                                      '客戶'
     A                                    +7
     A                                      '項'
     A                                    +0
     A                                      '據'
     A                                    +0
     A                                      '科目'
     A                                    +0
     A                                      '貸'
     A                                    +3
     A                                      '銀行名稱'
     A                                    +3
     A                                      '銀行帳號'
     A                                    +1
     A                                      '票據號碼'
     A                                   +12
     A                                      '到期日'
     A                                    +1
     A                                      '地'
     A                                    +4
     A                                      '票據金額'
     A                                    +4
     A                                      '銀行帳號'
     A                                    +6
     A                                      '備註'
     A                                    +6
     A                                      '代號'
     A*----------------------------------------------------------------
     A                                     2
     A                                      ' -------- '
     A                                      SPACEB(001)
     A                                    +0
     A                                      '------'
     A                                    +1
     A                                      '------'
     A                                    +1
     A                                      '----------'
     A                                    +2
     A                                      '--'
     A                                    +3
     A                                      '-'
     A                                    +2
     A                                      '---- '
     A                                    +2
     A                                      '-'
     A                                    +2
     A                                      '--------------'
     A                                    +1
     A                                      '----------'
     A                                    +1
     A                                      '----------'
     A                                   +11
     A                                      '----------'
     A                                    +1
     A                                      '-'
     A                                    +3
     A                                      '---------------'
     A                                    +2
     A                                      '----------'
     A                                    +1
     A                                      '----------------'
     A                                    +2
     A                                      '----'
     A*---------------------------------------------------------------
     A                                    44
     A                                      '發票號碼'
     A                                      SPACEB(001)
     A                                    +1
     A                                      '請款單號'
     A                                    +1
     A                                      '繳款金額'
     A                                    +8
     A                                      '發票號碼'
     A                                    +1
     A                                      '請款單號'
     A                                    +6
     A                                      '繳款金額'
     A                                    +9
     A                                      '發票號碼'
     A                                    +2
     A                                      '請款單號'
     A                                    +5
     A                                      '繳款金額'
     A*
     A                                    44
     A                                      '----------'
     A                                      SPACEB(001)
     A                                    +1
     A                                      '-------'
     A                                    +1
     A                                      '---------------'
     A                                    +6
     A                                      '----------'
     A                                    +1
     A                                      '-------'
     A                                    +5
     A                                      '---------------'
     A                                    +8
     A                                      '----------'
     A                                    +3
     A                                      '-------'
     A                                    +4
     A                                      '---------------'
     A*
     A                                     2
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============================'
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR073T1D
     A**************************************************************************
     A            R#RVNO         6A  O    12
     A                                      SPACEB(001)
     A            R#CUNO         6A  O    +1
     A            R#CUNM        10O  O    +1
     A            R#USTP         2A  O    +2
     A            R#NTTP         1A  O    +3
0109AA            R#ACNO         6A  O    45
     A            R#ACTP         1A  O    +3
     A            R#PBID        14O  O    +2
     A            R#PANO        10A  O    +1
     A            R#NTNO        20A  O    +1
     A            R#DUDT         8S  O    +1
     A                                      EDTWRD('    /  /  ')
     A            R#PLAC         1A  O    +1
     A            R#RAMT        11S 0O    +3
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR073T1E
     A**************************************************************************
     A            R#INN1        10A  O    44
     A                                      SPACEB(001)
     A            R#APN1         7A  O    +1
     A            R#INX1        11S 0O    +1
     A                                      EDTCDE(K)
     A            R#INN2        10A  O    +6
     A            R#APN2         7A  O    +1
     A            R#INX2        11S 0O    +5
     A                                      EDTCDE(K)
     A            R#INN3        10A  O    +8
     A            R#APN3         7A  O    +3
     A            R#INX3        11S 0O    +4
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR073T1S
     A**************************************************************************
     A                                    71
     A                                      '**繳款單小計:'
     A                                      SPACEB(003)
     A                                    96
     A                                      '票據金額：'
     A            R#SRNA        11S 0O   108
     A                                      EDTCDE(K)
     A                                   142
     A                                      '繳款金額：'
     A            R#SRXA        11S 0O    +0
     A                                      EDTCDE(K)
     A*
     A                                     2
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============================'
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR073T1T
     A**************************************************************************
     A                                    73
     A                                      '**本日合計:'
     A                                      SPACEB(001)
     A                                    96
     A                                      '票據金額：'
     A            R#TRNA        11S 0O   108
     A                                      EDTCDE(K)
     A                                   142
     A                                      '繳款金額：'
     A            R#TRXA        11S 0O    +0
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR073T1X
     A**************************************************************************
     A                                      SKIPB(059)
     A                                     8
     A                                      '<財會部門>'
     A                                    +0
     A                                      '主管:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '出納:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '入帳:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    88
     A                                      '<營業部門>'
     A                                    +0
     A                                      '主管:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '複核:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '繳款:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A*****************************************************************
     A          R AR073T1L
     A*****************************************************************
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR073T1M
     A*****************************************************************
     A                                      SPACEB(005)
     A                                    19
     A                                      '*****無條件符合之資料！！'
     A**************************************************************************
