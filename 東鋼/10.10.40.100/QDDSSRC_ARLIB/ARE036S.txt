     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE036S
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     已確認折讓單更改確認日期作業
     A*            5.DATE-WRITTEN  87/02/20
     A*
     A***************************************************************
     A*  畫面一:
     A***************************************************************
     A          R AR036F1                   SFL
     A  70                                  SFLNXTCHG
     A            S#ITEM         2S 0O  7 21
     A            S#TXNO         6   B  7 28
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
9708 A            S#ACDT         8  0B  7 38EDTWRD('    /  /  ')
 .   A  52                                  DSPATR(PC)
 .   A  52                                  DSPATR(RI)
 .   A            S#ACYM         6  0B    +1EDTWRD('    /  ')
 .   A  53                                  DSPATR(PC)
 .   A  53                                  DSPATR(RI)
 .   A            S#TXDT         8  0O    +3EDTWRD('    /  /  ')
9708 A            S#TXYM         6  0O    +2EDTWRD('    /  ')
     A***************************************************************
     A          R AR036F1C                  SFLCTL(AR036F1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0015)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      CF09(09)
     A                                      CF10(10)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A            S#SFN1         4S 0H
     A*-------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE036S-1'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 24'★已確認折讓單更正確認日期作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A                                  4  9'-----------------------------------
     A                                      -----------------------------------'
     A                                  5 18'項次'
     A                                  5 27'折讓單號'
9708 A                                  5 39'更正日期　　年月'
9708 A                                  5 58'原確認日期　　年月'
     A                                  6  9'-----------------------------------
     A                                      -----------------------------------'
     A***************************************************************
     A          R AR036F1M
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                 23  2'F3:結束'
     A                                 23 15'F12:回前'
     A                                 23 31'F9:重設'
     A                                 23 44'ENTER:檢核'
     A                                 23 60'F10:存檔'
     A            S#MSG1        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面六:發票明細存檔確認
     A***************************************************************
     A          R AR036F2H
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CA12(12)
     A*----------------------------------------------------------------
     A                                  1 70'ARE036S-2'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 24'★已確認折讓單更正確認日期作業★'
     A                                      DSPATR(RI)
     A*----------------------------------------------------------------
     A            S#DEVI        10A  O  2 70
     A                                  6  8'請注意：你正要對折讓單資料進行異+
     A                                      動存檔，請確定資料無誤後再輸入'
     A                                  8 16'異動原因，按執行鍵確認存檔。　'
     A                                 12 16'若資料有誤，請連絡系統工程部相關+
     A                                      負責人員。'
     A                                 16 16'異動原因:'
     A*----------------------------------------------------------------
     A                                 23  2'ENTER:執行'
     A                                      DSPATR(HI)
     A                                 23 21'F3:取消'
     A                                      DSPATR(HI)
     A                                 23 37'F12:取消'
     A                                      DSPATR(HI)
     A            S#CGCA        40O  I 16 28DSPATR(PC)
