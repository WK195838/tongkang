     A*%%TS  SD  19880625  140005  S02CLJ      REL-V3R7M0  5716-PW1
     A*****************************************************************
     A*
     A*            1.PROGRAM-ID    ARE026S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        HSIAO
     A*            4.FUNCTION     業務員基本資料輸入與查詢
     A*            5.DATE-WRITTEN  85/05/07
     A*            6.LAST UPDATE
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      INDARA
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前螢幕')
     A**********
     A          R HEAD
     A                                  1  4DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35   O  1 24
     A            S#SCRN        10   O  1 70
     A                                  2  4TIME
     A                                  2 27'も業務員基本資料輸入與查詢も'
     A                                      DSPATR(RI)
     A            S#DEVN        10   O  2 70
     A**********
     A          R MP01F2                    SFL
     A*%%TS  SD  19880625  134923  S02CLJ      REL-V3R7M0  5716-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT          1Y 0B 11  3EDTCDE(Z)
     A                                      VALUES(0 2 4)
     A            S#RVID         2A  O 11  9
     A            S#NAME        10A  O 11 12
     A            S#LOCA         1A  O 11 26
     A            S#LONM         6A  O 11 28
     A            S#WKNO         4A  O 11 38
     A            S#DEPT         4A  O 11 46
     A            S#DENM        14A  O 11 51
     A            S#USID        10A  O 11 69
     A**********
     A          R MP01F2CL                  SFLCTL(MP01F2)
     A*%%TS  SD  19880625  140005  S02CLJ      REL-V3R7M0  5716-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  73                                  SFLCLR
     A                                      CF06(06 '新增')
     A                                      ROLLUP(91 '次頁')
     A                                      OVERLAY
     A                                  4  1'選擇: 2=修改 4=刪除'
     A                                  6  1'　廠區代號:'
     A            S#AREA         1A  O  6 15
     A            S#ARNM         6O  O  6 17
     A                                  7  1'業務員代號:'
     A            S#NAM          2A  B  7 15
     A                                  7 18'起'
     A                                  9  2'選'
     A                                  9 11'業務員'
     A                                  9 37'工作'
     A                                 10  2'擇'
     A                                      DSPATR(UL)
     A                                 10 25'工作廠區'
     A                                      DSPATR(UL)
     A                                 10 37'編號'
     A                                      DSPATR(UL)
     A                                 10 46'部門代號　　　'
     A                                      DSPATR(UL)
     A                                 10 68'使用者帳號'
     A                                      DSPATR(UL)
     A            POINT          4S 0H      SFLRCDNBR
     A                                 10  9'代號　姓名'
     A                                      DSPATR(UL)
     A**********
     A          R MP01F2MG
     A                                      OVERLAY
     A                                 23  2'操作提示:'
     A                                    +1'PF3=結束作業'
     A                                    +1'PF12=回前螢幕'
     A                                    +1'PF6=新增'
     A                                    +1'PA1=下一頁'
     A                                    +1'PA2=上一頁'
     A            S#ERR         70   O 24  2DSPATR(HI)
     A**********
     A          R MP01F3                    SFL
     A*%%TS  SD  19880625  134923  S02CLJ      REL-V3R7M0  5716-PW1
     A  80                                  SFLNXTCHG
     A            S#RSEQ         2S 0O  9  3
     A  45                                  DSPATR(RI)
     A            S#RVID         2A  B  9  9
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A  45
     AO 62                                  DSPATR(PR)
     A            S#NAME        10J  B  9 12
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A  45
     AO 62                                  DSPATR(PR)
     A            S#LOCA         1A  B  9 26CHECK(RB)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A  45
     AO 62                                  DSPATR(PR)
     A            S#LONM         6A  O  9 28
     A            S#WKNO         4A  B  9 38CHECK(RB)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A  45
     AO 62                                  DSPATR(PR)
     A            S#DEPT         4A  B  9 46CHECK(RB)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A  45
     AO 62                                  DSPATR(PR)
     A            S#DENM        14A  O  9 51
     A            S#USID        10A  B  9 69
     A**********
     A          R MP01F3CL                  SFLCTL(MP01F3)
     A*%%TS  SD  19880625  140005  S02CLJ      REL-V3R7M0  5716-PW1
     A                                      SFLSIZ(0099)
     A                                      SFLPAG(0013)
     A                                      CF10(10 '存檔')
     A N81                                  SFLDSPCTL
     A N82                                  SFLDSP
     A N83                                  SFLEND
     A  83                                  SFLINZ
     A                                      SFLRNA
     A                                      OVERLAY
     A            S#MODE        10A  O  3 70DSPATR(RI)
     A                                  5  2'廠　　區:'
     A            S#AREA         1A  O  5 14
     A            S#ARNM         6A  O  5 16
     A                                  7  2'項'
     A                                  7 11'業務員'
     A                                  7 37'工作'
     A                                  8  2'目'
     A                                      DSPATR(UL)
     A                                  8 25'工作廠區'
     A                                      DSPATR(UL)
     A                                  8 37'編號'
     A                                      DSPATR(UL)
     A                                  8 46'部門代號　　　'
     A                                      DSPATR(UL)
     A                                  8 68'使用者帳號'
     A                                      DSPATR(UL)
     A                                  8  9'代號　姓名'
     A                                      DSPATR(UL)
     A**********
     A          R MP01F3MG
     A                                      OVERLAY
     A                                 23  2'操作提示:'
     A                                    +1'PF3=結束作業'
     A                                    +1'PF10=存檔'
     A                                    +1'PF12=回前螢幕'
     A            S#ERR         70   O 24  2DSPATR(HI)
     A          R MP01F4
     A*%%TS  SD  19880625  134923  S02CLJ      REL-V3R7M0  5716-PW1
     A                                      OVERLAY
     A                                      CF10(10 '存檔')
     A            S#MODE        10A  O  3 70DSPATR(RI)
     A                                  5  2'廠　　區    :'
     A            S#AREA         1A  O  5 18
     A            S#ARNM         6O  O  5 21
     A                                  6  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                  8 29'業務員代號:'
     A            D#RVID         2A  O  8 44
     A                                 10 29'業務員姓名:'
     A            D#NAME        10J  B 10 44
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A  61                                  DSPATR(PR)
     A                                 12 29'　工作廠區:'
     A            D#LOCA         1A  B 12 44
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A  61                                  DSPATR(PR)
     A            D#LONM         6A  O 12 46
     A                                 14 29'　工作編號:'
     A            D#WKNO         4A  B 14 44
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A  61                                  DSPATR(PR)
     A                                 16 29'　部門代號:'
     A            D#DEPT         4A  B 16 44
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A  61                                  DSPATR(PR)
     A            D#DENM        14A  O 16 49
     A                                 18 29'使用者帳號:'
     A            D#USID        10A  B 18 44
