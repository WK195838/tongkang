     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARQ016R
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     客戶暫收統計檔查詢程式
     A*            5.DATE-WRITTEN  88/10/28
     A*            6.DATE-UPDATE   92/09/10 BY S02CSF
     A*                           新增財會切傳票金額欄位
     A*
     A***************************************************************
     A*  畫面一:輸入客戶
     A***************************************************************
     A          R AR016F1                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT1         1A  B 11  4VALUES(' ' '5')
     A            S#CUNO         6A  O    +3
     A            S#CUNM        10A  O    +1
     A            S#AREA         1A  O    +2
     A            S#AMT1        11Y 0O    +2EDTCDE(J)
     A            S#AMT2        11Y 0O    +2EDTCDE(J)
     A            S#AMT3        11Y 0O    +2EDTCDE(J)
     A***************************************************************
     A          R AR016F1C                  SFLCTL(AR016F1)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      CF05(05)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A            S#SFN1         4S 0H
     A*---------------------------------------------------------------
     A                                  1 70'ARQ016S-1'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A            S#DEVI        10A  O  2 70
     A                                  2 27'★客戶暫收統計檔查詢作業★'
     A                                      DSPATR(RI)
     A*---------------------------------------------------------------
     A                                  5  2'選項: 5 --> 查詢。'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  2'選項'
     A                                  8 10'客戶編號名稱'
     A                                  8 25'廠區'
     A                                  8 37'已兌現'
     A                                  8 54'繳貨款'
     A                                  8 71'未兌現'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#INK1         6A  B 10  8
     A  51                                  DSPATR(PC)
     A***************************************************************
     A          R AR016F1M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 21  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22  4'F03:結束'
     A                                 22 18'F12:回前'
     A                                 22 32'F05:重新顯示'
     A            S#MSG1        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面二:編輯畫面
     A***************************************************************
     A          R AR016F2
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A*---------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARQ016S-2'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 27'★客戶暫收統計檔查詢作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*---------------------------------------------------------------
     A                                  5 27'客戶編號:'
     A            S#CUNO         6A  O  5 40
     A            S#CUNM        10O  O  5 47
     A                                  7 27'繳款廠區:'
     A            S#AREA         1A  O  7 40
     A                                  9 21'暫收已兌現金額:'
     A            S#AMT1        11Y 0O  9 40EDTCDE(J)
     A                                 11 21'暫收繳貨款金額:'
     A            S#AMT2        11Y 0O 11 40EDTCDE(J)
     A                                 13 21'暫收未兌現金額:'
     A            S#AMT3        11Y 0O 13 40EDTCDE(J)
     A                                 15 21'財會切傳票金額:'
     A            S#AMT4        11Y 0O 15 40EDTCDE(J)
     A                                 17 21'客戶暫收款餘額:'
     A            S#NBAL        11Y 0O 17 40EDTCDE(J)
     A                                 19 29'保留碼:'
     A            S#RESV        20O  O 19 40
     A*---------------------------------------------------------------
     A                                 22  6'F03:結束'
     A                                      DSPATR(HI)
     A                                 22 20'F12:回前'
     A                                      DSPATR(HI)
     A                                 22 34'PGUP:上筆'
     A                                      DSPATR(HI)
     A                                 22 49'PGDN:下筆'
     A                                      DSPATR(HI)
     A            S#MSG2        70A  O 24  2DSPATR(HI)
