     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE098S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     區間發票開立試算維護作業
     A*            5.DATE-WRITTEN  99/05/29
     A*            6.UPDATE  DATE 100/01/11  2011ARXXX  S00WCJ (0001A)
     A*                           100/01/14  2011AR564  S00WCJ (0001B)
     A*                           100/02/08  2011AR582  S00WCJ (0002A)
     A*                           101/12/24  2012AR708  S00WCJ (0112A)
     A*                           102/07/09  2013AR754  S00WCJ (0207A)
     A*                           新增備註欄位
     A*                           102/08/23  S00WCJ (0208A)
     A*                            客戶編號欄位須6碼
     A*                           104/09/22  2015AR953  S00WCJ (0409A)
     A*                            產品代碼3碼擴5碼
     A*                           107/06/01  2018AR1168 S00WCJ (0706A)
     A*                            增加F5功能鍵以訂單編號查詢試算
     A*                            單號
     A*                           108/07/26 2019AR00048 S00WCJ (0807A)
     A*                            程式抬頭改為區間指定發票開立試算
     A*                           108/09/11 S00WCJ (0809A)
     A*                            配合磅單過至項次修改
     A*                           108/09/17 S00WCJ (0809B)
     A*                            增加功能鍵可顯示第二行
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      INDARA
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前螢幕')
0706AA                                      CF05(05 '試算單號查詢')
     A*================================================================
     A          R TITLE
     A                                      OVERLAY
     A                                  1 70'ARE098S-1'
     A            COMP          35A  O  1 23
     A            S#DATE         8  0O  1  2EDTWRD('    /  /  ')
0807AA                                  2 26'*區間指定發票開立試算維護*'
     A            DEVNM         10A  O  2 70
     A*=========
     A                                  5 30'功　　能:'
     A            CODE           1A  B  5 42
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  5 44'(1:新增 2:修改'
     A                                  7 45'3:刪除 4:查詢5:列印)'
     A*
     A                                 11 30'訂單編號:'
     A            S#ORNO         6A  B 11 42
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A*
     A                                 13 30'日期區間:'
     A            S#DAT1         8  0B    +1
     A                                      EDTWRD('    /  /  ')
     A  43                                  DSPATR(PC RI)
     A                                    +1'∼'
     A            S#DAT2         8  0B    +1
     A                                      EDTWRD('    /  /  ')
     A  44                                  DSPATR(PC RI)
     A*
     A                                 15 30'試算單號:'
     A            S#NO          10A  B    +1
     A  45                                  DSPATR(PC RI)
     A*
0001BA                                 17 30'自動上Y'
0001BA            S#YN           1A  B    +1VALUES('Y' 'N')
0001BA                                    +1'(Y/N '
0112AA                                    +1'選Y表區間開立發票，'
0112AA                                 18 51' N表指定開立發票)'
0002AA                                 19 30'廠  區:'
0002AA            S#AREA         1A  B    +1VALUES(' ' 'P' 'M' 'T' 'U' +
0002AA                                      'H' 'K')
0002AA                                    +1'(P=台北M=台中T=八德'
0002AA                                 20 45'U=桃園H=苗栗K=高雄)'
     A            S#ERR         70A  O 23  2DSPATR(HI)
     A*=========
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
0706AA                                    +1'PF05:試算單號查詢'
     A*****************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            S#OPT          1A  B 12  3
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A            S#DATT         8  0O    +1EDTWRD('    /  /  ')
     A            S#TXNO         8A  O    +1
     A            S#ITEM         2  0O 12 27
0809AA            S#VRTM         2  0O 12 33
0809AA            S#PDS1         3A  O 12 38
0409AA            S#PDNM         5A  O 12 45
0409AA            S#UPRC         5  3O 12 52EDTCDE(K)
0001AA            S#QTY          7  0O 12 60EDTCDE(K)
0809AA            S#SQTY         7  0B 13 38EDTCDE(K)
0809AA                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
0809AA            S#SPRC         5  3B 13 52EDTCDE(K)
0809AA                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
0809AA            S#SAMT         6  0B 13 62EDTCDE(K)
0809AA                                      DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A                                      SFLSIZ(0011)
0809BA                                      SFLPAG(0004)
     A                                      OVERLAY
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
0809BA*N73                                  SFLEND(*MORE)
0809BA N73                                  SFLEND
0809BA                                      SFLDROP(CF08)
     A                                      SFLCSRRRN(&S#CSR)
     A            S#CSR          5  0H
     A            S#NBR          4  0H      SFLRCDNBR(CURSOR)
     A*-----------
     A                                  1 70'ARE098S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD           10A  O  2  2DSPATR(RI)
0807AA                                  2 26'*區間指定發票開立試算維護*'
     A            DEVNM         10A  O  2 70
     A*-----------
     A                                  4  2'試算編號:'
     A            S#NO          10A  O    +1
     A                                  4 33'單據日期:'
     A            S#ACDS         8  0O  4 45EDTCDE(Z)
     A                                  5  2'訂單編號:'
     A            S#ORNO         6   O    +1
     A                                  5 33'客戶編號:'
0208AA            S#CUNO         6A  O    +1
     A            S#CUNM        10A  O    +1
0207AA                                  6  2'備註:'
0207AA            S#TEX1        70O  B    +1
0207AA            S#TEX2        70O  B  7 10
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
0809AA                                  9  2'挑Ｙ日期'
0809AA                                    +1'磅單編號項次磅次徑度'
0809AA                                    +1'品名 單價  數 量'
0809AA                                 10 38'調整數量 調整單價'
0809AA                                      DSPATR(HI)
0809AA                                    +1'調整金額'
0809AA                                      DSPATR(HI)
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND01
     A                                      OVERLAY
0809AA                                 20  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A                                      DSPATR(HI)
0809AA                                 21 12'合　計:'
0809AA            S#TQTY         8  0O 21 59EDTCDE(2)
0809AA                                 22 12'調整合計:'
0809AA                                      DSPATR(HI)
0809AA            S#TQY2         7  0O 22 38EDTCDE(K)
0809AA                                      DSPATR(HI)
0809AA            S#TAMT         7  0O 22 60EDTCDE(K)
0809AA                                      DSPATR(HI)
     A            S#ERR         70A  O 23 02DSPATR(HI)
     A*
     A                                 24  5'PF03:結束'
     A                                    +2'PF12:回前畫面'
0809BA                                    +1'PF08:顯示調整資訊'
     A***************************************************************
