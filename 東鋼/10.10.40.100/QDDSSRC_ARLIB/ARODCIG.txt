     A*================================================================
     A* 管制類別: 01訂單開立02交單開立03出單開立
     A*             04發票開立05請款作業06傳票代開
     A*             20材證    21無輻射
     A*================================================================
     A*
     A* 管制類別為發票類者：
     A*
     A* 開立方式：    00每日隨車開立（一車一張）
     A*                 01每日一訂單一張（與一車一張一起處理）
     A*                 02每月一訂單一張
     A*                 03依日期區間彙總一張
     A*                 04挑選銷貨明細開立
     A*                 05人工開立發票
     A*
     A*                 **不在本管制記錄內者為隨車開立（一車一張）
     A*                 -----------------------------------
     A*
     A* 管制類別為材證、無輻射者：
     A*
     A* 開立方式：    00每日隨車開立（一車一張）
     A*                 01每日一訂單一張（與一車一張一起處理）
     A*                 03依日期區間彙總一張
     A*                 -----------------------------------
     A*
     A* 附屬類別一：  00不考慮加工費（手開調整單處理）
     A*                 01加工費合開在出貨發票內
     A*                 02加工費另開一張發票
     A*
     A*                 **不在本管制記錄內者視同不考慮
     A*                 -----------------------------------
     A*
     A* 附屬比率金額一：每公斤銷貨之加工費單價
     A*
     A*                 -----------------------------------
     A*
     A* 附屬類別二：  00不考慮廠租費（手開調整單處理）
     A*                 01廠租費合開在出貨發票內
     A*                 02廠租費另開一張發票
     A*
     A*                 **不在本管制記錄內者視同不考慮
     A*                 -----------------------------------
     A*
     A* 附屬比率金額二：每公斤銷貨之場租費單價
     A*
     A*================================================================
     A*                                     UNIQUE
     A          R RARODCI                   TEXT('訂單管制檔')
     A            AIFLAG         1A         COLHDG('處理旗標')
     A            AIORNO         9A         COLHDG('訂單編號')
     A            AICTKD         2A         COLHDG('管制類別')
     A            AIMTHD         2A         COLHDG('開立方式')
     A            AITYP1         2P 0       COLHDG('附屬類別一')
     A            AIRAT1        11P 3       COLHDG('附屬比率金額一')
     A            AITYP2         2P 0       COLHDG('附屬類別二')
     A            AIRAT2        11P 3       COLHDG('附屬比率金額二')
     A            AITYP3         2P 0       COLHDG('附屬類別三')
     A            AIRAT3        11P 3       COLHDG('附屬比率金額三')
     A*
     A            AISQNO        13A         COLHDG('傳輸序號')
     A            AITMDT         8S 0       COLHDG('傳輸日期')
     A            AITMTM         6S 0       COLHDG('傳輸時間')
     A            AITMOP        10A         COLHDG('傳輸人員')
     A            AITAWS        10A         COLHDG('傳輸工作站')
     A            AISTCL         1A         COLHDG('執行碼')
     A*
     A            AIUSER        10A         COLHDG('上傳者JOB')
     A            AICUSR        10A         COLHDG('上傳者CUR')
     A            AIXPDD         8S 0       COLHDG('上傳日期')
     A            AIXPDT         6S 0       COLHDG('上傳時間')
     A*
     A          K AIORNO
     A          K AICTKD
     A          K AISQNO
     A*
