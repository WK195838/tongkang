     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR093T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     應收帳款承購客戶請款單開立（請款單列印)
     A*           4.DATE-WRITTEN  91/10/07
     A*           5.WRITER        S02CSF
     A*
     A*****************************************************************
     A          R AR093T1H
     A**************************************************************************
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      ' PRT-ID  : ARR093T'
     A                                    39
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    79
     A                                      '列表日期:'
     A                                    90
     A                                      DATE
     A                                      EDTCDE(Y)
     A*
     A                                     2
     A                                      ' USR-ID  :'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                    42
     A                                      '※　請　款　單　※'
     A                                    79
     A                                      '列表時間:'
     A                                    90
     A                                      TIME
     A*
     A                                    79
     A                                      '頁　　次:'
     A                                      SPACEB(001)
     A                                    90
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*
     A                                     2
     A                                      '客戶編號:'
     A                                      SPACEB(002)
     A            T#CUNO         6A  O    14
     A                                   +14
     A                                      '訂單編號　:'
     A            T#ORNO         6A  O    +1
     A                                   +10
     A                                      '請款日期:'
     A            T#APDT         6S  O    +1
     A                                      EDTCDE(Y)
     A*
     A                                     2
     A                                      '客戶名稱:'
     A                                      SPACEB(001)
     A            T#CUNM        10O  O    13
     A                                   +11
     A                                      '收款業務員:'
     A            T#RVID         2A  O    +1
     A                                   +14
     A                                      '請款單號:'
     A            T#APNO         6A  O    +1
     A*
     A            T#LIN1       100A  O     1
     A                                      SPACEB(001)
     A*
     A                                     1
     A                                      '發票日期'
     A                                      SPACEB(001)
     A                                    11
     A                                      '發票號碼'
     A                                    21
     A                                      '磅單號碼'
     A                                    31
     A                                      '出貨數量'
     A                                    43
     A                                      '出貨金額'
     A                                    53
     A                                      '折讓／銷退'
     A                                    66
     A                                      '預收貨款'
     A                                    77
     A                                      '銷貨稅額'
     A                                    89
     A                                      '請款金額'
     A*
     A            T#LIN2       100A  O     1
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR093T1D
     A**************************************************************************
     A            T#BLK          1A  O     1
     A                                      SPACEB(001)
     A  51        T#INDT         6S  O     2
     A                                      EDTCDE(Y)
     A  51        T#INNO        10A  O    11
     A            T#WTNO         8A  O    22
     A            T#QTY          7S  O    31
     A                                      EDTCDE(J)
     A            T#AAMT         8S  O    42
     A                                      EDTCDE(J)
     A  51        T#BAMT         7S  O    54
     A                                      EDTCDE(J)
     A  51        T#CAMT         8S  O    65
     A                                      EDTCDE(J)
     A  51        T#DAMT         7S  O    77
     A                                      EDTCDE(J)
     A  51        T#EAMT         8S  O    88
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR093T1L
     A**************************************************************************
     A                                     1
     A                                      ' '
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR093T1M
     A**************************************************************************
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ============================='
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR093T1T
     A**************************************************************************
     A                                    11
     A                                      '合　　計:'
     A                                      SPACEB(001)
     A            T#QTYT         7S 0O    +9
     A                                      EDTCDE(J)
     A            T#TAAM         8S 0O    +1
     A                                      EDTCDE(J)
     A            T#TBAM         7S 0O    +1
     A                                      EDTCDE(J)
     A            T#TCAM         8S 0O    +1
     A                                      EDTCDE(J)
     A            T#TDAM         7S 0O    +1
     A                                      EDTCDE(J)
     A            T#TEAM         8S 0O    +0
     A                                      EDTWRD('  ,   , $0 -')
     A*
     A                                    81
     A                                      '發票總張數:'
     A                                      SPACEB(002)
     A            T#NUMS         3S 0O    +1
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR093T1X
     A**************************************************************************
     A                                     1
     A                                      '==================================-
     A                                      =========接　下　頁==============-
     A                                      ============================='
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR093T1Y
     A**************************************************************************
     A                                    11
     A                                      '客戶簽收:'
     A                                      SPACEB(005)
     A                                   +10
     A                                      '簽收日期:'
     A                                   +10
     A                                      '主管:'
     A                                   +10
     A                                      '業務員:'
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ============================='
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR093T1Z
     A**************************************************************************
     A                                     3
     A                                      '一、承蒙　貴客戶惠顧，不勝感激，-
     A                                      茲檢送　貴客戶本期應付帳款明細、-
     A                                      出貨單（磅單）、及發票（如附）'
     A                                      SPACEB(001)
     A                                     7
     A                                      '敬請查收並惠予核查，若發現帳款內-
     A                                      容與實際不符時，請即連絡本公司營-
     A                                      業部經辦人員予以更正。'
     A                                      SPACEB(001)
     A                                     3
     A                                      '二、核查無誤後，本公司業務員將依-
     A                                      貴我雙方約定之領款日（　年　月　-
     A                                      日）前往收款。'
     A                                      SPACEB(002)
     A                                     3
     A                                      '三、本公司帳款處理均已電腦化，敬-
     A                                      請貴客戶依本帳單所列金額核實付款-
     A                                      。'
     A                                      SPACEB(002)
     A*****************************************************************
     A          R AR093T1P
     A**************************************************************************
     A            T#PDN1         3A  O     5
     A                                      SPACEB(001)
     A            T#NAME        14A  O     9
     A            T#PDS1         3A  O    26
     A            T#QTYP         7S 0O    31
     A                                      EDTCDE(J)
     A            T#TAAP         8S 0O    +1
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR093T1Q
     A**************************************************************************
     A                                    11
     A                                      '合　　計:'
     A                                      SPACEB(001)
     A            T#QTYQ         7S 0O    31
     A                                      EDTCDE(J)
     A            T#TAAQ         8S 0O    +1
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR093TMG
     A**************************************************************************
     A                                    19
     A                                      '*****無符合條件之資料!!!'
     A                                      SPACEB(008)
     A
     A**************************************************************************
