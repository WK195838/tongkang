     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     H*
     A*            1.PROGRAM-ID    ARE030R
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     人工發票開立作業
     A*            5.DATE-WRITTEN  85/11/30
     A*
     A***************************************************************
     A*  畫面一:輸入客戶及訂單
     A*
     A*        記錄格式 AR030F1
     A***************************************************************
     A          R AR030F1                   CF03(03)
     A                                      CA12(12)
     A*--------------------------------------------------------------
     A                                  1 70'ARE030S-1'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 32'人工發票開立作業'
     A            S#DEVI        10A  O  2 70
     A*--------------------------------------------------------------
     A                                 10 20'訂單號碼:'
     A            S#ORNO         6A  B    +1
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A                                 23 12'F03:結束'
     A                                    +6'F12:回前'
     A                                    +6'ENTER:執行'
     A            S#MSG1        70A  O 24  2DSPATR(HI)
     A
     A***************************************************************
     A*  畫面二:人工發票開立作業畫面
     A*
     A*        記錄格式 AR030F2*
     A***************************************************************
     A          R AR030F2                   SFL
     A  70                                  SFLNXTCHG
     A            S#ITEM         2Y 0O 11  9
     A     43                               DSPATR(RI HI)
     A            S#PDNM         3A  B 11 16
     A     41                               DSPATR(PR)
     A     42                               DSPATR(RI)
     A            S#QTY          7Y 0B 11 22EDTWRD(' ,   ,   -')
     A     41                               DSPATR(PR)
     A     42                               DSPATR(RI)
     A            S#UPRC         7Y 5B 11 35EDTWRD(' 0.     ')
     A     41                               DSPATR(PR)
     A     42                               DSPATR(RI)
     A            S#AMT         11Y 0B 11 47EDTWRD('  ,   ,   ,   -')
     A     41                               DSPATR(PR)
     A     42                               DSPATR(RI)
     A            S#APNO         8A  B    +4
     A     41                               DSPATR(PR)
     A     42                               DSPATR(RI)
     A***************************************************************
     A          R AR030F2C                  SFLCTL(AR030F2)
     A                                      SFLSIZ(99)
     A                                      SFLPAG(11)
     A                                      OVERLAY
     A    N41                               CF03(03)
     A    N41                               CF05(05)
     A    N41                               CF10(10)
     A                                      CA12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR  SFLINZ
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A*--------------------------------------------------------------
     A                                  1 70'ARE030S-1'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 32'人工發票開立作業'
     A            S#DEVI        10A  O  2 70
     A*--------------------------------------------------------------
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  O    +1
     A                                    +1'客戶簡稱:'
     A            S#CUNM        10O  O    +1
     A                                    +2'訂單號碼:'
     A            S#ORNO         6A  O    +1
     A                                  5  2'發票類別:'
     A            S#TYPE         1A  O    +1
     A                                    +6'發票日期:'
     A            S#INDT         8Y 0B    +1
     A     41                               DSPATR(PR)
     A                                    +6'課稅別:'
     A            S#TXTP         1A  O    +1
     A                                    +6'發票聯式:'
     A            S#KIND         1A  B    +1
     A                                      VALUES('2' '3')
     A     41                               DSPATR(PR)
     A                                  6  2'銷售別:'
     A            S#SATP         1A  O    +1
     A                                    +8'出貨業務員:'
     A            S#SALE         2A  O    +1
     A                                    +8'收款業務員:'
     A            S#RVID         2A  O    +1
     A                                    +1'開立廠區:'
     A            S#AREA         1A  O    +1
     A                                  7  2'發票金額:'
     A            S#AAMT        11Y 0O    +1
     A                                      EDTWRD('  ,   ,   ,  0-')
     A                                      DSPATR(RI)
     A                                    +1'稅額:'
     A            S#ATAX        11Y 0O    +1
     A                                      EDTWRD('  ,   ,   ,  0-')
     A                                      DSPATR(RI)
     A                                    +1'扣預收:'
     A            S#BAMT        11Y 0O    +1
     A                                      EDTWRD('  ,   ,   ,  0-')
     A                                      DSPATR(RI)
     A*--------------------------------------------------------------
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9  7'項次'
     A                                    +1'品名'
     A                                    +4'數　量'
     A                                    +4'單價'
     A                                    +8'金　　額'
     A                                    +5'憑証編號'
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR030F2M                  TEXT('按鍵及錯誤訊息')
     A                                      OVERLAY
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  2'F3:回主畫面'
     A                                    +2'F5:銷貨明細沖銷'
     A                                    +2'F10:存檔'
     A                                    +2'F12:回前畫面'
     A                                    +2'ENTER:試算'
     A            S#MSG2        70A  O 24  2DSPATR(HI)
     A     42                               DSPATR(RI)
     A***************************************************************
     A*
     A*
     A***************************************************************
     A*  畫面三:銷貨明細沖銷畫面
     A*
     A*        記錄格式 AR030F3*
     A***************************************************************
     A
     A          R AR030F3                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT          1A  B 10  3VALUES(' ' '1')
     A            S#TXCD         4A  O 10  5
     A            S#TXNO         8A  O    +1
     A            S#TXIT         2Y 0O    +1
     A            S#TXDT         6Y 0O    +1
     A            S#TXPD         3A  O    +1
     A            S#TXQT         7Y 0O    +1EDTWRD('      0')
     A            S#TXUP         5Y 3O    +1EDTWRD(' 0.   ')
     A            S#TXAM        11Y 0O    +1EDTWRD('          0-')
     A            S#TXTA        11Y 0O    +1EDTWRD('          0-')
     A            S#TXSA         2A  O    +1
     A            S#TXRV         2A  O    +1
     A***************************************************************
     A          R AR030F3C                  SFLCTL(AR030F3)
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A                                      OVERLAY
     A                                      CF10(10)
     A                                      CA12(12)
     A                                      ROLLUP(91)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN3)
     A            S#NBR3         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN3         5S 0H
     A*--------------------------------------------------------------
     A                                  1 70'ARE030S-2'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTCDE(Y)
     A                                  2 34'銷貨明細沖銷'
     A            S#DEVI        10A  O  2 70
     A*--------------------------------------------------------------
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  O    +1
     A                                    +1'訂單號碼:'
     A            S#ORNO         6A  O    +1
     A                                    +2'沖銷起迄日期:'
     A            S#BGDT         6Y 0B    +1
     A                                    +1'至'
     A            S#ENDT         6Y 0B    +1
     A                                  5  4'(選項: 1 = 沖銷)'
     A                                  5 40'試算沖銷金額:'
     A            S#TSAM        11Y 0O    +1
     A                                      EDTWRD('  ,   ,   ,   -')
     A*--------------------------------------------------------------
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  1'選　單據　單據'
     A                                  7 18'項　單據　品'
     A                                  7 70'出貨收款'
     A                                  8  1'項　代號　號碼'
     A                                  8 18'次　日期　名'
     A                                  8 34'數量'
     A                                  8 42'單價'
     A                                  8 51'金額'
     A                                  8 63'稅額'
     A                                  8 72'業務員'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR030F3M                  TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  2'PGUP:上一頁'
     A                                    +2'PGDN:下一頁'
     A                                    +2'F10:存檔'
     A                                    +2'F12:取消'
     A                                    +2'ENTER:試算'
     A            S#MSG3        70A  O 24  2DSPATR(HI)
     A***************************************************************
