     A*%%TS  SD  19920515  113401  S02CSF      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE049SB
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CSF
     A*            4.FUNCTION     新版承購客戶沖銷作業
     A*            5.DATE-WRITTEN  92/03/24
     A*            6.DATE-MODIFY
     A*
     A***************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束')
     A                                      CA12(12 '回上頁')
     A***************************************************************
     A*  畫面一:新增、修改、刪除、查詢、沖銷、確認、確認還原畫面
     A*
     A*  *IN33 : ON 表示為查詢狀態
     A***************************************************************
     A          R AR049F1                   SFL
     A*%%TS  SD  19920515  113401  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT1         1A  B  9  4VALUES(' ' '2' '4' '5' '6' '8' '9')
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A            S#CUNO         6A  O  9  8
     A            S#CUNM        10O  O  9 15
     A            S#RCNO         6A  O  9 27
     A            S#ACDT         8Y 0O  9 36EDTWRD('    /  /  ')
     A            S#TAMT        11Y 0O  9 47EDTCDE(3)
     A            S#FAMT        11Y 0O  9 60EDTCDE(3)
     A            S#FL01         1A  O  9 73
     A            S#FL03         1A  O  9 75
     A            S#FL02         1A  O  9 77
     A***************************************************************
     A          R AR049F1C                  SFLCTL(AR049F1)
     A*%%TS  SD  19920515  113226  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0023)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CF05(05)
     A                                      CF06(06)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE049S-1'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 28'★新版承購客戶沖銷作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A                                  4  2'選項:'
     A                                  4 11'2:修改'
     A                                  4 21'4:刪除'
     A                                  4 31'5:查詢'
     A                                  4 42'6:沖銷'
     A                                  4 53'8:確認'
     A                                  4 64'9:確認還原'
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6  2'選項'
     A                                  6 10'客　　戶'
     A                                  6 27'編號'
     A                                  6 37'日期'
     A                                  6 48'沖銷合計'
     A                                  6 61'繳款合計'
     A                                  6 72'確印過'
     A                                  7  2'-----------------------------------
     A                                      ------------+---+---+----+---+---+--
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#KEY1         6A  B  8  8
     A***************************************************************
     A          R AR049F1M
     A*%%TS  SD  19920324  184642  S02CSF      REL-V4R4M0  5769-PW1
     A                                 22  2'-----------------------------------
     A                                      ------------+---+---+----+---+---+--
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  4'F03:結束'
     A                                 23 16'F12:回前'
     A                                 23 28'F05:重新顯示'
     A                                 23 44'F06:新增'
     A                                 23 57'ENTER:執行'
     A            S#ERR1        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面二:輸入繳款明細畫面
     A***************************************************************
     A          R AR049F2                   SFL
     A*%%TS  SD  19920325  181635  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#SFI2         2Y 0O 12  2
     A            S#ACTP         1A  B 12  7VALUES(' ' 'C' 'D')
     A  33                                  DSPATR(PR)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A            S#USTP         2A  B 12 10DUP(35)
     A  33                                  DSPATR(PR)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A            S#NTTP         1A  B 12 14DUP(36)
     A  33                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A            S#ACNO         4A  B 12 18
     A  33                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A            S#PBID        14O  B 12 26DUP(37)
     A  33                                  DSPATR(PR)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A            S#PANO        10A  B 12 41DUP(38)
     A  33                                  DSPATR(PR)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A            S#NTNO        10A  B 12 53
     A  33                                  DSPATR(PR)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A            S#RAMT        11Y 0B 12 65EDTWRD('  ,   ,   ,   ')
     A  33                                  DSPATR(PR)
     A  49                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A            S#RLNO        10E  B 13 18
     A  33                                  DSPATR(PR)
     A  50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A            S#PLAC         1A  B 13 32VALUES(' ' 'D' 'E')
     A                                      DUP(39)
     A  33                                  DSPATR(PR)
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A            S#SANO        10A  B 13 41
     A  33                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A            S#DUDT         8Y 0B 13 53EDTWRD('    /  /  ')
     A  33                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A***************************************************************
     A          R AR049F2C                  SFLCTL(AR049F2)
     A*%%TS  SD  19920513  085620  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A                                      CF01(01)
     A N33                                  CF10(10)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A            S#NBR2         4S 0H
     A            S#SFN2         4S 0H
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE037S-2'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 28'★承購沖銷明細輸入作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A            S#SF2T         6O  O  3 70DSPATR(RI)
     A                                  4  8'繳款編號:'
     A            S#RCNO         6A  O  4 20
     A                                  4 27'繳款日期:'
     A            S#ACDT         8Y 0B  4 39EDTWRD('    /  /  ')
     A  33                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A                                  5  8'客戶編號:'
     A            S#CUN2         6A  B  5 20
     A  33                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A                                  5 27'客戶名稱:'
     A            S#CUNM        10O  O  5 39
     A                                  5 50'繳款金額合計:'
     A            S#NAMT        11Y 0O  5 66EDTCDE(1)
     A                                      DSPATR(HI)
     A N33                              6  3'借貸方清成空白，該項明細視同刪除-
     A                                      '
     A                                      DSPATR(RI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  1'項　借　款　票'
     A                                  9  1'　　貸　項　款　會計科目'
     A                                  9 28'付款銀行　　付款帳戶'
     A                                  9 53'票據號碼　　　金　　　額'
     A                                 10  1'次　方　別　別　相關號碼'
     A                                 10 28'付款地　　　存入帳號'
     A                                 10 53'到期日期'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR049F2M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  4'F01:說明'
     A                                 23 19'F03:結束'
     A                                 23 34'F12:回前'
     A  33                             23 49'ENTER:執行'
     A N33                             23 49'ENTER:檢核'
     A N33                             23 66'F10:存檔'
     A            S#ERR2        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A* 畫面二：輸入資料說明視窗
     A***************************************************************
     A          R AR049F2W
     A                                      WINDOW(8 14 12 62)
     A                                      TEXT('說明視窗')
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      OVERLAY
     A                                  2  3'款項別'
     A                                      DSPATR(HI)
     A                                  2 18'票款別'
     A                                      DSPATR(HI)
     A                                  2 40'常用會計科目'
     A                                      DSPATR(HI)
     A                                  4  1'A1:一般貨款'
     A                                  4 16'A:支票'
     A                                  5  1'A2:預收貨款'
     A                                  5 16'B:背書支票'
     A                                  6 16'C:本票'
     A                                  6  1'Z5:其他'
     A                                  7 16'H:庫存現金'
     A                                  8 16'J:銀行存款'
     A                                  9 16'I:信用狀'
     A                                  4 32'1111庫存現金'
     A                                  4 48'1112銀行存款'
     A                                  5 32'1131應收票據'
     A                                  5 48'1137進項稅額'
     A                                  7 32'2159暫收其他'
     A                                  7 48'8246奇零尾數'
     A                                  9 32'7111（推）運　費'
     A                                 10 32'7112（推）檢驗費'
     A                                 11 32'7142（推）郵電費'
     A***************************************************************
     A*  畫面三:發票沖銷畫面
     A***************************************************************
     A          R AR049F3                   SFL
     A*%%TS  SD  19920327  163340  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#SFI3         2Y 0O 11  3
     A            S#APN1         6A  O 11  7
     A            S#INNO        10A  B 11 16
     A  33                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A            S#INDT         6Y 0O 11 29EDTWRD('  /  /  ')
     A            S#AMT1         9Y 0B 11 41EDTCDE(1)
     A  33                                  DSPATR(PR)
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(RI)
     A            S#AMT2         9Y 0O 11 54EDTCDE(1)
     A            S#AMT3         9Y 0O 11 67EDTCDE(1)
     A***************************************************************
     A          R AR049F3C                  SFLCTL(AR049F3)
     A*%%TS  SD  19920327  163340  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A N33                                  CF08(08)
     A N33                                  CF09(09)
     A N33                                  CF10(10)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A            S#NBR3         4S 0H
     A            S#SFN3         4S 0H
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE049S-3'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 26'★新版承購發票沖銷繳款作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A            S#SF3T         6O  O  3 70DSPATR(RI)
     A                                  4  8'繳款編號:'
     A            S#RCNO         6A  O  4 20
     A                                  4 39'繳款日期:'
     A            S#ACDT         8Y 0O  4 51EDTWRD('    /  /  ')
     A                                  5  8'客戶編號:'
     A            S#CUNO         6A  O  5 20
     A                                  5 39'客戶名稱:'
     A            S#CUNM        10O  O  5 51
     A                                  6 39'繳款金額合計:'
     A            S#NAMT        11Y 0O  6 55DSPATR(HI)
     A                                      EDTCDE(1)
     A                                  6  8'沖銷金額合計:'
     A            S#AMTT        11Y 0O  6 24DSPATR(HI)
     A                                      EDTCDE(1)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A N33                              8  3'清除發票號碼該筆明細視同刪除'
     A                                      DSPATR(RI)
     A                                  9  5'請款單號'
     A                                  9 16'發票號碼'
     A                                  9 28'發票日期'
     A                                  9 42'沖銷金額'
     A                                  9 55'發票金額'
     A                                  9 68'發票餘額'
     A                                 10  2'------------------+---+---+--------
     A                                      ---+---+---+---+---+---+------------
     A                                      +---+---+'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR049F3M
     A*%%TS  SD  19920515  104258  S02CSF      REL-V4R4M0  5769-PW1
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 21  2'------------------+---+---+--------
     A                                      ---+---+---+---+---+---+------------
     A                                      +---+---+'
     A                                      DSPATR(HI)
     A                                 22  4'F03:結束'
     A                                 22 24'F12:回前'
     A  33                             22 44'ENTER:執行'
     A N33                             22 44'ENTER:檢核'
     A N33                             23  4'F08:依承購單載入'
     A N33                             23 24'F09:依請款單載入'
     A N33                             23 44'F10:存檔'
     A            S#ERR3        70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR049W1
     A*%%TS  SD  19910312  160642  S02YSH      REL-V4R4M0  5769-PW1
     A                                      WINDOW(8 44 8 24)
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                  2  5'*承購單載入*'
     A                                  4  2'承購單號:'
     A            S#AP1W         6A  B  4 14
     A  90                                  DSPATR(RI)
     A                                  6  3'F3/F12:離開'
     A                                      DSPATR(HI)
     A            S#ERW1        20A  O  7  2DSPATR(HI)
     A*================================================================
     A          R AR049W2
     A*%%TS  SD  19910312  160642  S02YSH      REL-V4R4M0  5769-PW1
     A                                      WINDOW(8 44 8 24)
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                  2  5'*請款單載入*'
     A                                  4  2'請款單號:'
     A            S#AP2W         6A  B  4 14
     A  82                                  DSPATR(RI)
     A                                  6  3'F3/F12:離開'
     A                                      DSPATR(HI)
     A            S#ERW2        20A  O  7  2DSPATR(HI)
