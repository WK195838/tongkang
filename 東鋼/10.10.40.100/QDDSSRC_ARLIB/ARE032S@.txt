     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE032R
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     人工發票開立銷貨沖銷維護作業
     A*            5.DATE-WRITTEN  86/01/20
     A*
     A***************************************************************
     A*  畫面一:輸入訂單
     A***************************************************************
     A          R AR032F1                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT1         1A  B 11  9VALUES('1' ' ')
     A            S#ORNO         6A  O 11 15
     A            S#ORDT         6S 0O 11 25
     A            S#CTNO        10A  O 11 34
     A            S#CUNO         6A  O 11 47
     A            S#CUNM        10A  O 11 55
     A***************************************************************
     A          R AR032F1C                  SFLCTL(AR032F1)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A            S#SFN1         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE032S-1'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  2 28'人工發票開立發票沖銷維護'
     A                                  4  2'選項: 1 --> 維護該訂單已開立之+
     A                                      發票與銷貨明細之沖銷情形　　　'
     A                                  5 16'（可輸入完整或部份的訂單編號指定+
     A                                      位置）'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  5'選項'
     A                                  8 25'日期'
     A                                  8 34'購案編號'
     A                                  8 12'訂單編號'
     A                                  8 47'客戶編號及名稱'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#ORN1         6A  B 10 15
     A  51                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A***************************************************************
     A          R AR032F1M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#MSG1        70A  O 24  2DSPATR(HI)
     A                                 23  4'F03:結束'
     A                                 23 18'F12:回前'
     A                                 23 32'ENTER:執行'
     A                                 23 48'PGUP:上一頁'
     A                                 23 65'PGDN:下一頁'
     A***************************************************************
     A*  畫面二:發票選擇畫面
     A***************************************************************
     A          R AR032F2                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT2         1A  B 11  4VALUES('1' '2' '5' ' ')
     A            S#INAR         1A  O 11  7
     A            S#INNO        10A  O 11  9
     A            S#INDT         6Y 0O 11 20EDTCDE(Y)
     A            S#INAM        11Y 0O 11 30EDTWRD('  ,   ,   ,   -')
     A            S#INTX        11Y 0O 11 47EDTWRD('  ,   ,   ,   -')
     A            S#INBM        11Y 0O 11 64EDTWRD('  ,   ,   ,   -')
     A***************************************************************
     A          R AR032F2C                  SFLCTL(AR032F2)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE032S-2'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  2 28'人工發票開立發票沖銷維護'
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  O  4 14
     A                                  4 21'客戶簡稱:'
     A            S#CUNM        10O  O  4 33
     A                                  4 45'訂單號碼:'
     A            S#ORNO         6A  O  4 57
     A                                  4 64'廠區:'
     A            S#AREA         1A  O  4 72
     A                                  5 43'已開發票銷貨總額:'
     A                                  5 10'5 --> 查詢發票明細'
     A            S#S2AM        11Y 0O  5 64EDTWRD('  ,   ,   ,   -')
     A                                  6  2'選項: 1 --> 以該張發票來沖尚未+
     A                                      沖銷之銷貨明細（新增沖銷資料）'
     A                                  7 10'2 --> 維護該張發票已沖銷之銷貨明+
     A                                      細（查詢還原已沖資料）'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9  2'選項'
     A                                  9  9'發票號碼'
     A                                  9 20'日　期'
     A                                  9 32'金　額'
     A                                  9 49'稅　額'
     A                                  9 66'扣預收'
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR032F2M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#MSG2        70A  O 24  2DSPATR(HI)
     A                                 23  2'ENTER:執行'
     A                                 23 21'F3:結束本作業'
     A                                 23 43'F12:回前畫面'
     A***************************************************************
     A*  畫面三:未沖銷畫面
     A***************************************************************
     A          R AR032F3                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT3         1A  B 12  3VALUES(' ' '1')
     A  53                                  DSPATR(PR)
     A            S#TXCD         4A  O 12  5
     A            S#TXNO         8A  O    +1
     A            S#TXIT         2Y 0O    +1
     A            S#TXDT         6Y 0O    +1
     A            S#TXPD         3A  O    +1
     A            S#TXQT         7Y 0O    +1EDTWRD('      0')
     A            S#TXUP         5Y 3O    +1EDTWRD(' 0.   ')
     A            S#TXAM        11Y 0O    +1EDTWRD('          0-')
     A            S#TXTA        11Y 0O    +1EDTWRD('          0-')
     A            S#TXSA         2A  O    +1
     A            S#TXRV         2A  O    +1
     A***************************************************************
     A          R AR032F3C                  SFLCTL(AR032F3)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF10(10)
     A                                      CA12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN3)
     A            S#NBR3         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN3         5S 0H
     A            S#SFN3         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE030S-3'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTCDE(Y)
     A                                  2 32'沖銷銷貨明細作業'
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  O  4 14
     A                                  4 21'訂單號碼:'
     A            S#ORNO         6A  O  4 33
     A                                  4 49'發票號碼:'
     A            S#INNO        10A  O  4 61
     A                                  5 49'發票金額:'
     A            S#INAM        11Y 0O  5 61EDTWRD('  ,   ,   ,   -')
     A                                  6  2'未沖銷金額合計:'
     A            S#3XAM        11Y 0O  6 20EDTWRD('  ,   ,   ,   -')
     A                                  6 43'本發票已沖金額:'
     A            S#3TAM        11Y 0O  6 61EDTWRD('  ,   ,   ,   -')
     A                                  7  4'(選項: 1 = 沖銷)'
     A                                  7 45'試算沖銷金額:'
     A            S#TSAM        11Y 0O  7 61EDTWRD('  ,   ,   ,   -')
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9  1'選　單據　單據'
     A                                  9 18'項　單據　品'
     A                                  9 70'出貨收款'
     A                                 10  1'項　代號　號碼'
     A                                 10 18'次　日期　名'
     A                                 10 34'數量'
     A                                 10 42'單價'
     A                                 10 51'金額'
     A                                 10 63'稅額'
     A                                 10 72'業務員'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR032F3M                  TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  2'PGUP:上一頁'
     A                                    +2'PGDN:下一頁'
     A                                    +2'F10:存檔'
     A                                    +2'F12:取消'
     A                                    +2'ENTER:試算'
     A            S#MSG3        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面四:已沖銷畫面
     A***************************************************************
     A          R AR032F4                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT4         1A  B 12  3VALUES(' ' '1')
     A  53                                  DSPATR(PR)
     A            S#TXCD         4A  O 12  5
     A            S#TXNO         8A  O    +1
     A            S#TXIT         2Y 0O    +1
     A            S#TXDT         6Y 0O    +1
     A            S#TXPD         3A  O    +1
     A            S#TXQT         7Y 0O    +1EDTWRD('      0')
     A            S#TXUP         5Y 3O    +1EDTWRD(' 0.   ')
     A            S#TXAM        11Y 0O    +1EDTWRD('          0-')
     A            S#TXTA        11Y 0O    +1EDTWRD('          0-')
     A            S#TXSA         2A  O    +1
     A            S#TXRV         2A  O    +1
     A***************************************************************
     A          R AR032F4C                  SFLCTL(AR032F4)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF10(10)
     A                                      CA12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN4)
     A            S#NBR4         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN4         5S 0H
     A            S#SFN4         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE030S-4'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTCDE(Y)
     A                                  2 31'已沖銷銷貨明細維護'
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  O  4 14
     A                                  4 21'訂單號碼:'
     A            S#ORNO         6A  O  4 33
     A                                  4 46'發票號碼:'
     A            S#INNO        10A  O  4 58
     A                                  5 46'發票金額:'
     A            S#INAM        11Y 0O  5 58EDTWRD('  ,   ,   ,   -')
     A                                  6 42'已沖金額合計:'
     A            S#4TAM        11Y 0O  6 58EDTWRD('  ,   ,   ,   -')
     A                                  7  4'(選項: 1 = 還原)'
     A                                  7 42'試算還原金額:'
     A            S#TSAM        11Y 0O  7 58EDTWRD('  ,   ,   ,   -')
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9  1'選　單據　單據'
     A                                  9 18'項　單據　品'
     A                                  9 70'出貨收款'
     A                                 10  1'項　代號　號碼'
     A                                 10 18'次　日期　名'
     A                                 10 34'數量'
     A                                 10 42'單價'
     A                                 10 51'金額'
     A                                 10 63'稅額'
     A                                 10 72'業務員'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR032F4M                  TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  2'PGUP:上一頁'
     A                                    +2'PGDN:下一頁'
     A                                    +2'F10:存檔'
     A                                    +2'F12:取消'
     A                                    +2'ENTER:試算'
     A            S#MSG4        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面五:發票明細溜灠畫面
     A***************************************************************
     A          R AR032F5                   SFL
     A  70                                  SFLNXTCHG
     A            S#ACNT         2A  O 11  8
     A            S#ITEM         2Y 0O 11 15
     A            S#PDNM         3A  O 11 20
     A            S#QTY          7Y 0O 11 25EDTWRD(' ,   ,   -')
     A            S#UPRC         7Y 5O 11 37EDTWRD(' 0.     ')
     A            S#AMT         11Y 0O 11 49EDTWRD('  ,   ,   ,   -')
     A            S#APNO         8A  O 11 67
     A***************************************************************
     A          R AR032F5C                  SFLCTL(AR032F5)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A                                      CA12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN5)
     A            S#NBR5         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN5         5S 0H
     A            S#SFN5         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE032S-5'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 32'發票明細溜灠'
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  3  2'發票號碼:'
     A            S#INNO        10   O  3 14
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  O  4 14
     A                                  4 21'客戶簡稱:'
     A            S#CUNM        10O  O  4 33
     A                                  4 45'訂單號碼:'
     A            S#ORNO         6A  O  4 57
     A                                  5  2'發票類別:'
     A            S#TYPE         1A  O  5 14
     A                                  5 21'發票日期:'
     A            S#INDT         6Y 0O  5 33
     A                                  5 45'課稅別:'
     A            S#TXTP         1A  O  5 55
     A                                  5 62'發票聯式:'
     A            S#KIND         1A  O  5 74
     A                                  6  2'銷售別:'
     A            S#SATP         1A  O  6 12
     A                                  6 21'出貨業務員:'
     A            S#SALE         2A  O  6 35
     A                                  6 45'收款業務員:'
     A            S#RVID         2A  O  6 59
     A                                  6 62'開立廠區:'
     A            S#AREA         1A  O  6 74
     A                                  7  2'發票金額:'
     A            S#AAMT        11Y 0O  7 14EDTWRD('  ,   ,   ,  0-')
     A                                      DSPATR(RI)
     A                                  7 30'稅額:'
     A            S#ATAX        11Y 0O  7 38EDTWRD('  ,   ,   ,  0-')
     A                                      DSPATR(RI)
     A                                  7 54'扣預收:'
     A            S#BAMT        11Y 0O  7 64EDTWRD('  ,   ,   ,  0-')
     A                                      DSPATR(RI)
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9  5'種類'
     A                                  9 12'項次'
     A                                  9 19'品名'
     A                                  9 26'數　量'
     A                                  9 38'單價'
     A                                  9 51'金　　額'
     A                                  9 66'憑証編號'
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR032F5M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  5'F12:回前畫面'
     A                                 23 25'ENTER:結束'
     A            S#MSG5        70A  O 24  2DSPATR(HI)
