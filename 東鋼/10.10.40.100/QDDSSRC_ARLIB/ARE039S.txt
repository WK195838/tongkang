     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE039R
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     新版客戶訂單管制維護作業
     A*            5.DATE-WRITTEN  88/10/28
     A*
     A***************************************************************
     A*  畫面一:輸入客戶
     A***************************************************************
     A          R AR039F1                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT1         1A  B 11  9VALUES(' ' '2' '4' '6')
     A            S#FLAG         1A  O    +2
     A            S#CUNO         6A  O    +2
     A            S#CUNM        10A  O    +5
     A            S#CTKD         2Y 0O    +6
     A            S#CTNM         6O  O    +1
     A            S#RFOR         1A  O    +4
     A***************************************************************
     A          R AR039F1C                  SFLCTL(AR039F1)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      CF05(05)
     A                                      CF06(06)
     A                                      CF08(08)
     A                                      CF20(20)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A            S#SFN1         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE039S-1'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A            S#DEVI        10A  O  2 70
     A                                  2 26'★客戶訂單管制資料維護作業★'
     A                                      DSPATR(RI)
     A                                  3  2'客戶－主'
     A                                      DSPATR(RI)
     A*----------------------------------------------------------------
     A                                  5  2'選項: 2 --> 修改，    4-->註-
     A                                      記刪除，    6-->還原刪除。'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  5'選項'
     A                                    +3'客戶編號'
     A                                    +2'客戶名稱'
     A                                    +5'管制類別'
     A                                    +2'參照'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#INK1         6A  B 10 15
     A  51                                  DSPATR(PC)
     A***************************************************************
     A          R AR039F1M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 21  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22  4'F03:結束'
     A                                 22 18'F12:回前'
     A                                 22 32'F05:重新顯示'
     A                                 22 50'F06:新增'
     A                                 22 64'F08:訂單管制'
     A                                      DSPATR(HI)
     A                                 23  4'F20:傳輸異動'
     A                                      DSPATR(RI)
     A            S#MSG1        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面二:編輯畫面
     A***************************************************************
     A          R AR039F2
     A                                      CF01(01)
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      CF10(10)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE039S-2'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 26'★客戶訂單管制資料維護作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A                                  3  2'客戶－內容'
     A                                      DSPATR(RI)
     A*----------------------------------------------------------------
     A            S#EDTP         6O  O  5  4DSPATR(RI)
     A                                  7 22'客戶編號:'
     A            S#CUNO         6A  B  7 36
     A  52                                  DSPATR(PR)
     A  54                                  DSPATR(PC RI)
     C            S#CUNM        10O  O    +1
     A                                  9 22'管制類別:'
     A            S#CTKD         2Y 0B  9 36
     A  52                                  DSPATR(PR)
     A  56                                  DSPATR(PC RI)
     A                                  9 42'(01訂單, 02交單, 03出單,'
     A                                 10 43'04發票, 05請款, 06傳票)'
     A                                 12 22'是否參照訂單管制：'
     A            S#RFOR         1A  B    +1VALUES(' ' 'Y')
     A                                    +2'("Y"表示必須再參考訂單管制)'
     A                                 14 22'開立方式:'
     A            S#MTHD         2Y 0B 14 36
     A                                 16 22'附屬類別一:'
     A            S#TYP1         2Y 0B 16 38
     A            S#RAT1        11Y 3B 16 44EDTCDE(M)
     A                                 17 22'附屬類別二:'
     A            S#TYP2         2Y 0B 17 38
     A            S#RAT2        11Y 3B 17 44EDTCDE(M)
     A                                 18 22'附屬類別三:'
     A            S#TYP3         2Y 0B 18 38
     A            S#RAT3        11Y 3B 18 44EDTCDE(M)
     A*----------------------------------------------------------------
     A                                 22  6'F03:結束'
     A                                      DSPATR(HI)
     A                                 22 20'F12:回前'
     A                                      DSPATR(HI)
     A                                 22 34'PGUP:上筆'
     A                                      DSPATR(HI)
     A                                 22 49'PGDN:下筆'
     A                                      DSPATR(HI)
     A                                 22 64'F10:存檔'
     A                                      DSPATR(HI)
     A                                 23  6'F01:說明'
     A            S#MSG2        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面三:輸入訂單
     A***************************************************************
     A          R AR039F3                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT3         1A  B 11  9VALUES('1' ' ')
9105 A            S#ORNO         6A  O 11 15
     A            S#ORDT         6S 0O 11 25
     A            S#CTNO        10O  O 11 34
     A            S#CUNO         6A  O 11 47
     A            S#CUNM        10A  O 11 55
     A***************************************************************
     A          R AR039F3C                  SFLCTL(AR039F3)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      CF05(05)
     A                                      CF06(06)
     A                                      CF08(08)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN3)
     A            S#NBR3         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN3         5S 0H
     A            S#SFN3         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE039S-3'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 26'★客戶訂單管制資料維護作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A                                  3  2'訂單－主'
     A                                      DSPATR(RI)
     A*----------------------------------------------------------------
     A                                  4  2'選項: 1 --> 維護該訂單之特殊管+
     A                                      制情形。'
     A                                  5 16'（可輸入完整或部份的訂單編號指定+
     A                                      位置）'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  5'選項'
     A                                  8 25'日期'
     A                                  8 34'購案編號'
     A                                  8 12'訂單編號'
     A                                  8 47'客戶編號及名稱'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#INK3         6A  B 10 15
     A  51                                  DSPATR(PC)
     A***************************************************************
     A          R AR039F3M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 21  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22  4'F03:結束'
     A                                 22 18'F12:回前'
     A                                 22 32'F05:重新顯示'
     A                                 22 50'F06:新增'
     A                                 22 64'F08:載入訂單'
     A            S#MSG3        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面四:訂單項次選擇畫面
     A***************************************************************
     A          R AR039F4                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT4         1A  B 11  4VALUES(' ' '2' '4' '6')
     A            S#FLAG         1A  O    +1
9105 A            S#ITEM         3Y 0O    +2DSPATR(HI)
     A            S#CTKD         2Y 0O    +2
     A            S#CTNM         6O  O    +1
     A            S#MTHD         2Y 0O    +2
     A            S#TYP1         2Y 0O    +2
     A            S#RAT1        11Y 3O    +1EDTCDE(M)
     A            S#TYP2         2Y 0O    +1
     A            S#RAT2        11Y 3O    +1EDTCDE(M)
     A            S#TYP3         2Y 0O    +1
     A            S#RAT3        11Y 3O    +1EDTCDE(M)
     A***************************************************************
     A          R AR039F4C                  SFLCTL(AR039F4)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A                                      CF01(01)
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      CF06(06)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN4)
     A            S#NBR4         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN4         5S 0H
     A            S#SFN4         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE039S-4'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 26'★客戶訂單管制資料維護作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A                                  3  2'訂單－明細'
     A                                      DSPATR(RI)
     A*----------------------------------------------------------------
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  O  4 14
     A                                  4 27'客戶簡稱:'
     A            S#CUNM        10O  O  4 39
     A                                  5  2'訂單號碼:'
9105 A            S#ORNO         6A  O    +1DSPATR(HI)
     A                                    +8'購案編號:'
     A            S#CTNO        10O  O    +1
     A                                  7  2'選項: 2 --> 修改，    4-->註-
     A                                      記刪除，    6-->還原刪除。'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9  2'訂單項次　管制類別　方式'
     A                                    +2'附屬類別一'
     A                                    +5'附屬類別二'
     A                                    +5'附屬類別三'
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR039F4M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23 24'F03:結束'
     A                                 23 40'F12:回前'
     A                                 23 56'F06:新增'
     A                                      DSPATR(HI)
     A            S#MSG4        70A  O 24  2DSPATR(HI)
     A                                 23  8'F01:說明'
     A***************************************************************
     A*  畫面五:載入訂單
     A***************************************************************
     A          R AR039F5
     A                                      CF01(01)
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      CF10(10)
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE039S-5'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 26'★客戶訂單管制資料維護作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A                                  3  2'訂單－載入'
     A                                      DSPATR(RI)
     A*----------------------------------------------------------------
     A                                  7 22'訂單編號:'
9105 A            S#ORNO         6A  B  7 36
     A  54                                  DSPATR(PC RI)
     A                                  9 22'管制類別:'
     A            S#CTKD         2Y 0B  9 36
     A  56                                  DSPATR(PC RI)
     A                                  9 42'(01訂單, 02交單, 03出單,'
     A                                 10 43'04發票, 05請款, 06傳票)'
     A                                 12 22'開立方式:'
     A            S#MTHD         2Y 0B 12 36
     A                                 15 22'附屬類別一:'
     A            S#TYP1         2Y 0B 15 38
     A            S#RAT1        11Y 3B 15 44EDTCDE(M)
     A                                 16 22'附屬類別二:'
     A            S#TYP2         2Y 0B 16 38
     A            S#RAT2        11Y 3B 16 44EDTCDE(M)
     A                                 17 22'附屬類別三:'
     A            S#TYP3         2Y 0B 17 38
     A            S#RAT3        11Y 3B 17 44EDTCDE(M)
     A*
     A                                 19 22'載入的訂單其所有項次將會設為-
     A                                      本畫面的管制內容。'
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A                                 22  6'F03:結束'
     A                                      DSPATR(HI)
     A                                 22 20'F12:回前'
     A                                      DSPATR(HI)
     A                                 22 64'F10:存檔'
     A                                      DSPATR(HI)
     A                                 23  6'F01:說明'
     A            S#MSG5        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面六:編輯畫面
     A***************************************************************
     A          R AR039F6
     A                                      CF01(01)
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      CF10(10)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE039S-6'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 26'★客戶訂單管制資料維護作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A                                  3  2'訂單－內容'
     A                                      DSPATR(RI)
     A*----------------------------------------------------------------
     A            S#EDTP         6O  O  5  4DSPATR(RI)
     A                                  7 22'訂單編號:'
9105 A            S#ORNO         6A  B  7 36
     A  52                                  DSPATR(PR)
     A  54                                  DSPATR(PC RI)
9105 A            S#ITEM         3Y 0B  7 43
     A  53                                  DSPATR(PR)
     A  55                                  DSPATR(PC RI)
     A                                  9 22'管制類別:'
     A            S#CTKD         2Y 0B  9 36
     A  53                                  DSPATR(PR)
     A  56                                  DSPATR(PC RI)
     A                                  9 42'(01訂單, 02交單, 03出單,'
     A                                 10 43'04發票, 05請款, 06傳票)'
     A                                 12 22'開立方式:'
     A            S#MTHD         2Y 0B 12 36
     A                                 15 22'附屬類別一:'
     A            S#TYP1         2Y 0B 15 38
     A            S#RAT1        11Y 3B 15 44EDTCDE(M)
     A                                 16 22'附屬類別二:'
     A            S#TYP2         2Y 0B 16 38
     A            S#RAT2        11Y 3B 16 44EDTCDE(M)
     A                                 17 22'附屬類別三:'
     A            S#TYP3         2Y 0B 17 38
     A            S#RAT3        11Y 3B 17 44EDTCDE(M)
     A*----------------------------------------------------------------
     A                                 22  6'F03:結束'
     A                                      DSPATR(HI)
     A                                 22 20'F12:回前'
     A                                      DSPATR(HI)
     A                                 22 34'PGUP:上筆'
     A                                      DSPATR(HI)
     A                                 22 49'PGDN:下筆'
     A                                      DSPATR(HI)
     A                                 22 64'F10:存檔'
     A                                      DSPATR(HI)
     A                                 23  6'F01:說明'
     A            S#MSG6        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A          R AR039W1
     A                                      WINDOW(4 10 15 60)
     A                                      TEXT('說明視窗')
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A*----------------------------------------------------------------
     A                                  2  1'管制類別:'
     A                                      DSPATR(HI)
     A                                  2 14'01訂單,  02交單,  03出單,'
     A                                  3 14'04發票,  05請款,  06傳票'
     A                                  4  1'==================================-
     A                                      =========================='
     A                                  5 21'發票開立管制說明'
     A                                      DSPATR(RI)
     A                                  7 11'（未管制之訂單皆採預設值00來處+
     A                                      理。）'
     A                                      COLOR(RED)
     A                                  9  1'開立方式:'
     A                                      DSPATR(HI)
     A                                  9 14'00每日隨車,  01每日隨訂,  02+
     A                                      每月隨訂,'
     A                                 10 14'03區間隨訂,  04指定開立,  05+
     A                                      人工開立'
     A                                 12  1'附屬類別一（加工費）:'
     A                                      DSPATR(HI)
     A                                 12 26'00不處理,  01合開,  02另開'
     A                                 14  1'附屬類別二（廠租費）:'
     A                                      DSPATR(HI)
     A                                 14 26'00不處理,  01合開,  02另開'
