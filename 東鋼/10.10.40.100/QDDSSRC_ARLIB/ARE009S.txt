     A****************************************************************
     A*
     A*            UPDATE  DATE  101/09/18  2012AR682  S00WCJ (0109A)
     A*                          107/05/17  2018AR1155 S00WCJ (0705A)
     A*                           增加部門代號
     A*
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RCVDTL)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARE009S-1'
     A            COMP          35   O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 27'*** 非營業繳款通知單 ***'
     A            DEVNM         10   O  2 70
     A                                  5 30'功　　能:'
     A            CODE           1   B    +1
     A  41                                  DSPATR(PC RI)
     A                                    +1'(1:新增 2:修改'
     A                                  7 46'3:刪除 4:查詢)'
     A                                  9 30'繳款單號:'
     A            NO             6   B    +1
     A  42                                  DSPATR(PC RI)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            ITEM      R        O 12  2REFFLD(RVITEM)
     A            ACTP      R        B 12  7REFFLD(RVACTP)
     A  70                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
0109AA            A1NO      R        B 12 10REFFLD(RVACNO)
     A  70                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A            USTP      R        B 12 20REFFLD(RVUSTP)
     A                                      DUP(25)
     A  70                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A            NTTP      R        B 12 24REFFLD(RVNTTP)
     A                                      DUP(24)
     A  70                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A            PBID      R     O  B 12 26REFFLD(RVPBID)
     A                                      DUP(21)
     A  70                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A            PANO      R        B 12 41REFFLD(RVPANO)
     A                                      DUP(22)
     A  70                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A            PLAC      R        B 12 52REFFLD(RVPLAC)
     A                                      DUP(23)
     A  70                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A            RLNO          10A  B 12 56
     A  70                                  DSPATR(PR)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            RAMT          10Y 0B 12 67EDTWRD(' ,   ,   ,   ')
     A  70                                  DSPATR(PR)
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(RI)
     A            NTNO      R        B 13 10REFFLD(RVNTNO)
     A  70                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A            SANO      R        B 13 41REFFLD(RVSANO)
     A  70                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DUDT      R        B 13 56REFFLD(RVDUDT)
     A                                      EDTWRD('    /  /  ')
     A  70                                  DSPATR(PR)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A            FLAG      R        B 13 74REFFLD(RVFLAG)
     A  70                                  DSPATR(PR)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A*           AMCD      R        B 13 74REFFLD(RVAMCD)
     A* 70                                  DSPATR(PR)
     A* 65                                  DSPATR(PC)
     A* 65                                  DSPATR(RI)
     A*           SBID      R        H      REFFLD(RVSBID)
0705AA            S#DEPT         4   B 14 10
0705AA  70                                  DSPATR(PR)
0705AA  61                                  DSPATR(PC)
0705AA  61                                  DSPATR(RI)
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A*%%TS  SD  19811028  115156  JEFF        REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0090)
0705AA                                      SFLPAG(0003)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARE009S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 27'*** 非營業繳款通知單 ***'
     A            DEVNM         10A  O  2 70
     A                                  4  8'繳款編號:'
     A            NO             6A  O  4 20
     A                                  4 44'繳款部門:'
     A            DPNO      R        B  4 56REFFLD(RVDPNO)
     A  70                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A                                  5  8'繳款對象:'
     A            CUNM      R     E  B  5 20REFFLD(RVCUNM)
     A                                  5 44'繳款日期:'
     A            DATE      R        B  5 56REFFLD(RVDATE)
     A                                      EDTWRD('    /  /  ')
     A  70                                  DSPATR(PR)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  1'項　借　　　　'
     A                                  7 19'款　票'
     A                                  7 51'付'
     A                                  8  1'　　貸　會計科目　項　款'
     A                                  8 28'付款銀行　　付款帳戶'
     A                                  8 51'款'
     A                                  8 56'相關號碼　　　金　　額'
     A                                  9  1'次　方　票據號碼　別　別'
     A                                  9 28'　　　　　　存入帳號'
     A                                  9 51'地'
     A                                  9 56'到期日期　　　　標記　'
0705AA                                 10  9'部門代號'
0705AA                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND01
     A*%%TS  SD  19811005  100143  JEFF        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                 21  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22 57'合　計:'
     A            TTLAMT        10Y 0O 22 68EDTCDE(1)
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                 24 19'PF03-結束'
     A                                 24 32'PF10-沖銷輸入'
     A                                 24 49'PF12-回前畫面'
     A***************************************************************
     A          R SF3                       SFL
     A*%%TS  SD  19811214  173326  JEFF        REL-V2R1M1  5738-PW1
     A            ITEM      R        O 11  2REFFLD(RVITEM)
     A            DCCD           1A  B 11  7
     A  70                                  DSPATR(PR)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
0109AA            GLNO           9A  B 11 10
     A  70                                  DSPATR(PR)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A            WAYC           1A  B 11 20
     A  70                                  DSPATR(PR)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            DEPT           4A  B 11 25
     A  70                                  DSPATR(PR)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A            RLNO          10E  B 11 30
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            MUID          10E  B 11 41
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            AAMT          10Y 0B 11 54EDTWRD(' ,   ,   ,   ')
     A  70                                  DSPATR(PR)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A            FLAG           1A  B 11 70
     A  70                                  DSPATR(PR)
     A  39                                  DSPATR(PC)
     A  39                                  DSPATR(RI)
0705AA            S#BGCD         4A  B 12 12
0705AA  70                                  DSPATR(PR)
0705AA  44                                  DSPATR(PC)
0705AA  44                                  DSPATR(RI)
     A            COMM          30O  B 12 51DUP(22)
     A  70                                  DSPATR(PR)
     A            BNNM          10A  B 12 30
     A  70                                  DSPATR(PR)
     A  38                                  DSPATR(PC)
     A  38                                  DSPATR(RI)
     A            DUE1           8Y 0B 12 41
     A  70                                  DSPATR(PR)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A***************************************************************
     A          R SFCTL3                    SFLCTL(SF3)
     A*%%TS  SD  19811214  173326  JEFF        REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0005)
     A                                      CF10
     A                                      CF12
     A                                      CF03
     A                                      OVERLAY
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A  10                                  SFLEND
     A                                  1 70'ARE009S-3'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 27'*** 非營業繳款通知單 ***'
     A            DEVNM         10A  O  2 70
     A                                  4  5'繳款編號:'
     A            NO             6A  O  4 17
     A                                  4 30'繳款部門:'
     A            DPNO      R        O  4 42REFFLD(RVDPNO)
     A                                  4 51'繳款日期:'
     A            DATE      R        O  4 63REFFLD(RVDATE)
     A                                      EDTWRD('    /  /  ')
     A                                  5  5'繳款對象:'
     A            CUNM      R        O  5 17REFFLD(RVCUNM)
     A                                  5 30'繳款總金額:'
     A            TTLAMT        10Y 0O  5 44EDTWRD(' ,   ,   , 0 ')
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  1'項'
     A                                  9  1'次'
0705AA                                  8  6'借會計科目'
     A                                  8 19'方'
     A                                  9 19'式'
     A                                  8 24'部門　相關號碼'
     A                                  9 24'代號　銀行帳號'
     A                                  8 42'對象別　'
     A                                  8 59'金　額'
     A                                  8 68'標記'
0705AA                                  9  6'貸歸屬代號'
0705AA*                                 8 10'會計科目'
0705AA*                                 9 10'歸屬代號'
     A                                  9 51'輔助說明'
     A                                  9 42'到期日'
     A***************************************************************
     A          R UND03
     A*%%TS  SD  19811005  170123  JEFF        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                 21  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22 46'合計： '
     A            TAAMT         10Y 0O 22 56EDTWRD(' ,   ,   , 0 ')
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                 24 19'PF03:結束'
     A                                 24 32'PF10:存檔'
     A                                 24 45'PF12:回前畫面'
