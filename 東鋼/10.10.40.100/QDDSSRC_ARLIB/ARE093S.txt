     A*%%TS  SD  19911206  152238  S02CSF      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*         >>   PROGRAM  INTRODUCTION  <<
     A*         1.PROGRAM-ID      ARR093S
     A*         2.PROGRAM-TYPE    DSPF
     A*         3.AUTHOR          S02CSF
     A*         4.FUNCTION       應收帳款承購客戶請款開立
     A*         5.DATE-WRITTEN    91/10/02
     A*         6.DATE-MODIFY     91/12/04
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前畫面')
     A          R AR093H
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 28'東和鋼鐵企業股份有限公司'
     A                                  1 70'ARR093S'
     A                                  2  2TIME
     A                                  2 27'應收帳款承購客戶請款單開立'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            U#USID        10A  O  3  2
     A N35        S#MODE         6A  O  3 70
     A                                      DSPATR(RI)
     A*================================================================
     A          R AR093F1
     A                                      OVERLAY
     A                                  5 30'功　　能:'
     A            S#CODE         1A  B  5 42
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                  5 44'(1:新增 2:修改'
     A                                  7 45'3:刪除 4:查詢 '
     A                                  9 45'5:列印 )'
     A                                 11 30'客戶編號:'
     A            S#CUNO         6A  B 11 42
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                 13 30'訂單編號:'
     A            S#ORNO         6A  B 13 42
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                 15 30'請款單號:'
     A            S#APNO         6A  B 15 42
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            S#ERR1        70A  O 23  2DSPATR(HI)
     A                                 24  5'PF03:結束'
     A                                 24 20'PF12:回前'
     A***************************************************************
     A          R AR093F2                   SFL
     A*%%TS  SD  19911206  152238  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT          1A  B 10  5VALUES(' ' 'Y')
     A  34                                  DSPATR(PR)
     A                                      DSPATR(PC)
     A            S#APN1         6A  O 10 10
     A            S#INDT         8Y 0O 10 20EDTWRD('    /  /  ')
     A            S#INNO        10A  O 10 33
     A            S#DAMT         8Y 0O 10 46EDTCDE(1)
     A            S#BAMT         6Y 0O 10 61EDTCDE(1)
     A*================================================================
     A          R AR093F2C                  SFLCTL(AR093F2)
     A*%%TS  SD  19911206  152238  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0012)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A                                      OVERLAY
     A                                      CF05(05 '列印')
     A                                      CF10(10 '存檔')
     A                                  5  3'客戶代號:'
     A            S#CUNO         6A  O  5 15
     A            S#CUNM        10A  O  5 23
     A                                  5 59'請款日期:'
     A            S#APDT         8Y 0O  5 71EDTWRD('    /  /  ')
     A                                  6  3'訂單編號:'
     A            S#ORNO         6A  O  6 15
     A                                  6 23'請款單號:'
     A            S#APNO         6A  O  6 35
     A                                  6 59'請款金額:'
     A            S#EAMT         8Y 0O  6 71EDTCDE(2)
     A                                      DSPATR(HI)
     A                                  7  4'OP:Y選取'
     A                                  8  4'OP'
     A                                  8  8'承購單號'
     A                                  8 21'發票日期'
     A                                  8 33'發票號碼'
     A                                  8 47'發票金額'
     A                                  8 59'折讓金額'
     A            S#LIN1        78A  O  9  2
     A            S#NBR          4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR093F2M
     A*%%TS  SD  19911206  095611  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                 23  5'F3=結束'
     A                                 23 15'F12=回前畫面'
     A N76                             23 30'F10=存檔'
     A N75                             23 41'F05=列印'
     A                                 23 52'執行＝試算請款金額'
     A            S#LIN2        78A  O 22  2
     A            S#ERR2        70A  O 24  2DSPATR(HI)
