     A*%%TS  SD  19940822  104933  S02CSF      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARR122S
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CSF
     A*            4.FUNCTION     未開立發票明細表－即時
     A*            5.DATE-WRITTEN  94/08/18
     A*            6.DATE-MODIFY   101/10/30  S00WCJ (0110A)
     A*
     A*
     A*     CF:此報表與ARR036P不同處在於:
     A*         ARR036P會根據使用者所下的資料年月，將資料的時點回推！
     A*         ARR122P是根據使用者所下的條件，抓出目前及時的資料！
     A*
     A***************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前畫面')
     A          R AR122F1
     A*%%TS  SD  19940822  104933  S02CSF      REL-V4R4M0  5769-PW1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 28'東和鋼鐵企業股份有限公司'
     A                                  1 70'ARR122S'
     A                                  2  2TIME
     A                                  2 27'★未開立發票明細表－即時★'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USID        10A  O  3  2
     A                                  6 22'銷貨年月：'
     A            S#YMS          6Y 0B  6 35EDTWRD('    /  ')
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A                                  6 43'~~'
     A            S#YME          6Y 0B  6 46EDTWRD('    /  ')
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A                                  8 22'銷貨廠區：'
     A            S#AREA         1A  B  8 35
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
0110AA                                  8 37'(T:桃園 H:苗栗M:台中'
0110AA                                  9 38'K:高雄U:新桃空白表全部)'
     A                                 11 22'營業單位：'
     A            S#DEPT         4A  B 11 35
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                 11 40'(B010 , B020 , B030 , B040 '
     A                                 12 41'B050 ,空白表全部)'
     A                                 14 22'業務代號：'
     A            S#SAID         2A  B 14 35
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A                                 14 38'(空白表全部)'
     A                                 16 22'訂單編號：'
     A            S#ORNO         6A  B 16 35
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                 16 42'(空白表全部)'
     A                                 18 18'發票開立方式：'
     A            S#KIND         1A  B 18 35VALUES(' ' '1' '2')
     A                                 18 37'(1:隨車開立2:區間開立空白表+
     A                                      全部)'
     A            S#ERR         70A  O 23  2DSPATR(HI)
     A                                 24  5'PF03:結束'
     A                                 24 18'PF12:回前畫面'
