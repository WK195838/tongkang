     A*%%TS  SD  19901020  105917  S02LYW      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE047S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02LYW
     A*            4.FUNCTION     承購銀行基本資料登錄
     A*            5.DATE-WRITTEN  90/10/18
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束作業')
     A                                      CA12(12 '回前畫面')
     A*================================================================
     A          R AR047H
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 24'も　東和鋼鐵企業股份有限公司　も-
     A                                      '
     A            S#SRID        10      1 70
     A                                  2  2TIME
     A                                  2 30'★承購銀行資料登錄★'
     A                                      DSPATR(RI)
     A            S#DEVN        10   O  2 70
     A            S#USER        10      3  2
     A            S#MODE         6      3 70
     A  30                                  DSPATR(RI)
     A*================================================================
     A          R AR047F1                   SFL
     A*%%TS  SD  19901020  105917  S02LYW      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT          1Y 0B 10  4EDTCDE(Z)
     A                                      VALUES(0 2 4 5)
     A            S#BKID         3A  O 10  9
     A            S#BKNM        10A  O 10 20
     A*================================================================
     A          R AR047F1C                  SFLCTL(AR047F1)
     A*%%TS  SD  19901020  105917  S02LYW      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      ROLLUP(91 '下頁')
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  74                                  SFLCLR
     A                                      OVERLAY
     A                                      CA06(06 '新增')
     A                                  4  4'OP: 2=修改4=刪除5=查詢'
     A            S#LIN1        78A  O  5  2
     A                                  7  4'OP'
     A                                  7  8'銀行代號'
     A                                  7 20'銀行簡稱'
     A            S#LIN2        78A  O  8  2
     A            S#KEY1         3A  B  9  9
     A  31                                  DSPATR(PC)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR047FM
     A                                      OVERLAY
     A            S#LIN3        78   O 21  2
     A                                 23  2'F3=結束作業'
     A                                    +1'F12=回前畫面'
     A                                    +1'PA1=下頁'
     A                                    +1'PA2=上頁'
     A                                    +1'F6=新增'
     A                                      DSPATR(RI)
     A            S#ERR1        70   O 24  2DSPATR(HI)
     A*================================================================
     A          R AR047F2
     A                                      OVERLAY
     A  80                                  CF10(10 '存檔')
     A*                                     OVERLAY
     A                                  6  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                  8  2'銀行代號:'
     A            S#BKID         3A  B    +1
     A  89
     AO 81                                  DSPATR(PR)
     A  82                                  DSPATR(PC RI)
     A                                  9  2'銀行簡稱:'
     A            S#BKNM        10J  B    +1
     A  89
     AO 81                                  DSPATR(PR)
     A  83                                  DSPATR(PC RI)
     A                                 10  2'分行別  :'
     A            S#ACN1         4A  B    +1
     A  89
     AON80                                  DSPATR(PR)
     A                                 11  2'備償專戶帳號:'
     A            S#ACN2        10A  B    +1
     A  89
     AON80                                  DSPATR(PR)
     A                                 12  2'手續費計費方式:'
     A            S#SRTP         1A  B    +1VALUES(' ' '0' '1')
     A  89
     AON80                                  DSPATR(PR)
     A  84                                  DSPATR(PC RI)
     A                                    +1'(0:逐筆 1:總額)'
     A                                 13  2'手續費比例:'
     A            S#SRCG         6Y 5B    +1EDTCDE(3)
     A  89
     AON80                                  DSPATR(PR)
     A  85                                  DSPATR(PC RI)
     A                                 14  2'融資成數:'
     A            S#INRT         6Y 5B    +1EDTCDE(3)
     A  89
     AON80                                  DSPATR(PR)
     A  86                                  DSPATR(PC RI)
     A                                 15  2'承購起日:'
     A            S#DAT1         8Y 0B    +1EDTCDE(4)
     A  89
     AON80                                  DSPATR(PR)
     A  87                                  DSPATR(PC RI)
     A                                 16  2'承購止日:'
     A            S#DAT2         8Y 0B    +1EDTCDE(4)
     A  89
     AON80                                  DSPATR(PR)
     A  88                                  DSPATR(PC RI)
     A                                 17  2'異動人員:'
     A            S#UPDM        10A  O    +1
     A                                 18  2'異動日期:'
     A            S#UPDD         8  0O    +1EDTWRD('    /  /  ')
     A*                                     EDTCDE(4)
     A                                 19  2'異動時間:'
     A            S#UPDT         6  0O    +1EDTWRD('  :  :  ')
     A                                 22  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                 23  2'F3=結束作業'
     A                                 23 16'F12=回前畫面'
     A  80                             23 31'F10=存檔'
     A                                      DSPATR(RI)
     A            S#ERR2        70A  O 24  2DSPATR(HI)
