     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARR089T
     A*            2.PROGRAM-TYPE  PRTF
     A*            3.AUTHOR        S02YSH
     A*            4.FUNCTION     外購成品出貨明細表-品名別
     A*            5.DATE-WRITTEN  89/08/15
     A*            6.DATE-MODIFY   89/08/15
     A*
     A*****************************************************************
     A          R AR089H
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%FS 002
     A*%%***********************************************************************
     A                                      SKIPB(003)
     A                                     2
     A                                      ' RPT - ID : ARR089T'
     A                                    41
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    83
     A                                      '印表日期:'
     A                                    95
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      '日期區間:'
     A                                      SPACEB(001)
     A            T#DATS         8  0     +1
     A                                      EDTWRD('    /  /  ')
     A                                    +0
     A                                      '∼'
     A            T#DATE         8  0     +0
     A                                      EDTWRD('    /  /  ')
     A                                    40
     A                                      '外購成品出貨明細表-品名別'
     A                                    83
     A                                      '頁　　數:'
     A                                    95
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '品　　名:'
     A                                      SPACEB(001)
     A            T#PDNS         3        +1
     A                                    +1
     A                                      '∼'
     A            T#PDNE         3        +1
     A                                     2
     A                                      '報表類別:'
     A                                      SPACEB(001)
     A            T#PRTF         8        +1
     A            T#LIN1       105         2
     A                                      SPACEB(001)
     A                                     2
     A                                      '品名'
     A                                      SPACEB(001)
     A                                    +1
     A                                      '日　　期'
     A                                    +1
     A                                      '磅單編號'
     A                                    +1
     A                                      '客戶編號'
     A                                    +1
     A                                      '客戶名稱'
     A                                    +1
     A                                      '訂單號碼'
     A                                    +5
     A                                      '數量'
     A                                    +4
     A                                      '單價'
     A                                   +11
     A                                      '金額'
     A            T#LIN2       105         2
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR089T1
     A            T#PDNM         3         2
     A                                      SPACEB(001)
     A            T#ENTD         8  0     +3
     A                                      EDTWRD('    /  /  ')
     A            T#TXNO        10        +3
     A            T#CUNO         5        +4
     A            T#CUNM        10        +4
     A            T#ORNO         6        +3
     A            T#QTY          7  0     +4
     A                                      EDTCDE(J)
     A            T#UPRC         5  3     +2
     A                                      EDTCDE(J)
     A            T#AMT         11  0     +2
     A                                      EDTCDE(J)
     A**************************************************************
     A          R AR089T2
     A                                    50
     A                                      '品名小計:'
     A                                      SPACEB(001)
     A            T#QTY1         8  0     64
     A                                      EDTCDE(J)
     A            T#AMT1        11  0     86
     A                                      EDTCDE(J)
     A**************************************************************
     A          R AR089T3
     A                                    50
     A                                      '日期小計:'
     A                                      SPACEB(001)
     A            T#QTY2         8  0     64
     A                                      EDTCDE(J)
     A            T#AMT2        11  0     86
     A                                      EDTCDE(J)
     A**************************************************************
     A          R AR089T4
     A                                    50
     A                                      '合　　計:'
     A                                      SPACEB(001)
     A            T#QTY3         9  0     63
     A                                      EDTCDE(J)
     A            T#AMT3        11  0     86
     A                                      EDTCDE(J)
