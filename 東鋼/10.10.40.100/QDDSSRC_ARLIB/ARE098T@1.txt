     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARE098T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     區間發票開立試算表
     A*           4.DATE-WRITTEN  99/05/27 2010AR421
     A*           5.WRITER        S00WCJ
     A*           6.UPDATE  DATE  99/08/02 2010AR514  S00WCJ (9907A)
     A*
     A*****************************************************************
     A          R AR098T1H
     A*****************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      ' PRT-ID  : ARE098T'
     A                                    40
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    85
     A                                      '列表日期:'
     A                                    +1
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      ' USR-ID  :'
     A                                      SPACEB(001)
     A            U#USID        10        +1
     A                                    40
     A                                      '※區間發票開立試算表※  '
     A                                    85
     A                                      '列表時間:'
     A                                    +1
     A                                      TIME
     A                                    85
     A                                      '頁　　次:'
     A                                      SPACEB(001)
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '試算單號:'
     A                                      SPACEB(002)
     A            R#NO          10        +1
     A                                    +1
     A                                      '訂單編號:'
     A            R#ORNO         6        +1
     A                                    +1'客戶編號:'
     A            R#CUNO         6        +1
     A                                    +1'客戶名稱:'
     A            R#CUNM        10        +1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
     A                                      ================'
     A                                      SPACEB(001)
     A                                     1
     A                                      '日  期 '
     A                                      SPACEB(001)
     A                                    +1
     A                                      '磅單編號'
     A                                    +1
     A                                      '項次'
     A                                    +1
     A                                      '品名'
     A                                    +1
     A                                      '單價'
     A                                    47
     A                                      '數 量 '
9908AA                                    60
9908AA                                      '金     額'
9908AA                                    77
     A                                      '調整數量'
9908AA                                    89
     A                                      '調整單價'
9908AA                                   101
     A                                      '調整金額'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
     A                                      ================'
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR098T1D
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A  51        R#TXDE         8  0O     2EDTWRD('    /  /  ')
     A            R#TXNO         8   O    14
     A            R#ITEM         2  0O    26
     A            R#PDNM         3   O    32
     A            R#UPRC         5  3O    38EDTCDE(K)
     A            R#QTY          7  0O    47EDTCDE(K)
9908AA            R#TAM1        11  3O    60EDTCDE(K)
9908AA*           R#SQTY         7  0O    75EDTCDE(K)
9908AA            R#SQTY         7  0O    77EDTCDE(K)
9908AA*           R#SPRC         5  3O    86EDTCDE(K)
9908AA            R#SPRC         5  3O    89EDTCDE(K)
9908AA*           R#AMT          7  0O    99EDTCDE(K)
9908AA            R#AMT          7  0O   101EDTCDE(K)
     A**************************************************************************
     A          R AR098T1L
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                     1
     A                                      ' '
     A**************************************************************************
     A          R AR098T1M
     A**************************************************************************
     A N55                                 1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
     A                                      ================'
     A*                                     SPACEB(001)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A N55                                14 '品名小計:'
     A            R#SPDM         3   O    32
     A            R#TQTY         8  0     46EDTCDE(K)
9908AA            R#TAM2        12  3     59EDTCDE(K)
9908AA*           R#TQY2         8  0     59EDTCDE(K)
9908AA            R#TQY2         8  0     76EDTCDE(K)
9908AA*           R#TAMT        10  0     82EDTCDE(K)
9908AA            R#TAMT        10  0     98EDTCDE(K)
     A
     A**************************************************************************
     A          R AR098T1T
     A**************************************************************************
     A                                      SKIPB(057)
     A                                    14
     A                                      '備 註：_______________________+
     A                                      ___________________________________+
     A                                      __________________________'
     A**************************************************************************
     A          R AR098T2T
     A**************************************************************************
     A                                      SKIPB(063)
     A                                     2
     A                                      '副總:______________'
     A                                    +5'副理:______________'
     A                                    +5'複核:______________'
     A                                    +5'製表人:_____________'
     A**************************************************************************
     A          R AR098T1X
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     1
     A                                      '===============================-
     A                                      =============+
     A                                      ==接　下　頁==============-
     A                                      =============+
     A                                      ========================'
