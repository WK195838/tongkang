     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE037R
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     新版營業繳款輸入程式
     A*            5.DATE-WRITTEN  87/05/20
     A*            6.DATE-UPDATED  88/12/03
     A*                           101/09/19  2012AR682  S00WCJ (0109A)
     A*                           104/08/04  S00WCJ (0408A)
     A*                            增加710509其他費用
     A*                           110/02/25  2021AR00007 S00WCJ (1002A)
     A*                            發票沖銷項次99改為999
     A*
     A***************************************************************
     A*  畫面一:新增、修改、刪除、查詢、沖銷、複製、還原複製畫面
     A*
     A*  *IN33 : ON 表示為查詢狀態
     A***************************************************************
     A          R AR037F1                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT1         1A  B  9  4VALUES(' ' '2' '4' '5' '6' '8' '9')
     A  31                                  DSPATR(RI)
     A            S#RVDT         8Y 0O    +2EDTWRD('    /  /  ')
     A            S#RVNO         6A  O    +2
     A            S#CUNO         6A  O    +2
     A            S#CUNM        10O  O    +1
     A            S#XAMT        11Y 0O    +2EDTCDE(3)
     A            S#NAMT        11Y 0O    +2EDTCDE(3)
     A            S#FL01         1A  O    +3
     A            S#FL03         1A  O    +1
     A            S#FL02         1A  O    +1
     A***************************************************************
     A          R AR037F1C                  SFLCTL(AR037F1)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF06(06)
     A                                      CF12(12)
     A                                      CF17(17)
     A                                      CF18(18)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A            S#SFN1         4S 0H
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE037S-1'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 28'★新版營業繳款輸入程式★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  4  2'選項:'
     A                                  4 11'2:修改'
     A                                  4 21'4:刪除'
     A                                  4 31'5:查詢'
     A                                  4 42'6:沖銷'
     A                                  4 53'8:確認'
     A                                  4 64'9:確認還原'
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6  2'選項'
     A                                  6  9'日期'
     A                                  6 19'編號'
     A                                  6 30'客　　戶'
     A                                  6 48'沖銷合計'
     A                                  6 61'繳款合計'
     A                                  6 72'確印過'
     A                                  7  2'-----------------------------------
     A                                      ------------+---+---+----+---+---+--
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#SFK1         8Y 0B  8  9EDTCDE(4)
     A  30                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A***************************************************************
     A          R AR037F1M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 21  2'-----------------------------------
     A                                      ------------+---+---+----+---+---+--
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22  4'F03:結束'
     A                                 22 18'F12:回前'
     A                                 22 32'F17:首筆'
     A                                 22 46'F18:末筆'
     A                                 22 60'F05:重新顯示'
     A                                 23  4'F06:新增'
     A                                 23 18'PGUP:上頁'
     A                                 23 32'PGDN:下頁'
     A                                 23 46'ENTER:執行'
     A            S#MSG1        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面二:輸入繳款明細畫面
     A***************************************************************
     A          R AR037F2                   SFL
     A  70                                  SFLNXTCHG
     A            S#SFI2         2Y 0O 12  2
     A            S#ACTP         1A  B 12  7VALUES(' ' 'C' 'D')
     A  33                                  DSPATR(PR)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A            S#USTP         2A  B 12 10DUP(35)
     A  33                                  DSPATR(PR)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A            S#NTTP         1A  B 12 14DUP(36)
     A  33                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
0109AA            S#ACNO         6A  B 12 18
     A  33                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A            S#PBID        14O  B 12 26DUP(37)
     A  33                                  DSPATR(PR)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A            S#PANO        10A  B 12 41DUP(38)
     A  33                                  DSPATR(PR)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A            S#PLAC         1A  B 12 52VALUES(' ' 'D' 'E')
     A                                      DUP(39)
     A  33                                  DSPATR(PR)
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A            S#RLNO        10E  B 12 55
     A  33                                  DSPATR(PR)
     A  50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A            S#RAMT        11Y 0B 12 66EDTWRD('  ,   ,   ,   ')
     A  33                                  DSPATR(PR)
     A  49                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A            S#NTNO        20A  B 13 18
     A  33                                  DSPATR(PR)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
9105 A*           S#OLRV         6A  B 13 31
 .   A* 33                                  DSPATR(PR)
 .   A* 65                                  DSPATR(PC)
 .   A* 65                                  DSPATR(RI)
     A            S#SANO        10A  B 13 41
     A  33                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A            S#DUDT         8Y 0B 13 55EDTWRD('    /  /  ')
     A  33                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A            S#RESV        10O  B 13 68
     A  33                                  DSPATR(PR)
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(RI)
     A            S#SBID        10O  H
     A***************************************************************
     A          R AR037F2C                  SFLCTL(AR037F2)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A                                      CF01(01)
     A                                      CF03(03)
     A N33                                  CF10(10)
     A                                      CF12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE037S-2'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 28'★營業繳款明細輸入作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A            S#SF2T         6O  O  3  2DSPATR(RI)
     A*----------------------------------------------------------------
     A                                  4  8'繳款編號:'
     A            S#RVNO         6A  O  4 20
     A                                  4 31'繳款部門:'
     A            S#DPNO         4A  B  4 43
     A  33                                  DSPATR(PR)
     A  34                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                  4 52'收款業務員:'
     A            S#RVID         1A  B  4 66
     A  33                                  DSPATR(PR)
     A  54                                  DSPATR(PC RI)
     A                                  5  8'客戶編號:'
     A            S#CUNO         6A  B  5 20
     A  33                                  DSPATR(PR)
     A  55                                  DSPATR(PC RI)
     A                                  5 27'客戶名稱:'
     A            S#CUNM        10O  O  5 39
     A                                  5 54'繳款日期:'
     A            S#RVDT         8Y 0B  5 66EDTWRD('    /  /  ')
     A  33                                  DSPATR(PR)
     A  56                                  DSPATR(PC RI)
     A N33                              6  3'借貸方清成空白，該項明細視同刪除'
     A                                      DSPATR(RI)
     A                                  6 50'繳款金額合計:'
     A            S#NAMT        11Y 0O  6 66EDTCDE(1)
     A                                      DSPATR(HI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  1'項　借　款　票'
     A                                  8 51'付'
     A                                  9  1'　　貸　項　款　會計科目'
     A                                  9 28'付款銀行　　付款帳戶'
     A                                  9 51'款　相關號碼　　金　　　額'
     A                                 10  1'次　方　別　別　票據號碼'
     A*                                10 28'原繳款單號　存入帳號'
     A                                 10 28'　　　　　　存入帳號'
     A                                 10 51'地　到期日期  對　象　別'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR037F2M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  4'F01:說明'
     A                                 23 19'F03:結束'
     A                                 23 34'F12:回前'
     A  33                             23 49'ENTER:執行'
     A N33                             23 49'ENTER:檢核'
     A N33                             23 66'F10:存檔'
     A            S#MSG2        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A* 畫面二：輸入資料說明視窗
     A***************************************************************
     A          R AR037F2W
     A                                      WINDOW(7 14 15 62)
     A                                      TEXT('說明視窗')
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A                                  2  3'款項別'
     A                                      DSPATR(HI)
     A                                  2 18'票款別'
     A                                      DSPATR(HI)
     A                                  4  1'A1:一般貨款'
     A                                  4 16'A:支票'
     A                                  5  1'A2:預收貨款'
     A                                  5 16'B:背書支票'
     A                                  6  1'A3:延票利息'
     A                                  6 16'C:本票'
     A                                  7  1'A4:換票繳入'
     A                                  7 16'D:背書本票'
     A                                  8  1'A5:暫收款項'
     A                                  8 16'E:銀行本支票'
     A                                  9  1'Z5:其他'
     A                                  9 16'F:匯票'
     A                                 10 16'G:匯款'
     A                                 11 16'H:現金'
     A                                 12 16'I:信用狀'
     A                                 13 16'J:銀行存款'
     A                                  2 40'常用會計科目'
     A                                      DSPATR(HI)
     A                                  4 32'1111庫存現金'
     A                                  4 48'1112銀行存款'
     A                                  5 48'1131應收票據'
     A                                  8 32'2125應付費用'
     A                                  8 48'2159暫收其他'
     A                                 10 48'    　　　　'
     A                                  5 32'1114外幣存款'
     A                                  6 32'1134應收帳款'
     A                                  9 32'8149其他收益'
     A                                  9 48'8246奇零尾數'
     A                                  6 48'1137進項稅額'
     A                                 11 32'7111（推）運　費'
     A                                 12 32'7112（推）檢驗費'
     A                                 13 32'7114（推）交際費'
     A                                 14 32'7142（推）郵電費'
0109AA***************************************************************
0109AA* 畫面二：輸入資料說明視窗
0109AA***************************************************************
0109AA          R AR037F3W
0408AA                                      WINDOW(6 14 16 62)
0109AA                                      TEXT('說明視窗')
0109AA                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
0109AA                                       (*CHAR '        '))
0109AA                                      OVERLAY
0109AA                                      CF03(03)
0109AA                                      CF12(12)
0109AA                                  2  3'款項別'
0109AA                                      DSPATR(HI)
0109AA                                  2 17'票款別'
0109AA                                      DSPATR(HI)
0109AA                                  4  1'A1:一般貨款'
0109AA                                  4 15'A:支票'
0109AA                                  5  1'A2:預收貨款'
0109AA                                  5 15'B:背書支票'
0109AA                                  6  1'A3:延票利息'
0109AA                                  6 15'C:本票'
0109AA                                  7  1'A4:換票繳入'
0109AA                                  7 15'D:背書本票'
0109AA                                  8  1'A5:暫收款項'
0109AA                                  8 15'E:銀行本支票'
0109AA                                  9  1'Z5:其他'
0109AA                                  9 15'F:匯票'
0109AA                                 10 15'G:匯款'
0109AA                                 11 15'H:現金'
0109AA                                 12 15'I:信用狀'
0109AA                                 13 15'J:銀行存款'
0109AA                                  2 40'常用會計科目'
0109AA                                      DSPATR(HI)
0109AA                                  4 30'110101庫存現金'
0109AA                                  4 47'110102銀行存款'
0109AA                                  5 47'111001應收票據'
0109AA                                  8 30'210902應付費用'
0109AA                                  8 47'211301暫收其他'
0109AA                                 10 48'    　　　　'
0109AA                                  5 30'110104外幣存款'
0109AA                                  6 30'111101應收帳款'
0109AA                                  9 30'811210其他收益'
0109AA                                  9 47'821204奇零尾數'
0109AA                                  6 47'111407進項稅額'
0109AA                                 11 30'710101（推）運　費'
0109AA                                 12 30'710102（推）檢驗費'
0109AA                                 13 30'710104（推）交際費'
0109AA                                 14 30'710402（推）郵電費'
0408AA                                 15 30'710509（推）其他費用'
     A***************************************************************
     A*  畫面三:發票沖銷畫面
     A***************************************************************
     A          R AR037F3                   SFL
     A  70                                  SFLNXTCHG
1002AA            S#SFI3         3Y 0O 11  2
     A            S#INNO        10A  B    +2
     A  33                                  DSPATR(PR)
     A  57                                  DSPATR(PC RI)
     A            S#INXA        11Y 0B    +1EDTCDE(4)
     A  33                                  DSPATR(PR)
     A  58                                  DSPATR(PC RI)
     A            S#INOR         6A  O    +2
     A            S#INAM        11Y 0O    +1EDTCDE(4)
     A            S#INBA        11Y 0O    +1EDTCDE(4)
     A            S#INAP         6A  O    +2
     A            S#INRA        11Y 0O    +1EDTCDE(4)
     A***************************************************************
     A          R AR037F3C                  SFLCTL(AR037F3)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A N33                                  CF06(06)
     A N33                                  CF08(08)
     A N33                                  CF09(09)
     A N33                                  CF10(10)
     A                                      CF12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN3)
     A            S#NBR3         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN3         5S 0H
     A            S#SFN3         4S 0H
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE037S-3'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 26'★營業繳款發票沖銷繳款作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A            S#SF3T         6O  O  3  2DSPATR(RI)
     A*----------------------------------------------------------------
     A                                  4  8'繳款編號:'
     A            S#RVNO         6A  O  4 20
     A                                  4 31'繳款部門:'
     A            S#DPNO         4A  O  4 43
     A                                  4 52'收款業務員:'
     A            S#RVID         1A  O  4 66
     A                                  5  8'客戶編號:'
     A            S#CUNO         6A  O  5 20
     A                                  5 27'客戶名稱:'
     A            S#CUNM        10O  O  5 39
     A                                  5 54'繳款日期:'
     A            S#RVDT         8Y 0O  5 66EDTWRD('    /  /  ')
     A                                  6 50'繳款金額合計:'
     A            S#NAMT        11Y 0O  6 66EDTCDE(1)
     A                                      DSPATR(HI)
     A                                  6  8'沖銷金額合計:'
     A            S#XAMT        11Y 0O  6 24EDTCDE(1)
     A                                      DSPATR(HI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A N33                              8  3'清除發票號碼該筆明細視同刪除'
     A                                      DSPATR(RI)
     A                                  9  7'發票號碼'
     A                                  9 20'沖銷金額'
     A                                  9 31'訂單'
     A                                  9 39'發票金額'
     A                                  9 51'發票餘額　請款單'
     A                                  9 71'未過已沖'
     A                                 10  2'------------------+---+---+--------
     A                                      ---+---+---+---+---+---+------------
     A                                      +---+---+'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR037F3M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 21  2'------------------+---+---+--------
     A                                      ---+---+---+---+---+---+------------
     A                                      +---+---+'
     A                                      DSPATR(HI)
     A                                 22  4'F03:結束'
     A                                 22 23'F12:回前'
     A  33                             22 48'ENTER:執行'
     A N33                             22 48'ENTER:檢核'
     A N33                             23  4'F06:重設'
     A N33                             23 23'F08:依請款單載入'
     A N33                             23 48'F09:依訂單載入'
     A N33                             23 70'F10:存檔'
     A            S#MSG3        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面四:依請款單載入發票
     A***************************************************************
     A          R AR037F4                   SFL
     A  70                                  SFLNXTCHG
1002AA            S#SFI4         3Y 0O 10  2
     A            S#4NNO        10A  O    +2
     A            S#4NXA        11Y 0O    +1EDTCDE(3)
     A            S#4NOR         6A  O    +2
     A            S#4NAM        11Y 0O    +1EDTCDE(3)
     A            S#4NBA        11Y 0O    +1EDTCDE(3)
     A            S#4NAP         6A  O    +2
     A            S#4NRA        11Y 0O    +1EDTCDE(3)
     A***************************************************************
     A          R AR037F4C                  SFLCTL(AR037F4)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10(10)
     A                                      CF12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN4)
     A            S#NBR4         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN4         5S 0H
     A            S#SFN4         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE037S-4'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 26'★營業繳款依請款單載入發票★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A            S#CUNO         6A  O  4  2
     A            S#CUNM        10O  O    +1
     A                                  4 30'輸入請款單號:'
     A            S#SFK4         6A  B    +1DSPATR(PC)
     A                                  5  7'請款單發票總額:'
     A            S#SAM4        11Y 0O  5 25EDTCDE(1)
     A                                      DSPATR(HI)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  3'待沖銷金額為零者存檔時將忽略'
     A                                      DSPATR(RI)
     A                                  8  7'發票號碼'
     A                                  8 20'待沖銷額'
     A                                  8 31'訂單'
     A                                  8 39'發票金額'
     A                                  8 51'發票餘額　請款單'
     A                                  8 71'未過已沖'
     A                                  9  2'------------------+---+---+--------
     A                                      ---+---+---+------------------------
     A                                      +---+---+'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR037F4M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 20  2'------------------+---+------------
     A                                      ---+---+---+------------------------
     A                                      +---+---+'
     A                                      DSPATR(HI)
     A                                 21  7'待沖銷金額合計:'
     A            S#XAM4        11Y 0O    +1EDTCDE(1)
     A                                      DSPATR(HI)
     A*
     A                                 23  4'F03:結束'
     A                                 23 20'F12:回前'
     A                                 23 36'ENTER:檢核'
     A                                 23 54'F10:存檔'
     A            S#MSG4        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面五:依訂單載入發票
     A***************************************************************
     A          R AR037F5                   SFL
     A  70                                  SFLNXTCHG
1002AA            S#SFI5         3Y 0O 10  2
     A            S#5NNO        10A  O    +2
     A            S#5NXA        11Y 0O    +1EDTCDE(3)
     A            S#5NOR         6A  O    +2
     A            S#5NAM        11Y 0O    +1EDTCDE(3)
     A            S#5NBA        11Y 0O    +1EDTCDE(3)
     A            S#5NAP         6A  O    +2
     A            S#5NRA        11Y 0O    +1EDTCDE(3)
     A***************************************************************
     A          R AR037F5C                  SFLCTL(AR037F5)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10(10)
     A                                      CF12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN5)
     A            S#NBR5         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN5         5S 0H
     A            S#SFN5         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE037S-5'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 27'★營業繳款依訂單載入發票★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A            S#CUNO         6A  O  4  2
     A            S#CUNM        10O  O    +1
     A                                  4 30'輸入訂單編號:'
     A            S#SFK5         6A  B    +1DSPATR(PC)
     A                                  5  7'訂單發票總額:'
     A            S#SAM5        11Y 0O  5 25EDTCDE(1)
     A                                      DSPATR(HI)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  3'待沖銷金額為零者存檔時將忽略'
     A                                      DSPATR(RI)
     A                                  8  7'發票號碼'
     A                                  8 20'待沖銷額'
     A                                  8 31'訂單'
     A                                  8 39'發票金額'
     A                                  8 51'發票餘額　請款單'
     A                                  8 71'未過已沖'
     A                                  9  2'------------------+---+---+--------
     A                                      ---+---+---+------------------------
     A                                      +---+---+'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR037F5M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 20  2'------------------+---+------------
     A                                      ---+---+---+------------------------
     A                                      +---+---+'
     A                                      DSPATR(HI)
     A                                 21  7'待沖銷金額合計:'
     A            S#XAM5        11Y 0O    +1EDTCDE(1)
     A                                      DSPATR(HI)
     A*
     A                                 23  4'F03:結束'
     A                                 23 20'F12:回前'
     A                                 23 36'ENTER:檢核'
     A                                 23 54'F10:存檔'
     A            S#MSG5        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A* 畫面六：信用狀挑選視窗
     A***************************************************************
     A          R AR037F6                   SFL
     A            S#LLNO        20A  O  7  3
     A            S#BANK        14O  O    +2
     A            S#ENDT         8Y 0O    +2EDTWRD('    /  /  ')
     A***************************************************************
     A          R AR037F6C                  SFLCTL(AR037F6)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0010)
     A                                      WINDOW(3 24 19 52)
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN6)
     A            S#NBR6         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN6         5S 0H
     A            S#SFN6         4S 0H
     A*----------------------------------------------------------------
     A                                  1  6'★挑選信用狀★'
     A                                      DSPATR(RI)
     A                                    +6'ARE037S-6'
     A                                      DSPATR(HI)
     A                                  3  2'移動游標至該筆後，按輸入鍵即可。'
     A                                      DSPATR(HI)
     A                                  5  2'信用狀編號'
     A                                  5 27'開狀銀行'
     A                                  5 43'到期日'
     A                                  6  2'-----------------------------------
     A                                      ----------------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR037F6M
     A                                      WINDOW(AR037F6C)
     A                                      OVERLAY
     A                                 17  2'-----------------------------------
     A                                      ------'
     A                                      DSPATR(HI)
     A                                 18  2'F03:結束'
     A                                    +4'F12:回前'
