     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/GENSEQ)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARE021S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'*** 發票號碼維護作業 *** '
     A            DEVNM         10A  O  2 70
     A                                  5 30'功　　能:'
     A            CODE           1A  B  5 42
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  5 44'(1:新增 2:修改'
     A                                  7 45'3:刪除 4:查詢)'
     A                                 11 30'年　　月:'
     A            YYMM           6Y 0B 11 42EDTCDE(Z)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R DETAIL
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10
     A                                      CF12
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            COMP          35A  O  1 23
     A                                  1 70'ARE021S-2'
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 28'*** 發票號碼維護作業 *** '
     A            DEVNM         10A  O  2 70
     A                                  5 33'年　　月:'
     A            YYMM           6Y 0O  5 45
     A                                  8 30'二聯式'
     A                                      DSPATR(RI)
     A                                  8 50'三聯式'
     A                                      DSPATR(RI)
     A                                 10 20'字軌:'
     A            PRE2           2A  B 10 33
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A  70
     AO 71                                  DSPATR(PR)
     A            PRE3           2A  B 10 53
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A  70
     AO 71                                  DSPATR(PR)
     A                                 13 16'起始號碼:'
     A            SNO2           8Y 0B 13 30
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A  70
     AO 71                                  DSPATR(PR)
     A            SNO3           8Y 0B 13 50
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A  70
     AO 71                                  DSPATR(PR)
     A                                 12 12'檢核起始號碼:'
     A  70
     AO 71                                  DSPATR(ND)
     A            S#CKS2         8Y 0B 12 30
     A                                      DSPATR(ND)
     A N70
     AAN71                                  DSPATR(CS)
     A  70
     AO 71                                  DSPATR(PR)
     A            S#CKS3         8Y 0B 12 50
     A                                      DSPATR(ND)
     A N70
     AAN71                                  DSPATR(CS)
     A  70
     AO 71                                  DSPATR(PR)
     A                                 16 16'截止號碼:'
     A            ENO2           8Y 0B 16 30
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A  70                                  DSPATR(PR)
     A            ENO3           8Y 0B 16 50
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A  70                                  DSPATR(PR)
     A                                 15 12'檢核截止號碼:'
     A  70                                  DSPATR(ND)
     A            S#CKE2         8Y 0B 15 30
     A                                      DSPATR(ND)
     A N70                                  DSPATR(CS)
     AO 70                                  DSPATR(PR)
     A            S#CKE3         8Y 0B 15 50
     A                                      DSPATR(ND)
     A N70                                  DSPATR(CS)
     AO 70                                  DSPATR(PR)
     A                                 18 16'目前號碼:'
     A            CNO2           8Y 0O 18 30
     A            CNO3           8Y 0O 18 50
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                 24 19'PF03:結束'
     A                                 24 32'PF10:存檔'
     A                                 24 45'PF12:回前畫面'
     A***************************************************************
