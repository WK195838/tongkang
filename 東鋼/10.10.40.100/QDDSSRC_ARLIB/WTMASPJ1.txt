     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    WTMASPJ1
     A*            2.PROGRAM-TYPE  JLF
     A*            3.AUTHOR        C. C. LIN
     A*            4.FUNCTION     成品磅單檔
     A*            5.DATE-WRITTEN  84/09/25
     A*
     A*****************************************************************
     A                                      JDFTVAL
     A                                      DYNSLT
     A          R RWTMASPJ                  JFILE(BKLIB/WTMASP BKLIB/WTSBMP)
     A          J                           JOIN(WTMASP WTSBMP)
     A                                      JFLD(W6AREA W7AREA)
     A                                      JFLD(W6ENTD W7ENTD)
     A                                      JFLD(W6WTNO W7WTNO)
     A                                      JFLD(W6SEQN W7SEQN)
     A                                      JDUPSEQ(W7SQIT)
     A*
     A            W6FLAG         1A         COLHDG('處理代號')
     A            W6WTNO         1A         COLHDG('地磅別')
     A            W6ENTD         8S 0       COLHDG('過磅日期')
     A            W6SEQN         3S 0       COLHDG('當日序號')
     A            W6AREA         1A         COLHDG('廠區別')
     A            W6VNO1         2A         COLHDG('進出別')
     A            W6VNO2         5S 0       COLHDG('磅單序號')
     A            W6GROP         1A         COLHDG('產品大類')
     A            W6TRCD         2A         COLHDG('異動代號')
     A            W6STNO         4A         COLHDG('庫別代號')
     A            W6TYPE         1A         COLHDG('計價方式')
     A            W6PRNT         1S 0       COLHDG('列印次數')
     A            W6SALE         2A         COLHDG('業務員')
9008 A            W6ORNO         6A         COLHDG('訂單編號')
     A            W6OTNO         6A         COLHDG('出單編號')
     A            W6CUNO         6A         COLHDG('客戶編號')
     A            W6CUNM        10O         COLHDG('客戶名稱')
     A            W6DEST        22O         COLHDG('交貨地點')
     A            W6CHKW        12O         COLHDG('會磅地點')
     A            W6TFBT         5A         COLHDG('交貨區間')
     A            W6TFMK         5A         COLHDG('裝運代號')
     A            W6TFCP         1A         COLHDG('貨運行')
     A            W6CRNO        10A         COLHDG('車號')
     A            W6CRNM        10O         COLHDG('車行')
     A            W6WBIN         1A         COLHDG('進廠地磅')
     A            W6DTIN         8S 0       COLHDG('進廠日期')
     A            W6TMIN         4A         COLHDG('進廠時間')
     A            W6WBOT         1A         COLHDG('出廠地磅')
     A            W6DTOT         8S 0       COLHDG('出廠日期')
     A            W6TMOT         4A         COLHDG('出廠時間')
9008 A            W6PSDT         8S 0       COLHDG('客戶磅重')
     A            W6ENTM        10A         COLHDG('輸入人員')
     A            W6DELT         1A         COLHDG('刪除碼')
     A            W6DELD         8S 0       COLHDG('刪除日期')
     A            W6DESP        70O         COLHDG('備註')
     A            W6BK12        12A
     A            W6UPDD         8S 0       COLHDG('異動日期')
     A            W6UPDT         6S 0       COLHDG('異動時間')
     A            W7FLAG         1A         COLHDG('處理代號')
     A            W7ENTD         8S 0       COLHDG('填單日期')
     A            W7AREA         1A         COLHDG('廠區別')
     A            W7VNO1         2A         COLHDG('進出別')
     A            W7VNO2         5S 0       COLHDG('磅單序號')
     A            W7SQIT         2S 0       COLHDG('序號項次')
9008 A            W7ODIT         3S 0       COLHDG('訂單項次')
9008 A            W7DVNO         9A         COLHDG('交單編號')
     A            W7NAME         5A         COLHDG('產品類別')
     A            W7STND         1A         COLHDG('規範別')
     A            W7QULT         8A         COLHDG('鋼種')
     A            W7SPE1         5A         COLHDG('Ｈ／Ｗ／Ｄ')
     A            W7SPE2         5A         COLHDG('Ｂ／Ｔ')
9406 A            W7SPE5         7A         COLHDG('長度')
     A            W7BNUM         3P 0       COLHDG('支數')
     A            W7SPLT         1A         COLHDG('分項碼')
     A            W7TWGT         6P 0       COLHDG('總重')
     A            W7BWGT         6P 0       COLHDG('空重')
     A            W7NWGT         6P 0       COLHDG('淨重')
     A            W7SPGR         4P 1       COLHDG('單位重量')
     A            W7STDW         6P 0       COLHDG('理論重')
     A            W7PSDT         8S 0       COLHDG('入帳日期')
     A            W7ENTM        10A         COLHDG('輸入人員')
     A            W7DELT         1A         COLHDG('刪除碼')
     A            W7DELD         8S 0       COLHDG('刪除日期')
     A            W7BK12        12A
     A            W7QCCD         1A         COLHDG('品質等級')
     A            W7PILW         6P 0       COLHDG('入庫實重')
9008 A            W7ORNO         6A         COLHDG('訂單編號')
     A            W7OTNO         6A         COLHDG('出單編號')
9008 A            W7OTTM         3S 0       COLHDG('出單項次')
     A            W7IPTM         2S 0       COLHDG('出貨明細項次')
     A            W7UPDD         8S 0       COLHDG('異動日期')
     A            W7UPDT         6S 0       COLHDG('異動時間')
     A            W7SEQN         3S 0       COLHDG('當日序號')
     A            W7WTNO         1A         COLHDG('磅別')
     A            W7PNUM         5P 0       COLHDG('支數')
     A          K W6AREA
     A          K W6ENTD
     A          K W6WTNO
     A          K W6SEQN
     A          S W6DELT                    COMP(NE 'D')
     A            W7DELT                    COMP(NE 'D')
