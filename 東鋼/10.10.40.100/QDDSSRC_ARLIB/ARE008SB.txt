     A*****************************************************************
     A*
     A*            *****   PROGRAM INTRODUCTION   *****
     A*
     A*            1.PROGRAM-ID    ARE008SB
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     帳款更正磅單調整副程式
     A*            5.DATE-WRITTEN  108/12/25 2019AR000079
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      INDARA
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前螢幕')
     A          R AR08F1
     A                                      ASSUME
     A                                  1  2' '
     A          R AR08F2                    SFL
     A  88                                  SFLNXTCHG
     A            S#OPT          1A  B  6  4VALUES(' ' 'Y')
     A            S#PDNM         5A  O  6  8
     A            S#QTY          7Y 0O  6 15EDTWRD(' ,   ,   -')
     A            S#PRC          7Y 5O  6 26EDTWRD('0 .     ')
     A            S#AMT          9Y 0O  6 35EDTWRD('   ,   ,   -')
     A            S#ORNO         9A  O  6 48
     A            S#PDS1         3A  O  6 58
     A            S1ORN1         9A  B  6 62
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A            S#ITEM         2S 0H
     A            S#VRTM         2A  H
     A            S#IVNO        10A  H
     A          R AR08F2CL
     A*                                     CLRL(*NO) OVERLAY FRCDTA
     A                                      OVERLAY
     A                                       WINDOW(AR08F2MG)
     A*
     A                                      SFLCTL(AR08F2)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A                                      SFLPAG(7)
     A                                      SFLSIZ(8)
     A N73                                  SFLEND(*MORE)
     A  73                                  SFLCLR
     A                                  2  2'磅單編號:'
     A            S#TXNO         8   O    +1
     A                                    +2'客戶名稱:'
     A            S#CUNM        10   O    +1
     A            S#CUNO         6A  H
     A                                  4  3'(Y)'
     A                                  5  2'對沖品名'
     A                                      DSPATR(UL)
     A                                    +1'數量  單 價 金  額 '
     A                                      DSPATR(UL)
     A                                    +1'訂單編號徑度轉訂單'
     A                                      DSPATR(UL)
     A          R AR08F2MG
     A                                      WINDOW(6 02 16 72)
     A                                      WDWBORDER((*COLOR PNK) +
     A                                      (*CHAR '+-+||+-+'))
     A                                 14  2'操作提示:' COLOR(BLU)
     A                                    +1'F3,F12=回前頁'
     A                                      COLOR(BLU)
     A            S#ERR         24     15  2
