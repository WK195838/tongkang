     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARQ014R
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     銷貨帳款應收查詢作業
     A*            5.DATE-WRITTEN  88/07/15
     A*
     A***************************************************************
     A*  畫面一:（營業單位及帳齡）
     A***************************************************************
     A          R AR014F1
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CA12(12)
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARQ014S-1'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 28'★銷貨帳款應收查詢作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  8 20'營業單位:'
     A            S#DEPT         4A  B  8 33
     A  51                                  DSPATR(PC RI)
     A                                 10 22'業務員:'
     A            S#KEY1         2A  B 10 33
     A  52                                  DSPATR(PC RI)
     A                                 10 38'（空白表示該單位之全部業務員）'
     A                                 13 20'帳齡區間:'
     A            S#KEY2         4Y 0B 13 33EDTCDE(4)
     A  53                                  DSPATR(PC RI)
     A                                 13 38'天'
     A                                 13 44'至'
     A            S#KEY3         4Y 0B 13 50EDTCDE(4)
     A                                 13 55'天'
     A*----------------------------------------------------------------
     A                                 23  3'F3:結束'
     A                                 23 18'F12:回前'
     A                                 23 34'ENTER:執行'
     A            S#MSG1        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面二:客戶訂單別查詢
     A***************************************************************
     A          R AR014F2                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT2         1A  B 11  8VALUES(' ' '1' '2' '3' '4')
     A            S#CUNO         6A  O    +2
     A            S#CUNM        10O  O    +1
     A            S#ORNO         5A  O    +2
     A            S#RVID         2A  O    +2
     A            S#RVNM        10O  O    +1
     A            S#AAMT        11Y 0O    +2EDTCDE(1)
     A***************************************************************
     A          R AR014F2C                  SFLCTL(AR014F2)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      CF10(10)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARQ014S-2'
     A                                  2  2TIME
     A                                      EDTCDE(Y)
     A                                  2 28'★銷貨帳款應收查詢作業★'
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  3  2'客戶訂單查詢'
     A                                      DSPATR(RI)
     A                                  4  4'營業單位:'
     A            S#DEPT         4A  O  4 17
     A            S#DEPN        12O  O  4 22
     A                                  5  4'帳齡區間:'
     A            S#KEY2         4Y 0O  5 17EDTCDE(4)
     A                                  5 22'天'
     A                                  5 27'至'
     A            S#KEY3         4Y 0O  5 32EDTCDE(4)
     A                                  5 37'天'
     A                                  5 49'合計金額:'
     A                                      DSPATR(HI)
     A            S#2AMT        11Y 0O  5 61EDTCDE(1)
     A                                      DSPATR(HI)
     A                                  7  3'選擇狀態: 1未開發票, 2未請款-
     A                                      , 3未繳款, 4應收票據'
     A                                      DSPATR(RI)
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9 14'客　　戶'
     A                                  9 30'訂單'
     A                                  9 39'業　　務'
     A                                  9 53'區間訂單金額'
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR014F2M                  TEXT('按鍵及錯誤訊息')
     A                                 21  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22  2'F03:結束'
     A                                    +6'F12:回前'
     A                                    +6'ENTER:執行'
     A            S#MSG2        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面三:應收帳款明細查詢
     A***************************************************************
     A          R AR014F3                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT3         1A  B 11  4
     A            S#TXNO         8A  O 11  8
     A            S#TXDT         8Y 0O 11 20EDTCDE(3)
     A            S#INNO        10A  O 11 31
     A            S#APNO         6A  O 11 44
     A            S#INAM        11Y 0O 11 53EDTCDE(1)
     A            S#INDU         4Y 0O 11 70EDTCDE(3)
     A            S#TXCD         4A  H
     A***************************************************************
     A          R AR014F3C                  SFLCTL(AR014F3)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CA12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN3)
     A            S#NBR3         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN3         5S 0H
     A            S#SFN3         4S 0H
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARQ014S-3'
     A                                  2  2TIME
     A                                      EDTCDE(Y)
     A                                  2 28'★銷貨帳款應收查詢作業★'
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  3  2'應收帳款查詢'
     A                                      DSPATR(RI)
     A                                  4  4'營業單位:'
     A            S#DEPT         4A  O  4 17
     A            S#DEPN        12O  O  4 22
     A                                  4 51'業務員:'
     A            S#RVID         2A  O  4 61
     A            S#RVNM        10O  O  4 64
     A                                  5  4'客　　戶:'
     A            S#CUNO         6A  O  5 17
     A            S#CUNM        10O  O  5 24
     A                                  5 53'訂單:'
     A            S#ORNO         5A  O  5 61
     A                                  6  4'帳齡區間:'
     A            S#KEY2         4Y 0O  6 17EDTCDE(4)
     A                                  6 22'天'
     A                                  6 27'至'
     A            S#KEY3         4Y 0O  6 32EDTCDE(4)
     A                                  6 37'天'
     A                                  6 45'區間金額合計:'
     A            S#AAMT        11Y 0O  6 61EDTCDE(1)
     A                                  7  2'選擇狀態:'
     A                                      DSPATR(RI)
     A            S#STAT        10O  O  7 14DSPATR(RI)
     A                                  7 45'狀態金額合計:'
     A                                      DSPATR(HI)
     A            S#3AMT        11Y 0O  7 61EDTCDE(1)
     A                                      DSPATR(HI)
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9 10'磅單'
     A                                  9 19'磅單日期'
     A                                  9 31'發票號碼'
     A                                  9 43'請款單'
     A                                  9 56'應收金額'
     A                                  9 69'帳齡'
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR014F3M                  TEXT('按鍵及錯誤訊息')
     A                                 21  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22  2'F03:結束'
     A                                    +6'F12:回前'
     A                                    +6'ENTER:執行'
     A            S#MSG3        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面四:應收票據明細查詢
     A***************************************************************
     A          R AR014F4                   SFL
     A  70                                  SFLNXTCHG
     A            S#TXCD         4A  H
     A            S#OPT4         1A  B 11  2
     A            S#TXNO         8A  O 11  4
     A            S#TXDT         8Y 0O 11 13EDTCDE(3)
     A            S#INNO        10A  O 11 22
     A            S#RVNO         6   O 11 33
     A            S#INAM        11Y 0O 11 40EDTCDE(3)
     A            S#INDU         4Y 0O 11 52EDTCDE(3)
     A            S#RVLD         8Y 0O 11 57EDTCDE(3)
     A            S#ORLD         8Y 0O 11 66EDTCDE(3)
     A            S#DELY         4Y 0O 11 75EDTCDE(3)
     A***************************************************************
     A          R AR014F4C                  SFLCTL(AR014F4)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      CF10(10)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN4)
     A            S#NBR4         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN4         5S 0H
     A            S#SFN4         4S 0H
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARQ014S-4'
     A                                  2  2TIME
     A                                      EDTCDE(Y)
     A                                  2 28'★銷貨帳款應收查詢作業★'
     A            S#DEVI        10A  O  2 70
     A                                  3  2'應收票據查詢'
     A                                      DSPATR(RI)
     A                                  4  4'營業單位:'
     A            S#DEPT         4A  O  4 17
     A            S#DEPN        12O  O  4 22
     A                                  4 51'業務員:'
     A            S#RVID         2A  O  4 61
     A            S#RVNM        10O  O  4 64
     A                                  5  4'客　　戶:'
     A            S#CUNO         6A  O  5 17
     A            S#CUNM        10O  O  5 24
     A                                  5 53'訂單:'
     A            S#ORNO         5A  O  5 61
     A                                  6  4'帳齡區間:'
     A            S#KEY2         4Y 0O  6 17EDTCDE(4)
     A                                  6 22'天'
     A                                  6 27'至'
     A            S#KEY3         4Y 0O  6 32EDTCDE(4)
     A                                  6 37'天'
     A                                  6 45'區間金額合計:'
     A            S#AAMT        11Y 0O  6 61EDTCDE(1)
     A                                  7  2'-----------------------------------
     A                                      --------'
     A                                      DSPATR(HI)
     A                                  7 45'狀態金額合計:'
     A                                      DSPATR(HI)
     A            S#4AMT        11Y 0O  7 61EDTCDE(1)
     A                                      DSPATR(HI)
     A                                  8 14'磅單'
     A                                  8 52'帳'
     A                                  8 58'繳款'
     A                                  8 67'合約'
     A                                  8 75'延'
     A                                  9  5'磅單'
     A                                  9 14'日期'
     A                                  9 22'發票號碼'
     A                                  9 33'繳款'
     A                                  9 43'金額'
     A                                  9 52'齡'
     A                                  9 57'兌現日'
     A                                  9 66'兌現日'
     A                                  9 75'誤'
     A                                 10  2'-----------------------------------
     A                                      -------+---+------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR014F4M                  TEXT('按鍵及錯誤訊息')
     A                                 21  2'-----------------------------------
     A                                      -------+---+------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22  2'F03:結束'
     A                                    +6'F12:回前'
     A                                    +6'ENTER:執行'
     A            S#MSG4        70A  O 24  2DSPATR(HI)
     A*
     A***************************************************************
     A*  畫面五:應收狀況明細整筆及說明輸入
     A***************************************************************
     A          R AR014F5
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      CF10(10)
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARQ014S-5'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 28'★銷貨帳款應收查詢作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A                                  3  2'明細編審查詢'
     A                                      DSPATR(RI)
     A*----------------------------------------------------------------
     A                                  4 23'營業單位:'
     A            S#DEPT         4A  O  4 35
     A            S#DEPN        12O  O  4 40
     A                                  5 25'業務員:'
     A            S#RVID         2A  O  5 35
     A            S#RVNM        10O  O  5 38
     A                                  7 27'客戶:'
     A            S#CUNO         6A  O  7 35
     A            S#CUNM        10O  O  7 42
     A                                  8 27'訂單:'
     A            S#ORNO         5A  O  8 35
     A            S#TXCD         4A  O  9 16DSPATR(RI)
     A                                 10  4'磅單編號:'
     A            S#TXNO         8A  O 10 16
     A                                 10 27'出貨日期:'
     A            S#TXDT         8Y 0O 10 39EDTWRD('    /  /  ')
     A                                 10 52'出貨金額:'
     A            S#TXAM        11Y 0O 10 64EDTCDE(1)
     A                                 12 23'發票號碼:'
     A            S#INNO        10A  O 12 35
     A                                 12 48'請款單號:'
     A            S#APNO         6A  O 12 60
     A                                 13 23'繳款單號:'
     A            S#RVNO         6A  O 13 35
     A                                 13 48'繳款日期:'
     A            S#RVDT         8Y 0O 13 60EDTWRD('    /  /  ')
     A                                 15 17'合約最後兌現日:'
     A            S#ORLD         8Y 0O 15 35EDTWRD('    /  /  ')
     A                                 16 17'繳款最後兌現日:'
     A            S#RVLD         8Y 0O 16 35EDTWRD('    /  /  ')
     A                                 17 23'延誤日數:'
     A            S#DELY         4Y 0O 17 35EDTCDE(3)
     A                                 17 48'應收帳齡:'
     A            S#INDU         4Y 0O 17 60EDTCDE(3)
     A                                 19 23'應收金額:'
     A            S#INAM        11Y 0O 19 35EDTCDE(1)
     A            S#STNM        10O  O 19 60DSPATR(RI)
     A                                 21 27'說明:'
     A            S#RMRK        40O  B 21 35
     A*----------------------------------------------------------------
     A                                 23  3'F3:結束'
     A                                 23 18'F12:回前'
     A                                 23 34'F10:存檔'
     A            S#MSG5        70A  O 24  2DSPATR(HI)
