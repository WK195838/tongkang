     H*****************************************************************
     H*
     H*            *****   PROGRAM INTRODUCTION   *****
     H*
     H*            1.PROGRAM-ID    ARDSDT
     H*            2.PROGRAM-TYPE  PF
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION     折讓折扣磅單明細分攤檔
     H*            5.DATE-WRITTEN  103/01/07 2014AR796
     H*            6.UPDATE BY     104/08/19 2015AR951  S00WCJ (0408A)
     H*                            增加鋼種、尺寸、長度欄位
     H*                            104/09/11 2015AR953  S00WCJ (0409A)
     H*                            產品代碼3碼擴5碼
     H*
     H*****************************************************************
     A*                                     UNIQUE
     A          R RARDSDT                   TEXT('折讓折扣磅單明細分攤檔')
     A            ASFLAG         1A         COLHDG('處理代碼')
     A            ASKIND         1A         COLHDG('分攤類別')
     A*                                     K=折扣；D=折讓
     A            ASCUNO         6A         COLHDG('客戶代號')
     A            ASCUNM        10O         COLHDG('客戶名稱')
     A            ASORNO         6A         COLHDG('訂單編號')
     A            ASYYMM         6S 0       COLHDG('處理年月')
     A            ASCONT         3S 0       COLHDG('處理次數')
     A            ASTYPE         1A         COLHDG('發票聯式')
     A            ASSALE         2A         COLHDG('業務員別')
     A            ASAREA         1A         COLHDG('廠區別')
     A            ASAMT4         4S 3       COLHDG('折讓單價／公斤')
     A            ASINNO        10A         COLHDG('發票號碼')
     A            ASACNT         1A         COLHDG('發票類別')
     A            ASITEM         2S 0       COLHDG('發票項次')
     A            ASINDT         8S 0       COLHDG('發票日期')
     A            ASDECD         1A         COLHDG('發票作廢碼')
     A            ASDEDT         8S 0       COLHDG('發票作廢日期')
     A            ASTXNO         8A         COLHDG('磅單編號')
     A            ASTXIT         2S 0       COLHDG('磅單項次')
     A            ASDATE         8S 0       COLHDG('出貨日期')
0409AA            ASPDNM         5A         COLHDG('品名')
     A            ASQTY1         7S 0       COLHDG('銷貨數量')
     A            ASPRC1         5S 3       COLHDG('銷貨單價')
     A            ASAMT1        11S 0       COLHDG('銷貨金額')
     A            ASKTY2         7S 0       COLHDG('折扣數量')
     A            ASKRC2         5S 3       COLHDG('折扣單價')
     A            ASKMT2         9S 0       COLHDG('折扣金額')
     A            ASDMT1        12S 0       COLHDG('折讓金額')
     A            ASDMT2         8S 0       COLHDG('折讓稅額')
     A            ASDXNO         8A         COLHDG('折讓單號')
     A            ASUPDM        10A         COLHDG('異動人員')
     A            ASUPDD         8S 0       COLHDG('異動日期')
     A            ASUPDT         6S 0       COLHDG('異動時間')
     A            ASRESV        10A         COLHDG('保留碼')
     A*
0408AA            ASDXIT         2S 0       COLHDG('折讓單項次')
0408AA            ASMETL         8A         COLHDG('鋼種')
0408AA            ASSPE1         5A         COLHDG(' H/W/D ')
0408AA            ASSPE2         5A         COLHDG(' B/T ')
0408AA            ASSPE5         6A         COLHDG('長度M')
0408AA            ASCHYN         1A         COLHDG('分攤方式')
0408AA*
0408AA* 1-->依磅單
0408AA* 2-->依鋼種
0408AA* 3-->依鋼種+尺寸
0408AA* 4-->依鋼種+尺寸+長度
0408AA* 5-->依尺寸
0408AA* 6-->依尺寸+長度
0408AA* 7-->依長度
0408AA* 8-->依鋼種+長度
     A*
     A          K ASKIND
     A          K ASINNO
     A          K ASACNT
     A          K ASITEM
     A          K ASTXNO
     A          K ASTXIT
0408AA          K ASDXNO
