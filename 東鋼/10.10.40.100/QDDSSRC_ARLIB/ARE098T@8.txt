     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARE098T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     區間發票差異試算表
     A*           4.DATE-WRITTEN  99/05/27 2010AR421
     A*           5.WRITER        S00WCJ
     A*           6.UPDATE  DATE  99/08/02 2010AR514  S00WCJ (9907A)
     A*                           99/12/14 2010AR560  S00WCJ (9912A)
     A*                          101/11/12 2012AR669  S00WCJ (0111A)
     A*                          101/11/19 S00WCJ (0111B)
     A*                          101/11/28 S00WCJ (0111C)
     A*                          101/12/24 2012AR708  S00WCJ (0112A)
     A*                          102/07/09 2013AR754  S00WCJ (0207A)
     A*                           增加備註欄位
     A*                          104/09/22 2015AR953  S00WCJ (0409A)
     A*                           產品代碼3碼擴5碼
     A*
     A*****************************************************************
     A          R AR098T1H
     A*****************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      ' PRT-ID  : ARE098T'
     A                                    40
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   105
     A                                      '列表日期:'
     A                                    +1
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      ' USR-ID  :'
     A                                      SPACEB(001)
     A            U#USID        10        +1
     A                                    40
     A                                      '※區間發票開立試算表※  '
     A                                   105
     A                                      '列表時間:'
     A                                    +1
     A                                      TIME
     A                                   105
     A                                      '頁　　次:'
     A                                      SPACEB(001)
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '試算單號:'
     A                                      SPACEB(002)
     A            R#NO          10        +1
     A                                    +1
     A                                      '訂單編號:'
     A            R#ORNO         6        +1
     A                                    +1'客戶編號:'
     A            R#CUNO         6        +1
     A                                    +1'客戶名稱:'
     A            R#CUNM        10        +1
0112AA                                    +1'發票開立方式:'
0112AA            R#IVYN         1        +1
0112AA  45                               105'區間開立'
0112AA N45                               105'指定開立'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
0111AA                                      ===================+
     A                                      ================'
     A                                      SPACEB(001)
     A                                     1
     A                                      '日  期 '
     A                                      SPACEB(001)
     A                                    +1
     A                                      '磅單編號'
     A                                    +0
     A                                      '項次'
     A                                    +0
     A                                      '品名'
     A                                    +0
     A                                      '單價'
     A                                    42
     A                                      '數 量 '
9908AA                                    55
9908AA                                      '金     額'
9908AA                                    72
0111AA                                      '差異數量'
9908AA                                    82
0111AA                                      '差異單價'
9908AA                                    93
0111AA                                      '差異金額'
0111AA                                   103
0111AA                                      '調整後數量'
0111AA                                   118
0111AA                                      '調整後金額'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
0111AA                                      ===================+
     A                                      ================'
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR098T1D
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A  51        R#TXDE         8  0O     2EDTWRD('    /  /  ')
     A            R#TXNO         8   O    14
     A            R#ITEM         2  0O    24
0409AA            R#PDNM         5   O    29
     A            R#UPRC         5  3O    34EDTCDE(K)
     A            R#QTY          7  0O    42EDTCDE(K)
0111BA            R#TAM1        11  0O    55EDTCDE(K)
9908AA            R#SQTY         7  0O    72EDTCDE(K)
9908AA            R#SPRC         5  3O    84EDTCDE(K)
9908AA            R#AMT          7  0O    93EDTCDE(K)
0111AA            R#UQTY         7  0O   105EDTCDE(K)
0111AA            R#UAMT         7  0O   120EDTCDE(K)
     A**************************************************************************
     A          R AR098T1L
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                     1
     A                                      ' '
     A**************************************************************************
     A          R AR098T1M
     A**************************************************************************
     A N55                                 1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
0111AA                                      ===================+
     A                                      ================'
     A*                                     SPACEB(001)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A N55                                14 '品名小計:'
0409AA            R#SPDM         5   O    29
     A            R#TQTY         8  0     41EDTCDE(K)
0111BA            R#TAM2        12  0     54EDTCDE(K)
9908AA            R#TQY2         8  0     71EDTCDE(K)
9908AA            R#TAMT        10  0     89EDTCDE(K)
0111AA            R#UQY2         8  0    104EDTCDE(K)
0111AA            R#UAM2        10  0    116EDTCDE(K)
     A
9912AA**************************************************************************
9912AA          R AR098T2M
9912AA**************************************************************************
9912AA                                     1
9912AA                                      '==================================-
9912AA                                      ===================================-
9912AA                                      ==========================-
0111AA                                      ===================+
9912AA                                      ================'
9912AA                                      SPACEA(001)
9912AA*----------------------------------------------------------------
9912AA                                    14 '合  計:'
9912AA            R1TQTY         8  0     41EDTCDE(K)
0111BA            R1TAM2        12  0     54EDTCDE(K)
9912AA            R1TQY2         8  0     71EDTCDE(K)
9912AA            R1TAMT        10  0     89EDTCDE(K)
0111AA            R1UQY2         8  0    104EDTCDE(K)
0111AA            R1UAM2        10  0    116EDTCDE(K)
     A
     A**************************************************************************
     A          R AR098T1T
     A**************************************************************************
     A                                      SKIPB(059)
     A                                    14
0207AA                                      '備 註：'
0207AA            R#TEX1        72        +1UNDERLINE
0207AA                                      SPACEA(001)
0207AA            R#TEX2        72        26UNDERLINE
     A**************************************************************************
0112AA*         R AR098T2T
     A**************************************************************************
0112AA*                                     SKIPB(063)
0112AA*                                    2
0112AA*                                     '副總:______________'
0111AA*                                   +5'經理:______________'
0112AA*                                   +5'副理:______________'
0112AA*                                   +5'複核:______________'
0112AA*                                   +5'製表人:_____________'
     A**************************************************************************
     A          R AR098T1X
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     1
     A                                      '===============================-
     A                                      =======================+
     A                                      ==接　下　頁==============-
     A                                      =======================+
     A                                      ========================'
