     A*%%TS  SD  19870304  094353  S02CLJ      REL-V3R7M0  5716-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SACUST)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A*%%TS  SD  19870304  094353  S02CLJ      REL-V3R7M0  5716-PW1
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARE034S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 24'** 折讓退回維護作業（異常版） **-
     A                                      '
     A            DEVNM         10A  O  2 70
     A                                  5 30'功　　能:'
     A            CODE           1A  B  5 42
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  5 44'(1:新增 2:修改'
     A                                  6 45'3:刪除 4:查詢)'
     A                                  7 45'5:確認 6:列印)'
     A                                 11 30'折讓單號:'
     A            NO             6A  B 11 42
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A                                 15 13'說明：本作業是針對一、折讓稅額計+
     A                                      算差二元以上限制及'
     A                                 16 31'二、已確認之折讓單不得作廢之限制-
     A                                      '
     A                                 18 21'由於以上二個限制而無法作業時才使+
     A                                      用本程式'
     A                                 19 21'其他正常的使用，請利用原維護作業+
     A                                      程式處理'
     A***************************************************************
     A          R SF1                       SFL
     A*%%TS  SD  19820304  111903  JEFF        REL-V2R1M1  5738-PW1
     A  88                                  SFLNXTCHG
     A            ITEM           2Y 0O 11  4
     A            INVOX         10A  B 11  9DUP(21)
     A  70                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A            ACNT           1A  B 11 21
     A  70                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A            PDNM           3A  B 11 27
     A  70                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A            QTY            7Y 0B 11 32EDTWRD(' ,   ,   -')
     A  70                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A            UPRC           5Y 3B 11 43EDTWRD('0 .   ')
     A  70                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A            AMT            9Y 0B 11 50EDTWRD('   ,   ,   -')
     A  70                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A            TAX            9Y 0B 11 63EDTWRD('   ,   ,   -')
     A  70                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A            WHY            1A  B 11 77VALUES('1' '2' '3' '4' '5' '6' '7' +
     A                                             '8' '9' ' ')
     A  70                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A            FLAG           1A  B 11 79
     A  70                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A            ORNO           5A  H
     A            RVID           2A  H
     A            SALE           2A  H
     A            SATP           1A  H
     A            KIND           1A  H
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A*%%TS  SD  19820304  111903  JEFF        REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARE034S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 24'** 折讓退回維護作業（異常版） **'
     A            DEVNM         10A  O  2 70
     A                                  4  5'折讓單號:'
     A            NO             6A  O  4 17
     A                                  5  5'客戶編號:'
     A            CUNO           6A  B  5 17
     A  70                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A                                  5 30'客戶名稱:'
     A            CUNM      R        O  5 42REFFLD(BCCUNO)
     A                                  6  5'開立日期:'
     A            DATE           6Y 0B  6 17EDTWRD('  /  /  ')
     A  70                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A                                  6 30'確認日期:'
     A            ACDT           6Y 0B  6 42EDTWRD('  /  /  ')
     A  72                                  DSPATR(PR)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A                                  6 55'入帳年月:'
     A            ACYM           4Y 0B  6 67EDTWRD('  /  ')
     A  72                                  DSPATR(PR)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8 76'原'
     A                                  9  2'項次　發票號碼　種類'
     A                                  9 26'品名　數　量'
     A                                  9 42'單價　　折讓金額'
     A                                  9 64'營業稅額　　因'
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      -------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND01
     A                                      OVERLAY
     A                                 21  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------'
     A                                      DSPATR(HI)
     A                                 22 33'合　計:'
     A            TTLAMT         9  0O 22 50EDTWRD('   ,   ,   -')
     A            TTLTAX         9  0O 22 63EDTWRD('   ,   ,   -')
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                    +2'PF03:結束'
     A                                    +2'PF10:存檔'
     A                                    +2'PF12:回前畫面'
     A*****************************************************************
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE034R
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     折讓維護異常處理確認畫面
     A*            5.DATE-WRITTEN  86/03/20
     A*
     A***************************************************************
     A*  畫面六:確認相差二元之限制
     A***************************************************************
     A          R AR034F6
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A*--------------------------------------------------------------
     A                                  7  5'　注意：此本折讓單有發票之折讓稅+
     A                                      額與折讓金額計算出之稅額相差達２+
     A                                      元以上'
     A                                  8 11'，若確定要存檔的話，請在輸入簡+
     A                                      要的說明後，再按執行鍵以確認之。'
     A                                 12 16'異動原因:'
     A            S#CGCA        40O  I 12 28DSPATR(PC)
     A*--------------------------------------------------------------
     A                                 23 12'F3:結束程式'
     A                                 23 29'F12:取消存檔'
     A                                 23 47'ENTER:確定存檔'
     A                                      DSPATR(RI)
     A***************************************************************
     A*  畫面七:確認作廢已確認之折讓單
     A***************************************************************
     A          R AR034F7
     A*%%TS  SD  19860606  100944  S02CLJ      REL-V3R6M0  5716-PW1
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A                                  7  5'注意：你正要對一已確認之折讓單進+
     A                                      行做廢的動作，請確定此動作不會對+
     A                                      財會'
     A                                  8 11'及應收系統造成不當的影響。在確定+
     A                                      要做廢的動作後，請先輸入異動原'
     A                                  9 11'因以便記錄說明。'
     A                                 12 16'異動原因:'
     A            S#CGCA        40O  I 12 28DSPATR(PC)
     A                                 23 12'F3:結束程式'
     A                                 23 29'F12:取消存檔'
     A                                 23 47'ENTER:確定存檔'
     A                                      DSPATR(RI)
     A                                 16 11'如果該折讓單內含有它廠開立的發票+
     A                                      時，處理時間會較久，請耐心等待'
     A                                      DSPATR(HI)
