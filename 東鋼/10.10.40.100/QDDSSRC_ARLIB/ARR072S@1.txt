     A*%%TS  SD  19920624  191625  S02CSF      REL-V4R4M0  5769-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARR072S
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     客戶別各廠成品別出貨表
     A*            5.DATE-WRITTEN  87/06/29
     A*            5.DATE-MODIFY   92/06/24
     A*
     A***************************************************************
     A*  畫面一:處理日期
     A*
     A*        記錄格式 AR072F1
     A***************************************************************
     A          R AR072F1
     A*%%TS  SD  19920624  191625  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CA12(12)
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARR072S '
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 25'★客戶別各廠成品出貨明細表★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A                                  6 13'內外銷別:'
     A            S#COD1         1A  B  6 26VALUES('1' '2' '3')
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A                                  6 28'(1內銷2外銷3內外銷)'
     A                                  8 13'成品大類:'
     A            S#TYPE         1A  B  8 26VALUES('2' '3' '4' '7')
     A                                  8 28'(2鋼筋3型鋼4鋼板5槽鋼7型+
     A                                      鋼＋鋼板＋槽鋼)'
     A                                 10 13'年月區間:'
     A            S#FRYM         6Y 0B 10 26EDTCDE(4)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A                                 10 33'至'
     A            S#TOYM         6Y 0B 10 38EDTCDE(4)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A                                 10 45'(第二欄位空白只挑單月)'
     A                                 12 13'客戶代號:'
     A            S#CUN1         6A  B 12 26
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                 12 33'至'
     A            S#CUN2         6A  B 12 38
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A                                 12 46'(第二欄位空白只挑單客戶)'
     A                                 13 46'(二欄皆空白代表全部客戶)'
     A                                 23  3'F3:結束'
     A                                 23 17'F12:回前畫面'
     A                                 23 37'ENTER:執行'
     A            S#MSG1        70A  O 24  2DSPATR(HI)
