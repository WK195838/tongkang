     A*****************************************************************
     A*
     A*        *****   PROGRAM INTRODUCTION   *****
     A*
     A*        1.PROGRAM-ID    ARE107S
     A*        2.PROGRAM-TYPE  DSPF
     A*        3.AUTHOR        S00WCJ
     A*        4.FUNCTION     電子押匯確認上傳作業
     A*        5.DATE-WRITTEN  112/06/08 2023AR00031
     A*
     A*****************************************************************
     A                                      PRINT
     A                                      INDARA
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前螢幕')
     A          R HEAD
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 26'東和鋼鐵企業股份有限公司'
     A                                  1 70'ARE107S'
     A                                  2  2TIME
     A                                  2 28'電子押匯確認上傳作業'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            U#USID        10A  O  3  2
     A          R AR07F1
     A                                      SFL
     A  88                                  SFLNXTCHG
     A            S#OPT          1   B 10  3VALUES('Y' ' ')
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A            S#RVNO         6   O 10  7
     A  33                                  DSPATR(HI RI)
     A            S#CUNM        10   O    +1
     A            S#NTNO        20   O    +1
     A  33                                  DSPATR(HI RI)
     A            S#PBID        14   O    +1
     A            S#RAMT        11  0O    +1EDTCDE(J)
     A            S#ELCD         1   O    +1
     A*
     A            S#CUNO         6   H
     A            S#PSYN         1   H
     A
     A*
     A          R AR07F1CL
     A                                      SFLCTL(AR07F1)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A                                      SFLPAG(10)
     A                                      SFLSIZ(11)
     A N73                                  SFLEND(*MORE)
     A  73                                  SFLCLR
     A*                                     SFLDROP(CF04)
     A                                      SFLCSRRRN(&S#CSR)
     A                                      OVERLAY
     A            S#CSR          5  0H
     A            S#NBR          4  0H      SFLRCDNBR(CURSOR)
     A                                  5  1'繳款單到期日:'
     A            S#DUDT         8  0B    +1
     A  26                                  DSPATR(RI PC) COLOR(RED)
     A                                  6  1'電子押匯:'
     A            S#MODT         1   B    +1VALUES(' ' '1' '2')
     A                                    +1'(1=已押匯2未押匯 +
     A                                      空白表全部)'
     A                                  8  1'選擇繳款'
     A                                  8 74'電子'
     A                                  9  2'(Y)  單號客戶簡稱 '
     A                                      DSPATR(UL)
     A                                    +2'信用狀號碼'
     A                                      DSPATR(UL)
     A                                    +1' 付款銀行 ' DSPATR(UL)
     A                                    +1'   押匯金額 押匯'
     A                                      DSPATR(UL)
     A          R AR07F1MG
     A                                      OVERLAY
     A                                 21  2'------------------------+
     A                                       ------------------------+
     A                                       -----+
     A                                       ------------------------'
     A            S#ERR         70   O 22  2DSPATR(HI)
     A                                 23  2'F3=結束' COLOR(BLU)
     A                                    +1'F12=回前畫面' COLOR(BLU)
     A                                    +1'ENTER=執行' COLOR(BLU)
     A****************************************************************
     A          R AR07F3
     A                                      WINDOW(8  8 10 50)
     A                                      WDWBORDER((*COLOR PNK) -
     A                                       (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      OVERLAY
     A*                                     CF03(03 '回前畫面')
     A*                                     CF12(12 '回前畫面')
     A                                  2  3'信用狀號碼：'
     A            S1NTNO        20   O    +1
     A                                  3  3'付款銀行：'
     A            S1PBID        14   O    +1
     A                                  4  3'押匯金額：'
     A            S1RAMT        11  0O    +1EDTCDE(J)
     A                                  6  3'確認上傳押匯資料：'
     A            S1CNYN         1   B    +1VALUES('Y' 'N')
     A                                    +1'(Y/N)'
     A            S@ERR1        20      8  2DSPATR(HI)
     A                                  9  2'操作提示:' COLOR(BLU)
     A                                    +1'F3,F12=回前螢幕'
     A                                      COLOR(BLU)
