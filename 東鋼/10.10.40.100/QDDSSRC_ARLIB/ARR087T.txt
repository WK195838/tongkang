     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARR087T
     A*            2.PROGRAM-TYPE  PRTF
     A*            3.AUTHOR        S02HYS
     A*            4.FUNCTION     專案退稅明細表
     A*            5.DATE-WRITTEN  89/05/03
     A*            6.DATE-MODIFY   89/05/03
     A*
     A*****************************************************************
     A          R ARR087H
     A*****************************************************************
     A                                      SKIPB(003)
     A                                     2
     A                                      ' RPT - ID : ARR087T'
     A                                    90
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   176
     A                                      '印表日期:'
     A                                   188
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      '歸屬年月:'
     A                                      SPACEB(001)
     A            T#YYMM         6        +1
     A                                    +1
     A                                      '至'
     A            T#YYM1         6        +1
     A                                    95
     A                                      '專案退稅明細表'
     A                                   176
     A                                      '頁　　數:'
     A                                   188
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A            T#LIN1       195         2
     A                                      SPACEB(001)
     A                                    48
     A                                      '合約'
     A                                      SPACEB(001)
     A                                    58
     A                                      '發票'
     A                                    66
     A                                      '作廢'
     A                                    93
     A                                      '扣預收'
     A                                   151
     A                                      '發票'
     A                                   159
     A                                      '重開'
     A                                     2
     A                                      '統一編號'
     A                                      SPACEB(001)
     A                                    +7
     A                                      '客戶名稱'
     A                                    48
     A                                      '號碼'
     A                                    58
     A                                      '日期'
     A                                    64
     A                                      '發票號碼'
     A                                    74
     A                                      '索編'
     A                                    81
     A                                      '銷貨收入'
     A                                    94
     A                                      '貨款'
     A                                   106
     A                                      '銷售額'
     A                                   120
     A                                      '稅額'
     A                                   132
     A                                      '總計'
     A                                   138
     A                                      '單據編號'
     A                                   151
     A                                      '日期'
     A                                   157
     A                                      '發票號碼'
     A                                   169
     A                                      '發票金額'
     A                                   185
     A                                      '稅額'
     A                                   191
     A                                      '索編'
     A            T#LIN2       195         2
     A                                      SPACEB(001)
     A**************************************************************************
     A          R ARR0871
     A**************************************************************************
     A            T#MUID         8         2
     A                                      SPACEB(001)
     A            T#CUNM        36        11
     A            T#ORNO         6        48
     A N97        T#INDT         8  0     55
     A*                                     EDTCDE(K)
     A N97        T#INNO        10        64
     A N97                                75
     A                                      'A'
     A N97        T#NNNO         3  0     76
     A N97        T#AAMT         8  0     80
     A                                      EDTCDE(K)
     A N97        T#BAMT         8  0     92
     A                                      EDTCDE(1)
     A N97        T##AMT         8  0    103
     A                                      EDTCDE(K)
     A N97        T#ATAX         8  0    115
     A                                      EDTCDE(K)
     A N97        T#AATT         8  0    127
     A                                      EDTCDE(K)
     A            T#VNNO         8       139
     A N96        T#ENDT         8  0    148
     A*                                     EDTCDE(K)
     A            T#SNNO        10       157
     A            T#NAMT         8  0    168
     A                                      EDTCDE(K)
     A            T#NTAX         8  0    180
     A                                      EDTCDE(K)
     A                                   192
     A                                      'B'
     A            T#NNN1         3  0    193
     A**************************************************************************
     A          R ARR0872
     A**************************************************************************
     A                                    63
     A                                      '合約編號小計'
     A                                      SPACEB(001)
     A            T#AAM1         9  0     79
     A                                      EDTCDE(K)
     A            T#BAM1         9  0     91
     A                                      EDTCDE(1)
     A            T##AM1         9  0    102
     A                                      EDTCDE(K)
     A            T#ATA1         9  0    114
     A                                      EDTCDE(K)
     A            T#AAT1         9  0    126
     A                                      EDTCDE(K)
     A            T#NAM1         9  0    167
     A                                      EDTCDE(K)
     A            T#NTA1         9  0    179
     A                                      EDTCDE(K)
     A**************************************************************
     A          R ARR0873
     A**************************************************************
     A
     A                                    65
     A                                      '客戶別合計'
     A                                      SPACEB(001)
     A            T#AAM2         9  0     79
     A                                      EDTCDE(K)
     A            T#BAM2         9  0     91
     A                                      EDTCDE(1)
     A            T##AM2         9  0    102
     A                                      EDTCDE(K)
     A            T#ATA2         9  0    114
     A                                      EDTCDE(K)
     A            T#AAT2         9  0    126
     A                                      EDTCDE(K)
     A            T#NAM2         9  0    167
     A                                      EDTCDE(K)
     A            T#NTA2         9  0    179
     A                                      EDTCDE(K)
     A            T#LIN3       195         2
     A                                      SPACEB(001)
     A**************************************************************
     A          R ARR0874
     A**************************************************************
     A                                    67
     A                                      '總計'
     A                                      SPACEB(001)
     A            T#AAM3        11  0     73
     A                                      EDTCDE(K)
     A            T#BAM3        11  0     88
     A                                      EDTCDE(1)
     A            T##AM3        11  0    102
     A                                      EDTCDE(K)
     A            T#ATA3        11  0    117
     A                                      EDTCDE(K)
     A            T#AAT3        11  0    132
     A                                      EDTCDE(K)
     A            T#ACNT         3  0    153
     A                                      EDTCDE(1)
     A            T#LIN4       195         2
     A                                      SPACEB(001)
     A**************************************************************************
     A          R ARR0875
     A**************************************************************************
     A            T#CUN4         7        58
     A                                      SPACEB(001)
     A                                    66
     A                                      '月份總計'
     A            T#AAM4        11  0     76
     A                                      EDTCDE(K)
     A            T#BAM4        11  0     91
     A                                      EDTCDE(1)
     A            T##AM4        11  0    105
     A                                      EDTCDE(K)
     A            T#ATA4        11  0    120
     A                                      EDTCDE(K)
     A            T#AAT4        11  0    135
     A                                      EDTCDE(K)
     A            T#LIN5       195         2
     A                                      SPACEB(001)
     A**************************************************************************
     A          R ARR0876
     A**************************************************************************
     A*單月
     A            T#CUN5         4  0     61
     A                                      SPACEB(001)
     A                                    66
     A                                      '月份總計'
     A            T#AAM5        11  0     76
     A                                      EDTCDE(K)
     A            T#BAM5        11  0     91
     A                                      EDTCDE(1)
     A            T##AM5        11  0    105
     A                                      EDTCDE(K)
     A            T#ATA5        11  0    120
     A                                      EDTCDE(K)
     A            T#AAT5        11  0    135
     A                                      EDTCDE(K)
     A            T#LIN6       195         2
     A                                      SPACEB(001)
     A**************************************************************************
     A          R ARR0877
     A**************************************************************************
     A*訊息
     A                                    67
     A                                      '輸入區段'
     A                                      SPACEB(001)
     A            T#YYM1         6        78
     A                                    85
     A                                      '至'
     A            T#YYM2         6        90
     A                                    97
     A                                      '抱歉該區段無資料! '
     A**************************************************************************
     A          R ARR087A
     A**************************************************************************
0519 A*發票年月
     A N94                                76
     A                                      '銷貨收入'
     A                                      SPACEB(001)
     A N94                                91
     A                                      '扣預收貨款'
     A N94                               107
     A                                      '銷售額'
     A N94                               125
     A                                      '稅額'
     A N94                               139
     A                                      '總計'
     A N94                               152
     A                                      '張數'
     A            T#CUNC         2  0     50
     A                                      SPACEB(001)
     A            T#CUND         2  0     52
     A                                    54
     A                                      '-'
     A            T#CUNA         2  0     55
     A            T#CUNB         2  0     57
     A                                    59
     A                                      '作廢發票合計'
     A            T#AAMA        11  0     73
     A                                      EDTCDE(K)
     A            T#BAMA        11  0     88
     A                                      EDTCDE(1)
     A            T##AMA        11  0    102
     A                                      EDTCDE(K)
     A            T#ATAA        11  0    117
     A                                      EDTCDE(K)
     A            T#AATA        11  0    132
     A                                      EDTCDE(K)
     A            T#CUNT         3  0    153
     A                                      EDTCDE(1)
     A            T#LIN7       195         2
     A                                      SPACEB(001)
     A**************************************************************************
