     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR130T2
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.WRITER        S02LBU
     A*           4.FUNCTION     已開發票押匯狀況彙總表
     A*           5.DATE-WRITTEN  96/11/14
     A*           6.DATE-MODIFY
     A*
     A*
     A**************************************************************************
     A          R AR130T1H
     A**************************************************************************
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報　　表:ARR130T2'
     A                                    40
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    73
     A                                      '列表日期:'
     A                                    84
     A                                      DATE
     A                                      EDTCDE(Y)
     A*
     A                                     2
     A                                      '截止日期:'
     A                                      SPACEB(001)
     A            T#DATE         8  0O    13
     A                                      EDTWRD('    /  /  ')
     A                                    41
     A                                      '已開發票押匯狀況彙總表'
     A                                    73
     A                                      '列表時間:'
     A                                    84
     A                                      TIME
     A*
     A                                     2
     A                                      '部　　門:'
     A                                      SPACEB(001)
     A            T#DPNM        10   O    13
     A                                    41
     A                                      '    (已請款未繳款)    '
     A                                    73
     A                                      '頁　　次:'
     A                                    88
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*----------
     A                                     2
     A                                      '客戶代號'
     A                                      SPACEB(002)
     A                                    15
     A                                      '客戶簡稱'
     A                                    28
     A                                      '訂單編號'
     A                                    41
     A                                      '請款單號'
     A                                    54
     A                                      '請款日期'
     A                                    67
     A                                      '數　　　　量'
     A                                    84
     A                                      '金　　　　額'
     A*----------
     A                                     2
     A                                      '----------'
     A                                      SPACEB(001)
     A                                    15
     A                                      '----------'
     A                                    28
     A                                      '----------'
     A                                    41
     A                                      '----------'
     A                                    54
     A                                      '----------'
     A                                    67
     A                                      '--------------'
     A                                    84
     A                                      '--------------'
     A*****************************************************************
     A          R AR130T1D
     A*****************************************************************
     A            T#CUNO         6A        4
     A                                      SPACEB(001)
     A            T#CUNM        10O       15
     A            T#ORNO         6A       30
     A            T#APNO         7A       43
     A            T#APDT         8  0     54
     A                                      EDTWRD('    /  /  ')
     A            T#QTY         11  0     67
     A                                      EDTCDE(1)
     A            T#AMT         11  0     84
     A                                      EDTCDE(1)
     A*****************************************************************
     A          R AR130T1T
     A*****************************************************************
     A            T#RVID         2        10
     A                                      SPACEB(001)
     A                                    12
     A                                      '業務小計：'
     A            T#RQTY        12  0     66
     A                                      EDTCDE(1)
     A            T#RAMT        12  0     83
     A                                      EDTCDE(1)
     A*****************************************************************
     A          R AR130T2T
     A*****************************************************************
     A                                    12
     A                                      '合　　計：'
     A                                      SPACEB(002)
     A            T#TQTY        12  0     66
     A                                      EDTCDE(1)
     A            T#TAMT        12  0     83
     A                                      EDTCDE(1)
     A*****************************************************************
     A          R AR130T1M
     A*****************************************************************
     A                                    45
     A                                      '無符合之資料!!! '
     A                                      SPACEB(008)
     A*****************************************************************
