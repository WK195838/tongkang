     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR001R2
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     請款單列印（表單）
     A*           4.DATE-WRITTEN  88/10/12
     A*           5.WRITER        S02CLJ
     A*           6.UPDATE  DATE  99/04/26 2010AR464 S00WCJ (9904A)
     A*
     A*****************************************************************
     A          R AR001T1H
     A*****************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      ' PRT-ID  : ARR001T'
     A                                    37
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    79
     A                                      '列表日期:'
     A                                    90
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      ' USR-ID  :'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A  70                                38
     A                                      '　※　請　款　單　※　'
     A N70                                38
     A                                      '※應收帳款轉讓申請書※'
     A                                    79
     A                                      '列表時間:'
     A                                    90
     A                                      TIME
     A                                    79
     A                                      '頁　　次:'
     A                                      SPACEB(001)
     A                                    90
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '客戶編號:'
     A                                      SPACEB(002)
     A            R#CUNO         6A  O    14
     A                                   +12
     A                                      '訂單編號:'
     A            R#ORNO         6A  O    +1
     A                                    +8
     A                                      '請款日期:'
     A            R#APDT         8S  O    +1
     A                                      EDTWRD('    /  /  ')
     A                                     2
     A                                      '客戶名稱:'
     A                                      SPACEB(001)
     A            R#CUNM        10O  O    13
     A                                    +7
     A                                      '　收款業務:'
     A            R#RVID         2A  O    +1
     A                                   +12
     A                                      '請款單號:'
     A            R#APNO         6A  O    +1
     A                                    +6
     A                                      '工程號:'
     A            R#CTNO        10A  O    +1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==============================='
     A                                      SPACEB(001)
     A                                     1
     A                                      '發票日期'
     A                                      SPACEB(001)
     A                                    11
     A                                      '發票號碼'
     A                                    21
     A                                      '磅單號碼'
     A                                    31
     A                                      '出貨數量'
     A                                    43
     A                                      '出貨金額'
     A                                    55
     A                                      '預收貨款'
     A                                    65
     A                                      '折讓／銷退'
     A                                    79
     A                                      '銷貨稅額'
     A                                    90
     A                                      '請款金額'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==============================='
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR001T1D
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A  51        R#INDT         8S  O     2
     A*                                     EDTCDE(Y)
     A  51        R#INNO        10A  O    11
     A            R#WTNO         8A  O    22
     A            R#QTY          7S  O    31
     A                                      EDTCDE(J)
     A            R#AAMT         9S  O    42
     A                                      EDTCDE(J)
     A  51        R#CAMT         8S  O    54
     A                                      EDTCDE(J)
     A  51        R#BAMT         7S  O    67
     A                                      EDTCDE(J)
     A  51        R#DAMT         8S  O    78
     A                                      EDTCDE(J)
     A  51        R#EAMT         9S  O    90
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR001T1A
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A  61        R#PLE1        40A  O    23
     A  62        R#NO           6A  O    67
     A**************************************************************************
     A          R AR001T1L
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                     1
     A                                      ' '
     A**************************************************************************
     A          R AR001T1M
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==============================='
     A**************************************************************************
     A          R AR001T1T
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                    11
     A                                      '合　　計:'
     A            R#QTYT         7S 0O    +9
     A                                      EDTCDE(J)
     A            R#TAAM         9S 0O    +1
     A                                      EDTCDE(J)
     A            R#TCAM         8S 0O    +0
     A                                      EDTCDE(J)
     A            R#TBAM         7S 0O    +2
     A                                      EDTCDE(J)
     A            R#TDAM         8S 0O    +1
     A                                      EDTCDE(J)
     A            R#TEAM         9S 0O    +0
     A                                      EDTWRD('   ,   , $0 -')
     A*
     A                                    83
     A                                      '發票總張數:'
     A                                      SPACEB(002)
     A            R#NUMS         3S 0O    97
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR001T1X
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     1
     A                                      '==================================-
     A                                      =========接　下　頁==============-
     A                                      ==============================='
     A**************************************************************************
     A          R AR001T1Y
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                    11
     A                                      '客戶簽收:'
     A                                   +10
     A                                      '簽收日期:'
     A                                   +10
     A                                      '主管:'
     A                                   +10
     A                                      '業務員:'
     A*
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==============================='
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR001T1Z
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     3
     A                                      '一、承蒙　貴客戶惠顧，不勝感激，-
     A                                      茲檢送　貴客戶本期應付帳款明細、-
     A                                      出貨單（磅單）、及發票（如附）'
     A                                     7
     A                                      '敬請查收並惠予核查，若發現帳款內-
     A                                      容與實際不符時，請即連絡本公司營-
     A                                      業部經辦人員予以更正。'
     A                                      SPACEB(001)
     A                                     3
     A                                      '二、核查無誤後，本公司業務員將依-
     A                                      貴我雙方約定之領款日（　年　月　-
     A                                      日）前往收款。'
     A                                      SPACEB(002)
     A                                     3
     A                                      '三、本公司帳款處理均已電腦化，敬-
     A                                      請貴客戶依本帳單所列金額核實付款-
     A                                      。'
     A                                      SPACEB(002)
     A*****************************************************************
     A          R AR001T1P
     A**************************************************************************
     A                                      SPACEA(001)
9904AA*           R#PDN1         3A  O     5
9904AA*           R#NAME        16A  O     9
9904AA*           R#PDS1         3A  O    26
9904AA*           R#QTYP         7S 0O    31
     A*
9904AA            R#PDN1         3A  O     4
9904AA            R#NAME        20A  O    +0
9904AA            R#PDS1         3A  O    +0
9904AA            R#QTYP         7S 0O    +1
     A                                      EDTCDE(J)
     A            R#TAAP         8S 0O    42
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR001T1Q
     A**************************************************************************
     A                                      SPACEA(001)
     A                                     9
     A                                      '合　　計:'
     A            R#QTYQ         7S 0O    31
     A                                      EDTCDE(J)
     A            R#TAAQ         8S 0O    +1
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR001T1F
     A**************************************************************************
     A            R#OCNM         7A  O     5
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR001T1G
     A**************************************************************************
     A            R#OCNO        10A  O    13
     A                                      SPACEB(001)
     A            R#OQTY         7S 0O    31
     A                                      EDTCDE(J)
     A            R#OAMT         9S 0O    42
     A                                      EDTCDE(J)
     A            R#OCMT         8S 0O    54
     A                                      EDTCDE(J)
     A            R#OBMT         7S 0O    67
     A                                      EDTCDE(J)
     A            R#ODMT         8S 0O    77
     A                                      EDTCDE(J)
     A            R#OEMT         9S 0O    90
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR001T1W
     A**************************************************************************
     A                                    13'合計：'
     A                                      SPACEB(001)
     A            R#TQTY         7S 0O    31
     A                                      EDTCDE(J)
     A            R#TAMT         9S 0O    42
     A                                      EDTCDE(J)
     A            R#TCMT         8S 0O    54
     A                                      EDTCDE(J)
     A            R#TBMT         7S 0O    67
     A                                      EDTCDE(J)
     A            R#TDMT         8S 0O    77
     A                                      EDTCDE(J)
     A            R#TEMT         9S 0O    89
     A                                      EDTWRD('   ,   , $0 -')
     A**************************************************************************
