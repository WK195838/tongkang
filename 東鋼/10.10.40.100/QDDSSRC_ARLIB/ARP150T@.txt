     A**************************************
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARP150T
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION      型鋼折讓單資料上傳 -錯誤清單
     A*            5.DATE-WRITTEN  108/04/29 2019AR00025
     A*
     A*****************************************************************
     A          R AR50T21
     A**************************************************************************
     A                                      SKIPB(003)
     A                                    60
     A                                      '東和鋼鐵企業股份有限公司'
     A                                      SPACEA(001)
     A                                     1
     A                                      '報表編號: ARP150T'
     A                                    58
     A                                      '型鋼折讓單資料上傳－錯誤清單'
     A                                   150
     A                                      '印表日期:'
     A            W#DATE         8  0    162EDTWRD('    /  /  ')
     A                                      SPACEA(1)
     A                                     1
     A                                      '上傳者: '
     A            C#USER        10        13
     A                                   150
     A                                      '印表時間:'
     A                                   163
     A                                      TIME
     A                                      SPACEA(1)
     A                                   150'頁　　數:'
     A                                   162PAGNBR EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     2'客戶訂單'
     A                                    26'折讓折讓項'
     A                                      SPACEA(001)
     A                                     2'編號編號'
     A                                    16'發票號碼'
     A                                    26'種類單號次'
     A                                    +0'開立日期'
     A                                    +1'品名'
     A                                    +0'鋼種'
     A                                    +1' H/T/D '
     A                                    +0'  B/W  '
     A                                    +0'  T1'
     A                                    +1'長度 '
     A                                    92'數量'
     A                                   103'單價'
     A                                   115'金額 '
     A                                   127'傳輸日期'
     A                                   138'時間'
     A                                   145'傳輸人員'
     A                                    +1'傳送訊息'
     A                                      SPACEA(001)
     A                                     1
     A                                      '=================================='
     A                                    35
     A                                      '=================================='
     A                                    69
     A                                      '=================================='
     A                                   103
     A                                      '=============================='
     A                                   133
     A                                      '=============================='
     A                                   163
     A                                      '=============================='
     A                                      SPACEA(001)
     A**************************************************************************
     A          R AR50T22
     A**************************************************************************
     A                                      SPACEA(001)
     A            A6CUNO         6         2
     A            S#ORNO         6        +1
     A            A6IVNO        10        +1
     A            A6ACNT         1        +3
     A            S#NO           6        31
     A            A6ITEM         2  0     +1
     A            A6DATE         8  0     41EDTWRD('    /  /  ')
     A            A6PDNM         5        52
     A            A6METL         8        +1
     A            A6SPE1         5        +2
     A            A6SPE2         5        +2
     A            A6SPE3         3        +2
     A            A6SPE5         7        +1
     A            A6QTY          7  0     +1EDTCDE(K)
     A            A6UPRC         5  3     +1EDTCDE(K)
     A            A6AMT         11  0     +1EDTCDE(K)
     A*
     A            A6TMDT         8  0    129
     A            A6TMTM         6  0    138
     A            A6TMUS        10       145
     A            S#STCL        36       156
     A**************************************************************************
     A          R AR50T23
     A**************************************************************************
     A                                      SPACEA(001)
     A                                    59
     A                                      '---- END OF DATA ----'
     A**************************************************************************
     A          R AR50T24
     A**************************************************************************
     A                                      SPACEA(001)
     A                                    91'合計:'
     A            R#AMT1        12  0    112EDTCDE(K)
     A*
