     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A***************************************************************
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARQ013S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'***  票據兌現查詢  ***   '
     A            DEVNM         10A  O  2 70
     A                                  7 30'到期日期起:'
     A            DATE1          8Y 0B  7 44EDTWRD('    /  /  ')
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  9 30'　　　　迄:'
     A            DATE2          8Y 0B  9 44EDTWRD('    /  /  ')
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            W01       R        O 10  2REFFLD(ACBNAM BANKAC)
     A            W02       R        O 10 14REFFLD(RVTANO RCVDTL)
     A            W03       R        O 11 36REFFLD(RVNTNO RCVDTL)
     A            W04       R        O 10 40REFFLD(RVDATE RCVDTL)
     A                                      EDTWRD('    /  /  ')
     A            W05       R        O 10 51REFFLD(RVDUDT RCVDTL)
     A                                      EDTWRD('    /  /  ')
     A            W06       R        O 10 64REFFLD(RVRAMT RCVDTL)
     A                                      EDTCDE(K)
     A            W21       R        O 11  2REFFLD(ACBNAM BANKAC)
     A            W22       R        O 11 14REFFLD(RVSANO RCVDTL)
     A            W23       R        O 11 26REFFLD(RVNO RCVDTL)
     A            W24           10O  O 10 25
     A            W25            7A  O 11 62
     A            W26            6O  O 11 72
     A            W27       R        O 10 37REFFLD(RVUSTP RCVDTL)
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARQ013S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'***  票據兌現查詢  ***   '
     A            DEVNM         10A  O  2 70
     A                                  4  4'到期日期:'
     A            DATE1          8Y 0O  4 16EDTWRD('    /  /  ')
     A                                  4 27'-'
     A            DATE2          8Y 0O  4 29EDTWRD('    /  /  ')
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6 19'入'
     A                                  6  7'存'
     A                                  6 21'入'
     A                                  7  5'（託'
     A                                  7 19'收）　客戶名稱　款項　繳款日期'
     A*                                 7 41'繳款日期'
     A                                  7 52'到期日期'
     A                                  7 69'票據金額'
     A                                  8  3'帳號名稱'
     A                                  8 15'銀行帳號　繳款編號　票據號碼'
     A*                                 8 37'票據號碼'
     A                                  8 61'傳票號碼　付款地'
     A***************************************************************
     A          R UND01
     A                                      OVERLAY
     A                                 22  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A                                      DSPATR(HI)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'PF03:結束'
     A                                    +2'PF12:回前畫面'
     A***************************************************************
