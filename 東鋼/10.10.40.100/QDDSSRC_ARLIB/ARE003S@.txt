     A*%%TS  SD  19811019  155440  VIN         REL-V2R1M1  5738-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/INVMST)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A*%%TS  SD  19810813  162452  JEFF        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A                                  1 70'ARE003S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'***  請款單維護作業  *** '
     A            DEVNM         10A  O  2 70
     A                                  5 30'功　　能:'
     A            CODE           1A  B  5 42
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  5 44'(1:新增 2:修改'
     A                                  7 45'3:刪除 4:查詢 '
     A                                  9 45'5:列印 6:傳真)'
     A                                 11 30'客戶編號:'
     A            CUNO           6A  B 11 42
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A                                 13 30'訂單編號:'
     A            ORNO           6A  B 13 42
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A                                 15 30'請款單號:'
     A            APNO           6A  B 15 42
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A                                    +4'PF12:回前'
     A***************************************************************
     A          R SF1                       SFL
     A*%%TS  SD  19810813  162452  JEFF        REL-V2R1M1  5738-PW1
     A  88                                  SFLNXTCHG
     A            OPT            1A  B 10  2
     A  70                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A            INVNO     R        O 10  4REFFLD(INNO)
     A            AAMT           8Y 0O    +1EDTWRD('  ,   ,   ')
     A            BAMT           8  0O    +1EDTCDE(J)
     A            CAMT           8  0O    +1EDTCDE(J)
9102 A            DAMT           7Y 0O    +1EDTWRD('       ')
     A            EAMT           8  0O    +1EDTCDE(J)
     A            NBAL           8Y 0O    +1EDTWRD('  ,   ,   ')
     A            OPT1           1A  H
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A*%%TS  SD  19811019  155440  VIN         REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARE003S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 28'***  請款單維護作業  *** '
     A            DEVNM         10A  O  2 70
     A                                  4  5'客戶編號:'
     A            CUNO      R        O  4 17REFFLD(INCUNO)
     A                                  4 28'客戶名稱:'
     A            CUNM      R        O  4 40REFFLD(INCUNM)
     A                                  4 52'訂單編號:'
     A            ORNO           6A  O  4 64
     A                                  5  5'請款編號:'
     A            APNO           6A  O  5 17
     A                                  5 26'收款業務員:'
     A            RVID      R        O  5 40REFFLD(INRVID)
     A                                  5 52'請款日期:'
     A            APDT           6Y 0B  5 64EDTWRD('  /  /  ')
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  1'選　　　　　'
     A                                  7 16'　　　　　折讓／'
     A                                  8  1'擇　發票號碼'
     A                                  8 16'出貨金額　退回金額'
     A                                  8 39'預收貨款　稅　額'
     A                                  8 58'已收金額　　發票餘額'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND01
     A                                      OVERLAY
     A                                 21  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A                                      DSPATR(HI)
     A                                 22  3'發票筆數:'
     A            S#INCT         4Y 0O    +1EDTCDE(3)
     A                                 22 57'合　計:'
     A  74                                  DSPATR(ND)
     A            TTLAMT         9  0O 22 68EDTCDE(J)
     A  74                                  DSPATR(ND)
     A                                 24  5'ENTER:檢核'
     A                                    +2'PF03:結束'
     A                                    +2'PF10:存檔'
     A                                    +2'PF12:回前畫面'
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A***************************************************************
