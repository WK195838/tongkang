     A*%%TS  SD  19920321  171235  S02CSF      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARI050S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02CSF
     A*            4.FUNCTION     應收帳款承購狀況查詢
     A*            5.DATE-WRITTEN  92/03/06
     A*            6.DATE-MODIFY
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束作業')
     A                                      CA12(12 '回前畫面')
     A*================================================================
     A          R AR050T1H
     A*%%TS  SD  19920306  141706  S02CSF      REL-V4R4M0  5769-PW1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 26'も東和鋼鐵企業股份有限公司も'
     A            S#SRID        10A  O  1 70
     A                                  2  2TIME
     A                                  2 28'★應收帳款承購狀況查詢★'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3  2
     A*================================================================
     A          R AR050F1                   SFL
     A*%%TS  SD  19920321  160746  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT          1Y 0B 10  5EDTCDE(Z)
     A                                      VALUES(0 5)
     A            S#CUNO         6A  O 10  9
     A            S#CUNM        10A  O 10 17
     A            S#BKID         3A  O 10 32
     A            S#BKNM        10A  O 10 39
     A            S#FAMT        10Y 0O 10 51EDTCDE(J)
     A            S#GAMT        10Y 0O 10 66EDTCDE(J)
     A*================================================================
     A          R AR050F1C                  SFLCTL(AR050F1)
     A*%%TS  SD  19920306  163424  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  74                                  SFLCLR
     A N73                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A                                      CF05(05 '列印')
     A                                  5  4'OP:5=查詢'
     A            S#LIN1        78A  O  6  2
     A                                  7  4'OP'
     A                                  7  7'客戶編號'
     A                                  7 18'客戶簡稱'
     A                                  7 29'銀行代號'
     A                                  7 40'銀行簡稱'
     A                                  7 51'本日承購金額'
     A                                  7 67'可動撥餘額'
     A            S#LIN2        78A  O  8  2
     A            S#CRN1         5S 0H
     A            S#NBR          4S 0H      SFLRCDNBR(CURSOR)
     A            S#KEY1         6A  B  9  9
     A*================================================================
     A          R AR050F1M
     A*%%TS  SD  19920321  135830  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A            S#LIN3        78A  O 21  2
     A                                 23  2'F3=結束作業'
     A                                 23 16'F12=回前畫面'
     A                                 23 31'F5=列印'
     A                                 23 41'PA1=下頁'
     A                                 23 52'PA2=上頁'
     A            S#ERR1        70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR050F2
     A*%%TS  SD  19920321  150623  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      CF05(05 '列印')
     A                                  6  4'客戶代號：'
     A            S#CUNO         6A  O  6 18
     A                                  6 26'|'
     A                                  6 28'客戶名稱：'
     A            S#CUNM        10A  O  6 41
     A                                  6 53'|'
     A                                  6 56'預支比例：'
     A            S#BRAT         3Y 2O  6 69EDTCDE(3)
     A                                  6 74'%'
     A            S#LIN4        78A  O  7  2
     A                                  9  4'承購額度：'
     A            S#AAMT        10Y 0O  9 23EDTCDE(J)
     A                                      DSPATR(UL)
     A                                  9 40'融資額度：'
     A            S#BAMT        10Y 0O  9 59EDTCDE(J)
     A                                      DSPATR(UL)
     A                                 11  4'前日承購餘額：'
     A            S#EAMT        10Y 0O 11 23EDTCDE(J)
     A                                      DSPATR(UL)
     A                                 11 40'已融資金額：'
     A            S#CAMT        10Y 0O 11 59EDTCDE(J)
     A                                      DSPATR(UL)
     A                                 13  4'本日承購金額：'
     A            S#FAMT        10Y 0O 13 23EDTCDE(J)
     A                                      DSPATR(UL)
     A                                 13 40'本日可融資金額：'
     A            S#JAMT        10Y 0O 13 59EDTCDE(J)
     A                                      DSPATR(UL)
     A                                 15  4'本日承購餘額：'
     A            S#AAM1        10Y 0O 15 23EDTCDE(J)
     A                                      DSPATR(UL)
     A                                 15 40'可動撥餘額：'
     A            S#GAMT        10Y 0O 15 59EDTCDE(J)
     A                                      DSPATR(UL)
     A                                 17  4'逾期未兌現金額：'
     A            S#HAMT        10Y 0O 17 23EDTCDE(J)
     A                                      DSPATR(UL)
     A                                 17 40'帳上餘額：'
     A            S#IAMT        10Y 0O 17 59EDTCDE(J)
     A                                      DSPATR(UL)
     A                                 23  2'F3=結束作業'
     A                                 23 16'F12=回前畫面'
     A                                 23 32'F5=列印'
     A            S#ERR2        70A  O 24  2DSPATR(HI)
