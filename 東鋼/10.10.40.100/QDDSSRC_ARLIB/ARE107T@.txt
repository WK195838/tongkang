     A*****************************************************************
     A*
     A*        1.PROGRAM-ID    ARE107T
     A*        2.PROGRAM-TYPE  PRTF
     A*        3.FUNCTION     電子押匯確認上傳檢核報表
     A*        4.WRITER        S00WCJ
     A*        5.DATE-WRITTEN  112/06/08 2023AR00031
     A*        6.UPDATE  DATE
     A*
     A*****************************************************************
     A          R AR107T1H
     A*****************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      ' PRT-ID  : ARE107T'
     A                                    35
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    78
     A                                      '列表日期:'
     A                                    +1
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      ' USR-ID  :'
     A                                      SPACEB(001)
     A            U#USID        10        +1
     A                                    33
     A                                      '※電子押匯確認上傳檢核報表※'
     A                                    78
     A                                      '列表時間:'
     A                                    +1
     A                                      TIME
     A                                    78
     A                                      '頁　　次:'
     A                                      SPACEB(001)
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '繳款單到期日:'
     A                                      SPACEB(002)
     A            R#DUDT         8  0     +1EDTWRD('    /  /  ')
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
     A                                      =========='
     A                                      SPACEB(001)
     A                                     1
     A                                      '繳款單號'
     A                                      SPACEB(001)
     A                                    13
     A                                      '客戶名稱'
     A                                    29
     A                                      '信用狀號碼'
     A                                    50
     A                                      '付款銀行'
     A                                    65
     A                                      '押匯金額'
     A                                    82
     A                                      '發票號碼'
     A                                    93
     A                                      '發票日期'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
     A                                      =========='
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR107T1D
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A  51        R#RVNO         6   O     3
     A  51        R#CUNO         6   O    12
     A  51        R#CUNM        10   O    +0
     A  51        R#NTNO        20   O    +1
     A  51        R#PBID        14   O    +1
     A  51        R#RAMT        11  0O    +1EDTCDE(J)
     A            R#INNO        10   O    82
     A            R#INDT         8  0O    93EDTWRD('    /  /  ')
     A**************************************************************************
     A          R AR107T1L
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                     1
     A                                      ' '
     A**************************************************************************
     A          R AR107T1X
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                    10
     A                                      '============================-
     A                                      ==接　下　頁===========-
     A                                      ================'
     A**************************************************************************
     A          R AR107T1T
     A                                      SPACEB(002)
     A                                    45
     A                                      '**上傳資料列印完畢**    '
     A*****************************************************************
     A          R AR107T1M
     A**************************************************************************
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A                                    19
     A                                      '*****無條件符合之資料！！'
     A**************************************************************************
