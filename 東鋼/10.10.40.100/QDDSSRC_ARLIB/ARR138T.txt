     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR138T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     營業發票作廢彙總表
     A*           4.DATE-WRITTEN  102/03/01  2013AR731
     A*           5.WRITER        S00WCJ
     A*           6.UPDATE DATE
     A*
     A*
     A*****************************************************************
     A          R AR138TH
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A                                     2
     A                                      '報表代號: ARR138T'
     A                                    45
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    95
     A                                      '列表日期:'
     A                                   107
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            T#USID        10A       +1
     A                                    49
     A                                      '營業發票作廢彙總表'
     A                                    95
     A                                      '列表時間:'
     A                                   107
     A                                      TIME
     A                                     2
     A                                      ' '
     A                                      SPACEB(001)
     A                                    95
     A                                      '頁　　次:'
     A                                   107
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '填單地區:'
     A                                      SPACEB(001)
     A            T#ARE1         1        +1
     A            T#ARNM         6        +1
     A                                    +2'業務員代號:'
     A            T#SALE         1        +1
     A            T#SANM         6        +0
     A                                    +2'日期區間:'
     A            T#DAT1         8  0     +1EDTWRD('    /  /  ')
     A                                    +1'~'
     A            T#DAT2         8  0     +1EDTWRD('    /  /  ')
     A*
     A            T#LIN1       120         1
     A                                      SPACEB(001)
     A                                     2
     A                                      '客戶編號'
     A                                      SPACEB(001)
     A                                    +2
     A                                      '客戶名稱'
     A                                    +2
     A                                      '訂單號碼'
     A                                    +2
     A                                      '發票廠區'
     A                                    +2
     A                                      '發票作廢單號'
     A                                    +3
     A                                      '發票日期'
     A                                    +2
     A                                      '發票號碼'
     A                                    +2
     A                                      '發票金額'
     A            T#LIN2       120A        1
     A                                      SPACEB(001)
     A          R AR138T1
     A                                      SPACEA(001)
     A  51        T#CUNO         6A        4
     A  51        T#CUNM        10O       14
     A            T#ORNO         6A       +5
     A            T#INAR         1A       43
     A            T#DLNO         6A       55
     A N43        T#DATE         8  0     67
     A                                      EDTWRD('    /  /  ')
     A            T#INNO        10A       79
     A            T#AAMT        12S 0     91
     A                                      EDTCDE(J)
     A****************************************************************
     A          R AR138T2
     A****************************************************************
     A                                      SPACEA(001)
     A                                     4
     A                                      '小計：'
     A            T#AMT1        12S 0     91
     A                                      EDTCDE(J)
     A****************************************************************
     A          R AR138T3
     A****************************************************************
     A                                      SPACEA(001)
     A                                     4
     A                                      '總  計：'
     A            T#AMT2        13S 0     89
     A                                      EDTCDE(J)
     A****************************************************************
     A          R AR138T5
     A****************************************************************
     A                                      SKIPB(060)
     A                                     2
     A                                      '副總:'
     A                                    +1
     A                                      '            '
     A                                      UNDERLINE
     A                                    35
     A                                      '經理:'
     A                                    +1
     A                                      '            '
     A                                      UNDERLINE
     A                                    65
     A                                      '副理:'
     A                                    +1
     A                                      '            '
     A                                      UNDERLINE
     A                                    95
     A                                      '經辦:'
     A                                    +1
     A                                      '            '
     A                                      UNDERLINE
     A          R AR138T4
     A                                      SPACEB(0002)
     A                                    59'無相關資料!!!'
     A***************************************************************
     A          R AR138T6
     A***************************************************************
     A                                      SPACEA(001)
     A            T#LIN3       120A        1
