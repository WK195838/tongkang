     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE102S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     獎勵金單價設定維護作業
     A*            5.WRITER-DATE   101/02/15 2012AR641 S00WCJ
     A*            5.DATE-UPDATE
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束作業')
     A                                      CA07(07 '結束作業')
     A                                      CA12(12 '回前畫面')
     A*================================================================
     A          R AR102H
     A                                  1  2DATE
     A                                      EDTWRD('01  /  /  ')
     A                                  1 24'も　東和鋼鐵企業股份有限公司　も'
     A            S#SRID        10A  O  1 70
     A                                  2  2TIME
     A                                  2 29'★獎勵金單價設定維護★  '
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3  2
     A  30        S#MODE         6A  O  3 70DSPATR(RI)
     A*================================================================
     A          R AR102F2                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPTX         1Y 0B  9  3EDTCDE(Z)
     A                                      VALUES(0 2 3 4)
     A            S#PRXO         1A  O  9  8
     A            S1NAME        18O  B  9 14
     A  53                                  DSPATR(PC RI)
     A            S1PRIC         6  3B  9 37EDTCDE(1) CHECK(RB)
     A  53                                  DSPATR(PC RI)
     A*
     A            S#UPXM        10A  O  9 49
     A*
     A*================================================================
     A          R AR102F2C                  SFLCTL(AR102F2)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(91 '下頁')
     A                                      CA09(09 '列印')
     A*                                     SFLDROP(CF04)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  74                                  SFLCLR
     A                                      OVERLAY
     A                                  4  2'OP: 1=新增 2=修改 3=複製 +
     A                                      4=刪除'
     A                                  4 43'【第一列僅可執行1;其餘可2,3,4-
     A                                      】'
     A            S#LIN1        78A  O  5  2
     A                                  6  3'OP'
     A                                  6  6'代號'
     A                                    +3'說    明'
     A                                  6 36'單價'
     A                                  6 48'異動人員'
     A            S#LIN2        78A  O  7  2
     A            S#OPTA         1Y 0B  8  3EDTCDE(Z)
     A                                      VALUES(0 1)
     A  31                                  DSPATR(PC)
     A            S#PRNO         1A  B  8  8
     A  50                                  DSPATR(PC RI)
     A            S#NAME        18O  B  8 14
     A  51                                  DSPATR(PC RI)
     A            S#PRIC         6  3B  8 37EDTCDE(1) CHECK(RB)
     A  51                                  DSPATR(PC RI)
     A            S#NBR          4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR102F2M
     A                                      OVERLAY
     A            S#LIN3        78A  O 22  2
     A                                 23  2'F3=結束作業'
     A                                 23 16'F12=回前畫面'
     A                                 23 31'執行=存檔'
     A                                      DSPATR(RI)
     A            S#ERR         70A  O 24  2DSPATR(HI)
