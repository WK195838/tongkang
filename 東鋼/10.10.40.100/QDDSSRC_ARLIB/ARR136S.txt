     A*****************************************************************
     A*
     A*            *****   PROGRAM INTRODUCTION   *****
     A*
     A*            1.PROGRAM-ID    ARR136S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     銷貨發票開立方式一覽表
     A*            5.DATE-WRITTEN  98/12/18 2009OD64
     A*            6.UPDATE  DATE  98/12/29 2009AR407 S00WCJ
     A*                           113/01/05 2023AR00056 S00WCJ (1301A)
     A*                            修正廠區，T改為U桃園；出貨業務
     A*                            代號改為業務代號篩選接單、出貨、收
     A*                            款；新增客戶代號篩選及增加訂單結案
     A*                            選項
     A*                           113/01/22 S00WCJ (1301B)
     A*                            增加發票開立方式選項4每日隨訂
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/INVMST)
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03)
     A                                      CF12(12)
     A          R TITLE
     A                                      OVERLAY
     A                                  1 70'ARR136S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                  2 28'**銷貨發票開立方式一覽表**'
     A            DEVNM         10A  O  2 70
1301BA                                  6 20'訂單日期:'
     A            S#DATS         8Y 0B    +1EDTWRD('    /  /  ')
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                    +1'∼'
     A            S#DATE         8Y 0B    +1EDTWRD('    /  /  ')
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A                                  8 20'廠區:'
1301AA            S#AREA         1A  B    +1VALUES('U' 'M' 'K' ' ' 'P' 'N')
1301AA                                    +1'(U:桃園M:台中K:高雄+
1301AA                                        P:台北N:大業'
1301AA                                  9 30' 空白表全部)'
1301AA                                 11 20'業務代號(含接單、交貨+
1301AA                                       、收款):'
     A            S#SALS         1A  B    +1
     A                                    +1'(空白表全部)'
1301AA                                 13 20'客戶編號:'
1301AA            S#CUNS         6A  B    +1
1301AA                                    +1'(空白表全部)'
1301AA                                 15 20'發票開立方式:'
1301BA            S#INVS         1A  B    +1VALUES('1' '2' '3' ' ' '4')
     A                                    +1'(1隨車2區間+
1301BA                                       3指定4每日隨訂'
1301BA                                 16 40'空白表全部)'
1301AA                                 18 20'訂單結案:'
1301AA            S#CLOS         1A  B    +1VALUES('Y' 'N' ' ')
1301AA                                    +1'(Y=已結案N=未結案空+
1301AA                                      白表全部) '
     A            S#ERR         70A  O 23  2DSPATR(HI)
     A                                 24  5'F3=結束'
     A***************************************************************
