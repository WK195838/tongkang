     A*****************************************************************
     A*
     A*            1.PROGRAM-ID    ARE028S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S01LYC
     A*            4.FUNCTION     收款金額分類與查詢
     A*            5.DATE-WRITTEN  85/05/28
     A*            6.LAST UPDATE
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      INDARA
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前螢幕')
     A**********
     A          R HEAD
     A                                  1  4DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35   O  1 24
     A            S#SCRN        10   O  1 70
     A                                  2  4TIME
     A                                  2 31'*收款金額分類與查詢*'
     A                                      DSPATR(RI)
     A            S#DEVN        10   O  2 70
     A**********
     A          R MP01F2                    SFL
     A  70                                  SFLNXTCHG
     A            S#OPT          1Y 0B 11  2EDTCDE(Z)
     A                                      VALUES(0 4)
     A            S#RVNO         6A  O 11  4
     A            S#ITEM         2S 0O 11 11
     A            S#DATE         8Y 0O 11 14EDTCDE(4)
     A            S#DPNO         4A  O 11 23
     A            S#CUNO         6A  O 11 28
     A            S#CUNM        10O  O 11 35
     A            S#FAMT        11Y 0O 11 46EDTCDE(1)
     A            S#NTNO        20A  O 11 61
     A**********
     A          R MP01F2CL                  SFLCTL(MP01F2)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  73                                  SFLCLR
     A                                      CF06(06 '新增')
     A                                      ROLLUP(91 '次頁')
     A                                      OVERLAY
     A                                  4  1'ＯＰ: 4=刪除'
     A                                  6  1'廠區代號:'
     A            S#AREA         1A  O  6 13
     A            S#ARNM         6O  O  6 15
     A                                  7  1'繳款編號:'
     A            S#RVN          6A  B  7 13
     A                                  7 20'起'
     A                                  9  1' O 繳款　項'
     A                                  9 22'繳款'
     A                                 10  1' P 編號　次　繳款日　部門　客　+
     A                                       　　　　　戶'
     A                                      DSPATR(UL)
     A                                 10 50'收款金額'
     A                                      DSPATR(UL)
     A                                 10 62'票據編號'
     A                                      DSPATR(UL)
     A            POINT          4S 0H      SFLRCDNBR
     A**********
     A          R MP01F2MG
     A                                      OVERLAY
     A                                 23  2'操作提示:'
     A                                    +1'PF3=結束作業'
     A                                    +1'PF12=回前螢幕'
     A                                    +1'PF6=新增'
     A                                    +1'PA1=下一頁'
     A                                    +1'PA2=上一頁'
     A            S#ERR         70   O 24  2DSPATR(HI)
     A**********
     A          R MP01F3
     A                                      OVERLAY
     A                                      CF10(10 '存檔')
     A            S#MODE        10A  O  3 70DSPATR(RI)
     A                                  5  2'廠區代號:'
     A            W#AREA         1A  O  5 18
     A            W#ARNM         6O  O    +2
     A                                  6  1'_____________________+
     A                                      ______________________+
     A                                      ______________________+
     A                                      _______________'
     A                                  8 29'繳款書編號：'
     A            S#RVNO         6A  B    +1
     A  30                                  DSPATR(RI PC)
     A  61                                  DSPATR(PR)
     A                                 10 29'　　項　次：'
     A            S#ITEM         2  0B    +1EDTCDE(4)
     A  31                                  DSPATR(RI PC)
     A  61                                  DSPATR(PR)
     A                                 12 29'　　款項別：'
     A            S#USTP         2   O    +1
     A                                 14 28'　收款金額：'
     A            S#FAMT        11  0B    +1EDTCDE(K)
     A  32                                  DSPATR(RI PC)
     A  61                                  DSPATR(PR)
     A                                 23  2'操作提示:'
     A                                    +1'PF3=結束作業'
     A  28                             23 29'PF10=存檔'
     A N28                             23 29'PF10=刪除'
     A                                    +1'PF12=回前螢幕'
     A            S#ERR         70   O 24  2DSPATR(HI)
