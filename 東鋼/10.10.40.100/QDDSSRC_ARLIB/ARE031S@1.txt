     A*%%TS  SD  19851211  164510  S02KRH      REL-V2R3M0  5738-PW1
     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE031S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        KRH
     A*            4.FUNCTION     解除發票列印作業
     A*            5.DATE-WRITTEN  85/12/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束作業')
     A                                      CA12(12 '回前畫面')
     A*================================================================
     A          R AR031FH
     A                                  1  2'ARE031S-0'
     A            S#COMP        35A  O  1 23
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A            S#USER        10A  O  2 02
     A                                  2 29'解除發票列印控制碼'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A*================================================================
     A          R AR031F1                   SFL
     A  70                                  SFLNXTCHG
     A            S#OP           1  0B 11  2VALUES(0 1) EDTCDE(Z)
     A            S#NO          10   O 11  4
     A            S#INDT         8  0O 11 17EDTCDE(4)
     A            S#CUNO         6   O 11 28
     A            S#ORNO         6   O 11 39
     A            S#AAMT        11   O 11 47
     A            S#ATAX        11   O 11 60
     A            S#TYPE         1   O 11 75
     A*================================================================
     A          R AR031F1C                  SFLCTL(AR031F1)
     A                                      CF10(10)
     A                                      ROLLUP(91 '下頁')
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A                                      SFLPAG(10)
     A                                      SFLSIZ(11)
     A N73                                  SFLEND
     A  74                                  SFLCLR
     A                                      OVERLAY
     A                                  4  8'重印原因:'
     A            S#CGCA        40O  B  4 27
     A                                  6 10'(OP: 1=解除發票列印控制)'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                  8  1'OP'
     A                                  8  4'發票號碼'
     A                                  8 15'發票日期'
     A                                  8 26'客戶編號'
     A                                  8 37'訂單號碼'
     A                                  8 48'出貨金額'
     A                                  8 59'銷項稅額'
     A                                  8 70'發票類別'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A            S#NO1         10   B 10  4
     A            S#IND          8  0B 10 17EDTCDE(4)
     A            S#SFRN         4  0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR031F1M
     A                                      OVERLAY
     A                                 23  2'F3=結束作業'
     A                                    +1'F12=回前畫面'
     A                                    +1'PA1=下頁'
     A                                    +1'PA2=上頁'
     A                                    +1'F10=存檔'
     A            S#ERR         70   O 24  2DSPATR(HI)
     A*================================================================
     A          R AR031F2
     A*%%TS  SD  19851211  164510  S02KRH      REL-V2R3M0  5738-PW1
     A                                      OVERLAY
     A                                 18  2'-----------------------------------
     A                                      --------------'
     A                                 19  2'發票號碼'
     A            S#NO2         10A  O 19 14DSPATR(HI)
     A                                 19 26'的發票確定修改按<ENTER>'
     A                                 20  2'F3=取消 F12=取消'
     A                                 21  2'-----------------------------------
     A                                      --------------'
