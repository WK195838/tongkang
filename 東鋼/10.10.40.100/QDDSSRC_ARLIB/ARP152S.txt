     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARP152S
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     指定調整單發票開立作業(畫面)
     A*            5.DATE-WRITTEN  110/06/08 2021AR00020
     A*            6.UPDATE  DATE
     A*
     A***************************************************************
     A*  畫面一:輸入廠區及發票日期
     A*
     A*        記錄格式 AR152F1
     A***************************************************************
     A          R AR152F1
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      CF10(10)
     A*--------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARP152S'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 27'★指定調整單發票開立作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*--------------------------------------------------------------
     A                                  5 20'開立廠別:'
     A            S#AREA         1A  B    +1
     A  51                                  DSPATR(PC RI)
     A                                    +1'( P台北M台中H苗栗-
     A                                       K高雄U新桃)'
     A                                  7 20'發票日期:'
     A            S#DATE         8Y 0B  7 32EDTWRD('    /  /  ')
     A  52                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                    +2'(本月五日前可重開上月底的發票)'
     A                                  9 20'調整單號:'
     A            S#NOS          6A  B    +1
     A  54                                  DSPATR(PC RI)
     A                                    +1'~'
     A            S#NOE          6A  B    +1
     A  55                                  DSPATR(PC RI)
     A                                 10  5'-----------------------------------
     A                                      ------------------------------------
     A                                      -----'
     A                                 11 14'字軌'
     A                                 11 26'起號'
     A                                 11 40'迄號'
     A                                 11 54'目前'
     A                                 11 66'可用張數'
     A                                 13  5'二聯式'
     A            S#PRE2         2   O 13 16
     A            S#STN2         8  0O 13 25
     A            S#ENN2         8  0O 13 39
     A            S#CUN2         8  0O 13 53
     A            S#LEV2         8  0O 13 67EDTCDE(4)
     A                                 15  5'三聯式'
     A            S#PRE3         2   O 15 16
     A            S#STN3         8  0O 15 25
     A            S#ENN3         8  0O 15 39
     A            S#CUN3         8  0O 15 53
     A            S#LEV3         8  0O 15 67EDTCDE(4)
     A                                 16  5'-----------------------------------
     A                                      ------------------------------------
     A                                      -----'
     A                                 18 20'發票已開立日期:'
     A            S#XDAT         8  0O 18 38
     A                                      EDTWRD('    /  /  ')
     A*--------------------------------------------------------------
     A                                 22 12'F3:結束'
     A                                 22 26'F12:回前'
     A                                 22 41'ENTER:檢核'
     A                                 22 58'F10:執行'
     A                                      DSPATR(RI)
     A            S#MSG1        70A  O 24  2DSPATR(HI)
