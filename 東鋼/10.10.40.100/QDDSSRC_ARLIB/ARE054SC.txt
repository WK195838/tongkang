     A*%%TS  SD  19931013  133023  S02CSF      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE054RC
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CSF
     A*            4.FUNCTION     營業退款輸入作業（依退款日期）
     A*            5.DATE-WRITTEN  94/05/25
     A*            6.DATE-UPDATED  101/09/19  2012AR682 S00WCJ (0109A)
     A*
     A***************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03 '結束')
     A                                      CA12(12 '回上頁')
     A*================================================================
     A          R AR054H
     A*%%TS  SD  19930726  164759  S02CSF      REL-V4R4M0  5769-PW1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 28'東和鋼鐵企業股份有限公司'
     A                                  1 70'ARE054S'
     A                                  2  2TIME
     A                                  2 32'營業退款查詢程式'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USID        10A  O  3  2
     A  65        S#SF1T         6O  O  3 70DSPATR(RI)
     A*================================================================
     A          R AR054F1                   SFL
     A*%%TS  SD  19930728  162501  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT1         1A  B  9  5VALUES(' ' '5')
     A  31                                  DSPATR(RI)
     A            S#RTDT         8Y 0O  9  9EDTWRD('    /  /  ')
     A            S#CUNO         6A  O  9 21
     A            S#CUNM        10O  O  9 28
     A            S#ORNO         6A  O  9 41
     A            S#RTNO         6A  O  9 52
     A            S#AAMT         8Y 0O  9 61EDTCDE(2)
     A            S#FL01         1A  O  9 74
     A            S#FL02         1A  O  9 76
     A            S#FL03         1A  O  9 78
     A*================================================================
     A          R AR054F1C                  SFLCTL(AR054F1)
     A*%%TS  SD  19930728  162501  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A            S#SFN1         4S 0H
     A                                  4  2'選項:'
     A                                  4 11'5:查詢'
     A            S#LIN1        78A  O  5  2
     A                                  6  2'選項'
     A                                  6  9'退款日期'
     A                                  6 21'客　　　　戶'
     A                                  6 39'訂單編號'
     A                                  6 50'退款編號'
     A                                  6 61'退款金額'
     A                                  6 72'確印過'
     A            S#LIN2        78A  O  7  2
     A            S#KEY1         8Y 0B  8  9EDTWRD('    /  /  ')
     A  30                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A*================================================================
     A          R AR054F1M
     A*%%TS  SD  19930914  135414  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      TEXT('按鍵及錯誤訊息')
     A            S#LIN3        78A  O 21  2
     A                                 22  4'F03:結束'
     A                                 22 15'F12:回前'
     A            S#ERR1        70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR054F2                   SFL
     A*%%TS  SD  19931008  113411  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#SFI2         2Y 0O 11  3
     A            S#DC           1A  O 11  9
     A                                      DSPATR(UL)
     A            S#USTP         2A  O 11 13
     A                                      DSPATR(UL)
0109AA            S#ACNO         6A  O 11 18
     A                                      DSPATR(UL)
     A            S#RLNO        10A  O 11 23
     A                                      DSPATR(UL)
     A            S#DUDT         8Y 0O 11 34EDTWRD('    /  /  ')
     A                                      DSPATR(UL)
     A            S#CSNM        10O  O 11 45
     A                                      DSPATR(UL)
     A            S#DSC2        12O  O 11 56
     A                                      DSPATR(UL)
     A            S#AAMT         8Y 0O 11 69EDTCDE(1)
     A                                      DSPATR(UL)
     A            S#DSC1        60O  O 12 19
     A                                      DSPATR(UL)
     A***************************************************************
     A          R AR054F2C                  SFLCTL(AR054F2)
     A*%%TS  SD  19931013  133023  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A                                      CF01(01)
     A N81                                  SFLDSPCTL
     A N82                                  SFLDSP
     A  83                                  SFLCLR
     A N84                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A                                  4  8'退款編號:'
     A            S#RTNO         6A  O  4 20
     A                                  4 31'退款部門:'
     A            S#DPNO         4A  O  4 43
     A                                  4 51'退款日期:'
     A            S#RTDT         8Y 0O  4 63EDTWRD('    /  /  ')
     A                                  5  8'客戶編號:'
     A            S#CUNO         6A  O  5 20
     A                                  5 27'受款人:'
     A            S#FNAM        36O  O  5 37
     A                                  6 37'領款人:'
     A            S#SAID         1A  O  6 47
     A                                  6 51'退款金額:'
     A            S#OAMT         8Y 0O  6 63EDTCDE(1)
     A                                      DSPATR(HI)
     A            S#LIN4        78A  O  7  2
     A                                  8  2'項'
     A                                  8  7'借'
0109AA                                  8 12'款會計'
     A                                  8 24'相關號碼'
     A                                  8 35'到期日'
     A                                  8 46'對象別'
     A                                  8 57'輔助摘要'
     A                                  8 72'金額'
     A                                  9  2'次'
     A                                  9  7'貸'
0109AA                                  9 12'項科目'
     A                                  9 24'摘　　　　　要'
     A            S#LIN5        78A  O 10  2
     A***************************************************************
     A          R AR054F2M
     A                                      OVERLAY
     A            S#LIN6        78A  O 22  2
     A                                 23  4'F01:說明'
     A                                 23 19'F03:結束'
     A                                 23 34'F12:回前'
     A            S#ERR2        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A* 畫面二：輸入資料說明視窗
     A***************************************************************
     A          R AR054F2W
     A*%%TS  SD  19931008  114520  S02CSF      REL-V4R4M0  5769-PW1
     A                                      WINDOW(10 14 10 56)
     A                                      TEXT('說明視窗')
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      OVERLAY
     A                                  2  4'款項別'
     A                                      DSPATR(HI)
     A                                  4  2'A1:預收貨款'
     A                                  5  2'A2:銷退貨款'
     A                                  6  2'A3:銷折貨款'
     A                                  7  2'A4:暫收貨款'
     A                                  2 20'常用會計科目'
     A                                      DSPATR(HI)
     A                                  4 20'1111庫存現金'
     A                                  4 36'4112內銷銷貨退回'
     A                                  5 20'1112銀行存款'
     A                                  5 36'4113應收帳款折讓'
     A                                  6 20'2121應付票據'
     A                                  7 20'2127銷項稅額'
     A                                  8 20'2131預收貨款'
     A                                  9 20'2159暫收其他'
0109AA***************************************************************
0109AA* 畫面二：輸入資料說明視窗
0109AA***************************************************************
0109AA          R AR054F3W
0109AA                                      WINDOW(10 14 10 56)
0109AA                                      TEXT('說明視窗')
0109AA                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
0109AA                                       (*CHAR '        '))
0109AA                                      OVERLAY
0109AA                                  2  4'款項別'
0109AA                                      DSPATR(HI)
0109AA                                  4  2'A1:預收貨款'
0109AA                                  5  2'A2:銷退貨款'
0109AA                                  6  2'A3:銷折貨款'
0109AA                                  7  2'A4:暫收貨款'
0109AA*                                 8  2'A5:其他'
0109AA                                  2 20'常用會計科目'
0109AA                                      DSPATR(HI)
0109AA                                  4 16'110101庫存現金'
0109AA                                  4 34'410102內銷銷貨退回'
0109AA                                  5 16'110102銀行存款'
0109AA                                  5 34'410103應收帳款折讓'
0109AA                                  6 16'210701應付票據'
0109AA                                  6 34'111101應收帳款'
0109AA                                  7 16'210904銷項稅額'
0109AA                                  7 34'710402郵電費'
0109AA                                  8 16'211201預收貨款'
0109AA                                  9 16'211301暫收其他'
