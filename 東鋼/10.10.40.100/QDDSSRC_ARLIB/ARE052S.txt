     A*%%TS  SD  19920320  161457  S02CSF      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*****************************************************************
     A*
     A*          >>  PROGRAM INTRODUCTION  <<
     A*         1.PROGRAM-ID     ARE052S
     A*         2.PROGRAM-TYPE   RPG/400
     A*         3.AUTHOR         S02CSF
     A*         4.FUNCTION      應收帳款承購發票結案
     A*         5.DATE-WRITTEN   91/10/16
     A*         6.DATE-MODIFY    92/03/19
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束')
     A                                      CA12(12 '回上頁')
     A*================================================================
     A          R AR052H
     A*%%TS  SD  19911016  162606  S02CSF      REL-V4R4M0  5769-PW1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 28'東和鋼鐵企業股份有限公司'
     A                                  1 70'ARE052S'
     A                                  2  2TIME
     A                                  2 30'應收帳款承購發票結案'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            U#USID        10A  O  3  2
     A*================================================================
     A          R AR052F1
     A                                      OVERLAY
     A                                  8 28'客戶代號:'
     A            S#CUNO         6A  B  8 40
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                  8 50'(不可空白)'
     A                                 10 28'發票日期:'
     A            S#IND1         8A  B 10 40
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                 12 28'承購單號:'
     A            S#APNO         6A  B 12 40
     A            S#ERR         70A  O 23  2DSPATR(HI)
     A*================================================================
     A          R AR052F2                   SFL
     A*%%TS  SD  19920320  161457  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#INNO        10A  O  8  2
     A            S#INDT         8Y 0O  8 14
     A            S#DAMT         8Y 0O  8 24EDTCDE(1)
     A            S#FAMT         8Y 0O  8 35EDTCDE(1)
     A            S#AMT          8Y 0O  8 46EDTCDE(1)
     A            S#APN1         6A  O  8 58
     A            S#RCNO         6A  O  8 67
     A            S#CLOC         1A  B  8 77VALUES(' ' 'Y')
     A*================================================================
     A          R AR052F2C                  SFLCTL(AR052F2)
     A*%%TS  SD  19920320  161457  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0023)
     A                                      SFLPAG(0014)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A                                      OVERLAY
     A                                      CF10(10 '確認存檔')
     A                                      CF05(05 '全部／未結案')
     A                                  5  2'發票號碼'
     A                                  5 13'發票日期'
     A                                  5 24'發票金額'
     A                                  5 35'繳款金額'
     A                                  5 46'未繳金額'
     A                                  5 57'承購單'
     A                                  5 66'繳款單'
     A                                  5 75'結案'
     A            S#LIN1        78A  O  6  2
     A            S#KEY1        10A  B  7  2DSPATR(PC)
     A            S#NBR          4S 0H      SFLRCDNBR
     A*================================================================
     A          R AR052F2M
     A*%%TS  SD  19911016  164445  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                 23  5'F3=結束'
     A                                 23 15'F12=回前畫面'
     A N49                             23 31'F5=全部'
     A                                      DSPATR(RI)
     A  49                             23 35'F5=未結案'
     A                                      DSPATR(RI)
     A                                 23 52'F10=確認結案'
     A            S#LIN2        78A  O 22  2
     A            S#ERR2        70A  O 24  2DSPATR(HI)
