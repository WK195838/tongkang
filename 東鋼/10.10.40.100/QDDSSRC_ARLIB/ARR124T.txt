     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR124T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.WRITER        S02LBU
     A*           4.FUNCTION     客戶暫收款餘額核對清單
     A*           5.DATE-WRITTEN  95/03/29
     A*           6.DATE-MODIFY
     A*
     A*
     A*****************************************************************
     A          R AR124T1H
     A*****************************************************************
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報　　表: ARR124T'
     A                                    42
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    91
     A                                      '列表日期:'
     A                                   102
     A                                      DATE
     A                                      EDTCDE(Y)
     A*===========
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            T#USID        10A       14
     A                                    43
     A                                      '客戶暫收款餘額核對清單'
     A                                    91
     A                                      '列表時間:'
     A                                   102
     A                                      TIME
     A*===========
     A                                    91
     A                                      '頁　　次:'
     A                                      SPACEB(001)
     A                                   106
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*===========
     A            T#LIN1       109         2
     A                                      SPACEB(001)
     A*===========
     A                                     2
     A                                      '客戶代號'
     A                                      SPACEB(001)
     A                                    16
     A                                      '客戶簡稱'
     A                                    30
     A                                      '訂單號碼'
     A                                    44
     A                                      '財會系統金額'
     A                                    62
     A                                      '營業系統金額'
     A                                    80
     A                                      '差異金額'
     A                                    94
     A                                      '差　異　說　明'
     A*===========
     A            T#LIN2       109A        2
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR124T1D
     A**************************************************************************
     A                                     2
     A                                      ' '
     A                                      SPACEB(001)
     A            T#CUNO         6A        4
     A            T#CUNM        10O       16
     A            T#ORNO         6A       32
     A            T#MAMT        11S 0     43
     A                                      EDTCDE(J)
     A            T#BAMT        11S 0     61
     A                                      EDTCDE(J)
     A            T#DAMT         9S 0     78
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR124T1E
     A**************************************************************************
     A                                    20'主管:'
     A                                      SKIPB(062)
     A                                    65'製表:'
     A**************************************************************************
     A          R AR124T1M
     A**************************************************************************
     A                                      SPACEB(002)
     A                                    59
     A                                      '無相關資料!!!'
     A**************************************************************************
