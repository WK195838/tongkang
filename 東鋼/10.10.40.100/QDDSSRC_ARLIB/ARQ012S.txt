     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A***************************************************************
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARQ012S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 27'***  到期日票據查詢  ***'
     A            DEVNM         10A  O  2 70
     A                                  8 23'到期日期:'
     A            DATE1          8Y 0B  8 35EDTWRD('    /  /  ')
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  8 46'-'
     A            DATE2          8Y 0B  8 48EDTWRD('    /  /  ')
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            W00       R        O 10  2REFFLD(RVDUDT RCVDTL)
     A            W01       R        O 10 11REFFLD(ACBNAM BANKAC)
     A            W02       R        O 10 22REFFLD(RVTANO RCVDTL)
     A            W03       R        O 11 40REFFLD(RVNTNO RCVDTL)
     A            W04           10O  O 10 40
     A            W05           10A  O 10 55
     A            W06       R        O 10 66REFFLD(RVRAMT RCVDTL)
     A                                      EDTCDE(K)
     A            W21       R        O 11  7REFFLD(RVPBID RCVDTL)
     A            W22       R        O 11 22REFFLD(RVPANO RCVDTL)
     A            W23       R        O 11 33REFFLD(RVNO RCVDTL)
     A            W24            6A  O 10 33
     A            W25       R        O 11 61REFFLD(RVDATE RCVDTL)
     A            W26            6O  O 11 71
     A            W27       R        O 10 51REFFLD(RVUSTP RCVDTL)
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARQ012S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 27'***  到期日票據查詢  ***'
     A            DEVNM         10A  O  2 70
     A                                  4 23'到期日期:'
     A            DATE1          8Y 0O  4 35EDTWRD('    /  /  ')
     A                                  4 46'-'
     A            DATE2          8Y 0O  4 48EDTWRD('    /  /  ')
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6 13'託'
     A                                  6 24'收'
     A                                  7  1'到期日期（付'
     A                                  7 24'款）'
     A                                  8 10'銀行名稱'
     A                                  8 21'銀行帳號'
     A                                  8 32'繳款號　票據號碼'
     A                                  8 60'繳款日期　付款地'
     A                                  7 70'票據金額'
     A                                  7 32'異動別　票款別'
     A                                  7 49'款項　客戶名稱'
     A***************************************************************
     A          R UND01
     A                                      OVERLAY
     A                                 22  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                 24 19'PF03:結束'
     A                                 24 32'PF12:回前畫面'
     A                                 23 56'合　計:'
     A            TOT           11Y 0O 23 66EDTCDE(J)
     A***************************************************************
