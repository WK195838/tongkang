     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE095S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02LBU
     A*            4.FUNCTION     營業外銷報關資料維護
     A*            5.DATE-WRITTEN  98/01/21
     A*            6.DATE-MODIFY   98/03/10 S00LCL 2008HS135 (9803A)
     A*                            98/05/11 2009AR309 S00WCJ (9805A)
     A*                            98/12/02 2009AR386 S00WCJ (9812A)
     A*                           110/08/25 2021AR00031 S00WCJ (1008A)
     A*                           112/06/26 S00WCJ (1206A)
     A*                            增加新增功能之訂單檢核
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前畫面')
     A*================================================================
     A          R TITLE
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 70'ARE095S-1'
     A                                  1 26'東和鋼鐵企業股份有限公司'
     A                                  2 26'**營業外銷報關資料維護** '
     A            S#DEVN        10A  O  2 70
     A*
     A                                  9 26'功　　能:'
     A            S#CODE         1A  B  9 38
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
9803AA*                                 9 40'(2:修改4:查詢)'
9803AA                                  9 40'(1:新增2:修改4:查詢)'
     A                                 11 26'訂單號碼:'
     A            S#ORNO         6A  B 11 38
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
9803AA                                 13 26'報關年月:'
  |  A            SYMD           6S 0B 13 38
  |  A  43                                  DSPATR(RI)
  |  A  43                                  DSPATR(PC)
9803AA*                                15 27'(訂單編號或報關年月擇一輸入)'
9805AA                                 15 27'(功能選擇2或4時，+
9805AA                                       訂單編號或報關年月擇一+
9805AA                                       輸入)'
9803AA                                      COLOR(WHT)
     A*
     A            S#ERR         70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                    +1'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            S#OPT          1A  B  8  4
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
9803AA            S@ORNO         6A  O  8  8
     A            S#NTNO        20A  O  8 15
     A            S#CUNO         6A  O  8 36
     A            S#AAMT        10Y 2O  8 45EDTCDE(2)
     A            S#CDTE         8Y 0O  8 61EDTWRD('    /  /  ')
     A            S#AREA         1A  O  8 76
     A            H#SEQ          3  0H
     A            H#YYMM         6  0H
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A                                      SFLSIZ(0025)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A  71                                  SFLCLR
     A N72                                  SFLDSP
     A N73                                  SFLDSPCTL
     A  74                                  SFLEND
     A                                      CF06(06 '新增')
     A            S#NBR          4S 0H      SFLRCDNBR(CURSOR)
     A*----------
     A                                  1 70'ARE095S-2'
     A                                  1 26'東和鋼鐵企業股份有限公司'
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#MOD          6A  O  2  2DSPATR(RI)
     A                                  2 26'**營業外銷報關資料維護** '
     A*----------
     A                                  4  2'Ｙ：修改'
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
9803AA                                  6  2'選項訂單'
     A                                  6 18'報單號碼'
     A                                  6 34'客戶編號　外幣報關金額'
9803AA                                  6 61'報關日期報關廠區'
     A*                                 6 70'報關廠區'
     A***************************************************************
     A          R UND01
     A                                 21  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A            S#ERR         70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                    +1'PF03:結束'
     A                                    +1'PF06:新增'
     A                                      DSPATR(RI)
     A                                    +1'PF12:回前畫面'
     A***************************************************************
     A          R DETAIL
     A                                      CF10(10 '存檔')
9803AA                                      CF23(23 '刪除')
     A                                  1 70'ARE095S-3'
     A                                  1 26'東和鋼鐵企業股份有限公司'
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#MOD          6A  O  2  2DSPATR(RI)
     A                                  2 26'**營業外銷報關資料維護** '
     A            S#DEVN        10A  O  2 70
     A*======
     A                                  5 24'　訂單編號:'
9803AA*           S#ORND         6A  O  5 38
9803AA            S#ORND         6A  B  5 38
1206AC  49                                  DSPATR(PC RI)
     A*
     A                                  7 24'　報單號碼:'
     A            S#APNO        20A  B  7 38
     A  50                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A*
     A                                  9 24'　報關廠區:'
     A            S#ARED         1A  B  9 38
9812AA* 60                                  DSPATR(PR)
9812AA  50                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A*
     A                                 11 24'　報關日期:'
     A            S#CDTD         8  0B 11 38EDTWRD('    /  /  ')
9812AA* 60                                  DSPATR(PR)
9812AA  50                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A*
     A                                 13 24'　出貨日期:'
     A            S#DATS         8  0B 13 38EDTWRD('    /  /  ')
     A  50                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A                                 13 49'~'
     A            S#DATE         8  0B 13 51EDTWRD('    /  /  ')
     A  50                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A*
     A                                 15 24'　報關匯率:'
     A            S#TRAT         7  4B 15 38EDTCDE(2)
     A  50                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A*
     A                                 17 22'外幣報關金額:'
     A            S#FCUR        12  2B 17 38EDTCDE(2)
     A  50                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A*
     A                                 19 22'台幣報關金額:'
     A            S#AMT         10  0B 19 38EDTCDE(2)
     A  50                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A*
     A                                 21 22'　　報關數量:'
     A            S#QTY         12  3B 21 38EDTCDE(2)
     A  50                                  DSPATR(PR)
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(RI)
1008AA                                    +1'(公斤)'
     A*======
     A            S#ERR         70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                 24 20'PF03:結束'
     A                                 24 34'PF10:存檔'
     A                                 24 48'PF12:回前畫面'
9803AA  45                                +2'PF23:刪除此筆'
     A***************************************************************
