     A**************************************
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARP150T1
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION      型鋼折讓單資料上傳-開立清單
     A*            5.DATE-WRITTEN  108/04/29 2019AR00025
     A*            6.UPDATE  DATE  108/05/06 S00WCJ (0805A)
     A*                             增加發票廠區
     A*
     A*****************************************************************
     A          R AR50T11
     A**************************************************************************
     A                                      SKIPB(003)
     A                                    60
     A                                      '東和鋼鐵企業股份有限公司'
     A                                      SPACEA(001)
     A                                     1
     A                                      '報表編號: ARP150T1'
     A                                    58
     A                                      '型鋼折讓單資料上傳－開立清單'
     A                                   160
     A                                      '印表日期:'
     A            W#DATE         8  0    172EDTWRD('    /  /  ')
     A                                      SPACEA(1)
     A                                     1
     A                                      '上傳者: '
     A            C#USER        10        13
     A                                   160
     A                                      '印表時間:'
     A                                   173
     A                                      TIME
     A                                      SPACEA(1)
     A                                   160'頁　　數:'
     A                                   172PAGNBR EDTCDE(Z)
     A                                      SPACEA(001)
0805AA                                     2'發票客戶訂單'
     A                                    31'折讓折讓項'
     A                                      SPACEA(001)
0805AA                                     2'廠區編號編號'
     A                                    +1'發票號碼'
     A                                    +0'種類單號次'
     A                                    +0'開立日期'
     A                                    +1'品名'
     A                                    +0'鋼種'
     A                                    +1' H/T/D '
     A                                    +0'  B/W  '
     A                                    +0'  T1'
     A                                    +1'長度 '
     A                                    +1'數量'
     A                                   109'單價'
     A                                   119'金額 '
     A                                   131'傳輸日期'
     A                                   142'時間'
     A                                   149'傳輸人員'
     A                                    +1'傳送訊息'
     A                                      SPACEA(001)
     A                                     1
     A                                      '=================================='
     A                                    35
     A                                      '=================================='
     A                                    69
     A                                      '=================================='
     A                                   103
     A                                      '=============================='
     A                                   133
     A                                      '=============================='
     A                                   163
     A                                      '=================================='
     A                                      SPACEA(001)
     A**************************************************************************
     A          R AR50T12
     A**************************************************************************
     A                                      SPACEA(001)
0805AA            A6INAR         1         4
     A            A6CUNO         6        +1
     A            S#ORNO         6        +1
     A            A6IVNO        10        +1
     A            A6ACNT         1        +3
     A            S#NO           6        35
     A            A6ITEM         2  0     +1
     A            A6DATE         8  0     45EDTWRD('    /  /  ')
     A            A6PDNM         5        56
     A            A6METL         8        +1
     A            A6SPE1         5        +2
     A            A6SPE2         5        +2
     A            A6SPE3         3        +2
     A            A6SPE5         7        +1
     A            A6QTY          7  0     +1EDTCDE(K)
     A            A6UPRC         5  3     +1EDTCDE(K)
     A            A6AMT         11  0     +1EDTCDE(K)
     A*
     A            A6TMDT         8  0    133
     A            A6TMTM         6  0    142
     A            A6TMUS        10       149
     A            S#STCL        36       160
     A**************************************************************************
     A          R AR50T13
     A**************************************************************************
     A                                      SPACEA(001)
     A                                    59
     A                                      '---- END OF DATA ----'
     A**************************************************************************
     A          R AR50T14
     A**************************************************************************
     A                                      SPACEA(001)
     A                                    91'合計:'
     A            R#AMTA        12  0    116EDTCDE(K)
     A*
