     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR070T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     營業繳款未過入財會明細表
     A*           4.DATE-WRITTEN  87/05/25
     A*           5.WRITER        S02CLJ
     A*           6.UPDATE DATE  101/09/24  2012AR682 S00WCJ (0109A)
     A*
     A*       已確認及未確認之報表由傳入資料而定
     A*
     A*****************************************************************
     A          R AR070T1H
     A*****************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報表代號: ARR070T'
     A                                    82
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   169
     A                                      '列表日期:'
     A                                   180
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                    84
     A                                      '已確認營業繳款明細表'
     A                                   169
     A                                      '列表時間:'
     A                                   180
     A                                      TIME
     A                                     2
     A                                      '繳款建立日期:'
     A                                      SPACEB(001)
     A            R#RVDT         8S  O    +1
     A                                      EDTWRD('    /  /  ')
     A                                   169
     A                                      '頁　　次:'
     A            R#PGNO         4S 0O   180
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '繳款確認日期:'
     A                                      SPACEB(001)
     A            R#CFDT         8S  O    +1
     A                                      EDTWRD('    /  /  ')
     A                                     2
     A                                      '繳款部門:'
     A                                      SPACEB(001)
     A            R#DPNO         4A  O    +1
     A                                    +4
     A                                      '收款業務:'
     A            R#RVID         2A  O    +1
     A            R#RVNM        10O  O    +0
     A*----------------------------------------------------------------
     A                                     2
     A                                      '財會手填'
     A                                      SPACEB(002)
     A                                    +0
     A                                      '繳款'
     A                                   +19
     A                                      '款'
     A                                    +0
     A                                      '票'
     A                                    +0
     A                                      '會計'
     A                                   +11
     A                                      '借'
     A                                   +68
     A                                      '付款'
     A                                   +18
     A                                      '存入'
     A                                   183
     A                                      '資金'
     A                                     2
     A                                      '傳票編號'
     A                                      SPACEB(001)
     A                                    +0
     A                                      '單號'
     A                                    +6
     A                                      '客戶'
     A                                    +7
     A                                      '項'
     A                                    +0
     A                                      '據'
     A                                    +0
     A                                      '科目'
     A                                   +11
     A                                      '貸'
     A                                    78
     A                                      '銀行名稱'
     A                                    93
     A                                      '銀行帳號'
     A                                    +1
     A                                      '票據號碼'
     A                                   +12
     A                                      '到期日'
     A                                    +2
     A                                      '地'
     A                                    +3
     A                                      '票據金額'
     A                                   157
     A                                      '銀行帳號'
     A                                   176
     A                                      '備註'
     A                                   183
     A                                      '代號'
     A*----------------------------------------------------------------
     A                                     2
     A                                      ' -------- '
     A                                      SPACEB(001)
     A                                    +1
     A                                      ' -------- '
     A                                    +0
     A                                      '------'
     A                                    +1
     A                                      '------'
     A                                    +1
     A                                      '----------'
     A                                    +2
     A                                      '--'
     A                                    +5
     A                                      '-'
     A                                    +0
     A                                      '---- '
     A                                    +1
     A                                      '-'
     A                                    +3
     A                                      '--------------'
     A                                    +1
     A                                      '----------'
     A                                    +1
     A                                      '----------'
     A                                    +1
     A                                      '----------'
     A                                   +12
     A                                      '-'
     A                                    +2
     A                                      '---------------'
     A                                    +2
     A                                      '----------'
     A                                    +1
     A                                      '----------------'
     A                                    +4
     A                                      '----'
     A                                    +2
     A                                      '------'
     A*---------------------------------------------------------------
     A                                    32
     A                                      '發票日期'
     A                                      SPACEB(001)
     A                                    43
     A                                      '發票號碼'
     A                                    54
     A                                      '請款單號'
     A                                    66
     A                                      '繳款金額'
     A                                    82
     A                                      '發票日期'
     A                                    93
     A                                      '發票號碼'
     A                                   104
     A                                      '請款單號'
     A                                   126
     A                                      '繳款金額'
     A                                   142
     A                                      '發票日期'
     A                                   153
     A                                      '發票號碼'
     A                                   164
     A                                      '請款單號'
     A                                   176
     A                                      '繳款金額'
     A*
     A                                    32
     A                                      '----------'
     A                                      SPACEB(001)
     A                                    43
     A                                      '----------'
     A                                    55
     A                                      '-------'
     A                                    64
     A                                      '---------------'
     A                                    +3
     A                                      '----------'
     A                                    93
     A                                      '----------'
     A                                   105
     A                                      '-------'
     A                                   124
     A                                      '---------------'
     A                                   142
     A                                      '----------'
     A                                   153
     A                                      '----------'
     A                                   165
     A                                      '-------'
     A                                   174
     A                                      '---------------'
     A*
     A                                     2
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============='
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR070T1D
     A**************************************************************************
     A                                      SPACEA(001)
     A            R#RVNO         6A  O    12
     A            R#CUNO         6A  O    19
     A            R#CUNM        10O  O    26
     A            R#USTP         2A  O    38
     A            R#NTTP         1A  O    43
0109AA            R#ACNO         6A  O    45
     A            R#CHNM        10O  O    51
     A            R#ACTP         1A  O    63
     A            R#PBID        14O  O    78
     A            R#PANO        10A  O    93
     A            R#NTNO        20A  O    +1
     A            R#DUDT         8S  O    +1
     A                                      EDTWRD('    /  /  ')
     A            R#PLAC         1A  O    +1
     A            R#RAMT        11S 0O    +3
     A                                      EDTCDE(K)
     A N70        R#ORNO         8A  O    44
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR070T1E
     A**************************************************************************
     A                                      SPACEA(001)
     A*---------------------------------------------------------------
     A            R#IND1         8S  O    32
     A                                      EDTWRD('    /  /  ')
     A            R#INN1        10A  O    43
     A            R#APN1         7A  O    55
     A            R#INX1        11S 0O    64
     A                                      EDTCDE(K)
     A            R#IND2         8S  O    82
     A                                      EDTWRD('    /  /  ')
     A            R#INN2        10A  O    93
     A            R#APN2         7A  O   105
     A            R#INX2        11S 0O   124
     A                                      EDTCDE(K)
     A            R#IND3         8S  O   142
     A                                      EDTWRD('    /  /  ')
     A            R#INN3        10A  O   153
     A            R#APN3         7A  O   165
     A            R#INX3        11S 0O   174
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR070T1S
     A**************************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                     5
     A                                      '**繳款單小計:'
     A*
     A            R#SNM1        13O  O    22
     A            R#SRPA        11S 2O    35
     A                                      EDTCDE(K)
     A*                                     'USD押匯額：'
     A*
     A            R#SNM3        13O  O    52
     A            R#SREA         7S 2O    65
     A                                      EDTCDE(K)
     A*                                     'USD費用額：'
     A*
     A            R#SNM4        13O  O    77
     A  40        R#SRBA         7S 2O    90
     A                                      EDTCDE(J)
     A*                                     'USD利息額：'
     A*
     A            R#SNM2        13O  O   102
     A            R#SRAA        11S 2O   115
     A                                      EDTCDE(K)
     A*                                     'USD入帳額：'
     A*
     A                                   132
     A                                      '票據金額：'
     A            R#SRNA        11S 0O   144
     A                                      EDTCDE(K)
     A*
     A                                   161
     A                                      '繳款金額：'
     A            R#SRXA        11S 0O   173
     A                                      EDTCDE(K)
     A*
     A                                     2
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ============'
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR070T1T
     A**************************************************************************
     A                                      SPACEA(001)
     A                                     7
     A                                      '**本日合計:'
     A*
     A            R#TNM1        13O  O    22
     A            R#TRPA        11S 2O    35
     A                                      EDTCDE(K)
     A*                                     'USD押匯額：'
     A*
     A            R#TNM3        13O  O    52
     A            R#TREA         7S 2O    65
     A                                      EDTCDE(K)
     A*                                     'USD費用額：'
     A*
     A            R#TNM4        13O  O    77
     A  40        R#TRBA         7S 2O    90
     A                                      EDTCDE(J)
     A*                                     'USD利息額：'
     A*
     A            R#TNM2        13O  O   102
     A            R#TRAA        11S 2O   115
     A                                      EDTCDE(K)
     A*                                     'USD入帳額：'
     A*
     A                                   132
     A                                      '票據金額：'
     A            R#TRNA        11S 0O   144
     A                                      EDTCDE(K)
     A*
     A                                   161
     A                                      '繳款金額：'
     A            R#TRXA        11S 0O   173
     A                                      EDTCDE(K)
     A*
     A**************************************************************************
     A          R AR070T1X
     A**************************************************************************
     A                                      SKIPB(059)
     A                                     2
     A                                      '<財會>'
     A                                    +0
     A                                      '主管:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '複核:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '出納:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '入帳:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    93
     A                                      '<營業>'
     A                                    +0
     A                                      '主管:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '複核:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '繳款:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A*****************************************************************
     A          R AR070T1L
     A*****************************************************************
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR070T1M
     A*****************************************************************
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A                                    19
     A                                      '*****無條件符合之資料！！'
     A**************************************************************************
