     A*%%TS  SD  19921111  171211  S02CSF      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*****************************************************************
     A*
     A*          >>  PROGRAM INTRODUCTION  <<
     A*         1.PROGRAM-ID     ARE053S
     A*         2.PROGRAM-TYPE   RPG/400
     A*         3.AUTHOR         S02CSF
     A*         4.FUNCTION      應收帳款承購人工票據兌現
     A*         5.DATE-WRITTEN   92/02/10
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束')
     A                                      CA12(12 '回上頁')
     A*================================================================
     A          R AR053H
     A*%%TS  SD  19920212  103735  S02CSF      REL-V4R4M0  5769-PW1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 30'東和鋼鐵股份有限公司'
     A                                  1 70'ARE053S'
     A                                  2  2TIME
     A                                  2 28'應收帳款承購人工票據兌現'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            U#USID        10A  O  3  2
     A*================================================================
     A          R AR053F1                   SFL
     A*%%TS  SD  19921111  171211  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#CUNO         5A  O  8  4
     A            S#BKID         3A  O  8 18
     A            S#RCNO         9A  O  8 25
     A            S#DUDT         6Y 0O  8 35EDTCDE(Y)
     A  50                                  DSPATR(RI)
     A            S#RLNO        10A  O  8 47
     A            S#RAMT         8Y 0O  8 61EDTCDE(1)
     A            S#NTCD         1A  B  8 75VALUES(' ' 'Y')
     A            S#CUNM        10A  O  9  2
     A            S#BKNM        10A  O  9 14
     A            S#ACDT         6Y 0O  9 25EDTCDE(Y)
     A            S#PLAC         1A  O  9 39
     A            S#BANM        16A  O  9 44
     A            S#PANO        10A  O  9 61
     A            S#RCTM         2S 0H
     A*================================================================
     A          R AR053F1C                  SFLCTL(AR053F1)
     A*%%TS  SD  19921111  170310  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0023)
     A                                      SFLPAG(0007)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A                                      OVERLAY
     A                                      CF10(10 '確認存檔')
     A                                      CF05(05 '全部／未兌現')
     A                                  5  2'客戶代號'
     A                                  5 13'銀行代號'
     A                                  5 24'繳款單號'
     A                                  5 35'到期日'
     A                                  5 47'票據號碼'
     A                                  5 61'票據金額'
     A                                  6  2'客戶名稱'
     A                                  6 13'銀行名稱'
     A                                  6 24'繳款日期'
     A                                  6 35'付款地'
     A                                  6 47'付款銀行'
     A                                  6 61'付款帳號'
     A                                  5 72'兌現碼'
     A            S#LIN1        78A  O  7  2
     A            S#NBR          4S 0H      SFLRCDNBR
     A*================================================================
     A          R AR053F1M
     A*%%TS  SD  19911115  105014  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                 23  5'F3=結束'
     A                                 23 15'F12=回前畫面'
     A  49                             23 31'F5=全部'
     A                                      DSPATR(RI)
     A N49                             23 31'F5=未兌現'
     A                                      DSPATR(RI)
     A                                 23 45'F10=確認兌現'
     A            S#LIN2        78A  O 22  2
     A            S#ERR1        70A  O 24  2DSPATR(HI)
