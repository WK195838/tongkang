     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARR107T
     A*            2.PROGRAM-TYPE  PRTF
     A*            3.AUTHOR        S02CSF
     A*            4.FUNCTION     應收帳款承購(E-FACTOTING)託收票據明細表
     A*            5.DATE-WRITTEN  92/02/13
     A*            6.DATE-UPDATED  92/11/12
     A*
     A*****************************************************************
     A          R AR107T1H
     A*****************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報　　表: ARR107T'
     A                                      SPACEB(001)
     A                                    54
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   121
     A                                      '印表人員:'
     A            U#USID        10A      132
     A                                     2
     A                                      '銀行代號:'
     A                                      SPACEB(001)
     A            T#BKN1        10A       13
     A                                    47
     A                                      '應收帳款承購(E-FACTOTING)託收-
     A                                      票據明細表'
     A                                   121
     A                                      '印表日期:'
     A                                   132
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      '客戶名稱:'
     A                                      SPACEB(001)
     A            T#CUN1        10A       13
     A                                   121
     A                                      '頁　　數:'
     A                                   132
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A            T#LIN1       140A  O     2
     A                                      SPACEB(001)
     A                                     2
     A                                      '票據到期日'
     A                                      SPACEB(001)
     A                                    15
     A                                      '客戶代號'
     A                                    26
     A                                      '客戶名稱'
     A                                    37
     A                                      '繳款單號'
     A                                    48
     A                                      '繳款日期'
     A                                    59
     A                                      '票據號碼'
     A                                    84
     A                                      '票據金額'
     A                                    96
     A                                      '付款地'
     A                                   108
     A                                      '付款銀行'
     A                                   122
     A                                      '付款帳號'
     A                                   134
     A                                      '兌現碼'
     A            T#LIN2       140A  O     2
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR107T1D
     A*****************************************************************
     A                                     2
     A                                      ' '
     A                                      SPACEB(001)
     A N50        T#DUDT         8S 0O     3
     A                                      EDTWRD('    /  /  ')
     A N50        T#CUNO         6A  O    17
     A N50        T#CUNM        10A  O    26
     A            T#RCNO         6A  O    39
     A            T#ACDT         8S 0O    47
     A                                      EDTWRD('    /  /  ')
     A            T#RLNO        20A  O    60
     A            T#RAMT        10S 0O    81
     A                                      EDTCDE(1)
     A            T#PLAC         1A  O    99
     A            T#BANM        16A  O   105
     A            T#PANO        10A  O   122
     A            T#NTCD         1A  O   137
     A*****************************************************************
     A          R AR107T1S
     A*****************************************************************
     A                                    67
     A                                      '小　　計：'
     A                                      SPACEB(001)
     A            T#RAMS        12S 0O    79
     A                                      EDTCDE(1)
     A*****************************************************************
     A          R AR107T1A
     A*****************************************************************
     A                                    67
     A                                      '合　　計：'
     A                                      SPACEB(001)
     A            T#RAMA        12S 0O    79
     A                                      EDTCDE(1)
     A                                     2
     A                                      ' '
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR107T1T
     A*****************************************************************
     A                                    67
     A                                      '總　　計：'
     A                                      SPACEB(001)
     A            T#RAMO        12S 0O    79
     A                                      EDTCDE(1)
     A*****************************************************************
     A          R AR107T1L
     A*****************************************************************
     A            T#LIN3        40A  O    60
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR107T1M
     A*****************************************************************
     A                                    19
     A                                      '*****無符合條件之資料!!!'
     A                                      SPACEB(008)
     A*****************************************************************
