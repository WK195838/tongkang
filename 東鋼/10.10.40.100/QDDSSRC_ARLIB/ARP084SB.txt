     A*%%TS  SD  19901110  161341  S02YSH      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE084SB
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02YSH
     A*            4.FUNCTION     承購發票預支試算作業--確認
     A*            5.DATE-WRITTEN  90/11/10
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束作業')
     A                                      CA12(12 '回前畫面')
     A*================================================================
     A          R AR084H
     A*%%TS  SD  19901110  155843  S02YSH      REL-V4R4M0  5769-PW1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 26'　東和鋼鐵企業股份有限公司も'
     A            S#SRID        10A  O  1 70
     A                                  2  2TIME
     A                                  2 28'★承購發票預支試算確認★'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3 70
     A*================================================================
     A          R AR084F1                   SFL
     A*%%TS  SD  19901110  161341  S02YSH      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT1         1Y 0B 13  2EDTCDE(Z)
     A                                      VALUES(0 4)
     A            S#CUNO         6A  O 13  4
     A            S#CUNM        10A  O 13 11
     A            S#INDT         6Y 0O 13 22EDTCDE(Y)
     A            S#INNO        10A  O 13 32
     A            S#CAM1        10Y 0O 13 44EDTCDE(1)
     A            S#CAMX        10Y 0O 13 59EDTCDE(1)
     A*================================================================
     A          R AR084F1C                  SFLCTL(AR084F1)
     A*%%TS  SD  19901110  161341  S02YSH      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      ROLLUP(91 '下頁')
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  74                                  SFLCLR
     A                                      OVERLAY
     A                                      CF10(10 '確認存檔')
     A                                  4  2'預支銀行:'
     A            S#BANK         3A  O  4 15
     A            S#BANM        10A  O  4 20
     A                                  4 53'批次號碼:'
     A            S#BACH        13A  O  4 65
     A                                      DSPATR(RI)
     A                                  5  2'預支總額:'
     A            S#PAM1        12Y 0O  5 15EDTCDE(1)
     A                                      DSPATR(RI)
     A                                  5 53'試算總額:'
     A            S#PAM2        12Y 0O  5 65EDTCDE(1)
     A                                      DSPATR(RI)
     A                                  6  2'預支利率:'
     A            S#RATE         4Y 3O  6 15EDTCDE(1)
     A                                    +1'%'
     A                                  7  2'OP: 4=刪除'
     A            S#LIN1        78A  O  8  2
     A                                  9  2'O'
     A                                  9  9'客戶'
     A                                  9 21'發票日期'
     A                                  9 32'發票號碼'
     A                                  9 48'發票金額'
     A                                  9 62'試算金額'
     A                                 10  2'P'
     A                                 10  4'代號'
     A                                 10 13'名稱'
     A            S#LIN2        78A  O 11  2
     A            S#KEY1         6Y 0B 12 24EDTCDE(4)
     A            S#KEY2        10A  B 12 32
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR084F1M
     A                                      OVERLAY
     A            S#LIN3        78   O 22  2
     A                                 23  2'F3=結束作業'
     A                                    +1'F12=回前畫面'
     A                                    +1'PA1=下頁'
     A                                    +1'PA2=上頁'
     A                                    +1'F10=確認存檔'
     A                                      DSPATR(RI)
     A            S#ERR1        70   O 24  2DSPATR(HI)
