     A**************************************
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARP202T
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     訂單管制檔介面傳輸-錯誤清單
     A*            5.DATE-WRITTEN  105/09/09 2016AR1007
     A*
     A*****************************************************************
     A          R AR02T21
     A**************************************************************************
     A                                      SKIPB(003)
     A                                    60
     A                                      '東和鋼鐵企業股份有限公司'
     A                                      SPACEA(001)
     A                                     1
     A                                      '報表編號: ARP202T'
     A                                    58
     A                                      '訂單管制檔介面檔-錯誤清單'
     A                                   170
     A                                      '印表日期:'
     A            W#DATE         8  0    182EDTWRD('    /  /  ')
     A                                      SPACEA(1)
     A                                     1
     A                                      '上傳者: '
     A            C#USER        10        13
     A                                   170
     A                                      '印表時間:'
     A                                   183
     A                                      TIME
     A                                      SPACEA(1)
     A                                   170'頁　　數:'
     A                                   182PAGNBR EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'處'
     A                                      SPACEA(001)
     A                                     1'理'
     A                                     6'訂單編號'
     A                                    18'管制類別'
     A                                    30'開立方式'
     A                                    +2'類別一'
     A                                    +2'比率金額一'
     A                                    66'類別二'
     A                                    76'比率金額二'
     A                                    +2'類別三'
     A                                   100'比率金額三'
     A                                   116'傳輸序號'
     A                                   131'傳輸日期'
     A                                   142'時間'
     A                                   149'傳輸人員'
     A                                   161'傳送訊息'
     A                                      SPACEA(001)
     A                                     1
     A                                      '=================================='
     A                                    35
     A                                      '=================================='
     A                                    69
     A                                      '=================================='
     A                                   103
     A                                      '=============================='
     A                                   133
     A                                      '=============================='
     A                                   163
     A                                      '=================================='
     A                                   197
     A                                      '=='
     A                                      SPACEA(001)
     A**************************************************************************
     A          R AR02T22
     A**************************************************************************
     A                                      SPACEA(001)
     A            AIFLAG         1         2
     A            AIORNO         9         6
     A            AICTKD         2        22
     A            AIMTHD         2        34
     A            AITYP1         2  0     45EDTCDE(2)
     A            AIRAT1        11  3     50EDTCDE(2)
     A            AITYP2         2  0     69EDTCDE(2)
     A            AIRAT2        11  3     74EDTCDE(2)
     A            AITYP3         2  0     93EDTCDE(2)
     A            AIRAT3        11  3     99EDTCDE(2)
     A            AISQNO        13       116
     A            AITMDT         8  0    131
     A            AITMTM         6  0    142
     A            AITMOP        10       149
     A            S#STCL        38       161
     A**************************************************************************
     A          R AR02T23
     A**************************************************************************
     A                                      SPACEA(001)
     A                                    59
     A                                      '---- END OF DATA ----'
