     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR136T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     銷貨發票開立方式一覽表
     A*           4.DATE-WRITTEN  98/12/18 2009OD64
     A*           5.WRITER        S00WCJ
     A*           6.UPDATE  DATE 113/01/05 2023AR00056 S00WCJ (1301A)
     A*
     A*****************************************************************
     A          R AR136T1H
     A*****************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      ' PRT-ID  : ARR136T'
     A                                    27
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    60
     A                                      '列表日期:'
     A                                    +1
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      ' USR-ID  :'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                    27
     A                                      '銷貨發票開立方式一覽表'
     A                                    60
     A                                      '列表時間:'
     A                                    +1
     A                                      TIME
     A                                    60
     A                                      '頁　　次:'
     A                                      SPACEB(001)
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '日期:'
     A                                      SPACEB(002)
     A            R#DATS         8  0     +1EDTWRD('    /  /  ')
     A                                    +1'∼'
     A            R#DATE         8  0     +1EDTWRD('    /  /  ')
1301AA                                    +2'廠區:'
1301AA            R#AREA         1        +1
1301AA            R#ARNM        10        +1
1301AA                                     2
1301AA                                      '業務代號:'
1301AA                                      SPACEB(001)
1301AA            R#SALS         1        +1
1301AA                                    +1'(空白表全部) '
1301AA                                    38'客戶編號:'
1301AA            R#CUNS         6        +1
1301AA                                    +1'(空白表全部)'
1301AA                                     2
1301AA                                      '發票開立方式:'
1301AA                                      SPACEB(001)
1301AA            R#INVS         1        +1
1301AA            R#INVM        10        +1
1301AA                                    38'訂單結案:'
1301AA            R#CLOS         1        +1
1301AA                                    +1'(空白表全部)'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========='
     A                                      SPACEB(001)
     A                                     1
1301AA                                      '交貨業務'
     A                                      SPACEB(001)
     A                                    +0
     A                                      '發票開立方式'
     A                                    +0
     A                                      '客戶編號'
     A                                    +0
     A                                      '客戶名稱'
     A                                    +0
     A                                      '訂單編號'
     A                                    +0
     A                                      '購案號碼'
     A                                    +0
     A                                      '廠區'
     A                                    +0
     A                                      '銷貨未開'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========='
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR136T1D
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
1301AA  51        R#SND          2   O     5
     A  52        R#MTHD         6   O    14
     A  53        R#CUNO         6   O    25
     A  53        R#CUNM        10   O    35
     A            R#ORNO         9   O    45
     A            R#CTNO        10   O    55
     A            R#VREA         1   O    67
     A            R#TRYN         1   O    75
     A**************************************************************************
     A          R AR136T1L
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                     1
     A                                      ' '
     A**************************************************************************
     A          R AR136T1M
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                     1
     A                                      '----------------------------------+
     A                                      -----------------------------------+
     A                                      ----------'
     A
     A**************************************************************************
     A          R AR136T1T
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                    27
     A                                      '===列印結束===='
     A**************************************************************************
     A          R AR136T1X
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     1
     A                                      '=============================-
     A                                      ==接　下　頁==============-
     A                                      ======================'
