     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARP074T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     折讓退回代開傳票
     A*           4.DATE-WRITTEN  89/12/29
     A*           5.WRITER        S02YSH
     A*           6.UPDATE DATE  101/09/21 2012AR682 S00WCJ (0109A)
     A*
     A*****************************************************************
     A          R AR074TH
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      ' PRT-ID  : ARP074T'
     A                                    50
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   110
     A                                      '列表日期:'
     A                                   121
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      ' USR-ID  :'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                   +31
     A                                      '折讓退回入帳清單'
     A                                   110
     A                                      '列表時間:'
     A                                   121
     A                                      TIME
     A                                     2
     A                                      '入帳日期'
     A                                      SPACEB(001)
     A            R#DATE         8S  O    +1
     A                                      EDTWRD('    /  /  ')
     A                                    54
     A                                      '代開年月:'
     A            U#YYMM         6S 0O    66
     A                                      EDTWRD('    /  ')
     A                                   110
     A                                      '頁　　次:'
     A                                   121
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '折讓單號'
     A                                      SPACEB(002)
     A                                    13
     A                                      '訂單'
     A                                    21
     A                                      '客戶名稱'
     A                                    32
     A                                      '借貸'
     A                                    39
     A                                      '會計科目'
     A                                    60
     A                                      '方式'
     A                                    66
     A                                      '品　名'
     A                                    79
     A                                      '數　量'
     A                                    87
     A                                      '更正'
     A                                    93
     A                                      '借方金額'
     A                                   103
     A                                      '貸方金額'
     A                                   113
     A                                      '部門'
     A                                   123
     A                                      '傳票編號'
     A            T#LINE       132         1
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR074T1
     A            T#TXNO        10         2
     A                                      SPACEB(001)
     A            T#ORNO         6        13
     A            T#CUNM        10        +2
     A            T#DC           1        +4
0109AA            T#NO           6        +3
     A            T#NONM        16        +1
     A            T#TYCO         2        +1
     A            T#DSC2         3        +2
     A            T#DSCN         8        +1
     A            T#QTY          7  0     +1
     A                                      EDTCDE(4)
     A            T#CORT         2        +2
     A            T#DAMT         7  0     +2
     A                                      EDTCDE(J)
     A            T#CAMT         7  0     +1
     A                                      EDTCDE(J)
     A            T#DPNO         8       114
     A            T#VRNO         6       124
     A                                    +0
     A                                      '-'
     A            T#ITEM         3        +0
     A**************************************************************************
     A          R AR074T2
     A                                      SPACEB(002)
     A                                    33
     A                                      '入帳筆數:'
     A            T#RCNT         3S  O    45
     A                                      EDTCDE(J)
     A                                    52
     A                                      '數量合計:'
     A            T#QTYT         7  0     64
     A                                      EDTCDE(J)
     A                                    77
     A                                      '借方合計:'
     A            T#CNTD         9  0     89
     A                                      EDTCDE(J)
     A                                   104
     A                                      '貸方合計:'
     A            T#CNTC         9  0    116
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR074T3
     A                                      SPACEB(002)
     A                                    33
     A                                      '會計科目:'
     A            T#NO1          5        45
     A            T#NO2         14        50
     A**************************************************************************
     A          R AR074T4
     A                                      SPACEB(001)
     A                                    37
     A                                      '品名'
     A                                    45
     A                                      '數量合計'
     A                                    62
     A                                      '金額合計'
     A                                    78
     A                                      '稅額合計'
     A**************************************************************************
     A          R AR074T5
     A                                      SPACEB(001)
     A            T#PDN1         3        38
     A            T#QTY1         8  0     44
     A                                      EDTCDE(J)
     A            T#AMT1        10  0     58
     A                                      EDTCDE(J)
     A            T#TAX1         8  0     77
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR074T6
     A                                      SPACEB(001)
     A                                    37
     A                                      '合計'
     A            T#QTY2         8  0     44
     A                                      EDTCDE(J)
     A            T#AMT2        10  0     58
     A                                      EDTCDE(J)
     A            T#TAX2         8  0     77
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR074TX
     A                                      SKIPB(059)
     A                                     2
     A                                      '<財會>'
     A                                    +0
     A                                      '主管:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '複核:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '入帳:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A*****************************************************************
     A          R AR074TM
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A*----------------------------------------------------------------
     A                                    19
     A                                      '*****無條件符合之資料！！'
