     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARR069S1
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     訂單別出貨及發票明細表挑選磅單列印
     A*            5.WRITER-DATE   104/07/01 2015AR938 S00WCJ
     A*            5.DATE-UPDATE
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束作業')
     A                                      CA07(07 '結束作業')
     A                                      CA12(12 '回前畫面')
     A*================================================================
     A          R AR069H
     A                                  1  2DATE
     A                                      EDTWRD('01  /  /  ')
     A                                  1 24'も　東和鋼鐵企業股份有限公司　も'
     A            S#SRID        10A  O  1 70
     A                                  2  2TIME
     A                                  2 25'★訂單別出貨發票明細表挑選磅單★'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3  2
     A  30        S#MODE         6A  O  3 70DSPATR(RI)
     A*================================================================
     A          R AR069F2                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPTX         1A  B  9  3VALUES(' ' 'Y')
     A            S#DATE         8  0O  9  6EDTWRD('    /  /  ')
     A*
     A            S#WTNO         8A  O    +1
     A            S#ORNO         6A  H
     A*
     A*================================================================
     A          R AR069F2C                  SFLCTL(AR069F2)
     A                                      SFLSIZ(0041)
     A                                      SFLPAG(0032)
     A                                      SFLLIN(03)
     A                                      ROLLUP(91 '下頁')
     A                                      CA09(09 '列印')
     A*                                     SFLDROP(CF04)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N73                                  SFLEND
     A                                      SFLCSRRRN(&S#CSR)
     A                                      OVERLAY
     A            S#CSR          5  0H
     A            S#NBR          4  0H      SFLRCDNBR(CURSOR)
     A                                  4  2'OP:Y列印報表'
     A            S#LIN1        78A  O  5  2
     A                                  6  3'OP'
     A                                  6  7'日期'
     A                                    +2'磅單編號'
     A                                  6 28'OP'
     A                                  6 32'日期'
     A                                    +1'磅單編號'
     A                                  6 53'OP'
     A                                  6 57'日期'
     A                                    +1'磅單編號'
     A            S#LIN2        78A  O  7  2
     A*================================================================
     A          R AR069F2M
     A                                      OVERLAY
     A            S#LIN3        78A  O 22  2
     A                                 23  2'F3=結束作業'
     A                                 23 16'F12=回前畫面'
     A                                    +1'F9=列印'
     A                                    +1'執行=存檔'
     A                                      DSPATR(RI)
     A            S#ERR         70A  O 24  2DSPATR(HI)
