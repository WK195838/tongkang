     A*%%TS  SD  19820726  160515  TPUSER      REL-V2R1M0  5738-PW1
     A*%%EC
     A*       UPDATE  DATE   102/01/25  2013AR724  S00WCJ (0201A)
     A*                      104/09/18  2015AR953  S00WCJ (0409A)
     A*                       產品代碼3碼擴5碼
     A*                      105/11/07  S00WCJ (0511A)
     A*                       單價顯示可SHOW 0.XX
     A*
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TRNDTL)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARE008S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'**帳款更正作業（磅差）**'
     A            DEVNM         10A  O  2 70
     A                                  5 30'功　　能:'
     A            CODE           1A  B  5 42
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A  43                                  DSPATR(PR)
     A                                  5 44'(2:修改 4:查詢)'
     A                                 11 30'磅單編號:'
     A            NO             8A  B 11 42
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A*%%TS  SD  19820726  160515  TPUSER      REL-V2R1M0  5738-PW1
     A  88                                  SFLNXTCHG
     A            ITEM           2Y 0O  9  3
0409AA            ACNT           1A  B  9  6
     A  70                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
0409AA            PDNM           5A  B  9  8
     A  70                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A            QTY            7Y 0B  9 14
     A  70                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                      EDTWRD(' ,   ,   -')
0511AA            PRC            7Y 5B  9 25EDTWRD('0 .     ')
     A  70                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A            AMT            9Y 0B  9 34
     A  70                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A                                      EDTWRD('   ,   ,   -')
9008 A            ORNO           9A  B  9 47
     A  70                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
9710 A            ACDT           8Y 0B  9 57EDTWRD('    /  /  ')
CLJ  A  70 77                               DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
9710 A            FLAG           1A  B  9 68
     A  58                                  DSPATR(RI)
     A  58                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            IVNO          10A  B  9 70
     A  59                                  DSPATR(RI)
     A  59                                  DSPATR(PC)
     A  70 71                               DSPATR(PR)
     A            CUNO           6A  B 10 14
     A  70                                  DSPATR(PR)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            CUNM          10A  O 10 25
     A            SATP           1A  H
     A            RVID           1A  H
     A            SALE           1A  H
     A            IVTP           1A  H
     A            HLD            1A  H
     A            HFD1           1A  H
     A            HFD2           1A  H
     A            VUNO           7A  H
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A*%%TS  SD  19820726  160515  TPUSER      REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0006)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A*-------------------------------------------------------------
     A                                  1 70'ARE008S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 28'**帳款更正作業（磅差）**'
     A            DEVNM         10A  O  2 70
     A                                  4  3'磅單編號:'
     A            NO        R        O  4 15REFFLD(TXNO)
     A                                  4 50'單據日期:'
9710 A            DATE           8Y 0O  4 62EDTWRD('    /  /  ')
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
0409AA                                  6  2'項種品'
0409AA                                  7  2'次類名　客戶編號'
     A                                  6 16'數　量'
     A                                  6 26'單價'
     A                                  6 35'金　　額'
     A                                  7 70'發票號碼'
     A                                  7 56'入帳日期　記'
     A                                  6 66'標'
     A                                  7 25'客戶名稱'
     A                                  6 47'訂單'
     A                                  7 47'編號'
     A***************************************************************
     A          R UND01
     A*%%TS  SD  19811001  084717  JEFF        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                 21  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22  3'合　計:'
     A            TTLQTY         7Y 0O 22 14EDTWRD(' ,   ,   -')
     A            TTLAMT         9Y 0O 22 32EDTWRD('   ,   ,   -')
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                 24 19'PF03:結束'
     A                                 24 32'PF10:存檔'
     A                                 24 45'PF12:回前畫面'
