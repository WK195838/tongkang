     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR036T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     未開立發票明細表
     A*           4.DATE-WRITTEN  89/04/05
     A*           5.WRITER        S02YSH
     A*           6.UPDATE DATE  101/10/25  2012AR695 S00WCJ (0110A)
     A*                          103/04/11  2014AR820 S00WCJ (0304A)
     A*                          104/09/30  2015AR953 S00WCJ (0409A)
     A*                           產品代碼3碼擴5碼
     A*
     A*
     A*****************************************************************
     A          R AR036TH
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A                                     2
     A                                      '報表代號: ARR036T'
     A                                    55
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   111
     A                                      '列表日期:'
     A                                   123
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            T#USID        10A       +1
     A                                    59
     A                                      '未開立發票明細表'
     A                                   111
     A                                      '列表時間:'
     A                                   123
     A                                      TIME
     A                                     2
     A                                      '銷貨年月:'
     A                                      SPACEB(001)
     A            T#YYMM         6  0     14
     A                                      EDTWRD('    /  ')
     A                                   111
     A                                      '頁　　次:'
     A                                   123
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '銷貨廠區:'
     A                                      SPACEB(001)
0304AA            T#BARA        10        13
     A            T#LIN1       132         1
     A                                      SPACEB(001)
     A                                     2
     A                                      '客戶編號'
     A                                      SPACEB(001)
     A                                    +2
     A                                      '客戶名稱'
     A                                    +2
     A                                      '訂單號碼'
     A                                    +2
     A                                      '磅單號碼'
     A                                    +2
     A                                      '出貨日期'
     A                                    +3
     A                                      '品　名'
     A                                    +2
     A                                      '單　價'
     A                                    +3
     A                                      '數　量'
     A                                   101
     A                                      '金　額'
     A                                   111
     A                                      '傳票號碼'
     A                                   123
     A                                      '備　　註'
     A            T#LIN2       132A        1
     A                                      SPACEB(001)
     A          R AR036T1
     A            T#CUNO         6A        4
     A                                      SPACEB(001)
     A            T#CUNM        10O       14
     A            T#ORNO         6A       +5
     A            T#IVNO         8A       +5
     A N43        T#DATE         8  0     +2
     A                                      EDTWRD('    /  /  ')
0409AA            T#PDNM         5A       64
     A            T#UPRC         5S 2     +5
     A                                      EDTCDE(J)
     A            T#QTY          7S 0     +4
     A                                      EDTCDE(J)
     A            T#AMT         11S 0     +3
     A                                      EDTCDE(J)
     A            T#VUNO         7A       +4
     A            T#IVTP        10A       +3
     A          R AR036T2
     A                                    68
     A                                      '磅單小計：'
     A                                      SPACEB(001)
     A            T#QTY1         8S 0     83
     A                                      EDTCDE(J)
     A            T#AMT1        12S 0     +2
     A                                      EDTCDE(J)
     A          R AR036T3
     A                                    68
     A                                      '訂單小計：'
     A                                      SPACEB(001)
     A            T#QTY2         8S 0     83
     A                                      EDTCDE(J)
     A            T#AMT2        12S 0     +2
     A                                      EDTCDE(J)
     A          R AR036T4
     A                                    68
     A                                      '客戶小計：'
     A                                      SPACEB(001)
     A            T#QTY3         9S 0     82
     A                                      EDTCDE(J)
     A            T#AMT3        13S 0     +0
     A                                      EDTCDE(J)
     A          R AR036T5
     A                                    68
     A                                      '廠區小計：'
     A                                      SPACEB(001)
     A            T#QTY4         9S 0     82
     A                                      EDTCDE(J)
     A            T#AMT4        13S 0     +0
     A                                      EDTCDE(J)
     A          R AR036T6
     A                                      SPACEB(0002)
     A                                    59'無相關資料!!!'
