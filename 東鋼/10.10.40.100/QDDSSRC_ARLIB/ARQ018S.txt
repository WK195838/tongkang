     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARQ018S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02LBU
     A*            4.FUNCTION     銷貨折讓發票資料查詢
     A*            5.DATE-WRITTEN  96/07/13
     A*            6.DATE-MODIFY
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前畫面')
     A*================================================================
     A          R TITLE
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 70'ARQ018S-1'
     A                                  1 26'東和鋼鐵企業股份有限公司'
     A                                  2 26'**銷貨折讓發票資料查詢** '
     A            S#DEVN        10A  O  2 70
     A*
     A                                  8 27'功　　能:'
     A            S#CODE         1A  B  8 39
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A                                  8 41'( 1:查詢2:列印) '
     A*
     A                                 10 27'發票號碼:'
     A            S#IVNO        10A  B 10 39
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A*
     A                                 12 27'年　　月:'
     A            S#YMS          6Y 0B 12 39EDTWRD('    /  ')
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A                                 12 49'~'
     A            S#YME          6Y 0B 12 53EDTWRD('    /  ')
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A                                 13 39'(列印時才需輸入日期)'
     A                                      COLOR(BLU)
     A*
     A            S#ERR         70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                    +1'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            S#OPT          1A  B  8  4
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A            S#IVNO        10A  O  8 10
     A            S#ACDT         8Y 0O  8 21EDTWRD('    /  /  ')
     A            S#TXAR         1A  O  8 34
     A            S#AMT          9Y 0O  8 42EDTCDE(J)
     A            S#TAX          9Y 0O  8 60EDTCDE(J)
     A            S#APNO         8A  H
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A                                      SFLSIZ(0025)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A  71                                  SFLCLR
     A N72                                  SFLDSP
     A N73                                  SFLDSPCTL
     A  74                                  SFLEND
     A            S#NBR          4S 0H      SFLRCDNBR(CURSOR)
     A*----------
     A                                  1 70'ARQ018S-2'
     A                                  1 26'東和鋼鐵企業股份有限公司'
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 26'**銷貨折讓發票資料查詢** '
     A*----------
     A                                  4  2'Ｙ：挑選資料'
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6  2'選項'
     A                                  6  9'發票號碼'
     A                                  6 22'入帳日期　廠區'
     A                                  6 40'金　　　　額'
     A                                  6 58'稅　　　　額'
     A***************************************************************
     A          R UND01
     A                                 21  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A*
     A                                 22 30'合計:'
     A            S#AMTT         9Y 0O 22 42EDTCDE(J)
     A            S#TAXT         9Y 0O 22 60EDTCDE(J)
     A*
     A            S#ERR         70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                    +1'PF03:結束'
     A                                    +1'PF12:回前畫面'
     A***************************************************************
     A          R DETAIL
     A                                      CF10(10 '存檔')
     A                                  1 70'ARQ018S-3'
     A                                  1 26'東和鋼鐵企業股份有限公司'
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#MOD          6A  O  2  2DSPATR(RI)
     A                                  2 26'**銷貨折讓發票資料查詢** '
     A            S#DEVN        10A  O  2 70
     A*----------
     A                                  5 24'　發票號碼:'
     A            S#IVNO        10A  O  5 38
     A*
     A                                  7 24'傳票入帳日:'
     A            S#YMD          8Y 0O  7 38EDTWRD('    /  /  ')
     A*
     A                                  9 24'　傳票號碼:'
     A            S#VRNO         6A  O  9 38
     A*
     A                                 11 24'　折讓金額:'
     A            S#AMT          9Y 0O 11 38EDTCDE(2)
     A*
     A                                 13 24'　折讓稅額:'
     A            S#TAX          9Y 0O 13 38EDTCDE(2)
     A*
     A                                 15 24'　折讓單號:'
     A            S#NO           8A  O 15 38
     A*
     A                                 17 24'　訂單號碼:'
     A            S#ORNO         6A  O 17 38
     A*
     A                                 19 24'　客戶名稱:'
     A            S#CUNM        10O  O 19 38
     A*
     A                                 21 24'　統一編號:'
     A            S#MUID         8A  O 21 38
     A*----------
     A            S#ERR         70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                 24 20'PF03:結束'
     A                                 24 34'PF10:存檔'
     A                                 24 48'PF12:回前畫面'
     A***************************************************************
