     A*%%TS  SD  19920821  142011  S02CSF      REL-V4R4M0  5769-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A*%%TS  SD  19810914  090744  VIN         REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARQ003S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'***預收貨款餘額查詢***'
     A            DEVNM         10A  O  2 70
     A                                  7 27'客戶編號:'
     A            CUST1     R        B  7 39REFFLD(RVCUNO RCVDTL)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A                                 10 27'訂單號碼:'
     A            ORNO1     R        B    +1REFFLD(ARORNO ARSTAT)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                    +1'-'
     A            ORNO2     R        B    +1REFFLD(ARORNO ARSTAT)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A*%%TS  SD  19811117  084317  VIN         REL-V2R1M1  5738-PW1
     A  88                                  SFLNXTCHG
     A            W01       R        O  9  2REFFLD(ARORNO ARSTAT)
9011 A            W02            9  0O  9  9EDTCDE(J)
 .   A            W03            8  0O  9 22EDTCDE(J)
 .   A            W04            8  0O  9 34EDTCDE(J)
 .   A            W07            8  0O  9 46EDTCDE(J)
 .   A            W05            8  0O  9 58EDTCDE(J)
 .   A            W06            8  0O  9 70EDTCDE(J)
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A*%%TS  SD  19811117  084317  VIN         REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      CF05(05)
     A                                      CF07(07)
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARQ003S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'***預收貨款餘額查詢***'
     A            DEVNM         10A  O  2 70
     A                                  3 27'客戶編號:'
     A            CUST1     R        O  3 39REFFLD(RVCUNO RCVDTL)
     A            CUSTNM    R        O  3 47REFFLD(RVCUNM RCVDTL)
     A                                  4 27'訂單號碼:'
     A            ORNO1     R        O  4 39REFFLD(ARORNO ARSTAT)
     A                                  4 45'-'
     A            ORNO2     R        O  4 47REFFLD(ARORNO ARSTAT)
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6  1'訂單'
     A                                  7  1'編號'
     A                                  7  9'預收金額'
     A                                  7 20'扣預收貨款'
     A                                  6 32'扣預收金額'
     A                                  7 34'沖　　回'
     A                                  6 45'預　　收'
     A                                  7 45'退回金額'
     A                                  6 57'預　　收'
     A                                  7 57'轉入金額'
     A                                  7 70'預收餘額'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A***************************************************************
     A          R UND01
     A*%%TS  SD  19920821  130050  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                 22  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  3'PF03:結束'
     A                                 23 15'PF12:回前畫面'
     A                                 23 31'PF05:以財會觀點查詢'
     A                                 23 54'PF07:查詢訂單明細'
     A***************************************************************
     A*         R AR003W1
     A*                                     WINDOW(10 30 6 24)
     A*                                     WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A*                                      (*CHAR '        '))
     A*                                 2  2'訂單編號:'
     A*           S#ORNW         6A  B  2 14
     A* 82                                  DSPATR(RI)
     A*                                 4  3'F3/F12:離開'
     A*                                     DSPATR(HI)
     A*           S#ERW2        20A  O  5  2DSPATR(HI)
CLJB A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARQ003R
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     使用CPRBAL 來查詢預收餘額
     A*            5.DATE-WRITTEN  86/03/11
     A*
     A***************************************************************
     A*  畫面三:切換以CPBRAL 來查詢預收餘額
     A*
     A*        記錄格式 AR003F3
     A***************************************************************
     A          R AR003F3                   SFL
9009 A            S#ORNO         6   O  9  2
     A            S#PBAL        11  0O    +1EDTCDE(J)
     A            S#AAMT        11  0O    +1EDTCDE(J)
     A            S#BAMT        11  0O    +1EDTCDE(J)
     A            S#NBAL        11  0O    +1EDTCDE(J)
     A***************************************************************
     A          R AR003F3C                  SFLCTL(AR003F3)
     A*%%TS  SD  19860311  165011  S02CLJ      REL-V2R3M0  5738-PW1
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A  71                                  SFLCLR
     A  72                                  SFLDSP
     A  73                                  SFLDSPCTL
     A  74                                  SFLEND
     A                                  1 70'ARQ003S-3'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 26'***財會觀點預收餘額查詢***'
     A            DEVNM         10A  O  2 70
     A                                  3 27'客戶編號:'
     A            CUST1     R        O  3 39REFFLD(RVCUNO RCVDTL)
     A            CUSTNM    R        O  3 47REFFLD(RVCUNM RCVDTL)
     A                                  4 27'訂單號碼:'
     A            ORNO1     R        O  4 39REFFLD(ARORNO ARSTAT)
     A                                  4 45'-'
     A            ORNO2     R        O  4 47REFFLD(ARORNO ARSTAT)
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6  1'訂單'
     A                                  7  1'編號'
     A                                  7 13'預收金額'
     A                                  7 29'本期增加'
     A                                  7 45'本期減少'
     A                                  7 61'本期餘額'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A***************************************************************
     A          R AR003F3M
     A*                                     OVERLAY
     A                                 22  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A                                      DSPATR(HI)
     A                                 24 12'PF03:結束'
CLJE A                                    +5'PF12:回前畫面'
     A***************************************************************
     A          R AR003F4                   SFL
     A*%%TS  SD  19920821  142011  S02CSF      REL-V4R4M0  5769-PW1
     A            S#ORNO         6A  O  8  4
     A            S#INNO        10A  O  8 13
     A            S#EAMT        11Y 0O  8 24EDTCDE(1)
     A            S#RCNO         6A  O  8 42
     A            S#RCDT         8Y 0O  8 53EDTWRD('    /  /  ')
     A            S#NTCD         8A  O  8 65
     A***************************************************************
     A          R AR003F4C                  SFLCTL(AR003F4)
     A*%%TS  SD  19920821  132055  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A  71                                  SFLCLR
     A  72                                  SFLDSP
     A  73                                  SFLDSPCTL
     A  74                                  SFLEND
     A                                  1 70'ARQ003S-4'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 26'***以訂單查詢預收發票明細***'
     A            DEVNM         10A  O  2 70
     A                                  3 27'客戶編號:'
     A            CUST1     R        O  3 39REFFLD(INCUNO INVMST)
     A            CUSTNM    R        O  3 47REFFLD(INCUNM INVMST)
     A                                  4 27'訂單號碼:'
     A            ORNO1     R        O  4 39REFFLD(INORNO INVMST)
     A                                  4 45'-'
     A            ORNO2     R        O  4 47REFFLD(INORNO INVMST)
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6  2'訂單編號'
     A                                  6 14'發票號碼'
     A                                  6 27'繳款金額'
     A                                  6 40'繳款單號'
     A                                  6 53'繳款日期'
     A                                  6 65'兌現碼'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A***************************************************************
     A          R AR003F4M
     A*                                     OVERLAY
     A                                 22  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A                                      DSPATR(HI)
     A                                 23 12'PF03:結束'
     A                                    +5'PF12:回前畫面'
