     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR099T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     應收帳款承購（E-FACTORING）繳款呈核表
     A*           4.DATE-WRITTEN  91/10/04
     A*           5.WRITER        S02LBU
     A*
     A*****************************************************************
     A          R AR099T1H
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     1
     A                                      '報表代號:   ARR099T'
     A                                    75
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   162
     A                                      '列表日期:'
     A                                   174
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     1
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            U#USID        10A  O    15
     A                                    76
     A                                      '應收帳款承購繳款呈核表'
     A                                   162
     A                                      '列表時間:'
     A                                   174
     A                                      TIME
     A*                                    1
     A*                                     '繳款建立日期:'
     A*                                     SPACEB(001)
     A*           R#RVDT         8S  O    18
     A*                                     EDTWRD('    /  /  ')
     A                                     1
     A                                      '輸入日期:'
     A                                      SPACEB(001)
     A*           R#CFDT         8S  O    13
     A            R#RVDT         8S  O    13
     A                                      EDTWRD('    /  /  ')
     A                                   162
     A                                      '頁　　次:'
     A                                   178
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*----------------------------------------------------------------
     A                                     1
     A                                      '財會手填'
     A                                      SPACEB(002)
     A                                    12
     A                                      '繳款'
     A                                    21
     A                                      '客戶'
     A                                    32
     A                                      '客戶'
     A                                    41
     A                                      '款'
     A                                    46
     A                                      '票'
     A                                    51
     A                                      '會計'
     A                                    60
     A                                      '科目'
     A                                    69
     A                                      '借'
     A                                   135
     A                                      '付款'
     A                                   160
     A                                      '存入'
     A                                   190
     A                                      '資金'
     A                                     1
     A                                      '傳票編號'
     A                                      SPACEB(001)
     A                                    12
     A                                      '單號'
     A                                    21
     A                                      '代號'
     A                                    32
     A                                      '名稱'
     A                                    41
     A                                      '項'
     A                                    46
     A                                      '據'
     A                                    51
     A                                      '科目'
     A                                    60
     A                                      '名稱'
     A                                    69
     A                                      '貸'
     A                                    76
     A                                      '銀行名稱'
     A                                    90
     A                                      '銀行帳號'
     A                                   101
     A                                      '相關號碼'
     A                                   126
     A                                      '到期日'
     A                                   136
     A                                      '地'
     A                                   145
     A                                      '票據金額'
     A                                   158
     A                                      '銀行帳號'
     A                                   176
     A                                      '備註'
     A                                   190
     A                                      '代號'
     A*----------------------------------------------------------------
     A                                     2
     A                                      '--------'
     A                                      SPACEB(001)
     A                                    11
     A                                      '--------'
     A                                    20
     A                                      '--------'
     A                                    30
     A                                      '----------'
     A                                    41
     A                                      '----'
     A                                    46
     A                                      '----'
     A                                    51
     A                                      '------'
     A                                    58
     A                                      '----------'
     A                                    69
     A                                      '----'
     A                                    74
     A                                      '--------------'
     A                                    90
     A                                      '----------'
     A                                   101
     A                                      '----------'
     A                                   126
     A                                      '--------'
     A                                   135
     A                                      '------'
     A                                   143
     A                                      '--------------'
     A                                   158
     A                                      '----------'
     A                                   169
     A                                      '--------------------'
     A                                   190
     A                                      '------'
     A*---------------------------------------------------------------
     A                                    32
     A                                      '發票日期'
     A                                      SPACEB(001)
     A                                    43
     A                                      '發票號碼'
     A                                    54
     A                                      '請款單號'
     A                                    66
     A                                      '繳款金額'
     A                                    82
     A                                      '發票日期'
     A                                    93
     A                                      '發票號碼'
     A                                   104
     A                                      '請款單號'
     A                                   116
     A                                      '繳款金額'
     A                                   132
     A                                      '發票日期'
     A                                   143
     A                                      '發票號碼'
     A                                   153
     A                                      '請款單號'
     A                                   166
     A                                      '繳款金額'
     A*
     A                                    32
     A                                      '----------'
     A                                      SPACEB(001)
     A                                    43
     A                                      '----------'
     A                                    55
     A                                      '-------'
     A                                    64
     A                                      '---------------'
     A                                    82
     A                                      '----------'
     A                                    93
     A                                      '----------'
     A                                   105
     A                                      '-------'
     A                                   114
     A                                      '---------------'
     A                                    +3
     A                                      '----------'
     A                                   143
     A                                      '----------'
     A                                   154
     A                                      '--------'
     A                                   164
     A                                      '---------------'
     A*
     A                                     2
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      ======='
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR099T1D
     A**************************************************************************
     A                                      SPACEA(001)
     A N50        R#RVNO         6A  O    12
     A N51        R#CUNO         6A  O    22
     A N52        R#CUNM        10O  O    30
     A            R#USTP         2A  O    42
     A            R#NTTP         1A  O    49
     A            R#ACNO         4A  O    52
     A            R#CHNM        10O  O    58
     A            R#ACTP         1A  O    71
     A            R#PBID        16O  O    73
     A            R#PANO        10A  O    90
     A            R#NTNO        20A  O   101
     A            R#DUDT         8S  O   124
     A                                      EDTWRD('    /  /  ')
     A            R#PLAC         1A  O   137
     A            R#RAMT        11S 0O   143
     A                                      EDTCDE(K)
     A            R#RESV        20A  O   169
     A**************************************************************************
     A          R AR099T1E
     A**************************************************************************
     A                                      SPACEA(001)
     A*---------------------------------------------------------------
     A            R#IND1         8S  O    32
     A                                      EDTWRD('    /  /  ')
     A            R#INN1        10A  O    43
     A            R#APN1         7A  O    55
     A            R#INX1        11S 0O    65
     A                                      EDTCDE(K)
     A            R#IND2         8S  O    82
     A                                      EDTWRD('    /  /  ')
     A            R#INN2        10A  O    93
     A            R#APN2         7A  O   105
     A            R#INX2        11S 0O   115
     A                                      EDTCDE(K)
     A            R#IND3         8S  O   132
     A                                      EDTWRD('    /  /  ')
     A            R#INN3        10A  O   143
     A            R#APN3         7A  O   155
     A            R#INX3        11S 0O   164
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR099T1S
     A**************************************************************************
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A                                    71
     A                                      '**繳款單小計:'
     A                                    96
     A                                      '票據金額：'
     A            R#SRNA        11S 0O   108
     A                                      EDTCDE(K)
     A                                   127
     A                                      '繳款金額：'
     A            R#SRXA        11S 0O   140
     A                                      EDTCDE(K)
     A*
     A                                     2
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --'
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR099T1T
     A**************************************************************************
     A                                      SPACEA(001)
     A                                    73
     A                                      '**本日合計:'
     A                                    96
     A                                      '票據金額：'
     A            R#TRNA        11S 0O   108
     A                                      EDTCDE(K)
     A                                   127
     A                                      '繳款金額：'
     A            R#TRXA        11S 0O   140
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR099T1X
     A**************************************************************************
     A                                      SKIPB(059)
     A                                     2
     A                                      '<財會>'
     A                                    11
     A                                      '主管:'
     A                                    19
     A                                      '            '
     A                                      UNDERLINE
     A                                    32
     A                                      '複核:'
     A                                    40
     A                                      '            '
     A                                      UNDERLINE
     A                                    53
     A                                      '出納:'
     A                                    61
     A                                      '            '
     A                                      UNDERLINE
     A                                    74
     A                                      '入帳:'
     A                                    82
     A                                      '            '
     A                                      UNDERLINE
     A                                    99
     A                                      '<營業>'
     A                                   108
     A                                      '主管:'
     A                                   116
     A                                      '            '
     A                                      UNDERLINE
     A                                   129
     A                                      '複核:'
     A                                   137
     A                                      '            '
     A                                      UNDERLINE
     A                                   150
     A                                      '繳款:'
     A                                   158
     A                                      '            '
     A                                      UNDERLINE
     A*****************************************************************
     A          R AR099T1L
     A*****************************************************************
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR099T1M
     A*****************************************************************
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A                                    19
     A                                      '*****無條件符合之資料！！'
     A**************************************************************************
