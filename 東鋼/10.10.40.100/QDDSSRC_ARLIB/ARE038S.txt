     A*%%TS  SD  19900707  110540  S02LYW      REL-V4R4M0  5769-PW1
     A*%%EC
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE038R
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     新版營業繳款查詢程式
     A*            5.DATE-WRITTEN  87/10/26
     A*            UPDATE  DATE  101/04/20  2012AR648  S00WCJ (0104A)
     A*                          101/09/19  2012AR682  S00WCJ (0109A)
     A*
     A*
     A***************************************************************
     A*  畫面一:主畫面
     A*
     A***************************************************************
     A          R AR038F1                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT1         1A  B  9  4VALUES(' ' '5' '6')
     A            S#RVNO         6A  O    +3
     A            S#RVDT         8Y 0O    +2EDTWRD('    /  /  ')
     A            S#CUNO         6A  O    +2
     A            S#CUNM        10O  O    +1
     A            S#NAMT        11Y 0O    +2EDTCDE(3)
     A            S#SALE         2A  O    +4
     A            S#SANM        10O  O    +1
     A***************************************************************
     A          R AR038F1C                  SFLCTL(AR038F1)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      CF06(06)
     A                                      CF12(12)
     A                                      CF17(17)
     A                                      CF18(18)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A            S#SFN1         4S 0H
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE038S-1'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 28'★已過財會營業繳款查詢★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  4  2'選項:'
     A                                  4 11'5:查詢繳款明細'
     A                                  4 30'6:查詢沖銷明細'
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6  2'選項'
     A                                  6  9'編號'
     A                                  6 19'日期'
     A                                  6 30'客　　戶'
     A                                  6 48'繳款金額'
     A                                  6 61'業務'
     A                                  7  2'-----------------------------------
     A                                      -------------+---+---+--------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#SFK1         6A  B  8  8
     A  30                                  DSPATR(PC)
0104AA            S#SFK2         8  0B    +2EDTWRD('    /  /  ')
0104AA            S#SFK3         6   B    +2
0104AA            S#SFK4         2   B  8 62
     A***************************************************************
     A          R AR038F1M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 21  2'-----------------------------------
     A                                      -------------+---+---+--------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22  4'F03:結束'
     A                                 22 18'F12:回前'
     A                                 22 32'F17:首筆'
     A                                 22 46'F18:末筆'
     A                                 22 60'F05:重新顯示'
     A                                 23 18'PGUP:上頁'
     A                                 23 32'PGDN:下頁'
     A                                 23 46'ENTER:執行'
     A            S#MSG1        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面二:查詢繳款明細畫面
     A***************************************************************
     A          R AR038F2                   SFL
     A  70                                  SFLNXTCHG
     A            S#SFI2         2Y 0O 12  2
     A            S#ACTP         1A  O 12  7
     A            S#USTP         2A  O 12 10
     A            S#NTTP         1A  O 12 14
0109AA            S#ACNO         6A  O 12 18
     A            S#PBID        14O  O 13 17
     A            S#PANO        10A  O 13 32
     A*           S#PLAC         1A  O 12 52
     A            S#NTNO        20A  O 12 26
     A            S#RAMT        10Y 0O 12 67EDTWRD(' ,   ,   ,  0')
     A            S#RLNO        10E  O 12 56DSPATR(UL)
     A            S#OLRV         6A  O 12 47
     A            S#SANO        10A  O 13 45DSPATR(UL)
     A            S#DUDT         8Y 0O 13 56EDTWRD('    /  /  ')
     A                                      DSPATR(UL)
     A            S#RESV        12O  O 13 68DSPATR(UL)
     A*
     A            S#SBID        10O  H
     A***************************************************************
     A          R AR038F2C                  SFLCTL(AR038F2)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A                                      CF01(01)
     A                                      CF03(03)
     A N33                                  CF10(10)
     A                                      CF12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE038S-2'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'★營業繳款明細查詢★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  4  8'繳款編號:'
     A            S#RVNO         6A  O  4 20
     A                                  4 31'繳款部門:'
     A            S#DPNO         4A  O  4 43
     A                                  4 52'收款業務員:'
     A            S#RVID         1A  O  4 66
     A                                  5  8'客戶編號:'
     A            S#CUNO         6A  O  5 20
     A                                  5 27'客戶名稱:'
     A            S#CUNM        10O  O  5 39
     A                                  5 54'繳款日期:'
     A            S#RVDT         8Y 0O  5 66EDTWRD('    /  /  ')
     A                                  6 50'繳款金額合計:'
     A            S#NAMT        11Y 0O  6 66EDTCDE(1)
     A                                      DSPATR(HI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  1'項　借　款　票'
     A*                                 8 51'付'
     A                                  9  1'　　貸　項　款　會計科目'
     A                                  9 31'票據號碼　　　原繳款單'
     A                                  9 57'相關號碼 金　　　額'
     A                                 10  1'次　方　別　別　付款銀行'
     A                                 10 31'付款帳戶'
     A                                 10 45'存入帳號'
     A                                 10 57'到期日期 保　留　碼'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR038F2M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  4'F01:說明'
     A                                 23 19'F03:結束'
     A                                 23 34'F12:回前'
     A                                 23 49'ENTER:執行'
     A            S#MSG2        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A          R AR038F2W
     A                                      WINDOW(8 14 14 62)
     A                                      TEXT('說明視窗')
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A                                  2  3'款項別'
     A                                      DSPATR(HI)
     A                                  2 18'票款別'
     A                                      DSPATR(HI)
     A                                  4  1'A1:一般貨款'
     A                                  4 16'A:支票'
     A                                  5  1'A2:預收貨款'
     A                                  5 16'B:背書支票'
     A                                  6  1'A3:延票利息'
     A                                  6 16'C:本票'
     A                                  7  1'A4:換票繳入'
     A                                  7 16'D:背書本票'
     A                                  8  1'A5:暫收款項'
     A                                  8 16'E:銀行本支票'
     A                                  9  1'Z5:其他'
     A                                  9 16'F:匯票'
     A                                 10 16'G:匯款'
     A                                 11 16'H:現金'
     A                                 12 16'I:信用狀'
     A                                 13 16'J:銀行存款'
     A                                  2 40'常用會計科目'
     A                                      DSPATR(HI)
     A                                  4 32'1111庫存現金'
     A                                  4 48'1112銀行存款'
     A                                  5 48'1131應收票據'
     A                                  8 32'2125應付費用'
     A                                  8 48'2159暫收其他'
     A                                 10 48'    　　　　'
     A                                  5 32'1114外幣存款'
     A                                  6 32'1134應收帳款'
     A                                  9 32'8149其他收益'
     A                                  9 48'8246奇零尾數'
     A                                  6 48'1137進項稅額'
     A                                 11 32'7111（推）運　費'
     A                                 12 32'7112（推）檢驗費'
     A                                 13 32'7142（推）郵電費'
0109AA***************************************************************
0109AA          R AR038F3W
0109AA                                      WINDOW(8 14 14 62)
0109AA                                      TEXT('說明視窗')
0109AA                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
0109AA                                       (*CHAR '        '))
0109AA                                      OVERLAY
0109AA                                      CF03(03)
0109AA                                      CF12(12)
0109AA                                  2  3'款項別'
0109AA                                      DSPATR(HI)
0109AA                                  2 17'票款別'
0109AA                                      DSPATR(HI)
0109AA                                  4  1'A1:一般貨款'
0109AA                                  4 15'A:支票'
0109AA                                  5  1'A2:預收貨款'
0109AA                                  5 15'B:背書支票'
0109AA                                  6  1'A3:延票利息'
0109AA                                  6 15'C:本票'
0109AA                                  7  1'A4:換票繳入'
0109AA                                  7 15'D:背書本票'
0109AA                                  8  1'A5:暫收款項'
0109AA                                  8 15'E:銀行本支票'
0109AA                                  9  1'Z5:其他'
0109AA                                  9 15'F:匯票'
0109AA                                 10 15'G:匯款'
0109AA                                 11 15'H:現金'
0109AA                                 12 15'I:信用狀'
0109AA                                 13 15'J:銀行存款'
0109AA                                  2 40'常用會計科目'
0109AA                                      DSPATR(HI)
0109AA                                  4 30'110101庫存現金'
0109AA                                  4 47'110102銀行存款'
0109AA                                  5 47'111001應收票據'
0109AA                                  8 30'210902應付費用'
0109AA                                  8 47'211301暫收其他'
0109AA                                 10 48'    　　　　'
0109AA                                  5 30'110104外幣存款'
0109AA                                  6 30'111101應收帳款'
0109AA                                  9 30'811210其他收益'
0109AA                                  9 47'821204奇零尾數'
0109AA                                  6 47'111407進項稅額'
0109AA                                 11 30'710101（推）運　費'
0109AA                                 12 30'710102（推）檢驗費'
0109AA                                 13 30'710402（推）郵電費'
     A***************************************************************
     A*  畫面三:發票沖銷畫面
     A***************************************************************
     A          R AR038F3                   SFL
     A*%%TS  SD  19900707  110540  S02LYW      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#SFI3         2Y 0O 11  3
     A            S#INNO        10A  O 11  7
     A            S#INOR         6A  O 11 19
     A            S#INAP         6A  O 11 27
     A            S#INAM        11Y 0O 11 34EDTCDE(3)
     A            S#INXA        11Y 0O 11 46EDTCDE(3)
     A            S#INBA        11Y 0O 11 58EDTCDE(3)
     A***************************************************************
     A          R AR038F3C                  SFLCTL(AR038F3)
     A*%%TS  SD  19900707  110540  S02LYW      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN3)
     A            S#NBR3         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN3         5S 0H
     A            S#SFN3         4S 0H
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE038S-3'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 28'★營業繳款發票沖銷查詢★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A                                  4  8'繳款編號:'
     A            S#RVNO         6A  O  4 20
     A                                  4 31'繳款部門:'
     A            S#DPNO         4A  O  4 43
     A                                  4 52'收款業務員:'
     A            S#RVID         1A  O  4 66
     A                                  5  8'客戶編號:'
     A            S#CUNO         6A  O  5 20
     A                                  5 27'客戶名稱:'
     A            S#CUNM        10O  O  5 39
     A                                  5 54'繳款日期:'
     A            S#RVDT         8Y 0O  5 66EDTWRD('    /  /  ')
     A                                  6 50'繳款金額合計:'
     A            S#NAMT        11Y 0O  6 66EDTCDE(1)
     A                                      DSPATR(HI)
     A                                  6  8'發票沖銷合計:'
     A            S#XAMT        11Y 0O  6 24EDTCDE(1)
     A                                      DSPATR(HI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9  7'發票號碼'
     A                                  9 18'訂單'
     A                                  9 26'請款單'
     A                                  9 36'發票金額'
     A                                  9 47'沖銷金額'
     A                                  9 58'發票餘額'
     A                                 10  2'---------------------------------+-
     A                                      ---+---+---+---+---+---+---+---+----
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR038F3M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 21  2'---------------------------------+-
     A                                      ---+---+---+---+---+---+---+---+----
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22  4'F03:結束'
     A                                 22 23'F12:回前'
     A                                 22 48'ENTER:執行'
     A            S#MSG3        70A  O 24  2DSPATR(HI)
