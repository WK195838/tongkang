     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARR129T
     A*            2.PROGRAM-TYPE  PRTF
     A*            3.AUTHOR        S02LBU
     A*            4.FUNCTION     銷貨調整單開立發票彙總表
     A*            5.DATE-WRITTEN  96/11/05
     A*            6.DATE-UPDATE   99/10/19  2010AR534 S00WCJ (9910A)
     A*                           101/01/03  S00WCJ (0101A)
     A*                            增加銷貨調整發票字樣
     A*
     A*****************************************************************
     A          R AR129T1H
     A**************************************************************************
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報　　表: ARR129T'
     A                                      SPACEB(001)
     A                                    58
     A                                      '東和鋼鐵股份有限公司'
     A                                   109
     A                                      '印表人員:'
     A            U#USID        10A      120
     A*=======
     A                                     2
     A                                      '開立日期:'
     A                                      SPACEB(001)
     A            T#DAT1         8  0     14
     A                                      EDTWRD('    /  /  ')
     A                                    25
     A                                      '~~'
     A            T#DAT2         8  0     27
     A                                      EDTWRD('    /  /  ')
     A                                    56
     A                                      '銷貨調整單開立發票彙總表'
     A                                   109
     A                                      '印表日期:'
     A                                   122
     A                                      DATE
     A                                      EDTCDE(Y)
     A*=======
     A                                     2
     A                                      '選　　項:'
     A                                      SPACEB(001)
     A            T#OPT          6        14
     A                                   109
     A                                      '頁　　數:'
     A                                   126
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*=======
     A            T#LIN1       129A  O     2
     A                                      SPACEB(001)
     A*=======
     A                                    37
     A                                      '台北'
     A                                      SPACEB(001)
     A                                    57
     A                                      '桃園'
     A                                    77
     A                                      '台中'
     A                                    98
     A                                      '高雄'
     A                                   115
     A                                      '品名小計'
     A*=======
     A            T#LIN2       129A  O     2
     A                                      SPACEB(001)
9910AA*****************************************************************
9910AA          R AR129T3D
9910AA**************************************************************************
     A            T#BLK          1A  O     2
     A                                      SPACEB(001)
     A            T#PDNO         3A  O     3
     A            T#PDNM        20O  O     6
9910AA*****************************************************************
9910AA          R AR129T1D
9910AA**************************************************************************
9910AA                                    25'小計：'
9910AA                                      SPACEB(001)
     A            T#AMD1        11S 0O    31
     A                                      EDTCDE(1)
     A            T#AMD2        11S 0O    51
     A                                      EDTCDE(1)
     A            T#AMD3        11S 0O    71
     A                                      EDTCDE(1)
     A            T#AMD4        11S 0O    91
     A                                      EDTCDE(1)
     A            T#AMT         11S 0O   111
     A                                      EDTCDE(1)
9910AA*****************************************************************
9910AA          R AR129T2D
     A**************************************************************************
9910AA            T#ARNM        10A  O    10
9910AA                                      SPACEB(001)
9910AA            T1AMD1        11S 0O    31
9910AA                                      EDTCDE(1)
9910AA            T1AMD2        11S 0O    51
9910AA                                      EDTCDE(1)
9910AA            T1AMD3        11S 0O    71
9910AA                                      EDTCDE(1)
9910AA            T1AMD4        11S 0O    91
9910AA                                      EDTCDE(1)
9910AA            T1AMT         11S 0O   111
9910AA                                      EDTCDE(1)
     A*****************************************************************
     A          R AR129T1T
     A*****************************************************************
     A                                     2
9910AA*                                     '各廠小計：'
9910AA*                                     '總計：'
0101AA                                      '銷貨調整發票總計:'
     A                                      SPACEB(002)
     A            T#AMT1        11S 0O    31
     A                                      EDTCDE(1)
     A            T#AMT2        11S 0O    51
     A                                      EDTCDE(1)
     A            T#AMT3        11S 0O    71
     A                                      EDTCDE(1)
     A            T#AMT4        11S 0O    91
     A                                      EDTCDE(1)
     A            T#AMTT        11S 0O   111
     A                                      EDTCDE(1)
     A*****************************************************************
     A          R AR129T1W
     A*****************************************************************
     A                                    10
     A                                      '主　　　管:'
     A                                      SPACEB(008)
     A                                    50
     A                                      '審　　　核:'
     A                                    90
     A                                      '製　　　表:'
     A*****************************************************************
     A          R AR129T1M
     A**************************************************************************
     A                                    19
     A                                      '*****無符合條件之資料!!!'
     A                                      SPACEB(008)
     A**************************************************************************
