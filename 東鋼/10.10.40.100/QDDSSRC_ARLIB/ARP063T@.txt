     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARP063T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     客戶寄放品之代開傳票（出貨）
     A*           4.DATE-WRITTEN  87/11/30
     A*           5.WRITER        S02CLJ
     A*
     A*****************************************************************
     A          R AR063T1H
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      ' PRT-ID  : ARP063T'
     A                                    63
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   130
     A                                      '列表日期:'
     A                                   141
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      ' USR-ID  :'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                   +43
     A                                      '客戶寄放品出貨清單'
     A                                   130
     A                                      '列表時間:'
     A                                   141
     A                                      TIME
     A                                     2
     A                                      '代開年月:'
     A                                      SPACEB(001)
     A
     A            U#YYMM         4S 0O    +1
     A                                      EDTWRD('  /  ')
     A                                   130
     A                                      '頁　　次:'
     A                                   141
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*---------------------------------------------------------------
     A                                     2
     A                                      '入帳日期'
     A                                      SPACEB(002)
     A                                    +2
     A                                      '訂單'
     A                                    +0
     A                                      '客戶'
     A                                    +1
     A                                      '借貸'
     A                                    +3
     A                                      '會計科目'
     A                                   +21
     A                                      '品　　名'
     A                                   +18
     A                                      '重量'
     A                                    +9
     A                                      '借方金額'
     A                                    +9
     A                                      '貸方金額'
     A                                    +1
     A                                      '傳票編號'
     A                                     3
     A                                      '----------'
     A                                      SPACEB(001)
     A                                    +1
     A                                      '-----'
     A                                    +1
     A                                      '----------'
     A                                    +1
     A                                      '-'
     A                                    +3
     A                                      '---------------------'
     A                                    +1
     A                                      '-------------------------------'
     A                                    +1
     A                                      '------------'
     A                                    +1
     A                                      '------------------'
     A                                    +1
     A                                      '------------------'
     A                                    +1
     A                                      '---------'
     A                                      SPACEA(001)
     A**************************************************************************
     A          R AR063T1D
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A            R#DATE         8S  O     3
     A                                      EDTWRD('    /  /  ')
     A            R#ORNO         5A  O    14
     A            R#CUNO        10A  O    +1
     A            R#ACDC         1A  O    +1
     A            R#ACNO         4A  O    +3
     A            R#ACNM        14A  O    +1
     A            R#TYCO         1A  O    +1
     A            R#PDNO         3A  O    +1
     A            R#PDNM        28A  O    +0
     A            R#WTNO         9S 0O    +1
     A                                      EDTCDE(K)
     A            R#AMTD        13S 2O    +1
     A                                      EDTCDE(K)
     A            R#AMTC        13S 2O    +1
     A                                      EDTCDE(K)
     A            R#FRAR         1A  O    +1
     A            R#TOAR         1A  O    +0
     A            R#VRNO         4A  O    +0
     A                                    +0
     A                                      '-'
     A            R#VRIT         2S  O    +0
     A**************************************************************************
     A          R AR063T1S
     A                                      SPACEB(002)
     A N45                                40'品名合計:'
     A            R#PCD1         3A       57
     A            R#QTY1         9S 0     89EDTCDE(J)
     A            R#AMT1        13S 2    102EDTCDE(J)
     A                                      SPACEA(001)
     A**************************************************************************
     A          R AR063T1T
     A                                      SPACEB(001)
     A                                    40
     A                                      '入帳筆數:'
     A            R#RCNT         3S  O    57
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR063T1X
     A                                      SKIPB(059)
     A                                     2
     A                                      '<財會>'
     A                                    +0
     A                                      '主管:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '複核:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '入帳:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A*****************************************************************
     A          R AR063T1M
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A*----------------------------------------------------------------
     A                                    19
     A                                      '*****無條件符合之資料！！'
