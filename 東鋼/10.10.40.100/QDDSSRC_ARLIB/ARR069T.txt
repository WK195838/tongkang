     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR069T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     訂單別發票及出貨明細表
     A*           4.DATE-WRITTEN  86/10/26
     A*           5.WRITER        S02CLJ
     A*           6.UPDATE DATE  104/07/03  2015AR938  S00WCJ (0407A)
     A*                           增加磅單選擇欄位
     A*                          104/10/01  2015AR953  S00WCJ (0409A)
     A*                           產品代碼3碼擴5碼
     A*                          108/10/08  S00WCJ (0810A)
     A*                           增加磅次欄位
     A*
     A*****************************************************************
     A          R AR069T1H
     A*****************************************************************
     A                                      SKIPB(003)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      ' PRT-ID  : ARR069T'
     A                                    55
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   +32
     A                                      '列表日期:'
     A                                   124
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      ' USR-ID  :'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                   +33
     A                                      '訂單別出貨及發票明細表'
     A                                   +33
     A                                      '列表時間:'
     A                                   124
     A                                      TIME
     A                                     2
     A                                      '統計期間:'
     A                                      SPACEB(001)
     A            R#YMDS         8S  O    14
     A                                      EDTWRD('    /  /  ')
     A                                    +1
     A                                      '-'
     A            R#YMDE         8S  O    +1
     A                                      EDTWRD('    /  /  ')
     A                                   113
     A                                      '頁　　次:'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '訂單號碼:'
     A                                      SPACEB(001)
     A            R#ORNO         6A  O    +1
     A                                    +4
     A                                      '客戶編號:'
     A            R#CUNO         6A  O    +1
     A            R#CUNM        10A  O    +1
     A  58                                +1'挑選磅單:'
     A  58        R#OPT          1A       +1
     A                                     2
     A                                      '日期'
     A                                      SPACEB(002)
     A                                    +0
     A                                      '磅單編號'
     A                                    +0
     A                                      '品名'
     A                                    +0
     A                                      '材質'
0810AA                                    +0
0810AA                                      '磅次'
0810AA                                    +0
     A                                      '徑度'
     A                                    +1
     A                                      '數量'
     A                                    +4
     A                                      '發票號碼'
     A                                    +1
     A                                      '出貨金額'
     A                                    +3
     A                                      '銷退折讓'
     A                                    +4
     A                                      '預收款'
     A                                    +6
     A                                      '稅額'
     A                                    +5
     A                                      '發票餘額'
     A                                    +1
     A                                      '繳款單號'
     A                                     2
     A                                      '------'
     A                                      SPACEB(001)
     A                                    +1
     A                                      '--------'
     A                                    +2
     A                                      '---'
     A                                    +1
     A                                      '--------'
0810AA                                    +1
0810AA                                      '-----'
     A                                    +1
     A                                      '-----'
     A                                    +1
     A                                      '------- '
     A                                    +2
     A                                      '----------'
     A                                    +1
     A                                      '-----------'
     A                                    +2
     A                                      '-----------'
     A                                    +2
     A                                      '-----------'
     A                                    +2
     A                                      '-----------'
     A                                    +2
     A                                      '-----------'
     A                                    +2
     A                                      '------'
     A**************************************************************************
     A          R AR069T1D
     A**************************************************************************
     A                                      SPACEA(001)
     A            R#DATE         8S  O     2
     A            R#WTNO         8A  O    +1
0409AA            R#PDNM         5A  O    +0
     A            R#QULT         8A  O    +0
0810AA            R#VRTM         2A  O    +1
     A            R#SPE1         5A  O    +1
     A            R#QTY          7S  O    +1
     A                                      EDTCDE(L)
0810AA            R#ERR          1A  O    51
     A            R#IVNO        10A  O    +1
     A            R#IVAM        11S  O    +1
     A                                      EDTCDE(M)
     A            R#AR06        11S  O    +1
     A                                      EDTCDE(M)
     A            R#PRRV        11S  O    +1
     A                                      EDTCDE(M)
     A            R#IVTX        11S  O    +1
     A                                      EDTCDE(M)
     A            R#IVAR        11S  O    +1
     A                                      EDTCDE(M)
     A            R#APNO         6A  O    +1
     A**************************************************************************
     A          R AR069T1S
     A**************************************************************************
     A                                      SPACEA(002)
     A                                     8
     A                                      '日期小計:'
0810AA            R#SQTY         8S  O   +22
     A                                      EDTCDE(M)
     A**************************************************************************
     A          R AR069T1T
     A**************************************************************************
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                     8
     A                                      '合計:    '
0810AA            R#TQTY         8S  O   +22
     A                                      EDTCDE(M)
0810AA            R#TVAM        11S  O    64
     A                                      EDTCDE(M)
0810AA            R#TR06        11S  O    77
     A                                      EDTCDE(M)
0810AA            R#TRRV        11S  O    90
     A                                      EDTCDE(M)
0810AA            R#TVTX        11S  O   103
     A                                      EDTCDE(M)
0810AA            R#TVAR        11S  O   116
     A                                      EDTCDE(M)
     A**************************************************************************
     A          R AR069T1X
     A**************************************************************************
     A                                      SPACEB(001)
0409AA            R#XPDN         5A  O    18
     A            R#XSPE         5A  O    23
     A            R#XNAM        40O  O    +2
     A            R#XQTY         8S  O    +2
     A                                      EDTCDE(M)
     A**************************************************************************
     A          R AR069T1Y
     A**************************************************************************
     A                                    61'-------------------'
     A                                      SPACEB(001)
     A                                    60'品名合計:'
     A                                      SPACEB(001)
     A            R#YQTY         8S  O    72
     A                                      EDTCDE(M)
     A*****************************************************************
     A          R AR069T1M
     A*****************************************************************
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A*----------------------------------------------------------------
     A                                    19
     A                                      '*****無條件符合之資料！！'
     A*****************************************************************
