     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARR068RS
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     訂單別繳款明細表
     A*            5.DATE-WRITTEN  86/07/29
     A*
     A***************************************************************
     A*  畫面一:輸入訂單
     A***************************************************************
     A          R AR068F1                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT1         1A  B 11  9VALUES('1' ' ')
     A            S#ORNO         5A  O 11 15
     A            S#ORDT         6S 0O 11 25
     A            S#CTNO        10A  O 11 34
     A            S#CUNO         6A  O 11 47
     A            S#CUNM        10A  O 11 55
     A***************************************************************
     A          R AR068F1C                  SFLCTL(AR068F1)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A            S#SFN1         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARR068S'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  2 27'訂單別繳款明細表選墿訂單'
     A                                  5  2'選項: 1 --> 列印該訂單之繳款明+
     A                                      細表'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  5'選項'
     A                                  8 25'日期'
     A                                  8 34'購案編號'
     A                                  8 12'訂單編號'
     A                                  8 47'客戶編號及名稱'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#ORN1         5A  B 10 15
     A  51                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A***************************************************************
     A          R AR068F1M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  8'F3:回主畫面'
     A                                 23 25'ENTER:完成輸入'
     A            S#MSG1        70A  O 24  2DSPATR(HI)
     A                                 23 45'PGUP:上一頁'
     A                                 23 62'PGDN:下一頁'
