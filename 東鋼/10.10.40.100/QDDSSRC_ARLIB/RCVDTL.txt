     A****************************************************************
     A*
     A*            UPDATE  DATE  98/10/15 2009AR357 S00WCJ (9810A)
     A*                         101/09/18 2012AR682 S00WCJ (0109A)
     A*                         107/05/16 2018AR1155 S00WCJ (0705A)
     A*                          增加部門代號
     A*
     A****************************************************************
     A                                      UNIQUE
     A          R RVREC                     TEXT('繳款明細檔')
     A            RVFLAG         1A         COLHDG('處理代碼')
     A            RVNO           6A         COLHDG('繳款編號')
     A            RVITEM         2S 0       COLHDG('項次')
     A            RVDATE         8S 0       COLHDG('繳款日期')
     A            RVCUNO         6A         COLHDG('客戶編號')
     A            RVCUNM        10O         COLHDG('客戶名稱')
     A            RVTYPE         1A         COLHDG('繳款類別')
     A            RVDPNO         4A         COLHDG('繳款部門')
     A            RVVNO1         7A         COLHDG('收入傳票編號')
     A            RVDECD         1A         COLHDG('刪除碼')
     A            RVDEDT         8S 0       COLHDG('刪除日期')
     A            RVUSTP         2A         COLHDG('款項別')
     A            RVNTTP         1A         COLHDG('票據別')
     A            RVPBID        14O         COLHDG('付款銀行')
     A            RVPANO        10A         COLHDG('付款帳號')
     A            RVACTP         1A         COLHDG('借貸別')
0109AA            RVACNO         9A         COLHDG('會計科目')
     A            RVRLNO        10A         COLHDG('相關號碼')
     A            RVPLAC         1A         COLHDG('付款地')
     A            RVNTNO        20A         COLHDG('票據號碼')
     A            RVDUDT         8S 0       COLHDG('到期日')
     A            RVRAMT        11P 0       COLHDG('收款金額')
     A            RVSBID        10A         COLHDG('存入銀行')
     A            RVSANO        10A         COLHDG('存入帳號')
     A            RVAMCD         4A         COLHDG('資金代號')
     A            RVWRNO         6A         COLHDG('退回單號')
     A            RVNTCD         1A         COLHDG('票據異動別')
     A            RVNTDT         8S 0       COLHDG('票據異動日')
     A            RVPRCD         1A         COLHDG('托收碼')
     A            RVPRDT         8S 0       COLHDG('托收日期')
     A            RVTBID        10A         COLHDG('托收銀行')
     A            RVTANO        10A         COLHDG('托收帳號')
     A            RVVNO2         7A         COLHDG('票據傳票號碼')
     A            RVTXAR         1A         COLHDG('異動廠區')
     A            RVTXDT         8S 0       COLHDG('異動日期')
     A            RVAREA         1A         COLHDG('出納別')
     A            RVRVID         2A         COLHDG('收款業務員')
     A            RVRESV        12A         COLHDG('保留碼')
     A*890420 擴檔,紀錄原繳款單號及兌現日期
     A            RVOLRV         6A         COLHDG('原繳款單號')
     A            RVOLDT         8S 0       COLHDG('原兌現日期')
     A            RVRES2        12A         COLHDG('保留碼2')
     A*
     A*保留碼二，第一碼Y，表示資料上傳電子押匯介面檔
     A*保留碼二，第二碼Y，表示沖銷發票與上傳電子押匯之發票有重覆
     A*
9810AA            RVCURR         3A         COLHDG('原幣幣別')
9810AA            RVCAMT        13S 2       COLHDG('原幣金額')
0705AA            RVDEPT         4A         COLHDG('部門代號')
     A*
     A          K RVNO
     A          K RVITEM
