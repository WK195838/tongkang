     A*%%TS  SD  19910308  135010  S02YSH      REL-V4R4M0  5769-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARP090S
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02YSH
     A*            4.FUNCTION     現金流入預估處理(含人工調整)
     A*            5.DATE-WRITTEN  91/03/08
     A*
     A***************************************************************
     A                                      CF03(03)
     A                                      CF12(12)
     A          R AR090F1                   OVERLAY
     A*%%TS  SD  19910308  135010  S02YSH      REL-V4R4M0  5769-PW1
     A                                  1 70'ARP090S-1'
     A                                  1 27'東和鋼鐵企業股份有限公司'
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 26'現金流入預估處理－條件設定'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A            S#USER        10A  O  3 70
     A                                  8 27'預估產品別:'
     A            S#PROD         1A  B  8 41VALUES(' ' '2' '3' '4')
     A                                  8 43'(2:鋼筋3:型鋼＋鋼板 4:全部)'
     A                                 10 25'預估流入日期:'
     A            S#DATS         6Y 0B 10 41EDTCDE(Y)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                 10 50'∼'
     A            S#DATE         6Y 0B 10 55EDTCDE(Y)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                 12 27'調整參考別:'
     A            S#SLC1         1A  B 12 41VALUES('1' '2')
     A                                 12 43'(1:客戶別 2:業務員別)'
     A                                 14 25'調整參考年月:'
     A            S#YMS          4Y 0B 14 41EDTCDE(Y)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                 14 47'∼'
     A            S#YME          4Y 0B 14 52EDTCDE(Y)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                 15 41'(統計實際平均應收帳款帳齡)'
     A                                 18 15'1.統計區間是以繳款年月為準。'
     A                                      DSPATR(HI)
     A                                 19 15'2.區間內若有相關資料，則會依所選+
     A                                      定之參考別統計出建議調整天數。'
     A                                      DSPATR(HI)
     A                                 20 15'3.區間內若無相關資料，則依合約所+
     A                                      訂之付款條件預估之。'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR090F1M                  OVERLAY
     A                                 22  3'F3/F12:結束'
     A                                      DSPATR(RI)
     A            S#ERR1        70A  O 23  2DSPATR(HI)
     A*****************************************************************
     A          R AR090FH                   OVERLAY
     A                                  1 70'ARP090S-2'
     A                                  1 29'東和鋼鐵股份有限公司'
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 26'現金流入預估處理－調整設定'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A            S#USER        10A  O  3 70
     A                                  4 27'調整參考別:'
     A            S#SLC2         8A  O  4 41
     A            S#MODE        10A  O  4 70DSPATR(RI)
     A                                  5 25'調整參考年月:'
     A            S#YMS          4Y 0O  5 41EDTCDE(Y)
     A                                  5 47'∼'
     A            S#YME          4Y 0O  5 52EDTCDE(Y)
     A*****************************************************************
     A          R AR090F2                   SFL
     A*%%TS  SD  19910308  112038  S02YSH      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT1         1Y 0B 11  3EDTCDE(Z)
     A                                      VALUES(0 2)
     A            S#COD1         6A  O 11  7
     A            S#CONM        10A  O 11 16
     A            S#DAY1         4Y 0O 11 30EDTCDE(J)
     A            S#DAY2         4Y 0B 11 41EDTCDE(J)
     A  33                                  DSPATR(PC RI)
     A*****************************************************************
     A          R AR090F2C                  SFLCTL(AR090F2)
     A*%%TS  SD  19910308  112038  S02YSH      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      CF05(05 '快速調整')
     A                                      CF10(10 '存檔')
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  74                                  SFLCLR
     A                                      OVERLAY
     A                                  7  2'OP: 2=明細調整'
     A            S#LIN1        78A  O  8  2
     A                                  9  2'OP'
     A  32                              9  5'客戶代號'
     A N32                              9  5'業務員別'
     A                                  9 16'代號名稱'
     A                                  9 27'建議天數'
     A                                  9 38'調整天數'
     A            S#LIN2        78A  O 10  2
     A            S#NBR2         4S 0H      SFLRCDNBR
     A*****************************************************************
     A          R AR090F2M
     A                                      OVERLAY
     A            S#LIN3        78   O 22  2
     A                                 23  2'F3=結束作業'
     A                                    +1'F12=回前畫面'
     A                                    +1'F5=快速調整'
     A                                    +1'F10=存檔列印'
     A                                      DSPATR(RI)
     A            S#ERR2        78   O 24  2DSPATR(HI)
     A*****************************************************************
     A          R AR090F3                   SFL
     A*%%TS  SD  19910308  115346  S02YSH      REL-V4R4M0  5769-PW1
     A  75                                  SFLNXTCHG
     A            S#COD2         6A  O 10  6
     A            S#NO          10A  O 10 16
     A            S#DATE         6Y 0O 10 29EDTCDE(Y)
     A            S#DAT1         6Y 0O 10 40EDTCDE(Y)
     A            S#DAT2         6Y 0B 10 51EDTCDE(Y)
     A*================================================================
     A          R AR090F3C                  SFLCTL(AR090F3)
     A*%%TS  SD  19910308  115346  S02YSH      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      CF05(05 '快速調整')
     A                                      CF10(10 '存檔')
     A N76                                  SFLDSPCTL
     A N77                                  SFLDSP
     A N78                                  SFLEND
     A  79                                  SFLCLR
     A                                      OVERLAY
     A            S#LIN1        78A  O  7  2
     A                                  8  4'單據類別'
     A                                  8 16'單據號碼'
     A                                  8 28'單據日期'
     A                                  8 39'預計兌現'
     A                                  8 50'調整兌現'
     A            S#LIN2        78A  O  9  2
     A            S#NBR3         4S 0H      SFLRCDNBR
     A*================================================================
     A          R AR090F3M
     A                                      OVERLAY
     A            S#LIN3        78   O 22  2
     A                                 23  2'F3=結束作業'
     A                                    +1'F12=回前畫面'
     A                                    +1'F5=快速調整'
     A                                    +1'F10=存檔'
     A                                      DSPATR(RI)
     A            S#ERR3        78   O 24  2DSPATR(HI)
