     A*%%TS  SD  19910327  140141  S02LYW      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE049S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02LYW
     A*            4.FUNCTION     承購客戶沖銷作業
     A*            5.DATE-WRITTEN  91/02/18
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '離開')
     A                                      CA12(12 '回上頁')
     A*================================================================
     A          R AR049H
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 24'も　東和鋼鐵企業股份有限公司　も-
     A                                      '
     A            S#SRID        10A  O  1 70
     A                                  2  2TIME
     A                                  2 30'★承購客戶沖銷作業★'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3  2
     A  30        S#MODE         6A  O  3 70
     A                                      DSPATR(RI)
     A*================================================================
     A          R AR049F1
     A*%%TS  SD  19910218  164735  S02LYW      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                 10 30'客戶代號：'
     A            S#CUNO         6A  B    +1
     A  41                                  DSPATR(PC)
     A                                 23  2'F3/F12=離開'
     A            S#ERR         70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR049F2                   SFL
     A*%%TS  SD  19910320  164331  S02LYW      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OP           1Y 0B 10  2EDTCDE(Z)
     A                                      VALUES(0 2 4 5 6 7)
     A  48                                  DSPATR(PC RI)
     A            S#RCNO         6A  O 10  6
     A            S#ENDT         8Y 0O 10 15EDTWRD('    /  /  ')
     A            S#TAMT         9Y 0O 10 28EDTCDE(1)
     A            S#FL01         1A  O 10 46
     A            S#CDA1         1A  O 10 57
     A            S#CDB1         1A  O 10 66
     A            S#CLOX         1A  O 10 75
     A*================================================================
     A          R AR049F2C                  SFLCTL(AR049F2)
     A*%%TS  SD  19910320  164331  S02LYW      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(91 '下頁')
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  74                                  SFLCLR
     A                                      CA06(06 '新增')
     A  81                                  CF11(11 '刪除')
     A                                      OVERLAY
     A                                  4  2'客戶代號:'
     A            S#CUNO         6A  O  4 15
     A            S#CUNM        10A  O    +1
     A                                  5  2'OP:2=修改4=刪除'
     A                                  5 22'5=查詢6=確認7=確認還原'
     A            S#LIN1        78A  O  6  2
     A                                  7  2'OP'
     A                                  7  5'沖銷單號'
     A                                  7 16'沖銷日期'
     A                                  7 29'沖銷金額'
     A                                  7 41'營業確認碼'
     A                                  7 54'下載碼'
     A                                  7 63'清算碼'
     A                                  7 72'結案碼'
     A            S#LIN2        78A  O  8  2
     A            S#KEY1         6A  B  9  6
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR049F2M
     A                                      OVERLAY
     A            S#LIN3        78A    22  2
     A                                 23  2'F3=離開'
     A                                    +1'F12=回上頁'
     A                                    +1'PA1=下頁'
     A                                    +1'F6=新增'
     A                                      DSPATR(RI)
     A            S#ERR2        70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR049F3                   SFL
     A*%%TS  SD  19910327  104712  S02LYW      REL-V4R4M0  5769-PW1
     A  75                                  SFLNXTCHG
     A            S#ITEM         2S 0O 12  3
     A            S#APN1         6A  O 12  7
     A            S#INNO        10A  B 12 14
     A N84                                  DSPATR(PR)
     A  83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A            S#INDT         8Y 0O 12 26EDTWRD('    /  /  ')
     A            S#AMT1         9Y 0O 12 39EDTCDE(1)
     A            S#AMT2         9Y 0O 12 53EDTCDE(1)
     A            S#AMT3         9Y 0B 12 67EDTCDE(1)
     A N84                                  DSPATR(PR)
     A  90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A*================================================================
     A          R AR049F3C                  SFLCTL(AR049F3)
     A*%%TS  SD  19910327  140141  S02LYW      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0010)
     A N76                                  SFLDSPCTL
     A N77                                  SFLDSP
     A N78                                  SFLEND
     A  79                                  SFLCLR
     A                                      CF07(07 '依承購單載入')
     A                                      CF08(08 '快速沖銷')
     A                                      CF10(10 '存檔')
     A                                      OVERLAY
     A                                  4  2'客戶代號:'
     A            S#CUNO         6A  O  4 14
     A            S#CUNM        10A  O  4 21
     A                                  5  2'沖銷單號:'
     A            S#RCNO         6A  O  5 14
     A                                  5 25'沖銷日期:'
     A            S#ENDT         8Y 0B  5 37EDTWRD('    /  /  ')
     A N84                                  DSPATR(PR)
     A  91                                  DSPATR(PC)
     A  91                                  DSPATR(RI)
     A                                  6  4'到期日:'
     A            S#DUDT         8Y 0B  6 14EDTWRD('    /  /  ')
     A N84                                  DSPATR(PR)
     A  85                                  DSPATR(PC)
     A  85                                  DSPATR(RI)
     A                                  6 25'入帳日期:'
     A            S#ACDT         8Y 0B  6 37EDTWRD('    /  /  ')
     A N84                                  DSPATR(PR)
     A  86                                  DSPATR(PC)
     A  86                                  DSPATR(RI)
     A                                  6 48'票據金額:'
     A            S#RAMT        12Y 0B  6 60EDTCDE(1)
     A N84                                  DSPATR(PR)
     A  87                                  DSPATR(PC)
     A  87                                  DSPATR(RI)
     A                                  7  4'票款別:'
     A            S#NTTP         1A  B  7 14
     A N84                                  DSPATR(PR)
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A                                  7 25'相關號碼:'
     A            S#RLNO        10A  B  7 37
     A N84                                  DSPATR(PR)
     A  89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A                                  7 50'兌現碼:'
     A            S#NTCD         1A  O  7 60
     A            S#LIN4        78A  O  8  2
     A                                  9  2'項'
     A                                  9  7'承購'
     A                                  9 16'發票'
     A                                  9 28'發票'
     A            S#AMTT         9Y 0O  9 67EDTCDE(2)
     A  92                                  DSPATR(RI)
     A                                 10  2'次'
     A                                 10  7'單號'
     A                                 10 16'號碼'
     A                                 10 28'日期'
     A                                 10 40'發票金額'
     A                                 10 54'發票餘額'
     A                                 10 68'沖銷金額'
     A            S#LIN5        78A  O 11  2
     A            S#NBR3         4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR049F3M
     A                                      OVERLAY
     A            S#LIN6        78A    22  2
     A                                 23  2'F3=離開'
     A                                    +1'F12=回上頁'
     A                                    +1'PA1=下頁'
     A  80                                +1'F10=存檔'
     A  81                                +1'F7=依承購單載入'
     A  81                                +1'F8=快速沖銷'
     A            S#ERR3        70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR049W1
     A*%%TS  SD  19910312  160642  S02YSH      REL-V4R4M0  5769-PW1
     A                                      WINDOW(8 44 8 24)
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                  2  5'*請款單載入*'
     A                                  4  2'請款單號:'
     A            S#APNO         6A  B  4 14
     A  82                                  DSPATR(RI)
     A                                  6  3'F3/F12:離開'
     A                                      DSPATR(HI)
     A            S#ERRW        20A  O  7  2DSPATR(HI)
