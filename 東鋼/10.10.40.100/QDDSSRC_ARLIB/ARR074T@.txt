     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR074T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     鋼筋型鋼廠別應收明細表
     A*           4.DATE-WRITTEN  87/09/01
     A*           5.WRITER        S02CLJ
     A*
     A*
     A*****************************************************************
     A          R AR074T1H
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報表代號: ARR074T'
     A                                    48
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   101
     A                                      '列表日期:'
     A                                   112
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A N92                                50
     A                                      '鋼筋廠區別應收明細表'
     A  92                                50
     A                                      '型鋼廠區別應收明細表'
     A                                   101
     A                                      '列表時間:'
     A                                   112
     A                                      TIME
     A                                     2
     A                                      '統計年月：'
     A                                      SPACEB(001)
     A            R#YYMM         6S  O    +0
     A                                      EDTWRD('    /  ')
     A                                   101
     A                                      '頁　　次:'
     A                                   112
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*----------------------------------------------------------------
     A                                     2
     A                                      '客戶編號名稱'
     A                                      SPACEB(001)
     A                                    18
     A                                      '訂單'
     A                                    +1
     A                                      '台北應收帳款'
     A                                    +2
     A                                      '台中應收帳款'
     A                                    +2
     A                                      '桃園應收帳款'
     A                                    +2
     A                                      '苗栗應收帳款'
     A                                    +2
     A                                      '高雄應收帳款'
     A                                    +2
     A                                      '應收帳款合計'
     A*----------------------------------------------------------------
     A                                     2
     A                                      '----------------'
     A                                      SPACEB(001)
     A                                    +1
     A                                      '-----'
     A                                    +1
     A                                      '---------------'
     A                                    +1
     A                                      '---------------'
     A                                    +1
     A                                      '---------------'
     A                                    +1
     A                                      '---------------'
     A                                    +1
     A                                      '---------------'
     A                                    +1
     A                                      '==============='
     A**************************************************************************
     A          R AR074T1D
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A            R#CUNO         6A  O     2
     A            R#CUNM        10O  O     8
     A            R#ORNO         5A  O    19
     A            R#DAMP        11S 0O    25
     A                                      EDTCDE(K)
     A            R#DAMM        11S 0O    41
     A                                      EDTCDE(K)
     A            R#DAMT        11S 0O    57
     A                                      EDTCDE(K)
     A            R#DAMH        11S 0O    73
     A                                      EDTCDE(K)
     A            R#DAMK        11S 0O    89
     A                                      EDTCDE(K)
     A            R#DAMX        11S 0O   105
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR074T1S
     A                                      SPACEA(002)
     A*----------------------------------------------------------------
     A                                    12
     A                                      '客戶小計：'
     A            R#SAMP        11S 0O    25
     A                                      EDTCDE(K)
     A            R#SAMM        11S 0O    41
     A                                      EDTCDE(K)
     A            R#SAMT        11S 0O    57
     A                                      EDTCDE(K)
     A            R#SAMH        11S 0O    73
     A                                      EDTCDE(K)
     A            R#SAMK        11S 0O    89
     A                                      EDTCDE(K)
     A            R#SAMX        11S 0O   105
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR074T1T
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A                                    14
     A                                      '合計：'
     A            R#TAMP        11S 0O    25
     A                                      EDTCDE(K)
     A            R#TAMM        11S 0O    41
     A                                      EDTCDE(K)
     A            R#TAMT        11S 0O    57
     A                                      EDTCDE(K)
     A            R#TAMH        11S 0O    73
     A                                      EDTCDE(K)
     A            R#TAMK        11S 0O    89
     A                                      EDTCDE(K)
     A            R#TAMX        11S 0O   105
     A                                      EDTCDE(K)
     A*****************************************************************
     A          R AR074T1M
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A                                    19
     A                                      '*****無條件符合之資料！！'
     A**************************************************************************
