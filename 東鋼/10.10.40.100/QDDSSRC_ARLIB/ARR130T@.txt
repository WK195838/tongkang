     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR130T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.WRITER        S02LBU
     A*           4.FUNCTION     已開發票押匯狀況彙總表
     A*           5.DATE-WRITTEN  96/11/14
     A*           6.DATE-MODIFY
     A*
     A*
     A**************************************************************************
     A          R AR130T1H
     A**************************************************************************
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報　　表:ARR130T'
     A                                    40
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    73
     A                                      '列表日期:'
     A                                    84
     A                                      DATE
     A                                      EDTCDE(Y)
     A*
     A                                     2
     A                                      '發票日期:'
     A                                      SPACEB(001)
     A            T#DATS         8  0O    13
     A                                      EDTWRD('    /  /  ')
     A                                    24
     A                                      '~'
     A            T#DATE         8  0O    25
     A                                      EDTWRD('    /  /  ')
     A                                    41
     A                                      '已開發票押匯狀況彙總表'
     A                                    73
     A                                      '列表時間:'
     A                                    84
     A                                      TIME
     A*
     A                                     2
     A                                      '業　　務:'
     A                                      SPACEB(001)
     A            T#RVNM        10   O    13
     A                                    73
     A                                      '頁　　次:'
     A                                    88
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*
     A            T#LIN1        90         2
     A                                      SPACEB(001)
     A*----------
     A                                     2
     A                                      '客戶代號'
     A                                      SPACEB(001)
     A                                    15
     A                                      '客戶簡稱'
     A                                    28
     A                                      '已開立發票金額'
     A                                    48
     A                                      '已押匯（繳款）金額'
     A                                    72
     A                                      '未押匯（繳款）金額'
     A*----------
     A            T#LIN2        90         2
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR130T1D
     A*****************************************************************
     A            T#CUNO         6A        4
     A                                      SPACEB(001)
     A            T#CUNM        10O       15
     A            T#AAMT        11  0     29
     A                                      EDTCDE(1)
     A            T#BAMT        11  0     53
     A                                      EDTCDE(1)
     A            T#CAMT        11  0     77
     A                                      EDTCDE(1)
     A*****************************************************************
     A          R AR130T1T
     A*****************************************************************
     A            T#RVID         2        15
     A                                      SPACEB(001)
     A                                    17
     A                                      '小計：'
     A            T#A1MT        13  0     26
     A                                      EDTCDE(1)
     A            T#B1MT        13  0     50
     A                                      EDTCDE(1)
     A            T#C1MT        13  0     74
     A                                      EDTCDE(1)
     A                                      SPACEA(001)
     A*****************************************************************
     A          R AR130T2T
     A*****************************************************************
     A                                    15
     A                                      '合　計：'
     A                                      SPACEB(002)
     A            T#A2MT        13  0     26
     A                                      EDTCDE(1)
     A            T#B2MT        13  0     50
     A                                      EDTCDE(1)
     A            T#C2MT        13  0     74
     A                                      EDTCDE(1)
     A*****************************************************************
     A          R AR130T1M
     A*****************************************************************
     A                                    45
     A                                      '無符合之資料!!! '
     A                                      SPACEB(008)
     A*****************************************************************
