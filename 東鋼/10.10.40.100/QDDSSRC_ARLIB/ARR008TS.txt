     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR008T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.WRITER        S02LBU
     A*           4.FUNCTION     外銷應收帳款明細表
     A*           5.DATE-WRITTEN  96/01/26
     A*           6.DATE-MODIFY
     A*
     A*
     A**************************************************************************
     A          R AR008T1H
     A**************************************************************************
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報　表:ARR008T'
     A                                    41
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    92
     A                                      '列表日期:'
     A                                   103
     A                                      DATE
     A                                      EDTCDE(Y)
     A*
     A                                     2
     A                                      '客戶編號:'
     A                                      SPACEB(001)
     A            T#CUNS         6A  O    13
     A                                    19
     A                                      '至'
     A            T#CUNE         6A  O    23
     A                                    44
     A                                      '外銷應收帳款明細表'
     A                                    92
     A                                      '列表時間:'
     A                                   103
     A                                      TIME
     A*
     A                                     2
     A                                      '部門代號:'
     A                                      SPACEB(001)
     A            T#DPNO         6A  O    13
     A                                    92
     A                                      '頁　　次:'
     A                                   103
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*
     A            T#LIN1       110         2
     A                                      SPACEB(001)
     A*----------
     A                                     2
     A                                      '客戶編號'
     A                                      SPACEB(001)
     A                                    13
     A                                      '客戶名稱'
     A                                    24
     A                                      '業務員'
     A                                    33
     A                                      '訂單號碼'
     A                                    44
     A                                      '繳款編號'
     A                                    59
     A                                      '應收貨款'
     A                                    75
     A                                      '已收金額'
     A                                    91
     A                                      '應收帳款'
     A*----------
     A            T#LIN2       110         2
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR008T1D
     A*****************************************************************
     A            T#CUNO         6A        4
     A                                      SPACEB(001)
     A            T#CUNM        10O       13
     A            T#SALE         2A       27
     A            T#ORNO         6A       35
     A            T#RVNO         6A       46
     A            T#AMT1        10S 0     55
     A                                      EDTCDE(1)
     A            T#AMT2        10S 0     71
     A                                      EDTCDE(J)
     A            T#AMT3        10S 0     87
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR008T1S
     A*****************************************************************
     A            T#LIN3        63        38
     A                                      SPACEB(001)
     A                                    40
     A                                      '客戶小計:'
     A                                      SPACEB(001)
     A            T#SAM1        10S 0     55
     A                                      EDTCDE(1)
     A            T#SAM2        10S 0     71
     A                                      EDTCDE(J)
     A            T#SAM3        10S 0     87
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR008T1T
     A*****************************************************************
     A            T#LIN4        63        38
     A                                      SPACEB(001)
     A                                    38
     A                                      '部門總合計:'
     A                                      SPACEB(001)
     A            T#TAM1        10S 0     55
     A                                      EDTCDE(1)
     A            T#TAM2        10S 0     71
     A                                      EDTCDE(J)
     A            T#TAM3        10S 0     87
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR008T1M
     A*****************************************************************
     A                                    45
     A                                      '無符合之資料!!! '
     A                                      SPACEB(008)
     A*****************************************************************
