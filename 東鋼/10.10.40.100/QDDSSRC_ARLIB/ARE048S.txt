     A*%%TS  SD  19901109  093442  S02YSH      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE048S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02YSH
     A*            4.FUNCTION     承購發票維護作業
     A*            5.DATE-WRITTEN  90/11/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束作業')
     A                                      CA12(12 '回前畫面')
     A*================================================================
     A          R AR048H
     A*%%TS  SD  19901105  134343  S02YSH      REL-V4R4M0  5769-PW1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 26'も東和鋼鐵企業股份有限公司も'
     A            S#SRID        10A  O  1 70
     A                                  2  2TIME
     A                                  2 30'★承購發票維護作業★'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3  2
     A  30        S#MODE         6A  O  3 70DSPATR(RI)
     A*================================================================
     A          R AR048F1
     A                                      OVERLAY
     A*%%TS  SD  19901105  135237  S02YSH      REL-V4R4M0  5769-PW1
     A                                  9 20'銀行代號:'
     A            S#BANK         3A  B  9 32
     A  36                                  DSPATR(PC RI)
     A                                    +1'(不可空白)'
     A                                 11 20'客戶代號:'
     A            S#CUN1         6A  B 11 32
     A                                 11 39'∼'
     A            S#CUN2         6A  B 11 44
     A                                 11 51'(空白表全部)'
     A                                 13 22'結案碼:'
     A            S#CODE         1A  B 13 32VALUES(' ' 'Y' 'N')
     A                                 13 36'(N :未結案'
     A                                 13 49'Y :已結案'
     A                                 13 61'空白:全部)'
     A                                 23  2'F3/F12 :離開'
     A            S#ERR         70   O 24  2DSPATR(HI)
     A*================================================================
     A          R AR048F2                   SFL
     A*%%TS  SD  19901105  134116  S02YSH      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT2         1Y 0B 12  2EDTCDE(Z)
     A                                      VALUES(0 2 4 5 6 7 8)
     A  32                                  DSPATR(PC RI)
     A            S#CUNO         6A  O 12  4
     A            S#CUNM        10A  O 12 11
     A            S#INDT         6Y 0O 12 22
     A            S#INNO        10A  O 12 29
     A            S#DAM8         8Y 0O 12 40EDTCDE(1)
     A            S#EAM8         8Y 0O 12 51EDTCDE(1)
     A            S#FAM8         8Y 0O 12 62EDTCDE(1)
     A            S#DLC1         1A  O 12 74
     A            S#APCD         1A  O 12 76
     A            S#CLOC         1A  O 12 78
     A*================================================================
     A          R AR048F2C                  SFLCTL(AR048F2)
     A*%%TS  SD  19901105  134343  S02YSH      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(91 '下頁')
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  74                                  SFLCLR
     A                                      OVERLAY
     A                                  4  2'銀行代號:'
     A            S#BANK         3A  O  4 16
     A            S#BANM        10A  O    +2
     A                                  5  2'客戶代號:'
     A            S#CUN1         6A  O  5 16
     A                                  5 23'∼'
     A            S#CUN2         6A  O  5 28
     A                                  6  2'OP: 2=修改4=刪除5=查詢6=承+
     A                                      購確認7=沖銷8=結案'
     A            S#LIN1        78A  O  7  2
     A                                  8  2'O'
     A                                  8  9'客戶'
     A                                  8 22'發票'
     A                                  8 29'發票號碼'
     A                                  8 44'承購'
     A                                  8 55'預支'
     A                                  8 66'沖銷下確結'
     A                                  9  2'P'
     A                                  9  5'代號'
     A                                  9 13'簡稱'
     A                                  9 22'日期'
     A                                  9 44'金額'
     A                                  9 55'金額'
     A                                  9 66'金額載認案'
     A            S#LIN2        78A  O 10  2
     A            S#KEY1        10A  B 11 29
     A  31                                  DSPATR(PC)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR048F2M
     A                                      OVERLAY
     A            S#LIN3        78   O 22  2
     A                                 23  2'F3=結束作業'
     A                                    +1'F12=回前畫面'
     A                                    +1'PA1=下頁'
     A                                    +1'PA2=上頁'
     A            S#ERR2        70   O 24  2DSPATR(HI)
     A*================================================================
     A          R AR048F3
     A*%%TS  SD  19901105  144645  S02YSH      REL-V4R4M0  5769-PW1
     A  81                                  CF11(11 '刪除')
     A  80                                  CF10(10 '存檔')
     A                                      OVERLAY
     A                                  5  2'客戶代號:'
     A            S#CUNO         6A  O  5 14
     A                                  5 21'客戶簡稱:'
     A            S#CUNM        10A  O  5 33
     A                                  6  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                  7  2'發票號碼:'
     A            S#INNO        10A  O  7 14
     A                                  7 25'發票聯式:'
     A            S#INTP         1A  O  7 37
     A                                  7 39'發票日期:'
     A            S#INDT         6Y 0O  7 51EDTCDE(Y)
     A                                  8  2'訂單號碼:'
     A            S#ORNO         6A  O  8 14
     A                                  8 25'開立廠區:'
     A            S#AREA         1A  O  8 37
     A                                  8 39'請款單號:'
     A            S#APN2         6A  O  8 51
     A                                  8 59'請款日期:'
     A            S#APD2         6Y 0O  8 71EDTCDE(Y)
     A                                 10  2'銷貨金額:'
     A            S#AAMT        12Y 0O 10 14EDTCDE(1)
     A                                 10 30'銷貨稅額:'
     A            S#ATAX         9Y 0O 10 42EDTCDE(1)
     A                                 11  2'折讓金額:'
     A            S#BAMT        12Y 0O 11 14EDTCDE(1)
     A                                 11 30'折讓稅額:'
     A            S#BTAX         9Y 0O 11 42EDTCDE(1)
     A                                 12  2'發票金額:'
     A            S#DAMT        12Y 0O 12 14EDTCDE(1)
     A                                 13  2'預支金額:'
     A            S#EAMT        12Y 0O 13 14EDTCDE(1)
     A                                 13 30'沖銷金額:'
     A            S#FAMT        12Y 0O 13 42EDTCDE(1)
     A                                 15  2'承購確認:'
     A            S#APCD         1A  B 15 14VALUES(' ' 'Y')
     A  82                                  DSPATR(PR)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A                                 15 16'承購單號:'
     A            S#APN1         6A  O 15 28
     A                                 15 39'承購日期:'
     A            S#APD1         6Y 0O 15 51EDTCDE(Y)
     A                                 16  2'承購下載:'
     A            S#DLC1         1A  B 16 14
     A  82                                  DSPATR(PR)
     A                                 16 16'下載人員:'
     A            S#DLM1        10A  O 16 28
     A                                 16 39'下載日期:'
     A            S#DLD1         6Y 0O 16 51EDTCDE(Y)
     A                                 17  2'承購沖銷:'
     A            S#CHKC         1A  O 17 14
     A                                 17 16'沖銷人員:'
     A            S#CHKM        10A  O 17 28
     A                                 17 39'沖銷日期:'
     A            S#CHKD         6Y 0O 17 51EDTCDE(Y)
     A                                 18  2'沖銷下載:'
     A            S#DLC2         1A  B 18 14
     A  82                                  DSPATR(PR)
     A                                 18 16'下載人員:'
     A            S#DLM2        10A  O 18 28
     A                                 18 39'下載日期:'
     A            S#DLD2         6Y 0O 18 51EDTCDE(Y)
     A                                 19  2'手續費傳票號碼:'
     A            S#ACN1         7A  O 19 20
     A                                 19 33'手續費代開日期:'
     A            S#DAT1         6Y 0O 19 51EDTCDE(Y)
     A                                 20  2'結　　案:'
     A            S#CLOC         1A  B 20 14
     A  82                                  DSPATR(PR)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A                                 20 16'結案人員:'
     A            S#CLOM        10A  O 20 28
     A                                 20 39'結案日期:'
     A            S#CLOD         6Y 0O 20 51EDTCDE(Y)
     A                                 21  2'異動人員:'
     A            S#UPDM        10A  O 21 14
     A                                 21 26'異動日期:'
     A            S#UPDD         6Y 0O 21 38EDTCDE(Y)
     A                                 21 47'異動時間:'
     A            S#UPDT         6Y 0O 21 59EDTWRD('  :  :  ')
     A                                 22  2'==================================-
     A                                      ===================================-
     A                                      ========='
     A                                 23  2'F3=結束作業'
     A                                 23 16'F12=回前畫面'
     A  80                             23 31'F10=存檔'
     A                                      DSPATR(RI)
     A  81                             23 31'F11=刪除確認'
     A                                      DSPATR(RI)
     A            S#ERR3        70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR048F4                   SFL
     A*%%TS  SD  19901109  093442  S02YSH      REL-V4R4M0  5769-PW1
     A  75                                  SFLNXTCHG
     A            S#OPT4         1Y 0B 12  3EDTCDE(4)
     A                                      VALUES(0 4)
     A  83                                  DSPATR(PR)
     A            S#CVDT         6Y 0O 12  6EDTCDE(Y)
     A            S#TAMT        12Y 0O 12 15EDTCDE(1)
     A            S#RLN1        10A  O 12 33
     A            S#RLN2        10A  O 12 47
     A            S#ITEM         3S 0H
     A*================================================================
     A          R AR048F4C                  SFLCTL(AR048F4)
     A*%%TS  SD  19901109  093442  S02YSH      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CF10(10 '存檔')
     A                                      ROLLUP(91 '下頁')
     A N76                                  SFLDSPCTL
     A N77                                  SFLDSP
     A N78                                  SFLEND
     A  79                                  SFLCLR
     A                                      OVERLAY
     A                                  4  2'銀行代號:'
     A            S#BANK         3A  O  4 15
     A            S#BANM        10A  O  4 19
     A                                  5  2'客戶代號:'
     A            S#CUNO         6A  O  5 15
     A            S#CUNM        10A  O  5 22
     A                                  6  2'發票號碼:'
     A            S#INNO        10A  O  6 15
     A                                  6 27'可沖銷金額：'
     A            S#AMT         12Y 0O  6 42EDTCDE(J)
     A                                  7  2'OP: 4=刪除'
     A            S#LIN4        78A  O  8  2
     A                                  9  2'OP'
     A                                  9  5'沖銷日期'
     A                                  9 20'沖銷金額'
     A                                  9 32'相關號碼一'
     A                                  9 46'相關號碼二'
     A            S#LIN5        78A  O 10  2
     A            S#TAM1        12Y 0B 11 18EDTCDE(4)
     A                                      DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  83                                  DSPATR(PR)
     A            S#RLNX        10A  B 11 33
     A  35                                  DSPATR(RI)
     A  83                                  DSPATR(PR)
     A            S#RLNY        10A  B 11 47
     A  35                                  DSPATR(RI)
     A  83                                  DSPATR(PR)
     A                                 11 59'<-- 此列僅用於新增'
     A            S#NBR4         4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR048F4M
     A                                      OVERLAY
     A            S#LIN6        78   O 22  2
     A                                 23  2'F3=結束作業'
     A                                    +1'F12=回前畫面'
     A                                    +1'PA1=下頁'
     A                                    +1'PA2=上頁'
     A                                    +1'F10=存檔'
     A            S#ERR4        70   O 24  2DSPATR(HI)
