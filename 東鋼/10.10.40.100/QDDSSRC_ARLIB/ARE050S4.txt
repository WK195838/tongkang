     A*%%TS  SD  20021213  160801  S02YSH      REL-V5R3M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE050S
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02YSH
     A*            4.FUNCTION     進倉履約折讓自動處理作業
     A*            5.DATE-WRITTEN  91/02/20
     A*                           102/05/29 2013AR750 S00WCJ (0205A)
     A*                            增加入帳年月
     A*                           102/12/13 S00WCJ (0212A)
     A*                            增加挑選發票開立折讓單功能
     A*
     A***************************************************************
     A          R AR050H
     A*%%TS  SD  19910220  134451  S02YSH      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 23'も　東和鋼鐵企業股份有限公司　も-
     A                                      '
     A            S#SRID        10A  O  1 70
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 29'銷貨折讓自動處理作業-挑選發票'
     A            S#DEVI        10A  O  2 70
     A***************************************************************
     A          R AR050F2                   SFL
     A*%%TS  SD  19920624  111906  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT          1Y 0B 11  2VALUES(0 5)
     A                                      EDTCDE(4)
     A            S#CUNO         6A  O 11  5
     A            S#CUNM        10A  O 11 12
     A            S#ORNO         6A  O 11 24
     A            S#YYMM         6Y 0O 11 31EDTCDE(4)
     A            S#AREA         1A  O 11 40
     A            S#QTY1         7Y 0O 11 44EDTCDE(1)
     A            S#AMT1         9Y 0O 11 55EDTCDE(1)
     A            S#AMT2         9Y 0O 11 68EDTCDE(1)
     A            S#AMT4         4Y 3B 12  6EDTCDE(1)
     A  31                                  DSPATR(PR)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A            S#AMT5         9Y 0O 12 13EDTCDE(1)
     A            S#AMT3         9Y 0O 12 25EDTCDE(1)
     A            S#COD4         1A  O 12 39
     A            S#COD1         1A  B 12 43VALUES(' ' '1' '2')
     A  31                                  DSPATR(PR)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            S#COD2         1A  B 12 46VALUES(' ' 'Y')
     A  31                                  DSPATR(PR)
     A            S#ACYM         6Y 0B 12 48EDTCDE(4)
     A  31                                  DSPATR(PR)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A            S#COD3         1A  O 12 57
     A            S#QTY6         6Y 0O 12 61EDTCDE(1)
     A                                      DSPATR(HI)
     A            S#AMT6         8Y 0O 12 69EDTCDE(J)
     A                                      DSPATR(HI)
     A            H#SALE         2A  H
     A            H#AREA         2A  H
     A            H#CONT         3S 0H
     A***************************************************************
     A          R AR050F2C                  SFLCTL(AR050F2)
     A*%%TS  SD  19920624  111718  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  74                                  SFLCLR
     A                                      CA03(03 '離開')
     A                                      CA12(12 '回上頁')
     A                                      CF10(10 '存檔')
     A                                      CF06(06 '確認開立')
     A                                  4  2'業務員別:'
     A            S#SAL2         6A  O  4 14
     A                                  4 21'客戶代號:'
     A            S#CUN1         6A  O  4 33
     A            S#CUM1        10A  O  4 40
     A                                  4 62'第'
     A            S#CONT         3Y 0O  4 67EDTCDE(4)
     A                                      DSPATR(HI)
     A                                  4 71'次處理'
     A                                  5  2'處理年月:'
     A            S#YM3          6A  O  5 14
     A                                  5 21'訂單號碼:'
     A            S#ORN2         6A  O  5 33
     A                                  5 40'產品類別:'
     A            S#PRO2         6A  O  5 52
     A                                  5 62'廠區別:'
     A            S#ARE2         8A  O  5 72
     A                                  6  2'OP: 5=查詢開立明細'
     A                                  6 23'(方式代碼: 1=扣抵2=開票)'
     A                                  6 54'(進倉折扣: Y=折扣)'
     A            S#LIN1        78A  O  7  2
     A                                  8  2'O'
     A                                  8  4'客戶代號　　簡稱'
     A                                  8 23'訂單'
     A                                  8 31'年月'
     A                                  8 38'廠區'
     A                                  8 46'總數量'
     A                                  8 59'總金額'
     A                                  8 70'其他折讓'
     A                                  9  2'P'
     A                                  9  4'折讓單價'
     A                                  9 15'折讓總價'
     A                                  9 26'繳款金額'
     A                                  9 37'扣　方　確　入帳　開'
     A                                  9 60'加價數量　加價金額'
     A            S#LIN2        78A  O 10  2
     A            S#NBR2         4S 0H      SFLRCDNBR
     A*****************************************************************
     A          R AR050F2M
     A*%%TS  SD  19910222  151325  S02YSH      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A            S#LIN3        78A  O 21  2
     A                                 22 20'H廠合計:'
     A            S#HAMT        10Y 0O    +1EDTCDE(1)
     A                                 22 50'K廠合計:'
     A            S#KAMT        10Y 0O    +1EDTCDE(1)
     A                                 23  2'F3:離開'
     A            S#ERR2        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A          R AR050F3                   SFL
     A*%%TS  SD  20021213  160801  S02YSH      REL-V5R3M0  5722-WDS
     A  81                                  SFLNXTCHG
     A            S#CHIO         1A  B  9  3VALUES(' ' 'Y' 'N')
     A            S#INNO        10A  O  9  8
     A            S#AREX         1A  O  9 21
     A            S#PROD         3A  O  9 27
     A            S#QTYX         8Y 0O  9 33EDTCDE(4)
     A            S#AM1X         7Y 0O  9 44EDTCDE(1)
     A            S#AM2X         7Y 0O  9 56EDTCDE(1)
     A            S#TXNO         6A  O  9 69
     A            S#TXIT         2Y 0O  9 76EDTCDE(4)
     A***************************************************************
     A          R AR050F3C                  SFLCTL(AR050F3)
     A*%%TS  SD  19910313  112526  S02YSH      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A N76                                  SFLDSPCTL
     A N77                                  SFLDSP
     A N78                                  SFLEND
     A  79                                  SFLCLR
     A                                      CA03(03 '離開')
     A                                      CF10(10 '存檔')
     A                                      CA12(12 '回上頁')
     A                                  4  2'業務員別:'
     A            S#SAL3         6A  O  4 14
     A                                  4 21'客戶代號:'
     A            S#CUN3         6A  O  4 33
     A            S#CUM3        10A  O  4 40
     A                                  4 62'第'
     A            S#CONT         3Y 0O    +1EDTCDE(4)
     A                                      DSPATR(HI)
     A                                    +1'次處理'
     A                                  5  2'處理年月:'
     A            S#YM3          6A  O  5 14
     A                                  5 21'訂單號碼:'
     A            S#ORN3         6A  O  5 33
     A                                  5 40'產品類別:'
     A                                  5 62'廠區別:'
     A            S#ARE3         8A  O  5 72
     A            S#PRO3         6A  O  5 52
     A            S#LIN1        78A  O  6  2
     A                                  7  2'挑　　發票號碼'
     A                                  7 19'廠區'
     A                                  7 26'類別'
     A                                  7 36'數量'
     A                                  7 44'折讓金額'
     A                                  7 56'折讓稅額'
     A                                  7 68'折讓單號'
     A            S#LIN2        78A  O  8  2
     A            S#NBR3         4S 0H      SFLRCDNBR
     A*****************************************************************
     A          R AR050F3M
     A                                      OVERLAY
     A            S#LIN3        78A  O 22  2
     A                                 23  2'F3:離開'
     A                                    +1'F12:回上頁'
     A                                    +1'F10:存檔'
     A            S#ERR3        70A    24  2DSPATR(HI)
