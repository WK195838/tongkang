     A*%%TS  SD  19811116  144737  JEFF        REL-V2R1M1  5738-PW1
     A*%%EC
     A*       UPDATE  DATE  104/09/18  2015AR953  S00WCJ (0409A)
     A*                      產品代碼3碼擴5碼
     A*
     A***************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/INVDTL)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARE010S-1'
     A            COMP          35   O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'** 隔月換發票處理作業 **'
     A            DEVNM         10   O  2 70
     A                                  9 30'發票號碼:'
     A            NO            10   B    +1
     A  42                                  DSPATR(PC RI)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SFLREC                    SFL
     A*%%TS  SD  19811030  093920  JEFF        REL-V2R1M1  5738-PW1
     A  88                                  SFLNXTCHG
     A            ITEM      R        O 11  3REFFLD(IVITEM)
0409AA            ACNT      R        O 11  7REFFLD(IVACNT)
0409AA            PDNM      R        O 11 11REFFLD(IVPDCD)
     A            QTY            7Y 0O 11 17EDTWRD(' ,   ,   -')
     A            UPRC           5Y 3O 11 29EDTWRD('  .   ')
     A            AMT            9Y 0O 11 37EDTWRD('   ,   ,   -')
     A            ACDT           6Y 0O 11 51EDTWRD('  /  /  ')
     A            APNO      R        O 11 61REFFLD(IVAPNO)
     A            ACNO      R        O 11 71REFFLD(IVACNO)
     A***************************************************************
     A          R SFLCTL                    SFLCTL(SFLREC)
     A*%%TS  SD  19811116  144737  JEFF        REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0090)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARE010S-2'
     A            COMP          35A  O  1 24
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'** 隔月換發票處理作業 **'
     A            DEVNM         10A  O  2 70
     A                                  3  5'發票號碼:'
     A            NO            10A  O  3 17
     A                                  4  5'客戶編號:'
     A            CUNO           6A  O  4 17
     A                                  4 24'客戶名稱:'
     A            CUNM          10J  O  4 36
     A                                  4 48'銷售別　:'
     A            TYPE           1A  O  4 60
     A                                  5  5'訂單編號:'
     A            ORNO           6A  O  5 17
     A                                  5 24'業務員　:'
     A            SALE           1A  O  5 36
     A                                  5 48'發票別　:'
     A            CHK            1A  O  5 60
     A                                  4 63'收款別　:'
     A            RVID           1A  O  4 75
     A                                  6  5'發票種類:'
     A            TYP1           1A  O  6 17
     A                                  6 19'(1:出貨2:預收3:調整9:其他)-
     A                                      '
     A                                  5 63'開立廠區:'
     A            AREA           1A  O  5 75
     A                                  7  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  2'項　種'
     A                                  9  2'目　類'
     A                                  9 11'品名'
     A                                  9 19'數　量'
     A                                  9 30'單價'
     A                                  9 37'　金　　額'
     A                                  9 50'入帳日期憑證編號傳票編號'
     A                                 10  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6 63'發票已作廢'
     A N74                                  DSPATR(ND)
     A  74                                  DSPATR(RI BL)
     A***************************************************************
     A          R ERRFMT
     A*%%TS  SD  19811030  093920  JEFF        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                 21  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22 25'***總計:'
     A            TTLAMT         9Y 0O 22 37EDTCDE(J)
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 19'PF03:結束'
     A                                 24 32'PF10:存檔'
     A                                 24 45'PF12:回前畫面'
