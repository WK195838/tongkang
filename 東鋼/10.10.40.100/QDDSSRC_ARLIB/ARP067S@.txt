     A*%%TS  SD  19890412  101315  S02YSH      REL-V3R7M0  5716-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARP067RS
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     發票區間開立作業（畫面）
     A*            5.DATE-WRITTEN  88/01/31
     A*            6.UPDATE BY     S02YSH 89/04/12 (加訂單編號)
     A*
     A***************************************************************
     A*  畫面一:輸入廠區及發票日期
     A*
     A*        記錄格式 AR067F1
     A***************************************************************
     A          R AR067F1
     A*%%TS  SD  19890412  101315  S02YSH      REL-V3R7M0  5716-PW1
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      CF10(10)
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARP065S'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'★發票區間開立作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A                                  5 20'開立廠別:'
     A            S#AREA         1A  B  5 32
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A                                  5 35'( P台北 M台中 T桃園  H苗栗-
     A                                       K高雄)'
     A                                  7 20'發票日期:'
     A            S#DATE         8Y 0B  7 32EDTWRD('    /  /  ')
     A  52                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                  7 44'(本月五日前可重開上月底的發票)'
     A                                  9 20'銷貨區間:'
     A            S#TXD1         8Y 0B  9 32EDTWRD('    /  /  ')
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A                                  9 44'至'
     A            S#TXD2         8Y 0B  9 50EDTWRD('    /  /  ')
     A                                  9 61'（挑日期區間內）'
     A                                 11 20'訂單編號:'
     A            S#ORNO         6A  B 11 32
     A                                 11 39'(空白表全部)'
     A                                 13  5'-----------------------------------
     A                                      ------------------------------------
     A                                      -----'
     A                                 14 14'字軌'
     A                                 14 26'起號'
     A                                 14 40'迄號'
     A                                 14 54'目前'
     A                                 14 66'可用張數'
     A                                 16  5'二聯式'
     A            S#PRE2         2A  O 16 16
     A            S#STN2         8S 0O 16 25
     A            S#ENN2         8S 0O 16 39
     A            S#CUN2         8S 0O 16 53
     A            S#LEV2         8Y 0O 16 67EDTCDE(4)
     A                                 18  5'三聯式'
     A            S#PRE3         2A  O 18 16
     A            S#STN3         8S 0O 18 25
     A            S#ENN3         8S 0O 18 39
     A            S#CUN3         8S 0O 18 53
     A            S#LEV3         8Y 0O 18 67EDTCDE(4)
     A                                 19  5'-----------------------------------
     A                                      ------------------------------------
     A                                      -----'
     A                                 20 20'發票已開立日期:'
     A            S#XDAT         8Y 0O 20 38EDTWRD('    /  /  ')
     A                                 22 12'F3:結束'
     A                                 22 26'F12:回前'
     A                                 22 41'ENTER:檢核'
     A                                 22 58'F10:執行'
     A                                      DSPATR(RI)
     A            S#MSG1        70A  O 24  2DSPATR(HI)
