     H*****************************************************************
     H*
     H*            *****   PROGRAM INTRODUCTION   *****
     H*
     H*            1.PROGRAM-ID    ARDSDTG
     H*            2.PROGRAM-TYPE  PF
     H*            3.AUTHOR        S00WCJ
     H*            4.FUNCTION     折讓折扣磅單明細分攤檔
     H*            5.DATE-WRITTEN  105/10/22
     H*            6.UPDATE BY
     H*
     H*****************************************************************
     A*                                     UNIQUE
     A          R RARDSDTG                  TEXT('折讓折扣磅單明細分攤檔')
     A            TGFLAG         1A         COLHDG('處理代碼')
     A            TGKIND         1A         COLHDG('分攤類別')
     A*                                     K=折扣；D=折讓
     A            TGCUNO         6A         COLHDG('客戶代號')
     A            TGCUNM        10O         COLHDG('客戶名稱')
     A            TGORNO         6A         COLHDG('訂單編號')
     A            TGYYMM         6S 0       COLHDG('處理年月')
     A            TGCONT         3S 0       COLHDG('處理次數')
     A            TGTYPE         1A         COLHDG('發票聯式')
     A            TGSALE         2A         COLHDG('業務員別')
     A            TGAREA         1A         COLHDG('廠區別')
0510AA            TGAMT4         5S 3       COLHDG('折讓單價／公斤')
     A            TGINNO        10A         COLHDG('發票號碼')
     A            TGACNT         1A         COLHDG('發票類別')
     A            TGITEM         2S 0       COLHDG('發票項次')
     A            TGINDT         8S 0       COLHDG('發票日期')
     A            TGDECD         1A         COLHDG('發票作廢碼')
     A            TGDEDT         8S 0       COLHDG('發票作廢日期')
     A            TGTXNO         8A         COLHDG('磅單編號')
     A            TGTXIT         2S 0       COLHDG('磅單項次')
     A            TGDATE         8S 0       COLHDG('出貨日期')
0409AA            TGPDNM         5A         COLHDG('品名')
     A            TGQTY1         7S 0       COLHDG('銷貨數量')
     A            TGPRC1         5S 3       COLHDG('銷貨單價')
     A            TGAMT1        11S 0       COLHDG('銷貨金額')
     A            TGKTY2         7S 0       COLHDG('折扣數量')
     A            TGKRC2         5S 3       COLHDG('折扣單價')
     A            TGKMT2         9S 0       COLHDG('折扣金額')
     A            TGDMT1        12S 0       COLHDG('折讓金額')
     A            TGDMT2         8S 0       COLHDG('折讓稅額')
     A            TGDXNO         8A         COLHDG('折讓單號')
     A            TGUPDM        10A         COLHDG('異動人員')
     A            TGUPDD         8S 0       COLHDG('異動日期')
     A            TGUPDT         6S 0       COLHDG('異動時間')
     A            TGRESV        10A         COLHDG('保留碼')
     A*
0408AA            TGDXIT         2S 0       COLHDG('折讓單項次')
0408AA            TGMETL         8A         COLHDG('鋼種')
0408AA            TGSPE1         5A         COLHDG(' H/W/D ')
0408AA            TGSPE2         5A         COLHDG(' B/T ')
0408AA            TGSPE5         6A         COLHDG('長度M')
0408AA            TGCHYN         1A         COLHDG('分攤方式')
0510AA            TGDMT3         8S 0       COLHDG('折讓金額調尾差')
0510AA            TGDMT4         8S 0       COLHDG('折讓稅額調尾差')
0510AA            TGAMT5        11S 8       COLHDG('鋼筋分攤比例')
0510AA            TGDMT5        12S 0       COLHDG('折讓總金額')
0510AA            TGDMT6        12S 0       COLHDG('折讓總稅額')
0408AA*
0408AA* 1-->依磅單
0408AA* 2-->依鋼種
0408AA* 3-->依鋼種+尺寸
0408AA* 4-->依鋼種+尺寸+長度
0408AA* 5-->依尺寸
0408AA* 6-->依尺寸+長度
0408AA* 7-->依長度
0408AA* 8-->依鋼種+長度
     A*
     A          K TGKIND
     A          K TGDXNO
     A          K TGDXIT
