     A*%%TS  SD  19960806  142951  S02LBU      REL-V5R1M0  5722-WDS
     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-IF    ACE016S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S01LYH
     A*            4.FUNCTION     票據資料挑選設定
     A*            5.WRITER-DATE   90/12/06
     A*            6.DATE-UPDATE
     A*
     A*****************************************************************
     A*                                     DSPSIZ(24 80 *DS3)
     A*                                     PRINT
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CA03(03 '結束作業')
     A                                      CA12(12 '回前畫面')
     A*================================================================
     A          R AR016H
     A*%%TS  SD  19891229  152016  S01LYH      REL-V3R7M0  5716-PW1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 24'    東和鋼鐵企業股份有限公司　　-
     A                                      '
     A            S#SRID        10A  O  1 70
     A                                  2  2TIME
     A                                  2 31'★票據資料挑選設定★'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3  2
     A*================================================================
     A          R AR016F1                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT          1Y 0B 10  3EDTCDE(Z)
     A                                      VALUES(0 2)
     A            S#DUDT         8Y 0O 10  5EDTCDE(Z)
     A            S#NTNO        20A  O 10 14
     A            S#NO           6A  O 10 35
     A            S#ITEM         2S 0O 10 43
     A            S#DATE         8Y 0O 10 46EDTCDE(Z)
     A            S#PLAC         1A  B 10 55VALUES('D' 'E')
     A            S#RAMT        11Y 0O 10 57EDTCDE(4)
     A            S#AREA         1A  B 10 69
     A            S#DT           8Y 0B 10 71EDTCDE(4)
     A*================================================================
     A          R AR016F1C                  SFLCTL(AR016F1)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(91 '下頁')
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  74                                  SFLCLR
     A                                      OVERLAY
     A                                  4  2'OP: 2=修改'
     A                                  4 20'出納廠區:P台北'
     A            S#LIN1        78A  O  5  2
     A                                  6 35'繳款'
     A                                  6 42'項'
     A                                  6 53'付款'
     A                                  6 66'出納'
     A                                  7  3'OP'
     A                                  7  6'到期日　票據號碼'
     A                                  7 35'編號'
     A                                  7 42'次　繳款日　地'
     A                                  7 59'金額'
     A                                  7 66'廠區　寄送日'
     A            S#LIN2        78A  O  8  2
     A            S#OPTK         1Y 0B  9  3EDTCDE(4)
     A                                      VALUES(0 2)
     A  50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A            S#DUTK         8Y 0B  9  5EDTCDE(Z)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A            S#NTNK        20A  B  9 14
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A            S#NOK          6A  B  9 35
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A            S#NBR          4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A*
     A          R AR016F1M
     A                                      OVERLAY
     A            S#LIN3        78A  O 22  2
     A                                 23  2'F3=結束作業'
     A                                 23 16'F12=回前畫面'
     A                                 23 31'PA1=下頁'
     A                                 23 42'PA2=上頁'
     A            S#ERR         70A  O 24  2DSPATR(HI)
