     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARR108T
     A*            2.PROGRAM-TYPE  PRTF
     A*            3.AUTHOR        S02CSF
     A*            4.FUNCTION     銷貨調整單開立發票明細表
     A*            5.DATE-WRITTEN  92/02/21
     A*            6.DATE-UPDATE   95/04/12
     A*                            99/09/16  2010HS184  S00WCJ (9909A)
     A*                           100/08/16  2011AR621  S00WCJ (0008A)
     A*                           增加重量欄位及計算
     A*                           104/10/08  2015AR953  S00WCJ (0409A)
     A*                            產品代碼3碼擴5碼
     A*                           108/08/21 2019AR00057 S00WCJ (0808A)
     A*                            配合磅單過至項次修改
     A*                           109/09/22 2020AR00047 S00WCJ (0909A)
     A*                            增加客戶編號選項
     A*                           110/12/16 2021AR00050 S00WCJ (1012A)
     A*                            增加訂單編號篩選
     A*                           112/05/16 2023AR00028 S00WCJ (1205A)
     A*                            增加調整品名篩選欄位
     A*                           112/09/13 2023AR00041 S00WCJ (1209A)
     A*                            增加單價欄位
     A*                           113/08/15 S00WCJ (1308A)
     A*                            增加說明欄位長度，避免印出亂碼
     A*
     A*****************************************************************
     A          R AR108T1H
     A**************************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報　　表: ARR108T'
     A                                      SPACEB(001)
     A                                    68
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   149
     A                                      '印表人員:'
     A            U#USID        10A      160
     A            T#DAT2         8  0     28
     A                                      EDTWRD('    /  /  ')
     A                                      SPACEB(001)
     A*=======
     A                                     2
     A                                      '開立日期:'
     A            T#DAT1         8  0     14
     A                                      EDTWRD('    /  /  ')
     A                                    25
     A                                      '~~'
     A                                    68
     A                                      '銷貨調整單開立發票明細表'
     A                                   149
     A                                      '印表日期:'
     A                                   160
     A                                      DATE
     A                                      EDTWRD('01  /  /  ')
     A*=======
     A                                     2
     A                                      '開立廠區:'
     A                                      SPACEB(001)
     A            T#AREA         6   O    13
0909AA                                    +1'客戶編號:'
0909AA            T#CUNS         6   O    +1
1012AA                                    +1'訂單編號:'
1012AA            T#ORNS         6   O    +1
1012AA                                    +1'~'
1012AA            T#ORNE         6   O    +1
1205AA                                    +1'調整品名:'
1205AA            T#PDNS         3   O    +1
     A                                   149
     A                                      '頁　　數:'
     A                                   160
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*=======
1209AA            T#LIN1       193A  O     2
     A                                      SPACEB(001)
     A*=======
     A                                     4
     A                                      '開立日期'
     A                                      SPACEB(001)
     A                                    17
     A                                      '客戶代號'
     A                                    33
     A                                      '客戶名稱'
     A                                    50
     A                                      '訂單編號'
9909AA                                    63
9909AA*                                     '交單編號'
9909AA*                                   86
9909AA*                                     '磅單編號'
9909AA*                                  105
     A                                      '說　　明'
9909AA                                    86
     A                                      '發票號碼'
9909AA                                   104
     A                                      '調整單號'
9909AA                                   124
     A                                      '金　　額'
9909AA                                   140
9909AA                                      '磅單編號'
0808AA                                    +1
0808AA                                      '磅次'
0808AA                                    +1
0808AA                                      '徑度'
0808AA                                    +1
9909AA                                      '品名'
0808AA                                   176
0008AA                                      '重 量'
1209AA                                    +1
1209AA                                      '單價'
     A*=======
1209AA            T#LIN2       193A  O     2
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR108T1D
     A**************************************************************************
     A                                     2
     A                                      ' '
     A                                      SPACEB(001)
     A            T#DATE         8  0O     3
     A                                      EDTWRD('    /  /  ')
     A            T#CUNO         6A  O    19
     A            T#CUNM        10A  O    33
     A            T#ORNO         6A  O    52
9909AA*           T#DLVY         6A  O    71
9909AA*           T#PCNO         8A  O    87
1308AA            T#TEXT        22A  O    63
9909AA            T#IVNO        10A  O    86
9909AA            T#NO           8A  O   105
9909AA            T#AMT         10S  O   124
     A                                      EDTCDE(1)
9909AA            T#PCNO         8A  O   141
0808AA            T#VRTM         2A  O   153
0808AA            T#PDS1         3A  O   160
0409AA            T#PDN1         5A  O   167
0008AA            T#QTY          7  0O   176EDTCDE(1)
1209AA            T#UPRC         5  3     +1EDTCDE(1)
     A*****************************************************************
     A          R AR108T1L
     A*****************************************************************
1209AA            T#LIN3       193A  O     2
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR108T1S
     A*****************************************************************
9909AA                                   109
     A                                      '發票小計：'
     A                                      SPACEB(001)
9909AA            T#AMT1        12S  O   122
     A                                      EDTCDE(1)
0808AA            T#QTY1         8  0O   175EDTCDE(1)
     A*****************************************************************
     A          R AR108T2S
     A*****************************************************************
0409AA            T#PDNM        13A  O    92
     A                                      SPACEB(001)
0409AA            T#PDNO         5A  O    +0
9909AA                                    +0
     A                                      '品名小計：'
0409AA            T#AMT2        12S  O    +0
     A                                      EDTCDE(1)
0808AA            T#QTY2         8  0O   175EDTCDE(1)
     A*****************************************************************
     A          R AR108T1T
     A*****************************************************************
9909AA                                   113
     A                                      '總計：'
     A                                      SPACEB(001)
9909AA            T#AMTT        12S  O   122
     A                                      EDTCDE(1)
0808AA            T#QTYT         8  0O   175EDTCDE(1)
     A                                      SPACEA(001)
     A*****************************************************************
     A          R AR108T2T
     A*****************************************************************
9909AA                                   109
     A                                      '品名合計：'
     A                                      SPACEB(001)
9909AA            T#AMT3        12S  O   122
     A                                      EDTCDE(1)
0808AA            T#QTY3         8  0O   175EDTCDE(1)
     A*****************************************************************
     A          R AR108T1M
     A**************************************************************************
     A                                    19
     A                                      '*****無符合條件之資料!!!'
     A                                      SPACEB(008)
     A**************************************************************************
     A          R AR108T1Y
     A**************************************************************************
     A                                    12
     A                                      '主　　　管:'
     A                                      SPACEB(008)
     A                                    62
     A                                      '審　　　核:'
     A                                   112
     A                                      '製　　　表:'
     A**************************************************************************
