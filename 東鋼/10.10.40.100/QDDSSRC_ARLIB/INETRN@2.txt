     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    INETRN
     A*            2.PROGRAM-TYPE  PF
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     電子發票折讓單上送介面檔
     A*            5.DATE-WRITTEN  106/11/16
     A*            6.UPDATE  DATE  107/06/15 2018AR1162 S00WCJ (0706A)
     A*                            增加處理代號、廠區及列印碼欄位
     A*                            107/12/14 2018AR00038 S00WCJ (0712A)
     A*                             增加列印日期、列印時間、列印人員
     A*                             列印廠區
     A*
     A****************************************************************
     A                                      UNIQUE
     A          R RINETRN                   TEXT('電子發票上送介面主檔')
0706AA            TRFLAG         1A         COLHDG('處理代碼')
0706AA            TRAREA         1A         COLHDG('廠區')
     A            TRMSTE         5A         COLHDG('M訊息代碼')
     A            TRNO          16A         COLHDG('M折讓單號')
     A            TRINDT         8A         COLHDG('M折讓單開立日')
     A            TRSLID        10A         COLHDG('M賣方統編')
     A            TRSLNM        60O         COLHDG('M賣方名稱')
     A            TRSLAS       100O         COLHDG('O賣方地址')
     A            TRSLPE        30O         COLHDG('O賣方負責人姓名')
     A            TRSLTL        26O         COLHDG('O賣方電話號碼')
     A            TRSLFC        26O         COLHDG('O賣方傳真號碼')
     A            TRSLML        80O         COLHDG('O賣方EMAIL ')
     A            TRSLNO        20O         COLHDG('O賣方客戶編號')
     A            TRSLRK        40O         COLHDG('賣方營業人角色註記')
     A*
     A            TRBYID        10A         COLHDG('M買方統編')
     A            TRBYNM        60O         COLHDG('M買方名稱')
     A            TRBYAS       100O         COLHDG('O買方地址')
     A            TRBYPE        30O         COLHDG('O買方負責人姓名')
     A            TRBYTL        26O         COLHDG('O買方電話號碼')
     A            TRBYFC        26O         COLHDG('O買方傳真號碼')
     A            TRBYML        80O         COLHDG('O買方EMAIL')
     A            TRBYNO        20O         COLHDG('O買方客戶編號')
     A            TRBYRK        40O         COLHDG('買方營業人角色註記')
     A*
     A            TRALTE         1A         COLHDG('M折讓種類')
     A            TROIND         8A         COLHDG('M原發票日期')
     A            TROINO        10A         COLHDG('M原發票號碼')
     A            TROISN         3A         COLHDG('O原明細排列序號')
     A            TRPDCD       256O         COLHDG('M原品名')
     A            TRQTY          7P 0       COLHDG('M數量')
     A            TRUNIT         6O         COLHDG('O單位')
     A            TRUPRC         8S 3       COLHDG('M單價')
     A            TRAMT         11P 0       COLHDG('M金額')
     A            TRTAX         11P 0       COLHDG('M營業稅額')
     A            TRSENR         3A         COLHDG('M折讓單明細序號')
     A            TRTXTP         1A         COLHDG('M課稅別')
     A            TRATAX        11P 0       COLHDG('M營業稅合計')
     A            TRAAMT        11P 0       COLHDG('M金額合計')
     A*
     A            TRTMDT         8S 0       COLHDG('傳輸日期')
     A            TRTMTM         6S 0       COLHDG('傳輸時間')
     A            TRTMOP        10A         COLHDG('傳輸人員')
     A            TRTAWS        10A         COLHDG('傳輸工作站')
     A            TRSTCL         1A         COLHDG('執行碼')
     A*
     A            TRXPDD         8S 0       COLHDG('上送日期')
     A            TRXPDT         6S 0       COLHDG('上送時間')
     A            TRXUSR        10A         COLHDG('上送人員')
     A            TRMAR1        30A         COLHDG('備註')
0706AA            TRPTYN         1A         COLHDG('列印碼')
     A*
0712AA            TRPTDD         8S 0       COLHDG('列印日期')
0712AA            TRPTDT         6S 0       COLHDG('列印時間')
0712AA            TRPTSR        10A         COLHDG('列印人員')
0712AA            TRPTAR         1A         COLHDG('列印廠區')
     A*
     A          K TRNO
     A          K TRSENR
