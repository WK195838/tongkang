     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR127T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.WRITER        S02LBU
     A*           4.FUNCTION     外銷銷貨差異明細表
     A*           5.DATE-WRITTEN  96/01/26
     A*           6.DATE-MODIFY
     A*
     A*
     A**************************************************************************
     A          R AR127T1H
     A**************************************************************************
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     3
     A                                      '報　　表:ARR127T'
     A                                    55
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   118
     A                                      '列表日期:'
     A                                   129
     A                                      DATE
     A                                      EDTCDE(Y)
     A*
     A                                     3
     A                                      '報關年月:'
     A                                      SPACEB(001)
     A            T#YMS          6  0O    14
     A                                      EDTWRD('    /  ')
     A                                    22'~'
     A            T#YME          6  0O    23
     A                                      EDTWRD('    /  ')
     A                                    58
     A                                      '外銷應收帳款明細表'
     A                                   118
     A                                      '列表時間:'
     A                                   129
     A                                      TIME
     A*
     A                                     3
     A                                      '廠　　區:'
     A                                      SPACEB(001)
     A            T#AREA         6   O    14
     A                                   118
     A                                      '頁　　次:'
     A                                   133
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*
     A            T#LIN1       136         2
     A                                      SPACEB(001)
     A*----------
     A                                     3
     A                                      '報關日期'
     A                                      SPACEB(001)
     A                                    16
     A                                      '報　單　號　碼'
     A                                    37
     A                                      '客戶編號'
     A                                    49
     A                                      '客戶名稱'
     A                                    61
     A                                      '訂單號碼'
     A                                    73
     A                                      '品名'
     A                                    84
     A                                      '數　　量'
     A                                    98
     A                                      '報關金額'
     A                                   113
     A                                      '銷貨金額'
     A                                   128
     A                                      '差異金額'
     A*----------
     A            T#LIN2       136         2
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR127T1D
     A*****************************************************************
     A            T#BLK          1A        1
     A                                      SPACEB(001)
     A N41        T#CDTE         8  0      2
     A                                      EDTWRD('    /  /  ')
     A N41        T#NO          20A       15
     A N44        T#CUNO         6A       39
     A N44        T#CUNM        10O       49
     A N44        T#ORNO         6A       63
     A            T#PDNO         3A       74
     A            T#QTY         10S 0     80
     A                                      EDTCDE(1)
     A            T#AMT1        10S 0     94
     A                                      EDTCDE(J)
     A            T#AMT2        10S 0    109
     A                                      EDTCDE(J)
     A            T#AMT3        10S 0    124
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR127T1S
     A*****************************************************************
     A                                    65
     A                                      '訂單小計:'
     A                                      SPACEB(001)
     A            T#SQTY        10S 0     80
     A                                      EDTCDE(1)
     A            T#SAM1        10S 0     94
     A                                      EDTCDE(J)
     A            T#SAM2        10S 0    109
     A                                      EDTCDE(J)
     A            T#SAM3        10S 0    124
     A                                      EDTCDE(J)
     A                                      SPACEA(001)
     A*****************************************************************
     A          R AR127T2S
     A*****************************************************************
     A                                    65
     A                                      '報單小計:'
     A                                      SPACEB(001)
     A            T#QTYS        10S 0     80
     A                                      EDTCDE(1)
     A            T#AM1S        10S 0     94
     A                                      EDTCDE(J)
     A            T#AM2S        10S 0    109
     A                                      EDTCDE(J)
     A            T#AM3S        10S 0    124
     A                                      EDTCDE(J)
     A                                      SPACEA(001)
     A            T#LIN5        73        65
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR127T1T
     A*****************************************************************
     A                                    65
     A                                      '總合計:'
     A                                      SPACEB(001)
     A            T#TQTY        10S 0     80
     A                                      EDTCDE(1)
     A            T#TAM1        10S 0     94
     A                                      EDTCDE(J)
     A            T#TAM2        10S 0    109
     A                                      EDTCDE(J)
     A            T#TAM3        10S 0    124
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR127T1L
     A*****************************************************************
     A            T#LIN3        73        65
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR127T1N
     A*****************************************************************
     A            T#BLK          1         2
     A                                      SPACEB(003)
     A                                    65
     A                                      '品名'
     A                                    75
     A                                      '數　　量'
     A                                    90
     A                                      '報關金額'
     A                                   105
     A                                      '銷貨金額'
     A*****************************************************************
     A          R AR127T1P
     A*****************************************************************
     A            T#PDNM         3        66
     A                                      SPACEB(001)
     A            T#SAQT        10S 0     71
     A                                      EDTCDE(J)
     A            T#CAMT        10S 0     86
     A                                      EDTCDE(J)
     A            T#SAMT        10S 0    101
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR127T2T
     A*****************************************************************
     A            T#LIN4        49        65
     A                                      SPACEB(001)
     A                                    65
     A                                      '合計'
     A                                      SPACEB(001)
     A            T#TSQT        10S 0     71
     A                                      EDTCDE(J)
     A            T#TCAM        10S 0     86
     A                                      EDTCDE(J)
     A            T#TSAM        10S 0    101
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR127T1M
     A*****************************************************************
     A                                    45
     A                                      '無符合之資料!!! '
     A                                      SPACEB(008)
     A*****************************************************************
