     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR033T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     銷貨折讓依鋼種尺寸長度分攤─檢核表
     A*           4.DATE-WRITTEN  104/08/21
     A*           5.WRITER        S00WCJ
     A*           6.UPDATE DATE   104/10/02 S00WCJ (0410A)
     A*                            增加數量小計及合計
     A*                           106/05/02 2017AR10   S00WCJ (0605A)
     A*                            增加T1
     A*
     A*****************************************************************
     A          R AR033TH
     A**************************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A                                     2
     A                                      '報表代號: ARR033T'
     A                                    57
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   110
     A                                      '列表日期:'
     A                                    +1
     A                                      DATE
     A                                      EDTWRD('01  /  /  ')
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            T#USID        10        +1
     A                                    52
     A                                      '銷貨折讓依鋼種尺寸長度分攤檢核表'
     A                                   110
     A                                      '列表時間:'
     A                                    +1
     A                                      TIME
     A                                   110
     A                                      '頁　　次:'
     A                                      SPACEB(001)
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '折讓單號:'
     A                                      SPACEB(001)
     A            T#DXNO         6        +1
     A                                    +2
     A                                      '訂單編號:'
     A            T#ORNO         6        +1
     A                                     2
     A                                      '客戶代號:'
     A                                      SPACEB(001)
     A            T#CUNO         5        +1
     A            T#CUNM        10        +1
     A                                     3
     A                                      '發票號碼'
     A                                      SPACEB(001)
     A                                    +1
     A                                      '磅單編號'
     A                                    +0
     A                                      '項次'
     A                                    +1
     A                                      '鋼 種'
     A                                    +1
     A                                      ' H/W/D '
     A                                    +1
     A                                      ' B/T '
0605AA                                    +1
0605AA                                      ' T1'
     A                                    +1
     A                                      '長度'
     A                                    +1
     A                                      '銷貨數量'
     A                                    +1
     A                                      '折讓單價'
     A                                    +1
     A                                      '折讓金額'
     A                                   104
     A                                      '折讓稅額'
     A                                   117
     A                                      '總 額'
     A            T#LIN2       130         1
     A                                      SPACEB(001)
     A*%%***********************************************************************
     A          R AR033T1
     A*%%***********************************************************************
     A                                     2
     A                                      ' '
     A                                      SPACEB(001)
     A N60        T#INNO        10         3
     A            T#TXNO         8        +1
     A            T#TXIT         2  0     25
     A                                      EDTCDE(2)
     A            T#METL         8        31
     A            T#SPE1         5  0     +3
     A                                      EDTCDE(3)
     A            T#SPE2         5  0     +2
     A                                      EDTCDE(3)
0605AA            T#SPE3         3  0     +1
0605AC                                      EDTCDE(3)
     A            T#SPE5         6  0     +2
     A                                      EDTCDE(4)
     A            T#QTY1         7  0     67
     A                                      EDTCDE(2)
     A            T#AMT4         4  3     80
     A                                      EDTCDE(2)
     A            T#DMT1        12  0     87
     A                                      EDTCDE(2)
     A            T#DMT2         8  0    104
     A                                      EDTCDE(2)
     A            T#TAM1        12  0     +1
     A                                      EDTCDE(2)
     A*****************************************************************
     A          R AR033T2
     A*%%***********************************************************************
     A            T#LIN1       130         1
     A                                      SPACEB(001)
     A                                    33
     A                                      '發票小計:'
     A                                      SPACEB(001)
0410AA            T2QTY1         7  0     67
0410AA                                      EDTCDE(2)
     A            T2DMT1        12  0     87
     A                                      EDTCDE(2)
     A            T2DMT2         8  0    104
     A                                      EDTCDE(2)
     A            T#DMTA        12  0     +1
     A                                      EDTCDE(2)
     A*****************************************************************
     A          R AR033T3
     A*%%***********************************************************************
     A            T#LIN1       130         1
     A                                      SPACEB(001)
     A                                    33
     A                                      '總  計:'
     A                                      SPACEB(001)
0410AA            T1QTY1         7  0     67
0410AA                                      EDTCDE(2)
     A            T1DMT1        12  0     87
     A                                      EDTCDE(2)
     A            T1DMT2         8  0    104
     A                                      EDTCDE(2)
     A            T1DMTA        12  0     +1
     A                                      EDTCDE(2)
     A*****************************************************************
     A          R AR033TR
     A*%%***********************************************************************
     A                                      SKIPB(010)
     A                                    46
     A                                      '查無區間內資料'
