     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE002S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR
     A*            4.FUNCTION     調整維護作業
     A*            5.DATE-WRITTEN
     A*            6.DATE-MODIFY   96/05/10
     A*                           100/01/28  2011AR582 S00WCJ (0001A)
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TRNDTL)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*================================================================
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARE002S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'***銷貨調整單維護作業*** '
     A            DEVNM         10A  O  2 70
     A*=========
     A                                  5 30'功　　能:'
     A            CODE           1A  B  5 42
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  5 44'(1:新增 2:修改'
     A                                  7 45'3:刪除 4:查詢)'
     A*
     A                                 11 30'調整單號:'
     A            NO             6A  B 11 42
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A*
0001AA                                 13 30'廠  區:'
0001AA            S#AREA         1A  B    +1
0001AA                                      VALUES('P' 'T' 'U' 'H' 'K' +
0001AA                                      'M' ' ')
0001AA  45                                  DSPATR(PC)
0001AA  45                                  DSPATR(RI)
0001AA                                    +1'(P=台北M=台中T=八德'
0001AA                                 14 44' U=桃園H=苗栗K=高+
     A                                      雄)'
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A*=========
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            ITEM           2Y 0O  9  4
     A            JUST           1A  B  9 11
     A  70                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A            PDNM           3A  B  9 19
     A  70                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A            QTY            7Y 0B  9 27EDTWRD(' ,   ,   ')
     A  70                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A            PRC            5Y 3B  9 38EDTWRD(' 0.   ')
     A  70                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A            AMT            9Y 0B  9 46EDTCDE(J)
     A  70                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A            PNO            8A  B  9 59
     A  70                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A            PDNO           3A  B  9 70
     A  70                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A            FLAG           1A  B  9 76
     A  70                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CF03(03 '離開')
     A N69                                  CF08(08 '依日期載入')
     A                                      CF10(10 '存檔')
     A                                      CF12(12 '回前畫面')
     A  84                                  SFLCLR
     A N85                                  SFLDSP
     A N86                                  SFLDSPCTL
     A  75                                  SFLEND
     A*           S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A*-----------
     A                                  1 70'ARE002S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 28'***銷貨調整單維護作業*** '
     A            DEVNM         10A  O  2 70
     A*-----------
     A                                  4  4'調整單號:'
     A            NO             6A  O  4 16
     A                                  4 24'客戶編號:'
     A            CUNO      R        O  4 36REFFLD(TXCUNO)
     A                                  4 48'客戶名稱:'
     A            CUNM      R        O  4 60REFFLD(TXCUNM)
     A                                  5  4'訂單編號:'
     A            ORNO           6A  B  5 16
     A  70                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A                                  5 24'單據日期:'
     A            DATE      R        B  5 36REFFLD(TXDATE)
     A                                      EDTWRD('    /  /  ')
     A  70                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A*                                 5 48'發票號碼:'
     A            TXIVNO        10A  O  5 60
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  2'項次　調整別　調整品名'
     A                                  7 27'　數　量　　單價　'
     A                                  7 48'　金　額　磅單編號'
     A                                  7 74'標記'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND01
     A                                 21  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A                                      DSPATR(HI)
     A                                 22 12'合　計:'
     A            S#QTYS         9  0O 22 24EDTCDE(1)
     A            TTLAMT         9  0O 22 44EDTCDE(1)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                    +1'PF03:結束'
     A                                    +1'PF08:依訂單及日期載入'
     A                                    +1'PF10:存檔'
     A                                    +1'PF12:回前畫面'
     A*================================================================
     A          R SF2                       SFL
     A  89                                  SFLNXTCHG
     A            S#OPT          1A  B  9  5
     A            S#NO           8A  O  9 11
     A            S#ITEM         3  0O  9 23
     A            S#PDNO         3A  O  9 32
     A            S#QTY          7  0O  9 38EDTCDE(1)
     A            S#AMT          9  0O  9 50EDTCDE(J)
     A            S#UPRC         5  3H
     A            S#CUNO         6A  H
     A            S#CUNM        10O  H
     A***************************************************************
     A          R SFCTL2                    SFLCTL(SF2)
     A                                      SFLSIZ(0025)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CF03(03 '離開')
     A                                      CF05(05 '挑選')
     A                                      CF10(10 '存檔')
     A                                      CF12(12 '回前畫面')
     A  81                                  SFLCLR
     A N82                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A*-----------
     A                                  1 70'ARE002S-3'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 28'銷貨調整單依入帳日期載入'
     A            DEVNM         10A  O  2 70
     A*-----------
     A                                  4  2'請輸入－'
     A                                  4 13'訂單編號:'
     A            S#ORNO         6A  B  4 25
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  4 33'入帳日期:'
     A            S#ACDS         8Y 0B  4 45EDTCDE(Z)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  4 54'~'
     A            S#ACDE         8Y 0B  4 56EDTCDE(Z)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  5 13'調整金額:'
     A            S#TAMT         9  0B  5 25EDTCDE(1)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  5 37'調整別:'
     A            S#JUST         1A  B  5 47
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                  5 49'調整品名:'
     A            S#PDNM         3A  B  5 61
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  2'挑Ｙ'
     A                                  7 10'磅單編號'
     A                                  7 22'項次'
     A                                  7 30'品名'
     A                                  7 38'數　　量'
     A                                  7 52'銷貨金額'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND02
     A                                 21  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A                                      DSPATR(HI)
     A                                 22 12'合　計:'
     A            S#SQTY         9  0O 22 36EDTCDE(1)
     A            S#SAMT         9  0O 22 50EDTCDE(1)
     A            S#ERR         70A  O 23 02DSPATR(HI)
     A*
     A                                 24  5'ENTER:檢核'
     A                                    +2'PF03:結束'
     A                                    +2'PF05:全部挑選'
     A                                    +2'PF10:確認存檔'
     A                                    +2'PF12:回前畫面'
     A***************************************************************
