     A*%%TS  SD  19890412  101315  S02YSH      REL-V3R7M0  5716-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARP067RS
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     發票區間開立作業（畫面）
     A*            5.DATE-WRITTEN  88/01/31
     A*            6.UPDATE BY     S02YSH 89/04/12 (加訂單編號)
     A*                            102/09/05 2013AR768 S00WCJ (0209A)
     A*                            增加跨月選擇欄位
     A*                            104/10/13 2015AR955 S00WCJ (0410A)
     A*                             增加客戶及訂單皆為空白時，須按F23
     A*                             存檔
     A*
     A***************************************************************
     A*  畫面一:輸入廠區及發票日期
     A*
     A*        記錄格式 AR067F1
     A***************************************************************
     A          R AR067F1
     A*%%TS  SD  19890412  101315  S02YSH      REL-V3R7M0  5716-PW1
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      CF10(10)
0209AA                                      CF22(22)
0410AA                                      CF23(23)
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARP065S'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'★發票區間開立作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A                                  4 16'開立廠別:'
     A            S#AREA         1A  B  4 28
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A                                  4 31'( P台北 M台中 T桃園  H苗栗-
     A                                       K高雄)'
     A                                  6 16'發票日期:'
     A            S#DATE         8Y 0B  6 28EDTWRD('    /  /  ')
     A  52                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                  6 40'(本月五日前可重開上月底的發票)'
     A                                  8 16'銷貨區間:'
     A            S#TXD1         8Y 0B  8 28EDTWRD('    /  /  ')
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A                                  8 40'至'
     A            S#TXD2         8Y 0B  8 46EDTWRD('    /  /  ')
     A                                  8 57'（挑日期區間內）'
9103 A                                 10 16'客戶編號:'
LYW  A            S#CUNO         6A  B 10 28
0410AA  56                                  DSPATR(PC RI)
 .   A                                 10 35'(空白表全部)'
9706 A            S#CUNM        10O  O 10 50
     A                                 12 16'訂單編號:'
     A            S#ORNO         6A  B 12 28
0410AA  56                                  DSPATR(PC RI)
     A                                 12 35'(空白表全部)'
9706 A            S#CTNO        10O  O 12 50
0209AA                                    +1'跨月(Y/N)'
0209AA            S#YN           1A  B    +1VALUES('Y' 'N')
0209AA  55                                  DSPATR(PC RI)
     A                                 13  5'-----------------------------------
     A                                      ------------------------------------
     A                                      -----'
     A                                 14 14'字軌'
     A                                 14 26'起號'
     A                                 14 40'迄號'
     A                                 14 54'目前'
     A                                 14 66'可用張數'
     A                                 16  5'二聯式'
     A            S#PRE2         2A  O 16 16
     A            S#STN2         8S 0O 16 25
     A            S#ENN2         8S 0O 16 39
     A            S#CUN2         8S 0O 16 53
     A            S#LEV2         8Y 0O 16 67EDTCDE(4)
     A                                 18  5'三聯式'
     A            S#PRE3         2A  O 18 16
     A            S#STN3         8S 0O 18 25
     A            S#ENN3         8S 0O 18 39
     A            S#CUN3         8S 0O 18 53
     A            S#LEV3         8Y 0O 18 67EDTCDE(4)
     A                                 19  5'-----------------------------------
     A                                      ------------------------------------
     A                                      -----'
     A                                 20 20'發票已開立日期:'
     A            S#XDAT         8Y 0O 20 38EDTWRD('    /  /  ')
     A                                 22 12'F3:結束'
     A                                 22 26'F12:回前'
     A                                 22 41'ENTER:檢核'
     A                                 22 58'F10:執行'
     A                                      DSPATR(RI)
     A            S#MSG1        70A  O 24  2DSPATR(HI)
