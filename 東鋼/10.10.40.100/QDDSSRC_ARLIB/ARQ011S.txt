     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A***************************************************************
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARQ011S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 27'***  客戶別票據查詢  ***   '
     A            DEVNM         10A  O  2 70
     A                                  7 27'客戶編號:'
     A            CUST1     R        B  7 39REFFLD(RVCUNO RCVDTL)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A                                 10 27'到期日期:'
     A            DATE1          8Y 0B 10 39EDTWRD('    /  /  ')
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                 10 50'-'
     A            DATE2          8Y 0B 10 52EDTWRD('    /  /  ')
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            W01       R        O 10  5REFFLD(RVTBID RCVDTL)
     A            W02       R        O 10 16REFFLD(ACBNAM BANKAC)
     A            W03       R        O 11 35REFFLD(RVNTNO RCVDTL)
     A            W04       R    8 00O 10 36REFFLD(RVDATE RCVDTL)
     A                                      EDTWRD('    /  /  ')
     A            W05       R    8 00O 10 53REFFLD(RVDUDT RCVDTL)
     A                                      EDTWRD('    /  /  ')
     A            W06       R        O 10 64REFFLD(RVRAMT RCVDTL)
     A                                      EDTCDE(N)
     A            W21       R        O 11  1REFFLD(RVPBID RCVDTL)
     A            W22       R        O 11 16REFFLD(RVPANO RCVDTL)
     A            W23       R        O 11 27REFFLD(RVNO RCVDTL)
     A            W24            6A  O 10 27
     A            W25            7A  O 11 56
     A            W26            6O  O 11 65
     A            W27       R        O 11 75REFFLD(RVUSTP RCVDTL)
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARQ011S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 27'***  客戶別票據查詢  ***   '
     A            DEVNM         10A  O  2 70
     A                                  3 27'客戶編號:'
     A            CUST1     R        O  3 39REFFLD(RVCUNO RCVDTL)
     A            CUSTNM    R        O  3 47REFFLD(RVCUNM RCVDTL)
     A                                  4 27'到期日期:'
     A            DATE1          8Y 0O  4 39EDTWRD('    /  /  ')
     A                                  4 50'-'
     A            DATE2          8Y 0O  4 52EDTWRD('    /  /  ')
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6  8'託'
     A                                  6 18'收'
     A                                  8 15'銀行帳號'
     A                                  8 26'繳款編號'
     A                                  8 54'傳票編號　付款地　款項別'
     A                                  7  6'（付'
     A                                  7 18'款）'
     A                                  7 26'異動別'
     A                                  7 37'繳款日期'
     A                                  7 54'到期日期'
     A                                  7 70'票據金額'
     A                                  8  4'銀行名稱'
     A                                  8 37'票據號碼'
     A***************************************************************
     A          R UND01
     A                                      OVERLAY
     A                                 22  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23 37'客戶應收票據總額合計:'
     A            TTLAMT        11Y 0O 23 61EDTCDE(N)
     A                                 24  5'ENTER:檢核'
     A                                 24 19'PF03:結束'
     A                                 24 32'PF12:回前畫面'
     A***************************************************************
