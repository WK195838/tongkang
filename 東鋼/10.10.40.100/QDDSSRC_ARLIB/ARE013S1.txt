     A*%%TS  SD  19820506  160028  JEFF        REL-V2R1M1  5738-PW1
     A*%%EC
     A*            UPDATE  DATE  101/09/18  2012AR682  S00WCJ (0109A)
     A*
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RCVDTL)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A*%%TS  SD  19810921  115406  VIN         REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARE013S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'*** 票據退回申請單 ***'
     A            DEVNM         10A  O  2 70
     A                                  5 30'功　　能:'
     A            ACT            1A  B  5 42
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  5 46'(1:新增 2:修改'
     A                                  7 46' 3:刪除 4:查詢)'
     A                                  9 30'退回單號:'
     A            WRNO      R        B  9 42REFFLD(RVWRNO)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A                                 11 30'客戶代號:'
     A            CUN            6A  B 11 42
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                 13 30'繳款單號:'
     A            RNO            6A  B 13 42
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A*%%TS  SD  19811028  150657  JEFF        REL-V2R1M1  5738-PW1
     A  88                                  SFLNXTCHG
     A            SRL1      R        O  8  2REFFLD(RVITEM)
     A            NTNO      R        B  8  5REFFLD(RVNTNO)
     A  70                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A            PBID      R        O  8 26REFFLD(RVPBID)
     A            PANO      R        O  8 41REFFLD(RVPANO)
     A            NTTP      R        O  8 53REFFLD(RVNTTP)
     A            USTP      R        O  8 58REFFLD(RVUSTP)
     A            PLAC      R        O  8 64REFFLD(RVPLAC)
     A            RAMT           9Y 0O  8 68EDTWRD('   ,   ,   -')
     A            ACD1           1A  B  9 11VALUES('A' 'C' 'D' ' ')
     A  70
     AO 71                                  DSPATR(PR)
     A            DPNO      R        O  9 18REFFLD(RVDPNO)
     A            SANO      R        O  9 28REFFLD(RVSANO)
     A            RLNO          10A  O  9 41
     A            DUDT      R        O  9 57REFFLD(RVDUDT)
     A                                      EDTWRD('    /  /  ')
     A            AMCD      R        O  9 72REFFLD(RVAMCD)
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A*%%TS  SD  19820506  135021  JEFF        REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0036)
     A                                      SFLPAG(0001)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARE013S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 28'*** 票據退回申請單 ***'
     A            DEVNM         10A  O  2 70
     A                                  3 32'退回單號:'
     A            WRNO      R        O  3 44REFFLD(RVWRNO)
     A                                  3  1'客戶編號:'
     A            CUNO      R        B  3 13REFFLD(RVCUNO)
     A  70                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A            CUNM      R        O  3 21REFFLD(RVCUNM)
     A                                  4  1'申請日期:'
     A            DATE      R        B  4 13REFFLD(RVDATE)
     A                                      EDTWRD('    /  /  ')
     A  70                                  DSPATR(PR)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A                                  4 32'繳款單號:'
     A            RNO            6A  O  4 44
     A                                  4 60'合計:'
     A                                      DSPATR(RI)
     A            TTLAMT         9Y 0O  4 68EDTCDE(J)
     A                                      DSPATR(RI)
     A                                  5  1'項　票據號碼'
     A                                  5 27'付款銀行'
     A                                  5 40'付款帳戶　票款　款項　地點'
     A*                                 5  6'票據號碼'
     A                                  5 69'金　　額'
     A                                  6  1'次'
     A                                  6 17'繳款部門　存入帳號'
     A                                  6 40'相關號碼'
     A                                  6 58'到期日期'
     A                                  6 69'資金代號'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6  9'標記'
     A***************************************************************
     A          R SF2                       SFL
     A*%%TS  SD  19820506  160028  JEFF        REL-V2R1M1  5738-PW1
     A            SRL2      R        O 12  4REFFLD(RVITEM)
     A            DC             1A  B 12 10
     A  70                                  DSPATR(PR)
     A  21                                  DSPATR(PC)
     A  21                                  DSPATR(RI)
0108AA            GLNO           6A  B 12 15
     A  70                                  DSPATR(PR)
     A  22                                  DSPATR(PC)
     A  22                                  DSPATR(RI)
     A            WAY            1A  B 12 22
     A  70                                  DSPATR(PR)
     A  23                                  DSPATR(PC)
     A  23                                  DSPATR(RI)
     A            ACNO1         10A  B 12 25
     A  70                                  DSPATR(PR)
     A  24                                  DSPATR(PC)
     A  24                                  DSPATR(RI)
     A            DUE1           8Y 0B 12 36EDTWRD('    /  /  ')
     A  70                                  DSPATR(PR)
     A  25                                  DSPATR(PC)
     A  25                                  DSPATR(RI)
     A            RLNO          10E  B 12 47
     A  70                                  DSPATR(PR)
     A            CSNM          10E  B 12 58
     A  70                                  DSPATR(PR)
     A            AMT2           9Y 0B 12 69EDTCDE(1)
     A  70                                  DSPATR(PR)
     A  26                                  DSPATR(PC)
     A  26                                  DSPATR(RI)
     A          R SFCTL2                    SFLCTL(SF2)
     A*%%TS  SD  19820506  160028  JEFF        REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0002)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 10  2'項次　借貸　科目　方式　銀行帳號-
     A                                      '
     A                                 10 37'到期日期'
     A                                 10 48'相關號碼'
     A                                 10 59'對象別　'
     A                                 10 72'金　額'
     A***************************************************************
     A          R UND01
     A*%%TS  SD  19810922  091134  VIN         REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                 14  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R SF3                       SFL
     A*%%TS  SD  19811109  103119  VIN         REL-V2R1M1  5738-PW1
     A            SRL3      R        O 17 10REFFLD(RVITEM)
     A            INVNO         10A  B 17 20
     A  70                                  DSPATR(PR)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A            AAMT           9Y 0B 17 38
     A  70                                  DSPATR(PR)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A                                      EDTWRD('   ,   ,   -')
     A            ORNO           6A  O 17 56
     A            ACD3           1A  B 17 70VALUES('A' 'C' 'D' ' ')
     A  70
     AO 71                                  DSPATR(PR)
     A***************************************************************
     A          R SFCTL3                    SFLCTL(SF3)
     A*%%TS  SD  19810925  151719  VIN         REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0099)
     A                                      SFLPAG(0005)
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A                                      OVERLAY
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A  10                                  SFLEND
     A                                 15  8'項次'
     A                                 15 19'發票號碼'
     A                                 15 39'沖銷金額'
     A                                 15 55'訂單號碼'
     A                                 15 68'標記'
     A                                 16  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND03
     A*%%TS  SD  19811109  103633  VIN         REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  2'ENTER檢核'
     A                                 23 14'PF03結束'
     A                                 23 30'合計:'
     A                                      DSPATR(RI)
     A            TAAMT          9Y 0O 23 38DSPATR(RI)
     A                                      EDTWRD('   ,   , 0 -')
     A                                 23 53'PF10存檔'
     A                                 23 65'PF12回前畫面'
     A            ERRMSG        70A  O 24  2DSPATR(HI)
