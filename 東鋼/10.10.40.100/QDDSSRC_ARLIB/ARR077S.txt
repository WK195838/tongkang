     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARR077RS
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     銷貨帳款客戶帳齡分析表－畫面
     A*            5.DATE-WRITTEN  88/06/17
     A*
     A***************************************************************
     A*  畫面一:
     A*
     A*        記錄格式 AR077F1
     A***************************************************************
     A          R AR077F1
     A                                      CF03(03)
     A                                      CF12(12)
     A*--------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARR077S'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 27'★銷貨帳款客戶帳齡分析表★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*--------------------------------------------------------------
     A                                  7  4'領款管控日數：'
     A            S#DAY1         4Y 0B  7 21EDTCDE(1)
     A                                  7 29'逾期管控日數：'
     A            S#DAY2         4Y 0B  7 46EDTCDE(1)
     A  51                                  DSPATR(PC RI)
     A                                  7 54'催收管控日數：'
     A            S#DAY3         4Y 0B  7 71EDTCDE(1)
     A*--------------------------------------------------------------
     A                                  9  3'<指定列印管控日數區間上Y>'
     A                                      DSPATR(HI)
     A                                  9 32'正常'
     A                                      DSPATR(RI)
     A            S#FLG1         1A  B  9 39VALUES('Y' ' ')
     A  52                                  DSPATR(PC RI)
     A                                  9 44'逾領'
     A                                      DSPATR(RI)
     A            S#FLG2         1A  B  9 51VALUES('Y' ' ')
     A                                  9 56'逾期'
     A                                      DSPATR(RI)
     A            S#FLG3         1A  B  9 63VALUES('Y' ' ')
     A                                  9 68'催收'
     A                                      DSPATR(RI)
     A            S#FLG4         1A  B  9 75VALUES('Y' ' ')
     A*--------------------------------------------------------------
     A                                 11  3'<指定列印應收狀態上Y>'
     A                                      DSPATR(HI)
     A                                 11 27'未開發票'
     A                                      DSPATR(RI)
     A            S#STC1         1   B 11 38VALUES('Y' ' ')
     A  53                                  DSPATR(PC RI)
     A                                 11 42'未請款'
     A                                      DSPATR(RI)
     A            S#STC2         1   B 11 51VALUES('Y' ' ')
     A                                 11 55'未繳款'
     A                                      DSPATR(RI)
     A            S#STC3         1   B 11 64VALUES('Y' ' ')
     A                                 11 68'未兌現'
     A                                      DSPATR(RI)
     A            S#STC4         1   B 11 77VALUES('Y' ' ')
     A*--------------------------------------------------------------
     A                                 15 14'統計截止日:'
     A            S#ETXD         8Y 0B    +1EDTCDE(4)
     A                                    +1'(不輸入則表示統計所有資料)'
     A*--------------------------------------------------------------
     A                                 23  4'F3:結束'
     A                                 23 18'F12:回前'
     A                                 23 33'ENTER:執行'
     A            S#MSG1        70A  O 24  2DSPATR(HI)
