     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARR078RS
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     銷貨帳款每日應收明細表－畫面
     A*            5.DATE-WRITTEN  88/06/11
     A*
     A***************************************************************
     A*  畫面一:
     A***************************************************************
     A          R AR078F1                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT1         1A  B 12 26VALUES(' ' 'Y')
     A            S#DEPT         4A  O 12 32
     A            S#RVID         2A  O 12 40
     A            S#NAME        10A  O 12 44
     A***************************************************************
     A          R AR078F1C                  SFLCTL(AR078F1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      CF05(05)
     A                                      CF09(09)
     A                                      CF10(10)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A            S#SFN1         4S 0H
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARR078S-1'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 29'★銷貨帳款應收明細表★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  4  4'領款管控日數：'
     A            S#DAY1         4Y 0B  4 21EDTCDE(1)
     A                                  4 29'逾期管控日數：'
     A            S#DAY2         4Y 0B  4 46EDTCDE(1)
     A  51                                  DSPATR(PC RI)
     A                                  4 54'催收管控日數：'
     A            S#DAY3         4Y 0B  4 71EDTCDE(1)
     A                                  6  3'<指定列印管控日數區間上Y>'
     A                                      DSPATR(HI)
     A                                  6 32'正常'
     A                                      DSPATR(RI)
     A            S#FLG1         1A  B  6 39VALUES('Y' ' ')
     A  52                                  DSPATR(PC RI)
     A                                  6 44'逾領'
     A                                      DSPATR(RI)
     A            S#FLG2         1A  B  6 51VALUES('Y' ' ')
     A                                  6 56'逾期'
     A                                      DSPATR(RI)
     A            S#FLG3         1A  B  6 63VALUES('Y' ' ')
     A                                  6 68'催收'
     A                                      DSPATR(RI)
     A            S#FLG4         1A  B  6 75VALUES('Y' ' ')
     A                                  7  3'<指定列印應收狀態上Y>'
     A                                      DSPATR(HI)
     A                                  7 27'未開發票'
     A                                      DSPATR(RI)
     A            S#STC1         1   B  7 38VALUES('Y' ' ')
     A  53                                  DSPATR(PC RI)
     A                                  7 42'未請款'
     A                                      DSPATR(RI)
     A            S#STC2         1   B  7 51VALUES('Y' ' ')
     A                                  7 55'未繳款'
     A                                      DSPATR(RI)
     A            S#STC3         1   B  7 64VALUES('Y' ' ')
     A                                  7 68'未兌現'
     A                                      DSPATR(RI)
     A            S#STC4         1   B  7 77VALUES('Y' ' ')
     A                                  8 14'統計截止日:'
     A            S#ETXD         8Y 0B    +1EDTCDE(4)
     A                                    +1'(不輸入則表示統計所有資料)'
     A                                  9  9'-----------------------------------
     A                                      -----------------------------------'
     A                                 10 23'選項'
     A                                 10 31'部門'
     A                                 10 42'營業員'
     A                                 10 53'(要列印的人員選項設Y)'
     A                                      DSPATR(HI)
     A                                 11  9'-----------------------------------
     A                                      -----------------------------------'
     A***************************************************************
     A          R AR078F1M
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                 23  3'F3:結束'
     A                                 23 17'F12:回前'
     A                                 23 32'F5:清除'
     A                                 23 46'F9:全選'
     A                                 23 60'F10:執行'
     A            S#MSG1        70A  O 24  2DSPATR(HI)
