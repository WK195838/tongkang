     A**************************************
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARP201T
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     外銷報關記錄主檔介面傳輸-錯誤清單
     A*            5.DATE-WRITTEN  105/09/06 2016AR1007
     A*
     A*****************************************************************
     A          R AR01T21
     A**************************************************************************
     A                                      SKIPB(003)
     A                                    60
     A                                      '東和鋼鐵企業股份有限公司'
     A                                      SPACEA(001)
     A                                     1
     A                                      '報表編號: ARP201T'
     A                                    58
     A                                      '外銷報關記錄介面檔-錯誤清單'
     A                                   170
     A                                      '印表日期:'
     A            W#DATE         8  0    182EDTWRD('    /  /  ')
     A                                      SPACEA(1)
     A                                     1
     A                                      '上傳者: '
     A            C#USER        10        13
     A                                   170
     A                                      '印表時間:'
     A                                   183
     A                                      TIME
     A                                      SPACEA(1)
     A                                   170'頁　　數:'
     A                                   182PAGNBR EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1'處報關'
     A                                      SPACEA(001)
     A                                     1'理廠區'
     A                                    11'訂單編號'
     A                                    21'客戶編號'
     A                                    38'報關年月'
     A                                    +1'報關日期'
     A                                    +1'起出貨日'
     A                                    +1'迄出貨日'
     A                                    +1'報關數量'
     A                                    +2'台幣報關金額'
     A                                   116'傳輸序號'
     A                                   131'傳輸日期'
     A                                   142'時間'
     A                                   149'傳輸人員'
     A                                   161'傳送訊息'
     A                                      SPACEA(001)
     A                                     1
     A                                      '=================================='
     A                                    35
     A                                      '=================================='
     A                                    69
     A                                      '=================================='
     A                                   103
     A                                      '=============================='
     A                                   133
     A                                      '=============================='
     A                                   163
     A                                      '=================================='
     A                                   197
     A                                      '=='
     A                                      SPACEA(001)
     A**************************************************************************
     A          R AR01T22
     A**************************************************************************
     A                                      SPACEA(001)
     A            RIFLAG         1         2
     A            RIAREA         1         7
     A            RIORNO         6        11
     A            RIOCUS         6        21
     A            RIOCNM        10        +0
     A            RIYYMM         6  0     +1EDTWRD('    /  ')
     A            RICDTE         8  0     49EDTWRD('    /  /  ')
     A            RISACD         8  0     +1EDTWRD('    /  /  ')
     A            RIEACD         8  0     +1EDTWRD('    /  /  ')
     A            RIQTY         12  3     +1EDTCDE(K)
     A            RIAMT         10  0     +2EDTCDE(K)
     A            RISQNO        13       116
     A            RITMDT         8  0    131
     A            RITMTM         6  0    142
     A            RITMOP        10       149
     A            S#STCL        38       161
     A**************************************************************************
     A          R AR01T23
     A**************************************************************************
     A                                      SPACEA(001)
     A                                    59
     A                                      '---- END OF DATA ----'
