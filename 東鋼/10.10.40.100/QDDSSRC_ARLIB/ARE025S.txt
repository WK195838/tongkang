     A****************************************************************
     A*
     A*       UPDATE  DATE  107/12/24 2018AR00042 S00WCJ (0712A)
     A*                      增加折讓單確認及折讓單號輸入欄位
     A*
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/INVDTL)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03 '離開')
     A                                      CF12(12 '回前畫面')
     A                                      CF05(05 '查詢')
     A                                  1 70'ARE025S-1'
     A            COMP          35   O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'**發票繳款金額轉出作業**'
     A            DEVNM         10   O  2 70
     A                                  9 26'轉出發票號碼:'
     A            NO            10   B    +1
     A  42                                  DSPATR(PC RI)
     A                                 11 24'是否有轉入發票:'
     A            S#YN           1   B    +1
     A  44                                  DSPATR(PC RI)
     A                                    +1'(Y/N)'
0712AA                                 13 26'是否有折讓單:'
0712AA            S#TRYN         1   B    +1VALUES('Y' 'N')
0712AA  45                                  DSPATR(PC RI)
0712AA                                    +1'(Y/N)'
0712AA                                 15 30'折讓單號:'
0712AA            S#TRNO         6   B    +1
0712AA  46                                  DSPATR(PC RI)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'F03:結束'
     A                                 24 35'F12:回前'
     A                                 24 50'F05:查詢'
     A                                      DSPATR(RI)
     A***************************************************************
     A*  畫面二:轉出入發票畫面維護
     A***************************************************************
     A          R AR025F2                   SFL
     A  80                                  SFLNXTCHG
     A            S#OTEM         4Y 0O 12  6
     A            S#OIVN        10A  B 12 15
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A            S#OORN         6A  O 12 31
     A            S#OAMT         9Y 0B 12 42EDTCDE(J)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A            S#ONBL         9Y 0O 12 59EDTCDE(J)
     A***************************************************************
     A          R AR025F2C                  SFLCTL(AR025F2)
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0009)
     A                                      OVERLAY
     A                                      CF03(03 '離開')
     A                                      CF12(12 '回前畫面')
     A                                      CF08(08 '依訂單載入')
     A                                      CF09(09 '依請款單載入')
     A                                      CF10(10 '存檔')
     A N81                                  SFLDSPCTL
     A N82                                  SFLDSP
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A*-----------
     A                                  1 70'ARE025S-2'
     A            COMP          35A  O  1 24
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'**發票繳款金額轉出作業**'
     A            DEVNM         10A  O  2 70
     A*----------------------------------------------------------------
     A*
     A                                  4  5'轉出發票號碼:'
     A            NO            10A  O  4 21
0712AA            S#TRNO         6A  O    +2DSPATR(HI)
     A                                  4 43'轉出訂單編號:'
     A            ORNO           6A  O  4 59
     A*
     A                                  5  5'轉出客戶編號:'
     A            CUNO           6A  O  5 21
     A                                  5 43'轉出客戶名稱:'
     A            CUNM          10J  O  5 59
     A*
     A                                  6  5'發票開立種類:'
     A            TYP1           1A  O  6 21
     A                                  6 23'(1:出貨2:預收3:調整9:其他)-
     A                                      '
     A*
     A                                  7  5'發票開立廠區:'
     A            AREA           1A  O  7 21
     A                                  7 26'收款業務:'
     A            RVID           2A  O  7 38
     A                                  7 43'發票繳款金額:'
     A            RAMT           9Y 0O  7 59EDTCDE(J)
     A*
     A                                  8  5'已經轉出金額:'
     A            CAMT           9Y 0O  8 21EDTCDE(J)
     A                                  8 43'尚可轉出金額:'
     A                                      DSPATR(HI)
     A            NBAL           9Y 0O  8 59EDTCDE(J)
     A                                      DSPATR(HI)
     A*
     A                                  9  5'本次轉出金額:'
     A                                      DSPATR(HI)
     A            TAMT           9Y 0B  9 21EDTCDE(J)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A N51                                  DSPATR(HI)
     A                                  9 43'本次轉出日期:'
     A            S#DATE         8Y 0B  9 59EDTWRD('    /  /  ')
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A*
     A                                 10  6'-----------------------------------
     A                                      -------------------------------'
     A                                      DSPATR(HI)
     A*----------
     A                                 11  5'項次'
     A                                 11 12'欲轉入發票號碼'
     A                                 11 29'所屬訂單'
     A                                 11 42'欲轉入金額'
     A                                 11 56'欲轉入發票餘額'
     A*
     A***************************************************************
     A          R AR025F2M
     A*
     A                                 21  6'-----------------------------------
     A                                      -------------------------------'
     A                                      DSPATR(HI)
     A*
     A                                 22 27'合　計：'
     A            S#OTMT        10Y 0O 22 40EDTCDE(J)
     A*
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'PF3:結束'
     A                                 24 16'PF8:依訂單載入'
     A                                 24 33'PF9:依請款單載入'
     A                                 24 52'PF10:存檔'
     A                                 24 64'PF12:回前畫面'
     A***************************************************************
     A*  畫面三:瀏覽轉出入發票畫面
     A***************************************************************
     A          R AR025F3                   SFL
     A            S#ITEM         4Y 0O 12  3EDTCDE(3)
     A            S#OTAM        11Y 0O 12  8EDTCDE(J)
     A            S#OTNO        10A  O 12 25
     A            S#OTCU         6A  O 12 36
     A            S#OTOR         6A  O 12 43
     A            S#USER        10A  O 12 50
     A            S#TXDT         8Y 0O 12 62
     A            S#ACDT         8Y 0O 12 72
     A***************************************************************
     A          R AR025F3C                  SFLCTL(AR025F3)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03 '結束')
     A                                      CF12(12 '回前畫面')
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN3)
     A            S#NBR3         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN3         5S 0H
     A            S#SFN3         4Y 0H
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE025S-3'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'★發票轉出查詢作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  O  4 14
     A                                  4 29'客戶簡稱:'
     A            S#CUNM        10O  O  4 41
     A                                  4 57'訂單號碼:'
     A            S#ORNO         6A  O  4 69
     A                                  5  2'發票號碼:'
     A            S#INNO        10A  O  5 14DSPATR(HI)
     A                                  5 29'發票日期:'
     A            S#INDT         8Y 0O  5 41EDTWRD('    /  /  ')
     A                                  5 57'開立廠區:'
     A            S#AREA         1A  O  5 69
     A                                  6  2'發票類別:'
     A            S#INTP         1A  O  6 14
     A                                  6 29'發票聯式:'
     A            S#KIND         1A  O  6 41
     A                                  6 57'課稅別:'
     A            S#TXTP         1A  O  6 69
     A                                  7  2'銷售別:'
     A            S#SATP         1A  O  7 14
     A                                  7 29'出貨業務:'
     A            S#SALE         2A  O  7 41
     A                                  7 57'收款業務:'
     A            S#RVID         2A  O  7 69
     A                                  8  2'發票金額:'
     A            S#INAM        11Y 0O  8 14EDTCDE(J)
     A                                      DSPATR(HI)
     A                                  8 33'餘額:'
     A            S#INBN        11Y 0O  8 41EDTCDE(J)
     A                                      DSPATR(HI)
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 10  2'項次'
     A                                 10 11'轉出金額'
     A                                 10 25'轉入發票'
     A                                 10 36'客戶'
     A                                 10 43'訂單'
     A                                 10 50'異動人員'
     A                                 10 61'異動日期　轉出日期'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR025F3M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  4'F03:結束'
     A                                 23 22'F12:回前畫面'
     A                                 23 42'ENTER:執行'
     A            S#MSG3        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面四:轉出入發票畫面查詢
     A***************************************************************
     A          R AR025F4                   SFL
     A  89                                  SFLNXTCHG
     A            S#OPT          1A  B  7  7
     A            S#IVN1        10A  O  7 15
     A            S#ITE1         4Y 0O  7 30EDTCDE(3)
     A            S#IND1         8Y 0O  7 38EDTWRD('    /  /  ')
     A            S#AMT1         9Y 0O  7 50EDTCDE(J)
     A            S#AMT2         9Y 0O  7 65EDTCDE(J)
     A            H#ORN1         6A  H
     A***************************************************************
     A          R AR025F4C                  SFLCTL(AR025F4)
     A                                      SFLSIZ(0059)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      CF03(03 '結束')
     A                                      CF05(05 '全選')
     A                                      CF12(12 '回前畫面')
     A                                      CF10(10 '存檔')
     A N85                                  SFLDSPCTL
     A N86                                  SFLDSP
     A  87                                  SFLCLR
     A  88                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN4)
     A            S#NBR4         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN4         5S 0H
     A            S#SFN4         4Y 0H
     A                                  1 70'ARE025S-4'
     A            COMP          35A  O  1 24
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A  25                              2 22'**發票繳款金額轉出作業-依訂單+
     A                                      載入**'
     A N25                              2 22'**發票繳款金額轉出作業-依請款+
     A                                      載入**'
     A            DEVNM         10A  O  2 70
     A*----------------------------------------------------------------
     A*
     A                                  4  2'請輸入－'
     A  25                              4 13'訂單編號:'
     A N25                              4 13'請款單號:'
     A            S#ORN1         7A  B  4 25
     A  43                                  DSPATR(PC RI)
     A*
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A*----------
     A                                  6  5'挑Ｙ'
     A                                  6 15'發票號碼'
     A                                  6 29'項次'
     A                                  6 39'發票日期'
     A                                  6 52'發票金額'
     A                                  6 67'未收餘額'
     A*
     A***************************************************************
     A          R AR025F4M
     A*
     A                                 21  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A*
     A                                 22 30'合　計：'
     A            S#TAT1        10Y 0O 22 49EDTCDE(J)
     A            S#TAT2        10Y 0O 22 64EDTCDE(J)
     A*
     A            S#MSG4        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 19'PF03:結束'
     A                                 24 32'PF05:全選'
     A                                 24 45'PF10:存檔'
     A                                 24 58'PF12:回前畫面'
     A***************************************************************
