     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE042S
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     已作廢發票還原程式
     A*            5.DATE-WRITTEN  88/06/05
     A*            6.UPDATE  DATE  104/09/18  2015AR953  S00WCJ (0409A)
     A*                             產品代碼3碼擴5碼
     A*
     A***************************************************************
     A*  畫面一:
     A***************************************************************
     A          R AR042F1
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      CF05(05)
     A                                      CF10(10)
     A*---------------------------------------------------------------
     A                                  1 70'ARE042S-1'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 29'★已作廢發票還原程式★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*---------------------------------------------------------------
     A                                  6  8'請注意：你正要對已作廢的發票進行+
     A                                      還原動作，請確定資料無誤並輸入'
     A                                      DSPATR(HI)
     A                                  7 16'異動原因後，按F10 進入沖銷銷+
     A                                      貨明細畫面；在選擇正確的'
     A                                      DSPATR(HI)
     A                                  8 16'銷貨明細後再按F10 存檔。'
     A                                      DSPATR(HI)
     A                                 11 16'發票號碼:'
     A            S#INNO        10A  B 11 28
     A                                 13 16'異動原因:'
     A            S#CGCA        40O  B 13 28
     A*---------------------------------------------------------------
     A                                 23  3'F3:結束'
     A                                 23 18'F12:回前'
     A                                 23 34'ENTER:檢核'
     A                                 23 52'F10:還原'
     A                                      DSPATR(RI)
     A            S#MSG1        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面二:銷貨沖回畫面
     A***************************************************************
     A          R AR042F2                   SFL
     A  70                                  SFLNXTCHG
0409AA            S#OPT2         1A  B 12  2VALUES(' ' '1')
0409AA            S#TXCD         4A  O 12  4
     A            S#TXNO         8A  O    +1
     A            S#TXIT         2Y 0O    +1
0409AA            S#TXDT         7Y 0O    +1
0409AA            S#TXPD         5A  O    +1
     A            S#TXQT         7Y 0O    +1EDTWRD('      0')
     A            S#TXUP         5Y 3O    +1EDTWRD(' 0.   ')
     A            S#TXAM        11Y 0O    +1EDTWRD('          0-')
     A            S#TXTA        11Y 0O    +1EDTWRD('          0-')
     A            S#TXSA         2A  O    +1
     A            S#TXRV         2A  O    +1
     A***************************************************************
     A          R AR042F2C                  SFLCTL(AR042F2)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      CF10(10)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE030S-2'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTCDE(Y)
     A                                  2 30'★沖銷銷貨明細作業★'
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  O  4 14
     A                                  4 21'訂單號碼:'
     A            S#ORNO         6A  O  4 33
     A                                  4 49'發票號碼:'
     A            S#INNO        10A  O  4 61
     A                                  5 49'發票金額:'
     A            S#INAM        11Y 0O  5 61EDTWRD('  ,   ,   ,   -')
     A                                  7  4'(選項: 1 = 沖銷)'
     A                                  7 45'沖銷金額合計:'
     A            S#TSAM        11Y 0O  7 61EDTWRD('  ,   ,   ,   -')
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9  1'選　單據　單據'
     A                                  9 18'項　單據　品'
     A                                  9 70'出貨收款'
     A                                 10  1'項　代號　號碼'
     A                                 10 18'次　日期　名'
     A                                 10 34'數量'
     A                                 10 42'單價'
     A                                 10 51'金額'
     A                                 10 63'稅額'
     A                                 10 72'業務員'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR042F2M                  TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  2'F03:結束'
     A                                    +6'F12:回前'
     A                                    +6'ENTER:試算'
     A                                    +6'F10:存檔'
     A                                      DSPATR(RI)
     A            S#MSG2        70A  O 24  2DSPATR(HI)
