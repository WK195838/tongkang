     A*%%TS  SD  19900707  102945  S02LYW      REL-V4R4M0  5769-PW1
     A*                                                              **
     A*    PROGRAM   ID       :  ARE029S                             **
     A*    PROGRAM   FUNCTION : 訂單管制檔資料維護                 **
     A*    AUTHOR             :  ERIC. LEE.                          **
     A*    CREAT     DATE     :  85/10/14                            **
     A*    RELATION  PROGRAM  :  ARE029R                             **
     A*    NOTE               :                                      **
     A*    COMMON   INDICATOR :                                      **
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CA03(99 'END OF FILE')
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R AR029F1
     A                                  1  4DATE
     A                                      EDTCDE(Y)
     A            COMP          35   O  1 20
     A            DEVNM         10   O  1 70
     A                                  2  4TIME
     A                                  2 27'訂單管制檔資料維護'
     A                                  2 70'ARE029S'
     A            MODE           8   O  3 70
     A****************************************************************
     A          R AR029F2                   OVERLAY
     A                                  4 22'****************************'
     A                                  5 29'1.新　　　　增'
     A                                  7 29'2.修　　　　改'
     A                                  9 29'3.刪　　　　除'
     A                                 11 29'4.查　　　　詢'
     A                                 13 29'0.結　　　　束'
     A                                 14 22'****************************'
     A                                 15 25'請輸入選擇項目：'
     A            SEL            1Y 0B 15 44EDTCDE(4)
     A*                                     AUTO(RA)
     A                                      VALUES(1 2 3 4 0)
     A****************************************************************
     A          R AR029F3                   OVERLAY
     A*                                     PUTOVR
     A                                  5  4'1.訂單編號：'
     A            ORNO1          1   B  5 19
     A  30                                  DSPATR(RI PC)
     A            ORNO2          5   B   + 1CHECK(RZ)
     A  30                                  DSPATR(RI PC)
     A            ORNO3          3   B   + 1CHECK(RZ)
     A  30                                  DSPATR(RI PC)
     A                                  8  4'2.管制類別：'
     A            CTKD           2   B  8 19
     A  31                                  DSPATR(RI PC)
     A                                  8 22'(01訂單開立02交單開立'
     A                                   + 1'03出單開立04發票作業'
     A                                  9 23'05請款作業06傳票代開)'
     A                                 11  4'3.訂單類別：'
     A            ODKD           2   B 11 19
     A  32                                  DSPATR(RI PC)
     A                                 11 22'(01板車11定尺清含勞務'
     A                                   + 1'12定尺清料價13定尺清勞務'
     A                                 12 23'21外銷31同訂單不同客名)'
     A                                 14  4'4.項　　次：'
     A            ITEM           2Y 0B 14 19EDTCDE(4)
     A                                      RANGE(01 99)
     A                                 22 10'【ＰＦ３】：結束．'
     A****************************************************************
     A          R AR029F4
     A*%%TS  SD  19900707  102945  S02LYW      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                      CA02(02 'READ PRIVIOUS')
     A                                      CA11(11 'DELETE FILE')
     A                                  5  4'1.訂單編號：'
     A            ORNO1          1A  O  5 19
     A            ORNO2          5A  O  5 21
     A            ORNO3          3A  O  5 27
     A                                  8  4'2.管制類別：'
     A            CTKD           2A  O  8 19
     A                                 11  4'3.訂單類別：'
     A            ODKD           2A  O 11 19
     A                                 14  4'4.項　　次：'
     A            ITEM           2Y 0O 14 19
     A                                 17  4'5.開立方式：'
     A            MTHD           2A  B 17 19
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A  96                             22 10'刪除資料按【ＰＦ１１】，其它則否-
     A                                      '
     A  96                             22 45'刪除資料按【ＰＦ１１】，其它則否-
     A                                      '
     A  95                             22 10'按【執行】下一筆，【ＰＦ２】前一+
     A                                      筆．'
     A                                 15 23'(未列入本控制檔內的訂單項次皆為+
     A                                      隨車開立)'
     A                                 16 23'01:每日一訂單一張發票'
     A                                 17 23'02:每月一訂單一張發票'
     A                                 18 23'03:依日期區間彙總一張發票'
     A                                 19 23'04:挑選銷貨明細開立'
     A                                 20 23'05:人工開立方式'
     A****************************************************************
     A          R AR029F5                   OVERLAY
     A                                      CA11(11 'DELETE FILE')
     A            ERRMSG        50J  O 23 10DSPATR(HI)
     A            ASTD           1   B 23 78DSPATR(ND)
