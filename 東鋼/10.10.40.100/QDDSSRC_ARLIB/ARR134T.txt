     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR134T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.WRITER        S02CSF
     A*           4.FUNCTION     折讓單彙總表（含銷折／銷退／預收）
     A*           5.DATE-WRITTEN  93/01/08
     A*           6.DATE-MODIFY
     A*
     A*****************************************************************
     A          R AR134T1H
     A*****************************************************************
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報　　表:ARR134T'
     A                                    40
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    79
     A                                      '列表日期:'
     A                                    90
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      ' USR - ID :'
     A                                      SPACEB(001)
     A            U#USID        10A  O    13
     A                                    36
     A                                      '折讓單彙總表（含銷折／銷退／預收-
     A                                      ）'
     A                                    79
     A                                      '列表時間:'
     A                                    90
     A                                      TIME
     A*========
     A                                     2
     A                                      '開立廠區:'
     A                                      SPACEB(001)
     A            T#NAME         6A       13
     A*
     A                                    20
     A                                      '開立日期:'
     A            T#DATS         8  0     32EDTWRD('    /  /  ')
     A                                    43
     A                                      '~'
     A            T#DATE         8  0     45EDTWRD('    /  /  ')
     A*
     A                                    79
     A                                      '頁　　次:'
     A                                    90
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*========
     A            T#LIN1        96         2
     A                                      SPACEB(001)
     A*
     A                                    17
     A                                      '台北未稅金額'
     A                                      SPACEB(001)
     A                                    34
     A                                      '桃園未稅金額'
     A                                    51
     A                                      '台中未稅金額'
     A                                    68
     A                                      '高雄未稅金額'
     A                                    85
     A                                      '小　　　　計'
     A            T#LIN2        96         2
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR134T1D
     A*****************************************************************
     A            T#ACNT        10         2
     A                                      SPACEB(001)
     A N50        T#AMTP        11  0     16
     A                                      EDTCDE(J)
     A N50        T#AMTT        11  0     33
     A                                      EDTCDE(J)
     A N50        T#AMTM        11  0     50
     A                                      EDTCDE(J)
     A N50        T#AMTK        11  0     67
     A                                      EDTCDE(J)
     A N50        T#AMTS        11  0     84
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR134T1A
     A*****************************************************************
     A            T#ACN1        10         6
     A                                      SPACEB(001)
     A            T#AMP1        11  0     16
     A                                      EDTCDE(J)
     A            T#AMT1        11  0     33
     A                                      EDTCDE(J)
     A            T#AMM1        11  0     50
     A                                      EDTCDE(J)
     A            T#AMK1        11  0     67
     A                                      EDTCDE(J)
     A            T#AMS1        11  0     84
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR134T1B
     A*****************************************************************
     A            T#LIN4        92         6
     A                                      SPACEB(001)
     A                                     6
     A                                      '銷折小計'
     A                                      SPACEB(001)
     A            T#AMP2        11  0     16
     A                                      EDTCDE(J)
     A            T#AMT2        11  0     33
     A                                      EDTCDE(J)
     A            T#AMM2        11  0     50
     A                                      EDTCDE(J)
     A            T#AMK2        11  0     67
     A                                      EDTCDE(J)
     A            T#AMS2        11  0     84
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR134T1T
     A*****************************************************************
     A            T#LIN3        86        12
     A                                      SPACEB(001)
     A                                     2
     A                                      '合計：'
     A                                      SPACEB(001)
     A            T#TAMP        11  0     16
     A                                      EDTCDE(J)
     A            T#TAMT        11  0     33
     A                                      EDTCDE(J)
     A            T#TAMM        11  0     50
     A                                      EDTCDE(J)
     A            T#TAMK        11  0     67
     A                                      EDTCDE(J)
     A            T#TAMS        11  0     84
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR134T1M
     A*****************************************************************
     A                                    45
     A                                      '無符合之資料!!! '
     A                                      SPACEB(008)
     A*****************************************************************
