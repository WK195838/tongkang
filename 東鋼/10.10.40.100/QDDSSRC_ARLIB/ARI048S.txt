     A*%%TS  SD  19901220  085904  S02YSH      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARI048S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02YSH
     A*            4.FUNCTION     承購發票訂單別查詢作業
     A*            5.DATE-WRITTEN  90/12/18
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束作業')
     A                                      CA12(12 '回前畫面')
     A*================================================================
     A          R AR048H
     A*%%TS  SD  19901218  183438  S02YSH      REL-V4R4M0  5769-PW1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 26'も東和鋼鐵企業股份有限公司も'
     A            S#SRID        10A  O  1 70
     A                                  2  2TIME
     A                                  2 29'★承購發票訂單別查詢★'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3  2
     A*================================================================
     A          R AR048F1
     A                                      OVERLAY
     A*%%TS  SD  19901105  135237  S02YSH      REL-V4R4M0  5769-PW1
     A                                  9 20'銀行代號:'
     A            S#BANK         3A  B  9 32
     A  36                                  DSPATR(PC RI)
     A                                    +1'(不可空白)'
     A                                 11 20'客戶代號:'
     A            S#CUN1         6A  B 11 32
     A                                 11 39'∼'
     A            S#CUN2         6A  B 11 44
     A                                 11 51'(空白表全部)'
     A                                 23  2'F3/F12 :離開'
     A            S#ERR         70   O 24  2DSPATR(HI)
     A*================================================================
     A          R AR048F2                   SFL
     A*%%TS  SD  19901218  184521  S02YSH      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT2         1Y 0B 12  2EDTCDE(Z)
     A                                      VALUES(0 5)
     A            S#CUNO         6A  O 12  6
     A            S#CUNM        10A  O 12 14
     A            S#AMT1        12Y 0O 12 28EDTCDE(1)
     A            S#AMT2        12Y 0O 12 45EDTCDE(1)
     A            S#AMT3        12Y 0O 12 62EDTCDE(1)
     A*================================================================
     A          R AR048F2C                  SFLCTL(AR048F2)
     A*%%TS  SD  19901220  085904  S02YSH      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(91 '下頁')
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  74                                  SFLCLR
     A                                      OVERLAY
     A                                  4  2'銀行代號:'
     A            S#BANK         3A  O  4 16
     A            S#BANM        10A  O  4 21
     A                                  5  2'客戶代號:'
     A            S#CUN1         6A  O  5 16
     A                                  5 23'∼'
     A            S#CUN2         6A  O  5 28
     A                                  6  2'OP: 5=明細查詢'
     A            S#LIN1        78A  O  7  2
     A                                  8  2'O'
     A                                  8  6'客戶'
     A                                  9  2'P'
     A                                  9  6'代號'
     A                                  9 14'客戶簡稱'
     A                                  9 34'承購金額'
     A                                  9 51'預支金額'
     A                                  9 68'沖銷金額'
     A            S#LIN2        78A  O 10  2
     A            S#KEY1         6A  B 11  6
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR048F2M
     A                                      OVERLAY
     A            S#LIN3        78   O 22  2
     A                                 23  2'F3=結束作業'
     A                                    +1'F12=回前畫面'
     A                                    +1'PA1=下頁'
     A                                    +1'PA2=上頁'
     A            S#ERR2        70   O 24  2DSPATR(HI)
     A*================================================================
     A          R AR048F3                   SFL
     A*%%TS  SD  19901219  143357  S02YSH      REL-V4R4M0  5769-PW1
     A  79                                  SFLNXTCHG
     A            S#OPT3         1Y 0B 11  2EDTCDE(Z)
     A                                      VALUES(0 5)
     A            S#ORNO         6A  O 11  4
     A            S#AAMT        12Y 0O 11 12EDTCDE(1)
     A            S#BAMT        12Y 0O 11 28EDTCDE(1)
     A            S#CAMT        12Y 0O 11 44EDTCDE(1)
     A            S#DAT1         6Y 0O 11 61EDTCDE(Y)
     A            S#DAT2         6Y 0O 11 71EDTCDE(Y)
     A  69                                  DSPATR(ND)
     A*================================================================
     A          R AR048F3C                  SFLCTL(AR048F3)
     A*%%TS  SD  19901219  170953  S02YSH      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A N75                                  SFLDSPCTL
     A N76                                  SFLDSP
     A N78                                  SFLEND
     A  77                                  SFLCLR
     A                                      OVERLAY
     A                                  4  2'銀行代號:'
     A            S#BANK         3A  O  4 16
     A            S#BANM        10A  O  4 21
     A                                  5  2'客戶代號:'
     A            S#CUNO         6A  O  5 16
     A            S#CUNM        10A  O  5 28
     A                                  6  2'OP: 5=明細查詢'
     A            S#LIN4        78A  O  7  2
     A                                  8  2'O 訂單'
     A                                  9  2'P 號碼'
     A                                  9 17'承購金額'
     A                                  9 33'預支金額'
     A                                  9 49'沖銷金額'
     A                                  9 60'承購起日結案日期'
     A            S#LIN5        78A  O 10  2
     A*================================================================
     A          R AR048F3M
     A*%%TS  SD  19901218  184827  S02YSH      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A            S#LIN6        78A  O 21  2
     A            S#AMT1        12Y 0O 22 12EDTCDE(1)
     A                                      DSPATR(HI)
     A            S#AMT2        12Y 0O 22 28EDTCDE(1)
     A                                      DSPATR(HI)
     A            S#AMT3        12Y 0O 22 44EDTCDE(1)
     A                                      DSPATR(HI)
     A                                 23  2'F3=結束作業'
     A                                 23 16'F12=回前畫面'
     A                                 23 31'PA1=下頁'
     A                                 23 42'PA2=上頁'
     A            S#ERR3        70   O 24  2DSPATR(HI)
     A*================================================================
     A          R AR048F4                   SFL
     A*%%TS  SD  19901219  164147  S02YSH      REL-V4R4M0  5769-PW1
     A            S#INDT         6Y 0O 11  3EDTCDE(Y)
     A            S#INNO        10A  O 11 12
     A            S#DAM8         8Y 0O 11 24EDTCDE(1)
     A            S#EAM8         8Y 0O 11 40EDTCDE(1)
     A            S#FAM8         8Y 0O 11 56EDTCDE(1)
     A            S#DLC1         1A  O 11 70
     A            S#APCD         1A  O 11 74
     A            S#CLOC         1A  O 11 78
     A*================================================================
     A          R AR048F4C                  SFLCTL(AR048F4)
     A*%%TS  SD  19901219  164147  S02YSH      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A N80                                  SFLDSPCTL
     A N81                                  SFLDSP
     A N82                                  SFLEND
     A  83                                  SFLCLR
     A                                      OVERLAY
     A                                  4  2'銀行代號:'
     A            S#BANK         3A  O  4 16
     A            S#BANM        10A  O  4 21
     A                                  5  2'客戶代號:'
     A            S#CUNO         6A  O  5 16
     A            S#CUNM        10A  O  5 28
     A                                  6  2'訂單號碼:'
     A            S#ORNO         6A  O  6 16
     A            S#LIN7        78A  O  7  2
     A                                  8 68'下　確　結'
     A                                  9  2'發票日期'
     A                                  9 13'發票號碼'
     A                                  9 25'承購金額'
     A                                  9 41'預支金額'
     A                                  9 57'沖銷金額'
     A                                  9 68'載　認　案'
     A            S#LIN8        78A  O 10  2
     A*================================================================
     A          R AR048F4M
     A*%%TS  SD  19901219  164147  S02YSH      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A            S#LIN9        78A  O 21  2
     A            S#AAMT        12Y 0O 22 20EDTCDE(1)
     A                                      DSPATR(HI)
     A            S#BAMT        12Y 0O 22 36EDTCDE(1)
     A                                      DSPATR(HI)
     A            S#CAMT        12Y 0O 22 52EDTCDE(1)
     A                                      DSPATR(HI)
     A                                 23  2'F3=結束作業'
     A                                 23 16'F12=回前畫面'
     A                                 23 31'PA1=下頁'
     A                                 23 42'PA2=上頁'
