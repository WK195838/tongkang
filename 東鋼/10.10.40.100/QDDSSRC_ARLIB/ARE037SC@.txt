     A*%%TS  SD  19910506  162415  S02YSH      REL-V4R4M0  5769-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE037SC
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02YSH
     A*            4.FUNCTION     新版營業繳款輸入程式（外銷專用)
     A*            5.DATE-WRITTEN  91/05/06
     A*            6.DATE-UPDATED  91/05/06
     A*
     A***************************************************************
     A*  畫面一:新增、修改、刪除、查詢、沖銷、複製、還原複製畫面
     A*
     A*  *IN33 : ON 表示為查詢狀態
     A***************************************************************
     A          R AR037F1                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT1         1Y 0B  9  4VALUES(0 2 3 4 5 6 8 9)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(RI)
     A            S#CUNO         6A  O  9  8
     A            S#CUNM        10O  O  9 16
     A            S#RVDT         8Y 0O  9 27EDTWRD('    /  /  ')
     A            S#RVNO         6A  O  9 39
     A            S#XAMT        11Y 0O  9 46EDTCDE(3)
     A            S#NAMT        11Y 0O  9 59EDTCDE(3)
     A            S#FL01         1A  O  9 73
     A            S#FL03         1A  O  9 75
     A            S#FL02         1A  O  9 77
     A***************************************************************
     A          R AR037F1C                  SFLCTL(AR037F1)
     A*%%TS  SD  19910429  100859  S02LYW      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF06(06)
     A                                      CF12(12)
     A                                      ROLLUP(91)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE037SC-1'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 28'★外銷繳款明細輸入作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A                                  4  2'選項:'
     A                                  4 11'2:修改'
     A                                    +1'3:試算'
     A                                    +1'4:刪除'
     A                                    +1'5:查詢'
     A                                    +1'6:沖銷'
     A                                    +1'8:確認'
     A                                    +1'9:確認還原'
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6  2'選項'
     A                                  6  9'編號'
     A                                  6 16'客　戶'
     A                                  6 28'日期'
     A                                  6 48'沖銷合計'
     A                                  6 61'繳款合計'
     A                                  6 72'確印過'
     A                                  7  2'-----------------------------------
     A                                      ------------+---+---+----+---+---+--
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#KEY1         6A  B  8  8
     A  30                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A***************************************************************
     A          R AR037F1M                  OVERLAY
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------+---+---+----+---+---+--
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  2'F03:結束'
     A                                    +1'F12:回前'
     A                                    +1'F06:新增'
     A                                    +1'PGUP:上頁'
     A                                    +1'PGDN:下頁'
     A                                    +1'ENTER:執行'
     A            S#ERR1        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面二:輸入繳款明細畫面
     A***************************************************************
     A          R AR037F2                   SFL
     A*%%TS  SD  19910506  153054  S02YSH      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#SFI2         2Y 0O 12  2
     A            S#ACTP         1A  B 12  7VALUES(' ' 'C' 'D')
     A  33                                  DSPATR(PR)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A            S#USTP         2A  B 12 10DUP(35)
     A  33                                  DSPATR(PR)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A            S#NTTP         1A  B 12 14DUP(36)
     A  33                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A            S#ACNO         4A  B 12 18
     A  33                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A            S#PBID        14O  B 12 26DUP(37)
     A  33                                  DSPATR(PR)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A            S#NTNO        10A  B 12 42
     A  33                                  DSPATR(PR)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A            S#DUDT         8Y 0B 12 56EDTWRD('    /  /  ')
     A  33                                  DSPATR(PR)
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A            S#SANO        10A  B 12 69
     A  33                                  DSPATR(PR)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A            S#RLNO        10E  B 13 18
     A  33                                  DSPATR(PR)
     A  49                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A            S#CURY         3A  B 13 32
     A  33                                  DSPATR(PR)
     A  50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A            S#RAMT        11Y 2B 13 38EDTCDE(2)
     A  33                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A            S#EXC2         8Y 4B 13 56EDTCDE(2)
     A  33                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A***************************************************************
     A          R AR037F2C                  SFLCTL(AR037F2)
     A*%%TS  SD  19910506  153054  S02YSH      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A                                      CF01(01)
     A                                      CF03(03)
     A N33                                  CF10(10)
     A                                      CF12(12)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE037SC-2'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 28'★外銷繳款明細輸入作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A            S#SF2T         6O  O  3 70DSPATR(RI)
     A                                  3  2'繳款編號:'
     A            S#RVNO         6A  O  3 14
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  B  4 14
     A  61
     AO 33                                  DSPATR(PR)
     A  34                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                  4 22'客戶名稱:'
     A            S#CUNM        10O  O  4 34
     A                                  4 46'繳款日期:'
     A            S#RVDT         8Y 0B  4 58EDTWRD('    /  /  ')
     A  61
     AO 33                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A                                  5  2'繳款部門:'
     A            S#DPNO         4A  B  5 14
     A  61
     AO 33                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A                                  5 20'收款業務:'
     A            S#RVID         1A  B  5 32
     A  61
     AO 33                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A                                  5 34'財會匯率:'
     A            S#EXC1         8Y 4B  5 46EDTCDE(2)
     A  61
     AO 33                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A                                  5 57'報關匯率:'
     A            S#EXC3         8Y 4B  5 69EDTCDE(2)
     A  61
     AO 33                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  6 49'繳款金額合計:'
     A            S#NAMT        11Y 0O  6 65EDTCDE(1)
     A                                      DSPATR(HI)
     A N33                              6  3'借貸方清成空白，該項明細視同刪除-
     A                                      '
     A                                      DSPATR(RI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  1'項　借　款　票'
     A                                  8 51'　'
     A                                  9  1'　　貸　項　款　會計科目'
     A                                  9 29'付款銀行　　票據號碼'
     A                                  9 52'　　到期日期　　存入帳號'
     A                                 10  1'次　方　別　別　相關號碼'
     A                                 10 31'幣別　　　金　　　額'
     A                                 10 56'銀行匯率'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR037F2M                  OVERLAY
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  4'F01:說明'
     A                                 23 19'F03:結束'
     A                                 23 34'F12:回前'
     A  33                             23 49'ENTER:執行'
     A N33                             23 49'ENTER:檢核'
     A N33                             23 66'F10:存檔'
     A            S#ERR2        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A* 畫面二：輸入資料說明視窗
     A***************************************************************
     A          R AR037F2W
     A                                      WINDOW(8 14 14 62)
     A                                      TEXT('說明視窗')
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A                                  2  3'款項別'
     A                                      DSPATR(HI)
     A                                  2 18'票款別'
     A                                      DSPATR(HI)
     A                                  4  1'A1:一般貨款'
     A                                  4 16'A:支票'
     A                                  5  1'A2:預收貨款'
     A                                  5 16'B:背書支票'
     A                                  6  1'A3:延票利息'
     A                                  6 16'C:本票'
     A                                  7  1'A4:換票繳入'
     A                                  7 16'D:背書本票'
     A                                  8  1'A5:暫收款項'
     A                                  8 16'E:銀行本支票'
     A                                  9  1'Z5:其他'
     A                                  9 16'F:匯票'
     A                                 10 16'G:匯款'
     A                                 11 16'H:現金'
     A                                 12 16'I:信用狀'
     A                                 13 16'J:銀行存款'
     A                                  2 40'常用會計科目'
     A                                      DSPATR(HI)
     A                                  4 32'1114外幣存款'
     A                                  4 48'8112兌換損失'
     A                                  5 32'7119出口費用'
     A                                  5 48'8211利息費用'
     A                                  6 32'8246奇零尾數'
     A                                 11 32'7111（推）運　費'
     A                                 12 32'7112（推）檢驗費'
     A                                 13 32'7142（推）郵電費'
     A***************************************************************
     A*  畫面三:發票沖銷畫面
     A***************************************************************
     A          R AR037F3                   SFL
     A*%%TS  SD  19910506  162415  S02YSH      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#SFI3         2Y 0O 11  3
     A            S#INNO        10A  B 11  7
     A  33                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A            S#INXA        11Y 0B 11 18EDTCDE(3)
     A  33                                  DSPATR(PR)
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(RI)
     A            S#INOR         6A  O 11 31
     A            S#INAM        11Y 0O 11 38EDTCDE(3)
     A            S#INBA        11Y 0O 11 50EDTCDE(3)
     A            S#INAP         6A  O 11 62
     A            S#INRA        11Y 0O 11 69EDTCDE(3)
     A***************************************************************
     A          R AR037F3C                  SFLCTL(AR037F3)
     A*%%TS  SD  19910506  160004  S02YSH      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A N33                                  CF09(09)
     A N33                                  CF10(10)
     A                                      CF12(12)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A            S#NBR3         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE037SC-3'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 28'★外銷繳款明細輸入作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A            S#SF2T         6O  O  3 70DSPATR(RI)
     A                                  3  2'繳款編號:'
     A            S#RVNO         6A  O  3 14
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  B  4 14
     A  61
     AO 33                                  DSPATR(PR)
     A  34                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                  4 22'客戶名稱:'
     A            S#CUNM        10O  O  4 34
     A                                  4 46'繳款日期:'
     A            S#RVDT         8Y 0B  4 58EDTWRD('    /  /  ')
     A  61
     AO 33                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A                                  5  2'繳款部門:'
     A            S#DPNO         4A  B  5 14
     A  61
     AO 33                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A                                  5 20'收款業務:'
     A            S#RVID         1A  B  5 32
     A  61
     AO 33                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A                                  5 34'財會匯率:'
     A            S#EXC1         8Y 4B  5 46EDTCDE(2)
     A  61
     AO 33                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A                                  5 57'報關匯率:'
     A            S#EXC3         8Y 4O  5 69EDTCDE(2)
     A                                  6 49'繳款金額合計:'
     A            S#NAMT        11Y 0O  6 65EDTCDE(1)
     A                                  6  2'沖銷金額合計:'
     A            S#XAMT        11Y 0O  6 18EDTCDE(1)
     A                                      DSPATR(HI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A N33                              8  3'清除發票號碼該筆明細視同刪除'
     A                                      DSPATR(RI)
     A                                  9  7'發票號碼'
     A                                  9 20'沖銷金額'
     A                                  9 31'訂單'
     A                                  9 39'發票金額'
     A                                  9 51'發票餘額　請款單'
     A                                  9 71'未過已沖'
     A                                 10  2'------------------+---+---+--------
     A                                      ---+---+---+---+---+---+------------
     A                                      +---+---+'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR037F3M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 21  2'------------------+---+---+--------
     A                                      ---+---+---+---+---+---+------------
     A                                      +---+---+'
     A                                      DSPATR(HI)
     A                                 22  4'F03:結束'
     A                                 22 23'F12:回前'
     A  33                             22 48'ENTER:執行'
     A N33                             22 48'ENTER:檢核'
     A N33                             23  4'F06:重設'
     A N33                             23 23'F09:依訂單載入'
     A N33                             23 48'F10:存檔'
     A            S#ERR3        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面四:依訂單載入發票
     A***************************************************************
     A          R AR037F3W
     A*%%TS  SD  19910312  160642  S02YSH      REL-V4R4M0  5769-PW1
     A                                      WINDOW(8 44 8 24)
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      CF03(03)
     A                                      CF12(12)
     A                                  2  7'*訂單載入*'
     A                                  4  2'訂單單號:'
     A            S#ORNO         6A  B  4 14
     A  60                                  DSPATR(RI)
     A                                  6  3'F3/F12:離開'
     A                                      DSPATR(HI)
     A            S#ERRW        20A  O  7  2DSPATR(HI)
