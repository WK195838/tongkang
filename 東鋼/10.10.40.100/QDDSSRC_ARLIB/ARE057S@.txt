     A*%%TS  SD  19940106  145759  S02CSF      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*         >>   PROGRAM  INTRODUCTION  <<
     A*         1.PROGRAM-ID      ARE056S
     A*         2.PROGRAM-TYPE    DSPF
     A*         3.AUTHOR          S02CSF
     A*         4.FUNCTION       新版發票作廢作業
     A*         5.DATE-WRITTEN    93/12/30
     A*         6.DATE-MODIFY
     A*
     A*    *IN37 PROTECT燈號
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前畫面')
     A*****************************************************************
     A          R AR057H
     A*%%TS  SD  19931231  113702  S02CSF      REL-V4R4M0  5769-PW1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 26'東和鋼鐵企業股份有限公司'
     A                                  1 70'ARE057S'
     A                                  2  2TIME
     A                                  2 30'新版發票作廢作業'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            U#USID        10A  O  3  2
     A N38        S#MODE         6A  O  3 70DSPATR(RI)
     A*================================================================
     A          R AR057F1
     A*%%TS  SD  19931230  181319  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                  6 34'功能:'
     A            S#CODE         1A  B  6 42
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                  6 44'(1:做廢發票2查詢發票)'
     A                                  8 22'發票作廢申請單號:'
     A            S#DLNO         6A  B  8 42
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A            S#ERR1        70A  O 23  2DSPATR(HI)
     A                                 24  5'PF03:結束'
     A                                 24 20'PF12:回前'
     A***************************************************************
     A          R AR057F2                   SFL
     A*%%TS  SD  19940105  155800  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT          1A  B 11  5VALUES(' ' '5')
     A                                      DSPATR(PC)
     A            S#INNO        10A  O 11  8
     A            S#INDT         8Y 0O 11 19EDTWRD('    /  /  ')
     A            S#AMT1         8Y 0O 11 30EDTCDE(1)
     A            S#BAM1         8Y 0O 11 41EDTCDE(J)
     A            S#TAX1         8Y 0O 11 53EDTCDE(1)
     A            S#AAMT         9Y 0O 11 64EDTCDE(1)
     A            S#DECD         1A  O 11 77
     A*================================================================
     A          R AR057F2C                  SFLCTL(AR057F2)
     A*%%TS  SD  19940105  155800  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0011)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A                                      OVERLAY
     A                                      CF10(10 '存檔')
     A                                  5  2'原客戶代號:'
     A            S#CUNO         6A  O  5 16
     A            S#CUNM        10A  O  5 25
     A                                  5 59'申請日期:'
     A            S#APDT         8Y 0O  5 71EDTWRD('    /  /  ')
     A                                  6  2'原訂單編號:'
     A            S#ORNO         6A  O  6 16
     A                                  6 24'發票廠區:'
     A            S#AREA         1A  O  6 36
     A                                  6 59'申請單號:'
     A            S#DLNO         6A  O  6 73DSPATR(HI)
     A                                  8  3'5:查詢出貨磅單明細'
     A                                      DSPATR(HI)
     A                                  9  2'選項'
     A                                  9  9'發票號碼'
     A                                  9 20'發票日期'
     A                                  9 31'發票金額'
     A                                  9 43'扣預收'
     A                                  9 54'稅　額'
     A                                  9 64'應收金額'
     A                                  9 75'作廢'
     A            S#LIN1        78A  O 10  2
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR057F2M
     A*%%TS  SD  19931230  182547  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                 23  5'F3=結束'
     A                                 23 15'F12=回前畫面'
     A N37                             23 30'F10=確認作廢'
     A            S#LIN2        78A  O 22  2
     A            S#ERR2        70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR057F3                   SFL
     A*%%TS  SD  19940106  145710  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#WTNO        11A  O 11  3
     A            S#PDN1         3A  O 11 16
     A            S#QTY1         7Y 0O 11 21EDTCDE(J)
     A            S#PRC1         5Y 2O 11 32EDTCDE(1)
     A            S#AMT1         8Y 0O 11 39EDTCDE(J)
     A            S#QTY3         7Y 0O 11 51EDTCDE(J)
     A                                      COLOR(RED)
     A            S#PRC3         5Y 2O 11 62EDTCDE(J)
     A                                      COLOR(RED)
     A            S#AMT3         8Y 0O 11 70EDTCDE(J)
     A                                      COLOR(RED)
     A            S#QTY2         7Y 0O 12 23EDTCDE(1)
     A                                      COLOR(BLU)
     A            S#PRC2         5Y 2O 12 33EDTCDE(1)
     A                                      COLOR(BLU)
     A            S#AMT2         8Y 0O 12 40EDTCDE(1)
     A                                      COLOR(BLU)
     A            S#TAX2         6Y 0O 12 52EDTCDE(1)
     A                                      COLOR(BLU)
     A            S#AAMT         9Y 0O 12 61EDTCDE(1)
     A                                      COLOR(BLU)
     A***************************************************************
     A          R AR057F3C                  SFLCTL(AR057F3)
     A*%%TS  SD  19940106  145759  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A N81                                  SFLDSPCTL
     A N82                                  SFLDSP
     A  83                                  SFLCLR
     A N84                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A                                  4  2'新客戶代號：'
     A            S#CUNN         6A  O  4 17
     A            S#CUMM        10A  O  4 24
     A                                  4 37'新訂單編號：'
     A            S#ORNN         6A  O  4 52
     A            S#LIN3        78A  O  5  2
     A                                  6  2'|<-'
     A                                  6 10'交　　　　貨　　　　資　　　　料-
     A                                      '
     A                                  6 50'->|<-'
     A                                  6 59'差　異　資　料'
     A                                      COLOR(RED)
     A                                  6 78'->|'
     A                                  7  2'　磅單編號　品名'
     A                                  7 23'數　量'
     A                                  7 32'單價'
     A                                  7 40'金　額'
     A                                  7 53'數　量'
     A                                      COLOR(RED)
     A                                  7 63'單價'
     A                                      COLOR(RED)
     A                                  7 71'金　額'
     A                                      COLOR(RED)
     A                                  8 21'|<-'
     A                                      COLOR(BLU)
     A                                  8 25'　新　　開　　立　　發　　票　　+
     A                                      內　　容　'
     A                                      COLOR(BLU)
     A                                  8 70'->|'
     A                                      COLOR(BLU)
     A                                  9 25'數　量'
     A                                      COLOR(BLU)
     A                                  9 34'單價'
     A                                      COLOR(BLU)
     A                                  9 42'金　額'
     A                                      COLOR(BLU)
     A                                  9 53'稅額'
     A                                      COLOR(BLU)
     A                                  9 62'應收帳款'
     A                                      COLOR(BLU)
     A            S#LIN4        78A  O 10  2
     A***************************************************************
     A          R AR057F3M
     A*%%TS  SD  19931229  160948  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A            S#LIN5        78A  O 22  2
     A                                 23  2'F03:結束'
     A                                 23 13'F12:回前'
     A            S#ERR3        70A  O 24  2DSPATR(HI)
