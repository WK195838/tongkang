     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE007S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     折讓註銷作業
     A*            5.DATE-WRITTEN  107/11/23  2018AR00030
     A*            6.DATE-MODIFY
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARE007S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 30'*** 折讓註銷作業 ***  '
     A            DEVNM         10A  O  2 70
     A                                  8 30'折讓單號:'
     A            NO             6A  B  8 42
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            ITEM           2Y 0O 11  4
     A            INVOX         10A  B 11  9DUP(21)
     A  70                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A            ACNT           1A  B 11 21
     A  70                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A            PDNM           5A  B 11 25
     A  70                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A            QTY            7Y 0B 11 32EDTWRD(' ,   ,   -')
     A  70                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A            UPRC           5Y 3B 11 43EDTWRD('0 .   ')
     A  70                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A            AMT            9Y 0B 11 50EDTWRD('   ,   ,   -')
     A  70                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A            TAX            9Y 0B 11 63EDTWRD('   ,   ,   -')
     A  70                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A            WHY            1A  B 11 77VALUES('1' '2' '3' '4' '5' '6' '7' +
     A                                             '8' '9' ' ')
     A  70                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A            FLAG           1A  B 11 79
     A  70                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A            ORNO           6A  H
     A            RVID           2A  H
     A            SALE           2A  H
     A            SATP           1A  H
     A            KIND           1A  H
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10
     A                                      CF12(12)
     A                                      CF05(05)
     A                                      CF08(08)
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARE007S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 30'*** 折讓註銷作業 ***'
     A            DEVNM         10A  O  2 70
     A                                  4  5'折讓單號:'
     A            NO             6A  O  4 17
     A                                  4 30'折讓種類:'
     A            S#TYP1         1A  B    +1VALUES('1' '2')
     A  70                                  DSPATR(PR)
     A            S#TYNM        10O  O    +1
     A                                  5  5'客戶編號:'
     A            CUNO           6A  B  5 17
     A  70                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A                                  5 30'客戶名稱:'
     A            CUNM          10O  O  5 42
     A                                  5 55'訂單編號:'
     A            S#ORNO         6A  O    +1
     A                                  6  5'開立日期:'
     A            DATE           8Y 0B  6 17EDTWRD('    /  /  ')
     A  70                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A                                  6 30'確認日期:'
     A            ACDT           8Y 0B  6 42EDTWRD('    /  /  ')
     A  72                                  DSPATR(PR)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A                                  6 55'入帳年月:'
     A            ACYM           6Y 0B  6 67EDTWRD('    /  ')
     A  72                                  DSPATR(PR)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8 76'原'
     A                                  9  2'項次　發票號碼　種類品名'
     A                                  9 32'數　量'
     A                                  9 42'單價　　折讓金額'
     A                                  9 64'營業稅額　　因'
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      -------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND01
     A                                      OVERLAY
     A                                 21  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------'
     A                                      DSPATR(HI)
     A                                 22 33'合　計:'
     A            TTLAMT         9  0O 22 50EDTWRD('   ,   ,   -')
     A            TTLTAX         9  0O 22 63EDTWRD('   ,   ,   -')
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'F03:結束'
     A                                    +1'F05:修改開立日'
     A                                    +1'F08:確認還原'
     A                                    +1'F12:回前畫面'
     A***************************************************************
     A          R AR07F3
     A                                      WINDOW(7 23 10 48)
     A                                      WDWBORDER((*COLOR PNK) -
     A                                       (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A                                  3  3'開立日期:'
     A            S@YMDA         8  0B    +1EDTWRD('    /  /  ')
     A  34                                  DSPATR(PC RI)
     A                                  5  3'是否至財政部平台做註銷:'
     A            S@YN           1   B    +1
     A  35                                  DSPATR(PC RI)
     A                                    +1'(Y)'
     A                                  7  3'確認尚未做營業稅申報:'
     A            S@YN1          1   B    +1
     A  36                                  DSPATR(PC RI)
     A                                    +1'(Y)'
     A            S@ERR1        46      8  2COLOR(RED)
     A                                  9  2'操作提示:' COLOR(BLU)
     A                                    +1'F3,F12=回前螢幕'
     A                                      COLOR(BLU)
     A*****************************************************************
     A          R AR07F4
     A                                      WINDOW(7 23 10 48)
     A                                      WDWBORDER((*COLOR PNK) -
     A                                       (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A                                  3  3'確認還原單號:'
     A            S1NO           6   O    +1
     A                                  5  3'是否至財政部平台做註銷:'
     A            S1YN           1   B    +1
     A  37                                  DSPATR(PC RI)
     A                                    +1'(Y)'
     A                                  7  3'確認尚未做營業稅申報:'
     A            S1YN1          1   B    +1
     A  38                                  DSPATR(PC RI)
     A                                    +1'(Y)'
     A            S1ERR1        46      8  3COLOR(RED)
     A                                  9  2'操作提示:' COLOR(BLU)
     A                                    +1'F3,F12=回前螢幕'
     A                                      COLOR(BLU)
     A*****************************************************************
