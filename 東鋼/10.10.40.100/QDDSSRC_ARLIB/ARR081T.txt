     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR081T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     信用狀押匯提示明細表
     A*           4.DATE-WRITTEN  88/12/03
     A*           5.WRITER        S02CLJ
     A*           6.UPDATE  DATE  112/06/15 2023AR00031 S00WCJ (1206A)
     A*                           入帳銀行欄位帶入託收帳號；增加電子
     A*                           押匯欄位
     A*
     A*****************************************************************
     A          R AR081T1H
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報表代號: ARR081T'
     A                                    53
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   113
     A                                      '列表日期:'
     A                                   124
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                    55
     A                                      '信用狀押匯提示明細表'
     A                                   113
     A                                      '列表時間:'
     A                                   124
     A                                      TIME
     A                                     2
     A                                      '押匯日期:'
     A                                      SPACEB(001)
     A            R#RVDT         8S 0O    14
     A                                      EDTWRD('    /  /  ')
     A                                   113
     A                                      '頁　　次:'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*----------------------------------------------------------------*********
     A                                     5
     A                                      '客戶'
     A                                      SPACEB(002)
     A                                    18
     A                                      '信用狀號碼'
     A                                    42
     A                                      '押匯金額'
     A                                    57
     A                                      '開狀銀行'
     A                                    72
     A                                      '入帳銀行'
     A                                    86
     A                                      '郵電費'
     A                                    97
     A                                      '信用狀餘額'
     A                                   111
     A                                      'ＬＣ到期日'
     A                                    +4
     A                                      '帳戶及餘額'
1206AA                                    +1
1206AA                                      '電子押匯'
     A                                     2
     A                                      '------'
     A                                      SPACEB(001)
     A                                     8
     A                                      '----------'
     A                                    19
     A                                      '--------------------'
     A                                    40
     A                                      '--------------'
     A                                    55
     A                                      '--------------'
     A                                    +1
     A                                      '--------------'
     A                                    +1
     A                                      '----------'
     A                                    96
     A                                      '---------------'
     A                                   112
     A                                      '----------'
     A                                    +1
     A                                      '-------------------'
1206AA                                    +0
1206AA                                      '---------'
     A**************************************************************************
     A          R AR081T1D
     A                                      SPACEA(002)
     A            R#CUNO         6A  O     2
     A            R#CUNM        10O  O     8
     A            R#LLNO        20A  O    19
     A            R#RAMT        11S 0O    40
     A                                      EDTCDE(2)
     A            R#PBID        14O  O    55
     A            R#BANK        14O  O    +1
     A            R#LAMT        11S 0O    96
     A                                      EDTCDE(K)
     A            R#LEDT         8S 0O   112
     A                                      EDTWRD('    /  /  ')
     A            R#PBAL        11S 0O    +5
     A                                      EDTCDE(K)
1206AA            R#ELCD         1A  O   145
     A**************************************************************************
     A          R AR081T1T
     A                                      SPACEB(002)
     A                                    16
     A                                      '**本日合計:'
     A            R#TRNA        11S 0O    30
     A                                      EDTCDE(2)
     A*****************************************************************
     A          R AR081T1M
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A                                    19
     A                                      '*****無條件符合之資料！！'
     A**************************************************************************
