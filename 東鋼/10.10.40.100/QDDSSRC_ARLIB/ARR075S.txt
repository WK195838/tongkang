     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARR075RS
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     新版發票列印作業（畫面）
     A*            5.DATE-WRITTEN  88/03/13
     A*                           107/11/11 S00WCJ (0711A)
     A*                            增加日期及發票號碼檢核
     A*                           107/12/27 S00WCJ (0712A)
     A*                            增加注意事項說明
     A*                           109/04/14 2020AR00017 S00WCJ (0904A)
     A*                            增加業務員篩選欄位
     A*
     A***************************************************************
     A*  畫面一:輸入廠區及發票日期
     A*
     A*        記錄格式 AR075F1
     A***************************************************************
     A          R AR075F1
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      CF10(10)
0711AA                                      CF13(13)
0711AA                                      CF23(23)
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARR075S'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'★新版發票列印作業★'
     A                                      DSPATR(RI)
     A*----------------------------------------------------------------
     A            S#DEVI        10A  O  2 70
     A                                  5 13'列印廠別:'
     A            S#AREA         1A  O  5 25
     A                                  5 28'( P台北 M台中 T桃園  H苗栗-
0711AA                                       K高雄U新桃)'
0712AA                                  6  8'請注意：務必確認發票開立+
0712AA                                      作業在批次作業中已執行完成，+
0712AA                                      才可執行本作業'
0712AA                                      DSPATR(HI)
     A                                  7  9'-----------------------------------
     A                                      -------------------------------'
     A                                  9 36'起始值'
     A                                      DSPATR(RI)
     A                                  9 53'終止值'
     A                                      DSPATR(RI)
     A                                 11 18'發票日期區間:'
     A            S#FRDT         8Y 0B 11 36EDTCDE(4)
0711AA  33                                  DSPATR(PC RI)
     A                                 11 48'--'
     A            S#TODT         8Y 0B 11 53EDTCDE(4)
0711AC  34                                  DSPATR(PC RI)
     A                                 13 18'發票號碼區間:'
     A            S#FRNO        10A  B 13 36
0711AC  35                                  DSPATR(PC RI)
     A                                 13 48'--'
     A            S#TONO        10A  B 13 53
0711AC  36                                  DSPATR(PC RI)
0904AC                                 15 18'      業務員:'
0904AC            S#SALE         1A  B 15 36
0904AC                                    +1'(空白表全部)'
0904AA                                 16  9'-----------------------------------
     A                                      -------------------------------'
0904AA                                 18 23'(起始值空白表示不採該方式來列印+
     A                                      ，'
0904AA                                 19 24'終止值空白表示只採起始值來列印。-
     A                                      '
0904AA                                 20 24'可同時設定日期及號碼區間來列印。-
     A                                      )'
     A*----------------------------------------------------------------
     A                                 22 12'F3:結束'
     A                                 22 26'F12:回前'
     A                                 22 41'ENTER:執行'
     A  87                             22 54'F13:正本重印'
     A  87                             22 69'F23:補印'
     A            S#MSG1        70A  O 24  2DSPATR(HI)
