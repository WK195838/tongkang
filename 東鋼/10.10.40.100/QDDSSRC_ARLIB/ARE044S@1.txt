     A*%%TS  SD  19890706  154033  S02HYS      REL-V3R7M0  5716-PW1
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARZ044S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02HYS
     A*            4.FUNCTION     專案退稅明細輸入
     A*            5.DATE-WRITTEN  89/04/05
     A*            6.DATE-MODIFY   86/04/05
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束作業')
     A                                      CA12(12 '回前畫面')
     A          R ARE044H
     A*%%TS  SD  19890406  093540  S02HYS      REL-V3R7M0  5716-PW1
     A                                      OVERLAY
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 24'も　東和鋼鐵企業股份有限公司　も-
     A                                      '
     A            S#SRID        10A  O  1 70
     A                                  2  2TIME
     A                                  2 30'★專案退稅明細輸入★'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3  2
     A  30        S#MODE        10A  O  3 70
     A                                      DSPATR(RI)
     A          R ARE0F1
     A*%%TS  SD  19890405  145056  S02HYS      REL-V3R7M0  5716-PW1
     A                                      OVERLAY
     A                                 10 22'訂單號碼：'
     A            S#KEY1         6A  B 10 35
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A                                 11 22'歸屬年月：'
     A            S#KEY2         4S 0B 11 35
     A                                 11 41'(空白表全部)'
     A                                 23  2'F3=結束作業 F12=回前畫面'
     A            S#ERR         70A  O 24  2DSPATR(HI)
     A          R ARE0F2
     A                                      SFL
     A*%%TS  SD  19890406  094505  S02HYS      REL-V3R7M0  5716-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT          1  0B 13  4EDTCDE(Z) VALUES(0 2 4 5)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A            S#YYMX         4  0O 13  9EDTCDE(Y)
     A            S#IVNX        10A  O 13 22
     A            S#DEDT         6Y 0O 13 39EDTCDE(4)
     A            S#ATT1        11Y 0O 13 50EDTCDE(K)
     A            S#VUNX         7Y 0O 13 66EDTCDE(4)
     A            S#ITEM         3  0H
     A************
     A          R ARE0CL                    SFLCTL(ARE0F2)
     A*%%TS  SD  19890406  093540  S02HYS      REL-V3R7M0  5716-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  74                                  SFLCLR
     A                                      CF06(06 '新增')
     A                                      CF05(05 '查詢')
     A                                      ROLLUP(91 '次頁')
     A                                      OVERLAY
     A                                  5  2'訂單號碼﹔'
     A            S#ORNO         6A  O  5 15
     A                                  6  2'客戶編號﹔'
     A            S#CUNO         6A  O  6 15
     A                                  6 24'客戶名稱﹔'
     A            S#CUNM        10A  O  6 37
     A                                  7  2'歸屬廠區﹔'
     A            S#AREA         1A  O  7 15
     A                                  8  3'OP= 2:修改 4:刪除 5:查詢'
     A                                 10  3'OP'
     A                                 10  7'歸屬年月'
     A                                 10 22'發票號碼'
     A                                 10 37'發票日期'
     A                                 10 55'稅額'
     A                                 10 64'傳票號碼'
     A            S#1           80A  O 11  1
     A            S#KEY3         4  0B 12  9
     A            S#NBR          4  0H      SFLRCDNBR(CURSOR)
     A          R ARE0MG
     A*%%TS  SD  19890406  093540  S02HYS      REL-V3R7M0  5716-PW1
     A                                      OVERLAY
     A                                 23  2'PF3=結束作業'
     A                                 23 17'PF12=回前螢幕'
     A                                 23 33'PF6=新增'
     A                                 23 44'PA1=上頁'
     A                                 23 55'PA2=下頁'
     A                                    +1'PF5=單據查詢'
     A            S#ERR         70A  O 24  2DSPATR(HI)
     A*****************
     A          R ARE0F3
     A*%%TS  SD  19890406  092255  S02HYS      REL-V3R7M0  5716-PW1
     A                                      OVERLAY
     A                                      CF10(10 '存檔')
     A                                      CF07(07 '查詢')
     A                                  5  2'訂單:'
     A            S#ORN1         6A  O  5 10
     A  29                                  DSPATR(RI)
     A  29                                  DSPATR(PC)
     A                                  6  2'客戶:'
     A            S#1UNM        10A  O  6 10
     A                                  7  2'歸屬廠區:'
     A            S#ARE1         1A  O  7 14
     A                                  8  2'歸屬年月'
     A            S#YYMM         6  0B  8 13
     A  85                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                  9  2'作廢發票號碼:'
     A            S#IVNO        10A  B  9 18
     A  85                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A                                 10  2'作廢發票稅額:'
     A            S#ATTX        11  0O 10 18
     A                                      EDTCDE(K)
     A                                 11  2'單據別:'
     A            S#CODE         1A  B 11 12
     A  85                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
9109 A                                 11 14'1:磅單2:調整單3:預收貨款'
     A                                 12  2'單據號碼:'
     A            S#VNNO         8A  B 12 14
     A  85                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A                                 13  2'傳票號碼:'
     A            S#VUNO         7Y 0B 13 14EDTCDE(4)
     A  85                                  DSPATR(PR)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A                                 23  2'PF3=結束作業 PF12=回前螢幕'
     A  38                                +1'PF10=存檔'
     A  38                                +1'執行鍵:檢核'
     A* 38                                +1'PF7=單據號碼查詢'
     A                                      DSPATR(RI)
     A                                      DSPATR(PC)
     A            S#ERR         70A  O 24  2DSPATR(HI)
     A*****************************************************************
     A          R ARE04W                    SFL
     A*%%TS  SD  19890706  152048  S02HYS      REL-V3R7M0  5716-PW1
     A            S#IVNW        10A  O  6  3
     A            S#CODW         4A  O  6 16
     A            S#NW           8A  O  6 24
     A*================================================================
     A          R ARE04WC                   SFLCTL(ARE04W)
     A*%%TS  SD  19890706  150928  S02HYS      REL-V3R7M0  5716-PW1
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0005)
     A                                      WINDOW(5 38 16 38)
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      SFLDSPCTL
     A N86                                  SFLDSP
     A N87                                  SFLEND
     A  88                                  SFLINZ
     A  89                                  SFLCLR
     A                                      OVERLAY
     A                                  1 27'ARE04W'
     A                                  2  9'*單據號碼挑選畫面*'
     A                                  4  3'發票號碼'
     A                                  4 14'單據別'
     A                                  4 23'單據號碼'
     A                                  5  2'-----------------------------------
     A                                      -'
     A            S#NBRW         4S 0H      SFLRCDNBR(CURSOR)
     A*****************************************************************
     A          R ARE04WM
     A*%%TS  SD  19890706  154033  S02HYS      REL-V3R7M0  5716-PW1
     A                                      WINDOW(ARE04WC)
     A                                      OVERLAY
     A                                 12  2'-----------------------------------
     A                                      -'
     A                                 13  2'按執行鍵帶出單據號碼'
     A                                 14  3'F3/F12 =回前畫面'
     A            S#ARR         18A  O 15  2DSPATR(HI)
