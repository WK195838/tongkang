     A*%%TS  SD  19921204  133203  S02CSF      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE083S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02YSH
     A*            4.FUNCTION     承購發票下載試算作業--確認
     A*            5.DATE-WRITTEN  91/02/06
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束作業')
     A                                      CA12(12 '回前畫面')
     A*================================================================
     A          R AR083H
     A*%%TS  SD  19901110  155843  S02YSH      REL-V4R4M0  5769-PW1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 26'も東和鋼鐵企業股份有限公司も'
     A            S#SRID        10A  O  1 70
     A                                  2  2TIME
     A                                  2 28'★承購發票下載試算確認★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A            S#USER        10A  O  3 70
     A                                  5  2'銀行代號:'
     A            S#BKID         3A  O    +1
     A            S#BKNM        10A  O    +1
     A                                  6  2'客戶代號:'
     A            S#CUN1         6A  O    +1
     A            S#CUM1        10A  O    +1
     A                                  7  2'欲承購金額:'
     A                                      DSPATR(HI)
     A            S#AMT1         9Y 0O    +1EDTCDE(1)
     A                                      DSPATR(HI)
     A                                    +2'可承購金額:'
     A                                      DSPATR(HI)
     A            S#AMT2         9Y 0O    +1EDTCDE(1)
     A                                      DSPATR(HI)
     A                                    +2'總選取金額:'
     A                                      DSPATR(HI)
     A            S#AMT3         9Y 0O    +1EDTCDE(1)
     A                                      DSPATR(HI)
     A  30                                  DSPATR(RI)
     A                                  8  2'承購日期:'
     A N31                                  DSPATR(ND)
     A            S#APD1         6Y 0O    +1EDTCDE(Y)
     A N31                                  DSPATR(ND)
     A                                    +2'承購單號:'
     A N31                                  DSPATR(ND)
     A            S#APN1         6A  O    +1
     A N31                                  DSPATR(ND)
     A*================================================================
     A          R AR083F1                   SFL
     A*%%TS  SD  19910206  142526  S02YSH      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OPT1         1Y 0B 14  3EDTCDE(Z)
     A                                      VALUES(0 2 5)
     A            S#APD1         6Y 0O 14  6EDTCDE(Y)
     A            S#APN1         6A  O 14 18
     A            S#AMT4         9Y 0O 14 27EDTCDE(1)
     A            S#AMT5         9Y 0O 14 41EDTCDE(1)
     A*================================================================
     A          R AR083F1C                  SFLCTL(AR083F1)
     A*%%TS  SD  19910206  142526  S02YSH      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      CF05(05 '全選')
     A                                      CF10(10 '確認執行')
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  74                                  SFLCLR
     A                                      OVERLAY
     A                                 10  2'OP: 2=選取  5=明細'
     A            S#LIN1        78A  O 11  2
     A                                 12  2'OP'
     A                                 12  5'承購日期'
     A                                 12 16'承購單號'
     A                                 12 27'承購總金額'
     A                                 12 43'選取金額'
     A            S#LIN2        78A  O 13  2
     A            S#NBR1         4S 0H      SFLRCDNBR
     A*================================================================
     A          R AR083F1M
     A                                      OVERLAY
     A            S#LIN3        78   O 22  2
     A                                 23  2'F3=結束作業'
     A                                    +1'F12=回前畫面'
     A                                    +1'F5=全選'
     A                                    +1'F10=確認下載'
     A                                      DSPATR(RI)
     A                                    +1'執行鍵=試算'
     A            S#ERR1        78   O 24  2DSPATR(HI)
     A*================================================================
     A          R AR083F2                   SFL
     A*%%TS  SD  19921204  133203  S02CSF      REL-V4R4M0  5769-PW1
     A  75                                  SFLNXTCHG
     A            S#OPT2         1Y 0B 14  3EDTCDE(Z)
     A                                      VALUES(0 2 5)
     A            S#INNO        10A  O 14  6
     A            S#ORNO         6A  O 14 19
     A            S#INDT         6Y 0O 14 29EDTCDE(Y)
     A            S#INAM         9Y 0O 14 39EDTCDE(1)
     A*================================================================
     A          R AR083F2C                  SFLCTL(AR083F2)
     A*%%TS  SD  19921204  133203  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A N76                                  SFLDSPCTL
     A N77                                  SFLDSP
     A N78                                  SFLEND
     A  79                                  SFLCLR
     A                                      OVERLAY
     A                                 10  2'OP: 2=選取'
     A            S#LIN1        78A  O 11  2
     A                                 12  2'OP'
     A                                 12  6'發票號碼'
     A                                 12 17'訂單編號'
     A                                 12 28'開立日期'
     A                                 12 40'發票金額'
     A            S#LIN2        78A  O 13  2
     A            S#NBR2         4S 0H      SFLRCDNBR
     A*================================================================
     A          R AR083F2M
     A                                      OVERLAY
     A            S#LIN3        78   O 22  2
     A                                 23  2'F3=結束作業'
     A                                    +1'F12=回前畫面'
