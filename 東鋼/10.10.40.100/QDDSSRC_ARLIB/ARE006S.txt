     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE006S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR
     A*            4.FUNCTION     折讓退回維護作業
     A*            5.DATE-WRITTEN
     A*            6.DATE-MODIFY   98/01/22
     A*                            98/04/15 2009AR290 S00WCJ (9804A)
     A*                           104/08/14 2015AR951 S00WCJ (0408A)
     A*                            增加輸入鋼種尺寸長度欄位
     A*                           104/09/18 2015AR953 S00WCJ (0409A)
     A*                            產品代碼3碼擴5碼
     A*                           106/05/02 2017AR10   S00WCJ (0605A)
     A*                            增加T1欄位
     A*                           106/11/23 2017AR1112 S00WCJ (0611A)
     A*                            增加折讓種類欄位
     A*                           107/11/26 2018AR00030 S00WCJ (0711A)
     A*                            增加確認還原功能
     A*                           108/09/17 2019AR00064 S00WCJ (0809A)
     A*                            增加區間列印功能鍵
     A*                           109/04/23 2020AR00015 S00WCJ (0904A)
     A*                            增加區間確認功能
     A*                           110/10/18 S00WCJ (1010A)
     A*                            稅額欄位0由不顯示改為顯示
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
0711AA                                      CF05(05)
0809AA                                      CF07(07)
0904AA                                      CF20(20)
     A                                  1 70'ARE006S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'*** 折讓退回維護作業 ***  '
     A            DEVNM         10A  O  2 70
     A                                  5 30'功　　能:'
     A            CODE           1A  B  5 42
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  5 44'(1:新增 2:修改'
     A                                  6 45'3:刪除 4:查詢 '
     A                                  7 45'5:確認 6:列印 '
0711AA                                  8 45'9:確認還原)'
     A                                 11 30'折讓單號:'
     A            NO             6A  B 11 42
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
0611AA                                 13 30'折讓種類:'
0611AA            S#TYPE         1A  B    +1VALUES('1' '2')
0611AA  36                                  DSPATR(PC RI)
0611AA                                    +1'(1:客戶開立2:東和開立)'
0408AA*
0408AA  35                             15 30'依鋼種尺寸分攤:'
0408AA  35        S#CHYN         1A  B    +1VALUES('Y' 'N')
0408AA  35                                +1'(Y/N)'
0408AA*
0711AA  33                             17 30'開放還原單號:'
0711AA  33                                  DSPATR(HI)
0711AA  33        S#ITNO         6A  O    +1
0711AA  33                                  DSPATR(HI)
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
0809AA                                    +1'PF07:區間列印'
0904AA  35                                +1'PF20:區間確認'
0711AA  43                                +1'PF05:開放還原'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            ITEM           2Y 0O 11  4
     A            INVOX         10A  B 11  9DUP(21)
     A  70                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A            ACNT           1A  B 11 21
     A  70                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
0409AA            PDNM           5A  B 11 26
     A  70                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A            QTY            7Y 0B 11 32EDTWRD(' ,   ,   -')
     A  70                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A            UPRC           5Y 3B 11 43EDTWRD('0 .   ')
     A  70                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A            AMT            9Y 0B 11 50EDTWRD('   ,   ,   -')
     A  70                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
1010AA*           TAX            9Y 0B 11 63EDTWRD('   ,   ,   -')
1010AA            TAX            9Y 0B 11 63EDTCDE(J)
     A  70                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A            WHY            1A  B 11 77VALUES('1' '2' '3' '4' '5' '6' '7' +
     A                                             '8' '9' ' ')
     A  70                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A            FLAG           1A  B 11 79
     A  70                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
0408AA            S#METL         8A  B 12 18
0408AA  70                                  DSPATR(PR)
0408AA  60                                  DSPATR(PC RI)
0408AA            S#XHWD         5A  B 12 32
0408AA                                      CHECK(RZ)
0408AA  70                                  DSPATR(PR)
0408AA  60                                  DSPATR(PC RI)
0408AA            S#XXBT         5A  B 12 42
0408AA                                      CHECK(RZ)
0408AA  70                                  DSPATR(PR)
0408AA  60                                  DSPATR(PC RI)
0605AA            S#XXT1         3A  B 12 51
0605AA                                      CHECK(RZ)
0605AA  70                                  DSPATR(PR)
0605AA  60                                  DSPATR(PC RI)
0408AA            S#LENG         6A  B    +1
0408AA                                      CHECK(RZ)
0408AA  70                                  DSPATR(PR)
0408AA  60                                  DSPATR(PC RI)
9803 A            H#ORNO         6A  H
     A            RVID           2A  H
     A            SALE           2A  H
     A            SATP           1A  H
     A            KIND           1A  H
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
0408AA                                      SFLSIZ(0007)
0408AA                                      SFLPAG(0005)
     A                                      OVERLAY
     A                                      CF03(03)
     A N69                                  CF08(08 '依日期載入')
0408AA                                      SFLDROP(CA04)
     A                                      CF10
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARE006S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 28'*** 折讓退回維護作業 ***'
     A            DEVNM         10A  O  2 70
     A                                  4  5'折讓單號:'
     A            NO             6A  O  4 17
0611AA                                  4 30'折讓種類:'
0611AA            S#TYP1         1A  B    +1VALUES('1' '2')
0611AA  70                                  DSPATR(PR)
0611AA            S#TYNM        10O  O    +1
     A                                  5  5'客戶編號:'
     A            CUNO           6A  B  5 17
     A  70                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A                                  5 30'客戶名稱:'
     A            CUNM          10O  O  5 42
     A                                  5 55'訂單編號:'
     A            ORNO           6A  O  5 67
     A                                  6  5'開立日期:'
     A            DATE           8Y 0B  6 17EDTWRD('    /  /  ')
     A  70                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A                                  6 30'確認日期:'
     A            ACDT           8Y 0B  6 42EDTWRD('    /  /  ')
     A  72                                  DSPATR(PR)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A                                  6 55'入帳年月:'
     A            ACYM           6Y 0B  6 67EDTWRD('    /  ')
     A  72                                  DSPATR(PR)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8 76'原'
0408AA                                  8  2'項次　發票號碼　種類'
0408AA                                  8 26'品名　數　量'
0408AA                                  8 42'單價　　折讓金額'
0408AA                                  8 64'營業稅額'
0408AA                                  9  2' '
0408AA  35                                +1'F4=鋼種輸入'
0408AA                                       DSPATR(HI)
0408AA  35                              9 18'鋼 種'
0408AA  35                              9 32' H/T/D'
0408AA  35                              9 42' B/W'
0604AA  35                              9 50' T1'
0408AA  35                                +1'長度'
0408AA  35                                +1'分攤'
0408AA                                       DSPATR(HI)
0408AC  35        S1CHYN         1   O    +1 DSPATR(HI)
0408AA                                  9 76'因'
0408AA*                                 9 64'營業稅額　　因'
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      -------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND01
     A                                      OVERLAY
     A                                 21  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------'
     A                                      DSPATR(HI)
9804AA*                                22 33'合　計:'
9804AA                                 22 10'合　計:'
9804AA            TTLQTY         7  0O 22 32EDTWRD(' ,   ,   -')
     A            TTLAMT         9  0O 22 50EDTWRD('   ,   ,   -')
     A            TTLTAX         9  0O 22 63EDTWRD('   ,   ,   -')
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                    +1'PF03:結束'
     A                                    +1'PF08:依訂單及日期載入'
     A                                    +1'PF10:存檔'
     A                                    +1'PF12:回前畫面'
     A***************************************************************
     A          R SF2                       SFL
     A  89                                  SFLNXTCHG
     A            S#OPT          1A  B  9  5
     A            S#INNO        10A  O  9 11
0409AA            S#PDNO         5A  O  9 22
     A            S#QTY          7Y 0O  9 31EDTCDE(1)
     A            S#AMT          9Y 0O  9 45EDTCDE(1)
     A            S#TAX          8Y 0O  9 62EDTCDE(1)
     A            S#ITEM         3S 0H
     A***************************************************************
     A          R SFCTL2                    SFLCTL(SF2)
     A                                      SFLSIZ(0025)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CF03(03 '離開')
     A                                      CF05(05 '挑選')
     A                                      CF10(10 '存檔')
     A                                      CF12(12 '回前畫面')
     A  81                                  SFLCLR
     A N82                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A*-----------
     A                                  1 70'ARE006S-3'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 28'折讓退回單依入帳日期載入'
     A            DEVNM         10A  O  2 70
     A*-----------
     A                                  4  2'請輸入－'
     A                                  4 13'訂單編號:'
     A            S#ORNO         6A  B  4 25
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  4 33'發票日期:'
     A            S#ACDS         8Y 0B  4 45EDTCDE(Z)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  4 54'~'
     A            S#ACDE         8Y 0B  4 56EDTCDE(Z)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A                                  5 13'平均單價:'
     A            S#UPRC         5Y 3B  5 25EDTCDE(1)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  5 35'種類別:'
     A            S#ACNT         1A  B  5 45
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                  5 49'原因:'
     A            S#RESV         1A  B  5 57
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  2'挑Ｙ'
     A                                  7 10'發票號碼'
     A                                  7 22'品名'
     A                                  7 31'數　　量'
     A                                  7 46'發票金額'
     A                                  7 62'發票稅額'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND02
     A                                 21  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A                                      DSPATR(HI)
     A                                 22 12'合　計:'
     A            S#SQTY         9  0O 22 29EDTCDE(1)
     A            S#SAMT         9  0O 22 45EDTCDE(1)
     A            S#STAX         9  0O 22 62EDTCDE(1)
     A            S#ERR         70A  O 23 02DSPATR(HI)
     A*
     A                                 24  5'ENTER:檢核'
     A                                    +2'PF03:結束'
     A                                    +2'PF05:全部挑選'
     A                                    +2'PF10:確認存檔'
     A                                    +2'PF12:回前畫面'
     A***************************************************************
0904AA          R AR006W1
0904AA***************************************************************
0904AA                                      WINDOW( 6 22 14 52)
0904AA                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
0904AA                                       (*CHAR '        '))
0904AA                                      OVERLAY
0904AA                                      CF03(03)
0904AA                                      CF12(12)
0904AA                                  4  2'折讓單號:'
0904AA            S#NOS          6A  B  4 14
0904AA  37                                  DSPATR(PC RI)
0904AA                                    +1'~'
0904AA            S#NOE          6A  B    +1
0904AA  38                                  DSPATR(PC RI)
0904AA                                  6  2'確認日期:'
0904AA            S#ACDT         8Y 0O  6 14EDTWRD('    /  /  ')
0904AA  39                                  DSPATR(PC RI)
0904AA                                  8  2'入帳年月:'
0904AA            S#ACYM         6Y 0B  8 14EDTWRD('    /  ')
0904AA  40                                  DSPATR(PC RI)
0904AA                                 12  3'F3:離開'
0904AA                                      DSPATR(HI)
0904AA                                 12 13'12:回上頁'
0904AA                                      DSPATR(HI)
0904AA            S#ERW1        48A  O 13  2DSPATR(HI)
