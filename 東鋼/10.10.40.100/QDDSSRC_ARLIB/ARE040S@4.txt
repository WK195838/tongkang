     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE040R
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     發票指定開立作業
     A*            5.DATE-WRITTEN  88/02/05
     A*
     A***************************************************************
     A*  畫面一:輸入訂單
     A***************************************************************
     A          R AR040F1                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT1         1A  B 11  9VALUES('1' ' ')
     A  53                                  DSPATR(PC RI)
     A            S#ORNO         6A  O 11 15
     A            S#ORDT         8S 0O 11 23
     A            S#CTNO        10O  O 11 34
     A            S#CUNO         6A  O 11 47
     A            S#CUNM        10A  O 11 55
     A*
     A            S#PRAT         3Y 3H
     A***************************************************************
     A          R AR040F1C                  SFLCTL(AR040F1)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A            S#SFN1         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE040S-1'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  2 30'★發票指定開立作業★'
     A                                      DSPATR(RI)
     A                                  4  2'選項: 1 --> 進行該訂單之發票指+
     A                                      定開立。'
     A                                  5 16'（可輸入訂單編號或客戶編號來移至+
     A                                      最近的記錄位置）'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  5'選項'
     A                                  8 25'日期'
     A                                  8 34'購案編號'
     A                                  8 12'訂單編號'
     A                                  8 47'客戶編號及名稱'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#INK1         6A  B 10 15
     A  51                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A*
     A            S#CNK1         6A  B 10 47
     A  55                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A***************************************************************
     A          R AR040F1M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23 14'F03:結束'
     A                                 23 38'F12:回前'
     A            S#MSG1        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面二:銷貨明細選擇畫面
     A***************************************************************
     A          R AR040F2                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT2         1A  B 12  2VALUES(' ' '1')
     A            S#TXCD         4A  O 12  4
     A            S#TXNO         8A  O 12  9
     A            S#TXIT         2Y 0O 12 18
     A            S#TXDT         8Y 0O 12 21
     A            S#TXPD         3A  O 12 30
     A            S#TXQT         7Y 0O 12 34EDTWRD('      0')
     A            S#TXUP         5Y 3O 12 42EDTWRD(' 0.   ')
     A            S#TXAM        10Y 0O 12 49EDTWRD('         0-')
     A            S#TXTA         8Y 0O 12 61EDTWRD('       0-')
     A            S#TXOR         9A  O 12 72
     A*
     A            S#TYP1         2A  H
     A            S#RAT1        11Y 3H
     A            S#TYP2         2A  H
     A            S#RAT2        11Y 3H
     A            S#ACNT         1A  H
     A***************************************************************
     A          R AR040F2C                  SFLCTL(AR040F2)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      CF10(10)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE040S-2'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A            S#DEVI        10A  O  2 70
     A                                  2 30'★發票指定開立作業★'
     A                                      DSPATR(RI)
     A*----------------------------------------------------------------
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  O  4 14
     A            S#CUNM        10O  O  4 21
     A                                  4 37'出貨業務:'
     A            S#SALE         2A  O  4 49
     A                                  4 57'收款業務:'
     A            S#RVID         2A  O  4 69
     A                                  5  2'出貨金額:'
     A            S#AAMT        11Y 0O  5 14EDTWRD('  ,   ,   ,   -')
     A                                  5 30'稅額:'
     A            S#ATAX        11Y 0O  5 38EDTWRD('  ,   ,   ,   -')
     A                                  5 54'扣預收:'
     A            S#BAMT        11Y 0O  5 64EDTWRD('  ,   ,   ,   -')
     A                                  6  2'發票聯式:'
     A            S#KIND         1A  O  6 14
9706 A                                  6 16'出貨日期:'
 .   A            S#DATS         8Y 0B  6 28EDTCDE(Z)
 .   A  49                                  DSPATR(PC RI)
 .   A                                  6 37'~'
 .   A            S#DATE         8Y 0B  6 39EDTCDE(Z)
9706 A  50                                  DSPATR(PC RI)
     A                                  6 48'合計發票金額:'
     A                                      DSPATR(HI)
     A            S#NBAL        11Y 0O  6 64EDTWRD('  ,   ,   ,   -')
     A                                      DSPATR(HI)
     A                                  7  4'(選項: 1 = 開立)'
     A                                  7 30'發票日期:'
     A            S#INDT         8Y 0B  7 42EDTWRD('    /  /  ')
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A                                  7 54'合計重:'
     A                                      COLOR(RED)
     A            S#QTY         11Y 0O  7 64EDTWRD('  ,   ,   ,   -')
     A                                      COLOR(RED)
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9  1'選　單據　單據　項'
     A                                  9 22'單據　　品'
     A                                 10  1'項代號　號碼　次'
     A                                 10 22'日期　　名'
     A                                 10 36'數量'
     A                                 10 43'單價'
     A                                 10 52'金額'
     A                                 10 64'稅額'
     A                                 10 72'訂單'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR040F2M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  6'F03:結束'
     A                                 23 25'F12:回前'
     A                                 23 44'ENTER:試算'
     A                                 23 65'F10:開立'
     A                                      DSPATR(RI)
     A            S#MSG2        70A  O 24  2DSPATR(HI)
