     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARE056T1
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.WRITER        S00WCJ
     A*           4.FUNCTION     營業發票作廢申請單
     A*           5.DATE-WRITTEN  101/12/26  2012AR714
     A*           6.DATE-MODIFY   104/09/22  2015AR953 S00WCJ (0409A)
     A*                            產品代碼3碼擴5碼
     A*                           106/01/06  S00WCJ (0601A)
     A*                            修改簽核欄位
     A*
     A*****************************************************************
     A          R AR56T11H
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報　　表:ARE056T1'
     A                                    55
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   120
     A                                      '列表日期:'
     A                                    +1
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      ' USR - ID :'
     A                                      SPACEB(001)
     A            U#USID        10A  O    13
     A                                    58
     A                                      '營業發票作廢申請單'
     A                                   120
     A                                      '列表時間:'
     A                                    +1
     A                                      TIME
     A                                     2
     A                                      '廠　　區:'
     A                                      SPACEB(001)
     A            T#AREA         1A       13
     A                                   120
     A                                      '頁　　次:'
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '申請日期:'
     A                                      SPACEB(001)
     A            T#APDT         8  0     13
     A                                      EDTWRD('    /  /  ')
     A                                     2
     A                                      '申請單號:'
     A                                      SPACEB(001)
     A            T#DLNO         6A       13
     A                                     2
     A                                      '客戶代號:'
     A                                      SPACEB(001)
     A            T#CUNO         6A       13
     A            T#CUNM        10A       20
     A                                     2
     A                                      '訂單編號:'
     A                                      SPACEB(001)
     A            T#ORNO         6A       13
     A            T1LIN1       141         2
     A                                      SPACEB(001)
     A                                     2
     A                                      '|<--------'
     A                                      SPACEB(001)
     A                                    25
     A                                      '出　　　貨　　　資　　　料'
     A                                    69
     A                                      '-------->|<--------'
     A                                    94
     A                                      '重　新　開　立　發　票　內　容'
     A                                   133
     A                                      '-------->|'
     A            T1LIN2       141         2
     A                                      SPACEB(001)
     A                                     2
     A                                      '日　　期'
     A                                      SPACEB(001)
     A                                    14
     A                                      '發票號碼'
     A                                    26
     A                                      '磅單編號'
     A                                    38
     A                                      '品名'
     A                                    47
     A                                      '數　量'
     A                                    58
     A                                      '單價'
     A                                    70
     A                                      '金　額'
     A                                    83
     A                                      '數　量'
     A                                    93
     A                                      '單價'
     A                                   104
     A                                      '金　額'
     A                                   117
     A                                      '稅額'
     A                                   129
     A                                      '應收帳款'
     A            T1LIN3       141         2
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR56T11D
     A*****************************************************************
     A                                     2
     A                                      ' '
     A                                      SPACEB(001)
     A N50        T#INDT         8  0      3
     A                                      EDTWRD('    /  /  ')
     A N51        T#INNO        10        14
     A N52        T#WTNO         8        27
0409AA            T#PDN1         5        38
     A            T#QTY1         9  0     44
     A                                      EDTCDE(J)
     A            T#PRC1         5  2     57
     A                                      EDTCDE(1)
     A            T#AMT1        11  0     64
     A                                      EDTCDE(J)
     A            T#QTY2         9  0     79
     A                                      EDTCDE(J)
     A            T#PRC2         5  2     92
     A                                      EDTCDE(1)
     A            T#AMT2        11  0     99
     A                                      EDTCDE(J)
     A            T#TAX2         9  0    114
     A                                      EDTCDE(J)
     A            T#AAMT        11  0    126
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR56T11Y
     A*%%***********************************************************************
     A                                      SKIPB(055)
     A                                     2
     A                                      '異動原因說明:'
     A            T#DESP       100        17
     A                                      UNDERLINE
     A*****************************************************************
     A          R AR56T11X
     A*%%***********************************************************************
     A                                      SKIPB(060)
     A                                     2
     A                                      '副總經理:'
     A                                    13
     A                                      '            '
     A                                      UNDERLINE
     A                                    28
0601AA                                      '經理:'
     A                                    +1
     A                                      '            '
     A                                      UNDERLINE
     A                                    52
     A                                      '副理:'
     A                                    +1
     A                                      '            '
     A                                      UNDERLINE
     A                                    76
     A                                      '製單:'
     A                                    +1
     A                                      '            '
     A                                      UNDERLINE
     A                                   102
     A                                      '輸入:'
     A                                    +1
     A                                      '            '
     A                                      UNDERLINE
     A*****************************************************************
     A          R AR56T11M
     A*%%***********************************************************************
     A                                    45
     A                                      '無符合之資料!!! '
     A                                      SPACEB(008)
