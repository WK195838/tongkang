     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE002S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR
     A*            4.FUNCTION     調整維護作業
     A*            5.DATE-WRITTEN
     A*            6.DATE-MODIFY   96/05/10
     A*                           100/01/28  2011AR582 S00WCJ (0001A)
     A*                           100/07/04  S00WCJ (0007A)
     A*                            增加請款單號查詢
     A*                           102/09/18  2013AR771 S00WCJ (0209A)
     A*                            增加F1功能鍵
     A*                           102/10/02  S00WCJ (0210A)
     A*                            增加廠區V
     A*                           103/07/25 2014AR850 S00WCJ (0307A)
     A*                            增加廠區Q
     A*                           104/09/18 2015AR953 S00WCJ (0409A)
     A*                            產品代碼3碼擴5碼
     A*                           106/10/06  2017AR1100 S00WCJ (0610A)
     A*                            廠區U.Q.V可合併開立調整單
     A*                           107/07/19  2018AR1182 S00WCJ (0707A)
     A*                            增加挑選數量小計
     A*                           108/03/23  S00WCJ (0803A)
     A*                            增加廠區R及廠區U.Q.V.R可合併開
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TRNDTL)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*================================================================
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARE002S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'***銷貨調整單維護作業*** '
     A            DEVNM         10A  O  2 70
     A*=========
     A                                  5 30'功　　能:'
     A            CODE           1A  B  5 42
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  5 44'(1:新增 2:修改'
     A                                  7 45'3:刪除 4:查詢)'
     A*
     A                                 11 30'調整單號:'
     A            NO             6A  B 11 42
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A*
0001AA                                 13 30'廠  區:'
0001AA            S#AREA         1A  B    +1
0001AA                                      VALUES('P' 'T' 'U' 'H' 'K' +
0803AA                                      'M' ' ' 'V' 'Q' 'R')
0001AA  45                                  DSPATR(PC)
0001AA  45                                  DSPATR(RI)
0001AA                                    +1'(P=台北M=台中T=八德'
0001AA                                 14 44' U=桃園H=苗栗K=高+
0210AA                                      雄'
0307AA                                 15 44' V=保障Q=東鋼營造'
0803AA                                 16 44' R=友德'
0803AA                                 17 44'空白=U+V+Q+R)'
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A*=========
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            ITEM           2Y 0O  9  4
     A            JUST           1A  B  9 11
     A  70                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
0409AA            PDNM           5A  B  9 18
     A  70                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A            QTY            7Y 0B  9 27EDTWRD(' ,   ,   ')
     A  70                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A            PRC            5Y 3B  9 38EDTWRD(' 0.   ')
     A  70                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A            AMT            9Y 0B  9 46EDTCDE(J)
     A  70                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A            PNO            8A  B  9 59
     A  70                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
0409AA            PDNO           5A  B  9 69
     A  70                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A            FLAG           1A  B  9 76
     A  70                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
0209AA                                      CF01(01 '說明')
     A                                      CF03(03 '離開')
     A N69                                  CF08(08 '依日期載入')
     A                                      CF10(10 '存檔')
     A                                      CF12(12 '回前畫面')
     A  84                                  SFLCLR
     A N85                                  SFLDSP
     A N86                                  SFLDSPCTL
     A  75                                  SFLEND
     A*           S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A*-----------
     A                                  1 70'ARE002S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 28'***銷貨調整單維護作業*** '
     A            DEVNM         10A  O  2 70
     A*-----------
     A                                  4  4'調整單號:'
     A            NO             6A  O  4 16
     A                                  4 24'客戶編號:'
     A            CUNO      R        O  4 36REFFLD(TXCUNO)
     A                                  4 48'客戶名稱:'
     A            CUNM      R        O  4 60REFFLD(TXCUNM)
     A                                  5  4'訂單編號:'
     A            ORNO           6A  B  5 16
     A  70                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A                                  5 24'單據日期:'
     A            DATE      R        B  5 36REFFLD(TXDATE)
     A                                      EDTWRD('    /  /  ')
     A  70                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A*                                 5 48'發票號碼:'
     A            TXIVNO        10A  O  5 60
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  2'項次　調整別　調整品名'
     A                                  7 27'　數　量　　單價　'
     A                                  7 48'　金　額　磅單編號'
     A                                  7 74'標記'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND01
     A                                 21  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A                                      DSPATR(HI)
     A                                 22 12'合　計:'
     A            S#QTYS         9  0O 22 24EDTCDE(1)
     A            TTLAMT         9  0O 22 44EDTCDE(1)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:檢核'
0209AA                                    +1'F1:說明'
     A                                    +1'F3:結束'
     A                                    +1'F8:依訂單及日期載入'
     A                                    +1'F10:存檔'
     A                                    +1'F12:回前'
     A*================================================================
     A          R SF2                       SFL
     A  89                                  SFLNXTCHG
     A            S#OPT          1A  B  9  5
     A            S#NO           8A  O  9 11
     A            S#ITEM         3  0O  9 23
0409AA            S#PDNO         5A  O  9 31
     A            S#QTY          7  0O  9 38EDTCDE(1)
     A            S#AMT          9  0O  9 50EDTCDE(J)
     A            S#UPRC         5  3H
     A            S#CUNO         6A  H
     A            S#CUNM        10O  H
     A***************************************************************
     A          R SFCTL2                    SFLCTL(SF2)
     A                                      SFLSIZ(0025)
0707AA                                      SFLPAG(0011)
     A                                      OVERLAY
0209AA                                      CF01(01 '說明')
     A                                      CF03(03 '離開')
     A                                      CF05(05 '挑選')
     A                                      CF10(10 '存檔')
     A                                      CF12(12 '回前畫面')
     A  81                                  SFLCLR
     A N82                                  SFLDSP
     A N83                                  SFLDSPCTL
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A*-----------
     A                                  1 70'ARE002S-3'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 28'銷貨調整單依入帳日期載入'
     A            DEVNM         10A  O  2 70
     A*-----------
     A                                  4  2'請輸入－訂單編號:'
     A*                                 4 13'訂單編號:'
     A            S#ORNO         6A  B    +1
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A                                  4 33'入帳日期:'
     A            S#ACDS         8Y 0B  4 45EDTCDE(Z)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  4 54'~'
     A            S#ACDE         8Y 0B  4 56EDTCDE(Z)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
0409AA                                  5 08'調整金額:'
0409AA            S#TAMT         9  0B    +1EDTCDE(1)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
0409AA                                    +1'調整別:'
0409AA            S#JUST         1A  B    +1
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
0409AA                                    +1'調整品名:'
0409AA            S#PDNM         5A  B    +1
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
0007AA                                    +1'請款單號:'
0007AA            S#APNO         6A  B    +1
0007AA  67                                  DSPATR(PC)
0007AA  67                                  DSPATR(RI)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  2'挑Ｙ'
     A                                  7 10'磅單編號'
     A                                  7 22'項次'
     A                                  7 30'品名'
     A                                  7 38'數　　量'
     A                                  7 52'銷貨金額'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND02
0707AA                                 20  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A                                      DSPATR(HI)
0707AA                                 21 12'挑選小計:'
0707AA                                      DSPATR(HI)
0707AA            S1SQTY         9  0O 21 36EDTCDE(1)
0707AA                                      DSPATR(HI)
     A                                 22 12'合　計:'
     A            S#SQTY         9  0O 22 36EDTCDE(1)
     A            S#SAMT         9  0O 22 50EDTCDE(1)
     A            S#ERR         70A  O 23 02DSPATR(HI)
     A*
     A                                 24  5'ENTER:檢核'
0209AA                                    +1'F1:說明'
     A                                    +1'F03:結束'
     A                                    +1'F05:全部挑選'
     A                                    +1'F10:確認存檔'
     A                                    +1'F12:回前'
     A***************************************************************
0209AA          R AR02F1W
0209AA                                      WINDOW(12 14 6 56)
0209AA                                      TEXT('說明視窗')
0209AA                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
0209AA                                       (*CHAR '        '))
0209AA                                      OVERLAY
0209AA                                  2  2'701 補差價'
0209AA                                  3  2'702 補運費'
0209AA                                  4  2'703 扣保留款'
0209AA                                  5  2'704 補超長運費'
0209AA                                  2 20'705 補吊拖車費'
0209AA                                  3 20'706 補定尺費'
0209AA                                  4 20'707 其他收入'
0209AA                                  5 20'708 代收檢驗費'
0209AA                                  2 38'709 加工費'
0209AA                                  3 38'714 場地租金'
0209AA*
