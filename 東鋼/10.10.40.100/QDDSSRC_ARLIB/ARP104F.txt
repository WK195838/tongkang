     A****************************************************************
     A*
     A*       UPDATE  DATE  104/09/14  2015AR953  S00WCJ (0409A)
     A*                      產品代碼3碼擴5碼
     A*
     A****************************************************************
     A*
     A                                      UNIQUE
     A          R RARP104F                  TEXT('修正TRNDTL暫存檔')
     A            A4FLAG         1A         COLHDG('處理代碼')
     A            A4CODE         4A         COLHDG('單據代號')
     A            A4NO           8A         COLHDG('單據號碼')
     A            A4ITEM         2S 0       COLHDG('項次')
     A            A4ACNT         1A         COLHDG('原因別')
     A            A4DATE         8S 0       COLHDG('單據日期')
     A            A4ACDT         8S 0       COLHDG('入帳日期')
     A            A4CUNO         6A         COLHDG('客戶代號')
     A            A4CUNM        10O         COLHDG('客戶名稱')
     A            A4ORNO         9A         COLHDG('訂單編號')
     A            A4IVNO        10A         COLHDG('發票號碼')
     A            A4PCNO         8A         COLHDG('磅單號碼')
     A            A4VUNO         7A         COLHDG('傳票號碼')
     A            A4RVID         2A         COLHDG('收款業務員')
     A            A4SALE         2A         COLHDG('出貨業務員')
     A            A4SATP         1A         COLHDG('銷售別')
     A            A4IVTP         1A         COLHDG('發票別')
0409AA            A4PDNM         5A         COLHDG('品名代號')
     A            A4QTY          7P 0       COLHDG('數量')
     A            A4UPRC         5P 3       COLHDG('單價')
     A            A4AMT         11P 0       COLHDG('金額')
     A            A4TAX         11P 0       COLHDG('稅額')
     A            A4FL01         1A         COLHDG('折讓確認碼')
     A            A4FL02         1A         COLHDG('過發票碼')
     A            A4TXAR         1A         COLHDG('異動廠區')
     A            A4TXDT         8S 0       COLHDG('異動日期')
     A            A4RESV        15A         COLHDG('保留碼')
     A****
     A*           註:保留碼第十位元乃未傳台北旗標
     A*                保留碼第十一依及十二位為異動代碼
     A****
     A          K A4CODE
     A          K A4NO
     A          K A4ITEM
