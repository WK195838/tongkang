     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARP120T1
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     獎勵金製票代開處理報表─價格補助
     A*           4.DATE-WRITTEN  101/02/10 2012AR641
     A*           5.WRITER        S00WCJ
     A*           6.UPDATE  DATE  104/09/24 2015AR953  S00WCJ (0409A)
     A*                            產品代碼3碼擴5碼
     A*
     A*****************************************************************
     A          R AR120T1H
     A*****************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      ' PRT-ID  : ARP120T1'
     A                                    26
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    59
     A                                      '列表日期:'
     A                                    +1
     A                                      DATE
     A                                      EDTWRD('01  /  /  ')
     A                                    21
     A                                      '※獎勵金製票代開處理報表+
     A                                      ─價格補助※'
     A                                      SPACEB(001)
     A                                    59
     A                                      '列表時間:'
     A                                    +1
     A                                      TIME
     A                                     2
     A                                      ' USR-ID  :'
     A                                      SPACEB(001)
     A            U#USID        10        +1
     A                                    59
     A                                      '頁　　次:'
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '廠區:'
     A                                      SPACEB(002)
     A            R#AREA         1        +1
     A                                    +1
     A                                      '代開年月:'
     A            R#YYMM         6S 0     +1EDTWRD('    /  ')
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========='
     A                                      SPACEB(001)
     A                                     1
     A                                      '客戶代號 '
     A                                      SPACEB(001)
     A                                    13
     A                                      '客戶名稱'
     A                                    +1
     A                                      '產品名稱'
     A                                    36
     A                                      '鋼 種'
     A                                    47
     A                                      '數 量'
     A                                    60
     A                                      '金  額'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========='
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR120T1D
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A  51        R#CUNO         6   O     2
     A  51        R#CUNM        10   O    13
0409AA            R#PDNM         5   O    26
     A            R#METL         8   O    36
     A            R#QTY          9  0O    +1EDTCDE(J)
     A            R#TAM1         9  0O    61EDTCDE(J)
     A**************************************************************************
     A          R AR120T1L
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                     1
     A                                      ' '
     A**************************************************************************
     A          R AR120T1M
     A**************************************************************************
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========='
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                    14 '小計:'
     A            R#TQTY         9  0     45EDTCDE(K)
     A            R#TAM2        10  0     59EDTCDE(K)
     A
     A**************************************************************************
     A          R AR120T2M
     A**************************************************************************
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========='
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                    14 '合  計:'
     A            R1TQTY         9  0     45EDTCDE(K)
     A            R1TAM2        10  0     59EDTCDE(K)
     A
     A**************************************************************************
     A          R AR120T3M
     A**************************************************************************
     A                                      SPACEA(001)
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========='
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                     2 '總計:'
     A            R2TQTY         9  0     45EDTCDE(K)
     A            R2TAM2        10  0     59EDTCDE(K)
     A*
     A**************************************************************************
     A          R AR120T4M
     A**************************************************************************
     A N55                                 1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =========='
     A                                      SPACEA(001)
     A N55                                10 '品名小計:'
0409AA            R#SPDM         5   O    26
     A            R#TAMT        10  0     59EDTCDE(K)
     A
     A**************************************************************************
     A          R AR120T1X
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     1
     A                                      '=======================-
     A                                      ======+
     A                                      ==接　下　頁==============-
     A                                      ======+
     A                                      ================'
