     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR145T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     外銷應收帳款查詢報表
     A*           4.DATE-WRITTEN  108/08/02 2019AR00053
     A*           5.WRITER        S00WCJ
     A*           6.UPDATE DATE
     A*
     A*****************************************************************
     A          R AR145TH
     A**************************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A                                     2
     A                                      '報表代號: ARR145T'
     A                                    45
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    90
     A                                      '列表日期:'
     A                                    +1
     A                                      DATE
     A                                      EDTWRD('01  /  /  ')
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            T#USID        10        +1
     A                                    47
     A                                      '外銷應收帳款查詢報表'
     A                                    90
     A                                      '列表時間:'
     A                                    +1
     A                                      TIME
     A                                    90
     A                                      '頁　　次:'
     A                                      SPACEB(001)
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '產品別:'
     A                                      SPACEB(001)
     A            T#PROD         1        +1
     A                                    +1'(2=鋼筋(X訂單) 3=+
     A                                      型鋼空白=全部)'
     A                                     3
     A                                      '客戶編號'
     A                                      SPACEB(001)
     A                                    +0
     A                                      '客戶名稱'
     A                                    24
     A                                      '訂單編號'
     A                                    35
     A                                      '購案號碼'
     A                                    50
     A                                      '出貨金額'
     A                                    67
     A                                      '預收貨款'
     A                                    86
     A                                      '繳款金額'
     A                                   103
     A                                      '應收餘額'
     A            T#LIN2       120         1
     A                                      SPACEB(001)
     A*%%***********************************************************************
     A          R AR145T1
     A*%%***********************************************************************
     A                                     2
     A                                      ' '
     A                                      SPACEB(001)
     A N60        T#CUNO         6         3
     A N60        T#CUNM        10        13
     A N61        T#ORNO         6        25
     A            T#CTNO        10        35
     A            T#AAMT        11  0     +3EDTCDE(K)
     A            T#BAMT        11  0     +3EDTCDE(K)
     A            T#EAMT        11  0     +3EDTCDE(K)
     A            T#NBAL        11  0     +3EDTCDE(K)
     A*****************************************************************
     A          R AR145T2
     A*%%***********************************************************************
     A            T#LIN1       120         1
     A                                      SPACEB(001)
     A                                    23
     A                                      '客戶小計:'
     A                                      SPACEB(001)
     A            T1AAMT        12  0     47EDTCDE(K)
     A            T1BAMT        12  0     65EDTCDE(K)
     A            T1EAMT        12  0     83EDTCDE(K)
     A            T1NBAL        12  0    101EDTCDE(K)
     A*****************************************************************
     A          R AR145T3
     A*%%***********************************************************************
     A            T#LIN1       120         1
     A                                      SPACEB(001)
     A                                    23
     A                                      '總  計:'
     A                                      SPACEB(001)
     A            T2AAMT        12  0     47EDTCDE(K)
     A            T2BAMT        12  0     65EDTCDE(K)
     A            T2EAMT        12  0     83EDTCDE(K)
     A            T2NBAL        12  0    101EDTCDE(K)
     A*****************************************************************
     A          R AR145TR
     A*%%***********************************************************************
     A                                      SKIPB(010)
     A                                    56
     A                                      '查無資料'
