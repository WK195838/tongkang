     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARR080R
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     發票與銷貨金額差異表
     A*            5.DATE-WRITTEN  88/07/27
     A*            6.UPDATE  DATE  104/10/05  2015AR953  S00WCJ (0409A)
     A*                             產品代碼3碼擴5碼
     A*
     A***************************************************************
     A          R AR080T1H
     A                                      SKIPB(003)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報表代號: ARR080T'
     A                                    33
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    68
     A                                      '列表日期:'
     A                                    79
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                   +11
     A                                      '發票與銷貨金額差異表'
     A                                    68
     A                                      '列表時間:'
     A                                    79
     A                                      TIME
     A                                    68
     A                                      '頁　　次:'
     A                                      SPACEB(001)
     A                                    79
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '統計廠區:'
     A                                      SPACEB(001)
     A            R#AREA         8O  O    +1
     A                                    +4
     A                                      '統計年月:'
     A            R#YY           4S 0O    +1
     A                                      EDTCDE(4)
     A                                    +0
     A                                      '年'
     A            R#MM           2S 0O    +1
     A                                      EDTCDE(4)
     A                                    +0
     A                                      '月'
     A                                     6
     A                                      '客　　　戶'
     A                                      SPACEB(002)
     A                                    +5
     A                                      '訂單'
     A                                    +2
     A                                      '品名'
     A                                    +1
     A                                      '發票號碼'
     A                                    +1
     A                                      '發票出貨金額'
     A                                    +1
     A                                      '銷貨明細金額'
     A                                    +3
     A                                      '差額'
     A                                     3
     A                                      '------'
     A                                      SPACEB(001)
     A                                    +1
     A                                      '----------'
     A                                    +1
     A                                      '----------'
     A                                    +1
     A                                      '-----'
     A                                    +1
     A                                      '----------'
     A                                    +1
     A                                      '--------------'
     A                                    +1
     A                                      '---------------'
     A                                    +1
     A                                      '------------'
     A***************************************************************
     A          R AR080T1D
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A            R#CUNO         6A  O     3
     A            R#CUNM        10O  O    10
     A            R#ORNO         6A  O    23
0409AA            R#PDN3         5A  O    32
     A            R#INNO        10A  O    38
     A            R#AMTA        11S 0O    49
     A                                      EDTCDE(1)
     A            R#AMTB        11S 0O    64
     A                                      EDTCDE(J)
     A            R#AMTC         9S 0O    80
     A                                      EDTCDE(J)
     A            R#MARK         8A  O    92
     A            R#ORN1         6A  O   100
     A**************************************************************************
     A          R AR080T1S
     A                                      SPACEA(002)
     A*----------------------------------------------------------------
     A                                    24
     A                                      '訂單小計：'
     A            R#SAMA        11S 0O    49
     A                                      EDTCDE(1)
     A            R#SAMB        11S 0O    64
     A                                      EDTCDE(J)
     A            R#SAMC         9S 0O    80
     A                                      EDTCDE(J)
     A            R#MAR1         8A  O    93
     A            R#AMTD         9S 0O   101
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR080T1T
     A                                      SPACEB(002)
     A*----------------------------------------------------------------
     A                                    24
     A                                      '合計：'
     A            R#TAMA        11S 0O    49
     A                                      EDTCDE(1)
     A            R#TAMB        11S 0O    64
     A                                      EDTCDE(J)
     A            R#TAMC         9S 0O    80
     A                                      EDTCDE(J)
     A            R#MAR2         8A  O    93
     A            R#AMTE         9S 0O   101
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR080T1M
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A                                    19
     A                                      '*****無條件符合之資料！！'
