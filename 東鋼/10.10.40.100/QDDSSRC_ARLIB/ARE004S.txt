     A****************************************************************
     A*
     A*            UPDATE  DATE  101/09/18  2012AR682  S00WCJ (0109A)
     A*
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RCVDTL)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARE004S-1'
     A            COMP          35   O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 23'***營業繳款通知單（依請款單）***'
     A            DEVNM         10   O  2 70
     A                                  5 30'功　　能:'
     A            CODE           1   B    +1
     A  41                                  DSPATR(PC RI)
     A                                    +1'(1:新增   　　'
     A                                  7 46'3:刪除 4:查詢)'
     A                                  9 30'繳款單號:'
     A            NO             6   B    +1
     A  42                                  DSPATR(PC RI)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            ITEM      R        O 11  2REFFLD(RVITEM)
     A            ACTP      R        B 11  7REFFLD(RVACTP)
     A  70                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
0109AA            S#ACNO    R        B 11 10REFFLD(RVACNO)
     A  70                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A            USTP      R        B 11 20REFFLD(RVUSTP)
     A                                      DUP(25)
     A  70                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A            NTTP      R        B 11 24REFFLD(RVNTTP)
     A                                      DUP(24)
     A  70                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A            PBID      R     O  B 11 26REFFLD(RVPBID)
     A                                      DUP(21)
     A  70                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A            PANO      R        B 11 41REFFLD(RVPANO)
     A                                      DUP(22)
     A  70                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A            PLAC      R        B 11 52REFFLD(RVPLAC)
     A                                      DUP(23)
     A  70                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A            NTNO      R        B 12 10REFFLD(RVNTNO)
     A  70                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A            RAMT           9Y 0B 11 68EDTWRD('   ,   ,   ')
     A  70                                  DSPATR(PR)
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(RI)
     A            RLNO          10E  B 11 56
     A  70                                  DSPATR(PR)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            SANO      R        B 12 41REFFLD(RVSANO)
     A  70                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            DUDT      R        B 12 56REFFLD(RVDUDT)
     A                                      EDTWRD('    /  /  ')
     A  70                                  DSPATR(PR)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A            FLAG      R        B 12 74REFFLD(RVFLAG)
     A  70                                  DSPATR(PR)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A*           AMCD      R        B 12 74REFFLD(RVAMCD)
     A* 70                                  DSPATR(PR)
     A* 65                                  DSPATR(PC)
     A* 65                                  DSPATR(RI)
     A*           SBID      R        H      REFFLD(RVSBID)
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A*%%TS  SD  19811028  155640  JEFF        REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0090)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARE004S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 23'***營業繳款通知單（依請款單）***'
     A            DEVNM         10A  O  2 70
     A                                  4  5'繳款編號:'
     A            NO             6A  O  4 17
     A                                  4 28'繳款部門:'
     A            DPNO      R        B  4 40REFFLD(RVDPNO)
     A  70                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A                                  4 49'收款業務員:'
     A            RVID           1A  B    +1
     A  70                                  DSPATR(PR)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A                                  5  5'客戶編號:'
     A            CUNO      R        B  5 17REFFLD(RVCUNO)
     A  70                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A                                  5 24'客戶名稱:'
     A            CUNM      R        O  5 36REFFLD(RVCUNM)
     A                                  5 51'繳款日期:'
     A            DATE      R        B  5 63REFFLD(RVDATE)
     A                                      EDTWRD('    /  /  ')
     A  70                                  DSPATR(PR)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  1'項　借　　　　'
     A                                  7 19'款　票'
     A                                  7 51'付'
     A                                  8  1'　　貸　會計科目　項　款'
     A                                  8 28'付款銀行　　付款帳戶'
     A                                  8 51'款'
     A                                  8 56'相關號碼　　　金　　額'
     A                                  9  1'次　方　票據號碼　別　別'
     A                                  9 28'　　　　　　存入帳號'
     A                                  9 51'地'
     A                                  9 56'到期日期　　　　標記　'
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND01
     A                                      OVERLAY
     A                                 21  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A                                      DSPATR(HI)
     A                                 22 57'合　計:'
     A            TTLAMT         9  0O 22 68EDTCDE(1)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                    +2'PF03-結束'
     A                                    +2'PF10-沖銷輸入'
     A                                    +2'PF12-回前畫面'
     A***************************************************************
     A          R SF2                       SFL
     A  88                                  SFLNXTCHG
     A            ITEM      R        O  9 10REFFLD(RVITEM)
     A            APNO           6A  B  9 19
     A  70                                  DSPATR(PR)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A            FLAG           1A  B  9 33
     A  70                                  DSPATR(PR)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A            HAPNO          6A  H
     A            HFLAG          1A  H
     A***************************************************************
     A          R SFCTL2                    SFLCTL(SF2)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A*                                     CF03
     A*                                     CF05
     A*                                     CF10
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARE004S-3'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2
     A                                      DSPATR(RI)
     A                                  2 23'***營業繳款通知單（依請款單）***'
     A            DEVNM         10A  O  2 70
     A                                  4  5'繳款編號:'
     A            NO             6A  O  4 17
     A                                  4 28'繳款部門:'
     A            DPNO      R        O    +1REFFLD(RVDPNO)
     A                                  4 51'繳款日期:'
     A            DATE      R        O  4 63REFFLD(RVDATE)
     A                                      EDTWRD('    /  /  ')
     A                                  5  5'客戶編號:'
     A            CUNO      R        O  5 17REFFLD(RVCUNO)
     A                                  5 24'客戶名稱:'
     A            CUNM      R        O  5 36REFFLD(RVCUNM)
     A                                  5 49'繳款總金額:'
     A            TTLAMT         9Y 0O  5 63EDTWRD('   ,   , 0 ')
     A                                  6  2'       ----------------------------
     A                                      ---------
     A                                               '
     A                                      DSPATR(HI)
     A                                  7  8'項次'
     A                                  7 19'請款單號'
     C                                  7 30'標記'
     A                                  8  2'       ----------------------------
     A                                      --------
     A                                               '
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND02
     A                                      OVERLAY
     A                                 21  2'       ----------------------------
     A                                      --------
     A                                               '
     A                                      DSPATR(HI)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A*                                   +2'PF05-顯示發票'
     A*                                   +2'PF10-存檔'
     A                                    +2'PF12-回前畫面'
     A***************************************************************
     A          R SF3                       SFL
     A            ITEM      R        O  9 10REFFLD(RVITEM)
     A            APNO3          6A  O  9 20
     A            ORNO           6A  O  9 37
     A            INVNO         10A  O  9 52
     A            AAMT           9Y 0O  9 67EDTWRD('   ,   , 0 ')
     A***************************************************************
     A          R SFCTL3                    SFLCTL(SF3)
     A                                      SFLSIZ(0096)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CF10
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARE004S-4'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2
     A                                      DSPATR(RI)
     A                                  2 23'***營業繳款通知單（依請款單）***'
     A            DEVNM         10A  O  2 70
     A                                  4  5'繳款編號:'
     A            NO             6A  O  4 17
     A                                  4 28'繳款部門:'
     A            DPNO      R        O    +1REFFLD(RVDPNO)
     A                                  4 51'繳款日期:'
     A            DATE      R        O  4 63REFFLD(RVDATE)
     A                                      EDTWRD('    /  /  ')
     A                                  5  5'客戶編號:'
     A            CUNO      R        O  5 17REFFLD(RVCUNO)
     A                                  5 24'客戶名稱:'
     A            CUNM      R        O  5 36REFFLD(RVCUNM)
     A                                  5 49'繳款總金額:'
     A            TTLAMT         9Y 0O  5 63EDTWRD('   ,   , 0 ')
     A                                  6  2'       ----------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  8'項次'
     A                                  7 19'請款單號'
     A                                  7 35'訂單號碼'
     A                                  7 51'發票號碼'
     A                                  7 67'沖銷金額'
     A                                  8  2'       ----------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND03
     A                                      OVERLAY
     A                                 21  2'       ----------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22 20'合計： '
     A            TAAMT          9Y 0O 22 35EDTWRD('   ,   , 0 ')
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                    +2'PF10-存檔'
     A                                    +2'PF12-回前畫面'
     A***************************************************************
