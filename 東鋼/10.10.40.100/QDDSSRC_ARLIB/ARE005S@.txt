     A*%%TS  SD  19811203  123505  QPGMR       REL-V2R1M0  5738-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RCVDTL)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARE005S-1'
     A            COMP          35   O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 24'***營業繳款通知單（依發票）***'
     A            DEVNM         10   O  2 70
     A                                  5 30'功　　能:'
     A            CODE           1   B    +1VALUES('2' '4')
     A  41                                  DSPATR(PC RI)
     A                                    +1'(2:修改 4:查詢)'
     A                                  9 30'繳款單號:'
     A            NO             6   B    +1
     A  42                                  DSPATR(PC RI)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            ITEM      R        O 11  2REFFLD(RVITEM)
     A            ACTP      R        O 11  7REFFLD(RVACTP)
9707 A            ACNOW     R        O 11 12REFFLD(RVACNO)
     A            USTP      R        O 11 20REFFLD(RVUSTP)
     A            NTTP      R        O 11 24REFFLD(RVNTTP)
     A            PBID      R     O  O 11 26REFFLD(RVPBID)
     A            PANO      R        O 11 41REFFLD(RVPANO)
     A            PLAC      R        O 11 52REFFLD(RVPLAC)
     A            NTNO      R        O 11 56REFFLD(RVNTNO)
     A            RAMT           9Y 0O 11 68EDTWRD('   ,   ,   ')
     A            RLNO          10E  O 12 10
     A            SANO      R        B 12 41REFFLD(RVSANO)
     A  70                                  DSPATR(PR)
     A  62                                  DSPATR(PC RI)
     A            DUDT      R        O 12 56REFFLD(RVDUDT)
     A                                      EDTWRD('    /  /  ')
     A            FLAG      R        O 12 74REFFLD(RVFLAG)
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A                                      SFLSIZ(0090)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARE005S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 24'***營業繳款通知單（依發票）***'
     A            DEVNM         10A  O  2 70
     A                                  4  5'繳款編號:'
     A            NO             6A  O  4 17
     A                                  4 28'繳款部門:'
     A            DPNO      R        O  4 40REFFLD(RVDPNO)
     A                                  4 49'收款業務員:'
     A            RVID           1A  O  4 63
     A                                  5  5'客戶編號:'
     A            CUNO      R        O  5 17REFFLD(RVCUNO)
     A                                  5 24'客戶名稱:'
     A            CUNM      R        O  5 36REFFLD(RVCUNM)
     A                                  5 51'繳款日期:'
     A            DATE      R        O  5 63REFFLD(RVDATE)
     A                                      EDTWRD('    /  /  ')
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  1'項　借　　　　'
     A                                  7 19'款　票'
     A                                  7 51'付'
     A                                  8  1'　　貸　會計科目　項　款'
     A                                  8 28'付款銀行　　付款帳戶'
     A                                  8 51'款'
     A                                  8 56'票據號碼　　　金　　額'
     A                                  9  1'次　方　相關號碼　別　別'
     A                                  9 28'　　　　　　存入帳號'
     A                                  9 51'地'
     A                                  9 56'到期日期　　　　標記　'
     A                                 10  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND01
     A                                      OVERLAY
     A                                 21  3'------------------------------+
     A                                      ----------------------------------+
     A                                      --------------'
     A                                      DSPATR(HI)
     A                                 22 57'合　計:'
     A            TTLAMT         9Y 0O 22 68EDTCDE(1)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                    +2'PF03-結束'
     A                                    +2'PF10-沖銷輸入'
     A                                    +2'PF12-回前畫面'
     A***************************************************************
     A          R SF3                       SFL
     A            ITEM      R        O  9 10REFFLD(RVITEM)
     A            INVNO1         2A  O  9 20
     A            INVNO2         4A  O    +1
     A            INVNO3         4A  O    +1
     A            AAMT           9Y 0O  9 40EDTWRD('   ,   ,   ')
     A            ORNO           6A  O  9 60
     A            FLAG           1A  O  9 74
     A            INVNOH        10A  H
     A            AAMTH          9Y 0H
     A***************************************************************
     A          R SFCTL3                    SFLCTL(SF3)
     A*%%TS  SD  19811006  152046  JEFF        REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0096)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      CF10
     A                                      CF12
     A                                      CF03
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARE005S-3'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 24'***營業繳款通知單（依發票）***'
     A            DEVNM         10A  O  2 70
     A                                  4  5'繳款編號:'
     A            NO             6A  O  4 17
     A                                  4 28'繳款部門:'
     A            DPNO      R        O  4 40REFFLD(RVDPNO)
     A                                  4 51'繳款日期:'
     A            DATE      R        O  4 63REFFLD(RVDATE)
     A                                      EDTWRD('    /  /  ')
     A                                  5  5'客戶編號:'
     A            CUNO      R        O  5 17REFFLD(RVCUNO)
     A                                  5 24'客戶名稱:'
     A            CUNM      R        O  5 36REFFLD(RVCUNM)
     A                                  5 49'繳款總金額:'
     A            TTLAMT         9Y 0O  5 63EDTWRD('   ,   , 0 ')
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  8'項次'
     A                                  7 20'發票號碼'
     A                                  7 41'沖銷金額'
     A                                  7 58'訂單號碼'
     A                                  7 72'標記'
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R UND03
     A                                      OVERLAY
     A                                 21  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22 20'合計： '
     A            TAAMT          9Y 0O 22 35EDTWRD('   ,   , 0 ')
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                    +2'PF03:結束'
     A                                    +2'PF10:存檔'
     A                                    +2'PF12:回前畫面'
     A***************************************************************
