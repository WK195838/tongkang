     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR071T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     營業繳款未過入財會明細表
     A*           4.DATE-WRITTEN  87/05/25
     A*           5.WRITER        S02CLJ
     A*           6.UPDATE  DATE  101/09/25 2012AR682 S00WCJ (0109A)
     A*
     A*       已確認及未確認之報表由傳入資料而定
     A*
     A*****************************************************************
     A          R AR071T1H
     A*****************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報表代號: ARR071T'
     A                                    62
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   139
     A                                      '列表日期:'
     A                                   150
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                    64
     A                                      '未確認營業繳款明細表'
     A                                   139
     A                                      '列表時間:'
     A                                   150
     A                                      TIME
     A                                     2
     A                                      '繳款日期:'
     A                                      SPACEB(001)
     A            R#RVDT         8S  O    14
     A                                      EDTWRD('    /  /  ')
     A                                   139
     A                                      '頁　　次:'
     A            R#PGNO         4S 0O   150
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '繳款部門:'
     A                                      SPACEB(001)
     A            R#DPNO         4A  O    +1
     A                                    +4
     A                                      '收款業務:'
     A            R#RVID         2A  O    +1
     A            R#RVNM        10O  O    +0
     A*----------------------------------------------------------------
     A                                     2
     A                                      '繳款'
     A                                      SPACEB(002)
     A                                   +19
     A                                      '款'
     A                                    +0
     A                                      '票'
     A                                    +0
     A                                      '會計'
     A                                    +0
     A                                      '借'
     A                                   +57
     A                                      '付款'
     A                                   +27
     A                                      '請款'
     A                                     2
     A                                      '單號'
     A                                      SPACEB(001)
     A                                    +6
     A                                      '客戶'
     A                                    +7
     A                                      '項'
     A                                    +0
     A                                      '據'
     A                                    +0
     A                                      '科目'
     A                                    +0
     A                                      '貸'
     A                                    +3
     A                                      '銀行名稱'
     A                                    +3
     A                                      '銀行帳號'
     A                                    +1
     A                                      '票據號碼'
     A                                   +12
     A                                      '到期日'
     A                                    +1
     A                                      '地'
     A                                    +4
     A                                      '票據金額'
     A                                    +3
     A                                      '發票號碼'
     A                                    +1
     A                                      '單號'
     A                                    +5
     A                                      '繳款金額'
     A*----------------------------------------------------------------
     A                                     2
     A                                      '------'
     A                                      SPACEB(001)
     A                                    +1
     A                                      '------'
     A                                    +1
     A                                      '----------'
     A                                    +2
     A                                      '--'
     A                                    +3
     A                                      '-'
     A                                    +2
     A                                      '---- '
     A                                    +2
     A                                      '-'
     A                                    +2
     A                                      '--------------'
     A                                    +1
     A                                      '----------'
     A                                    +1
     A                                      '----------'
     A                                   +11
     A                                      '----------'
     A                                    +2
     A                                      '-'
     A                                    +2
     A                                      '---------------'
     A*
     A                                    +1
     A                                      '----------'
     A                                    +1
     A                                      '-------'
     A                                    +1
     A                                      '---------------'
     A**************************************************************************
     A          R AR071T1D
     A**************************************************************************
     A                                      SPACEA(001)
     A            R#RVNO         6A  O     2
     A            R#CUNO         6A  O    +1
     A            R#CUNM        10O  O    +1
     A            R#USTP         2A  O    +2
     A            R#NTTP         1A  O    +3
0109AA            R#ACNO         6A  O    35
     A            R#ACTP         1A  O    +3
     A            R#PBID        14O  O    +2
     A            R#PANO        10A  O    +1
     A            R#NTNO        20A  O    +1
     A            R#DUDT         8S  O    +1
     A                                      EDTWRD('    /  /  ')
     A            R#PLAC         1A  O    +2
     A            R#RAMT        11S 0O    +2
     A                                      EDTCDE(K)
     A*
     A            R#INNO        10A  O    +1
     A            R#APNO         7A  O    +1
     A            R#INXA        11S 0O    +1
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR071T1S
     A**************************************************************************
     A                                      SPACEA(002)
     A                                    74
     A                                      '**小計:'
     A            R#SRXA        11S 0O   143
     A                                      EDTCDE(K)
     A            R#SRNA        11S 0O   108
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR071T1T
     A**************************************************************************
     A                                      SPACEB(002)
     A                                      SPACEA(001)
     A                                    74
     A                                      '**合計:'
     A            R#TRNA        11S 0O   108
     A                                      EDTCDE(K)
     A            R#TRXA        11S 0O   143
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR071T1M
     A**************************************************************************
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A                                    19
     A                                      '*****無條件符合之資料！！'
     A**************************************************************************
