     A****************************************************************
     A*
     A*            TRNDTL LOG檔 99/11/09 S00WCJ
     A*                          104/09/16 2015AR953 S00WCJ (0409A)
     A*                           產品代碼3碼擴5碼
     A*
     A****************************************************************
     A*                                     UNIQUE
     A          R TXREC                     TEXT('銷貨明細檔')
     A            TXFLAG         1A         COLHDG('處理代碼')
     A            TXCODE         4A         COLHDG('單據代號')
     A            TXNO           8A         COLHDG('單據號碼')
     A            TXITEM         2S 0       COLHDG('項次')
     A            TXACNT         1A         COLHDG('原因別')
     A            TXDATE         8S 0       COLHDG('單據日期')
     A            TXACDT         8S 0       COLHDG('入帳日期')
     A            TXCUNO         6A         COLHDG('客戶代號')
     A            TXCUNM        10O         COLHDG('客戶名稱')
     A            TXORNO         9A         COLHDG('訂單編號')
     A            TXIVNO        10A         COLHDG('發票號碼')
     A            TXPCNO         8A         COLHDG('磅單號碼')
     A            TXVUNO         7A         COLHDG('傳票號碼')
     A            TXRVID         2A         COLHDG('收款業務員')
     A            TXSALE         2A         COLHDG('出貨業務員')
     A            TXSATP         1A         COLHDG('銷售別')
     A            TXIVTP         1A         COLHDG('發票別')
0409AA            TXPDNM         5A         COLHDG('品名代號')
     A            TXQTY          7P 0       COLHDG('數量')
     A            TXUPRC         5P 3       COLHDG('單價')
     A            TXAMT         11P 0       COLHDG('金額')
     A            TXTAX         11P 0       COLHDG('稅額')
     A            TXFL01         1A         COLHDG('折讓確認碼')
     A            TXFL02         1A         COLHDG('過發票碼')
     A            TXTXAR         1A         COLHDG('異動廠區')
     A            TXTXDT         8S 0       COLHDG('異動日期')
0409AA            TXRESV        30A         COLHDG('保留碼')
     A****
     A*           註:保留碼第十位元乃未傳台北旗標
     A*                保留碼第十一依及十二位為異動代碼
     A****
     A          K TXCODE
     A          K TXNO
     A          K TXITEM
