     A*%%TS  SD  19941007  150803  S02CSF      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARR113S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02CSF
     A*            4.FUNCTION     發票折扣核對清單
     A*            5.DATE-WRITTEN  92/05/31
     A*            6.DATE-MODIFY
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QPRINT)
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前畫面')
     A*****************************************************************
     A          R AR113H
     A*%%TS  SD  19940912  111645  S02CSF      REL-V4R4M0  5769-PW1
     A*                                     OVERLAY
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 24'東和鋼鐵企業股份有限公司'
     A                                  1 70'ARR113S'
     A                                  2  2TIME
     A                                  2 28'發票折扣核對清單'
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3  2
     A***************************************************************
     A          R AR113F1
     A                                      OVERLAY
     A                                  6 21'客戶代號：'
     A            S#CUNO         6A  B  6 34
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                  6 41'（空白表全部）'
     A                                  8 21'業務員別：'
     A            S#SALE         1A  B  8 34
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                  8 41'（空白表全部）'
     A                                 10 21'發票日期：'
     A            S#IND1         8Y 0B 10 34EDTWRD('    /  /  ')
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                 10 45'~~'
     A            S#IND2         8Y 0B 10 48EDTWRD('    /  /  ')
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A                                 12 21'發票廠區：'
     A            S#AREA         1A  B 12 34VALUES(' ' 'H' 'K')
     A                                 12 37'( H:苗栗 K:高雄空白=全部)'
     A                                 14 21'報表內容：'
     A            S#COD1         1A  B 14 34VALUES('1' '2')
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A                                 14 37'( 1:折扣 2:未折扣)'
     A                                 16 21'報表型態：'
     A            S#TYPE         1A  B 16 34VALUES('1' '2')
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A                                 16 37'( 1:明細表2:彙總表)'
     A                                 18 21'訂單挑選：'
     A            S#CHIC         1A  B 18 34VALUES('1' '2')
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A                                 18 37'( 1:挑選2:不挑選)'
     A                                 23  2'操作提示:'
     A                                 23 14'PF3=結束作業'
     A            S#ERR         70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR113F2                   SFL
     A*%%TS  SD  19941007  150803  S02CSF      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#ORNO         6A  B 10  4
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A*================================================================
     A          R AR113F2C                  SFLCTL(AR113F2)
     A*%%TS  SD  19941007  134629  S02CSF      REL-V4R4M0  5769-PW1
     A                                      SFLLIN(0005)
     A                                      SFLSIZ(0085)
     A                                      SFLPAG(0084)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A                                      OVERLAY
     A                                      CF05(05 '列印')
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A                                  5  2'客戶代號:'
     A            S#CUNO         6A  O  5 14
     A                                  5 23'業務員別:'
     A            S#SAID         1A  O  5 35
     A                                  6  2'發票日期:'
     A            S#IND1         8Y 0O  6 14EDTWRD('    /  /  ')
     A                                  6 25'~'
     A            S#IND2         8Y 0O  6 27EDTWRD('    /  /  ')
     A                                  6 39'發票廠區:'
     A            S#AREA         1A  O  6 51
     A                                  8  2'訂單編號'
     A                                  8 13'訂單編號'
     A                                  8 24'訂單編號'
     A                                  8 35'訂單編號'
     A                                  8 46'訂單編號'
     A                                  8 57'訂單編號'
     A                                  8 68'訂單編號'
     A            S#LIN1        78A  O  9  2
     A*================================================================
     A          R AR113F2M
     A*%%TS  SD  19940131  162633  S02CSF      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A            S#LIN2        78A  O 22  2
     A                                 23  5'F3=結束'
     A                                 23 15'F12=回前畫面'
     A                                 23 30'F05=列印'
     A            S#ERR2        70A  O 24  2DSPATR(HI)
