     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR061T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     逾齡應收帳款明細表
     A*           4.DATE-WRITTEN  89/09/25
     A*           5.WRITER        S02YSH
     A*
     A*
     A*****************************************************************
     A          R AR061TH
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報表代號: ARR061T'
     A                                    75
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   154
     A                                      '列表日期:'
     A                                    +1
     A                                      DATE
     A                                      EDTWRD('01  /  /  ')
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            T#USID        10A  O    +1
     A                                    78
     A                                      '逾齡應收帳款明細表'
     A                                   154
     A                                      '列表時間:'
     A                                   166
     A                                      TIME
     A            T#RPID        10         2
     A                                      SPACEB(001)
     A            T#YEAR         4  0     81
     A                                    85
     A                                      '年'
     A            T#MONT         2  0     89
     A                                    91
     A                                      '月'
     A                                   154
     A                                      '頁　　次:'
     A                                   166
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A            T#LIN1       174         1
     A                                      SPACEB(001)
     A                                    37
     A                                      '二個月('
     A                                      SPACEB(001)
     A            T#YEA2         4  0     46
     A                                    50
     A                                      '年'
     A            T#MON2         2  0     54
     A                                    56
     A                                      '月)'
     A                                    66
     A                                      '三個月('
     A            T#YEA3         4  0     75
     A                                    79
     A                                      '年'
     A            T#MON3         2  0     83
     A                                    85
     A                                      '月)'
     A                                    95
     A                                      '四個月('
     A            T#YEA4         4  0    104
     A                                   108
     A                                      '年'
     A            T#MON4         2  0    112
     A                                   114
     A                                      '月)'
     A                                   123
     A                                      '五個月('
     A            T#YEA5         4  0    132
     A                                   136
     A                                      '年'
     A            T#MON5         2  0    140
     A                                   142
     A                                      '月)以上'
     A                                     2
     A                                      '客戶名稱'
     A                                      SPACEB(001)
     A                                    +1
     A                                      '訂單編號'
     A                                    +1
     A                                      '業務員'
     A                                    +4
     A                                      '發票號碼'
     A                                    +5
     A                                      '金　　額'
     A                                    +4
     A                                      '發票號碼'
     A                                    +5
     A                                      '金　　額'
     A                                    +4
     A                                      '發票號碼'
     A                                    +5
     A                                      '金　　額'
     A                                    +4
     A                                      '發票號碼'
     A                                   138
     A                                      '金　　額'
     A                                   158
     A                                      '小　　計'
     A            T#LIN2       174         1
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR061T1
     A            T#CUNM        10         2
     A                                      SPACEB(001)
     A            T#ORNO         6        +3
     A            T#SALE         1        +7
     A            T#INN2        10        +7
     A            T#AMT2        10  0     +1
     A                                      EDTCDE(K)
     A            T#INN3        10        +4
     A            T#AMT3        10  0     +1
     A                                      EDTCDE(K)
     A            T#INN4        10        +4
     A            T#AMT4        10  0     +1
     A                                      EDTCDE(K)
     A            T#INN5        10       123
     A            T#AMT5        10  0    134
     A                                      EDTCDE(K)
     A            T#SUB1        11  0    153
     A                                      EDTCDE(K)
     A*****************************************************************
     A          R AR061T2
     A                                      SPACEA(001)
     A            T#CUN1        10         2
     A            T#ORN1         6        +3
     A                                    30
     A                                      '訂單小計:'
     A                                      SPACEB(001)
     A            T#TO12        11  0     46
     A                                      EDTCDE(K)
     A            T#TO13        11  0     75
     A                                      EDTCDE(K)
     A            T#TO14        11  0    104
     A                                      EDTCDE(K)
     A            T#TO15        11  0    133
     A                                      EDTCDE(K)
     A            T#SU1T        12  0    152
     A                                      EDTCDE(K)
     A*****************************************************************
     A          R AR061T3
     A                                    30
     A                                      '客戶小計:'
     A*                                     SPACEB(001)
     A            T#TO22        11  0     46
     A                                      EDTCDE(K)
     A            T#TO23        11  0     75
     A                                      EDTCDE(K)
     A            T#TO24        11  0    104
     A                                      EDTCDE(K)
     A            T#TO25        11  0    133
     A                                      EDTCDE(K)
     A            T#SU2T        12  0    152
     A                                      EDTCDE(K)
     A                                     2
     A                                      ' '
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR061T4
     A                                    30
     A                                      '總　　計:'
     A                                      SPACEB(001)
     A            T#TO32        12  0     45
     A                                      EDTCDE(K)
     A            T#TO33        12  0     74
     A                                      EDTCDE(K)
     A            T#TO34        12  0    103
     A                                      EDTCDE(K)
     A            T#TO35        12  0    132
     A                                      EDTCDE(K)
     A            T#SU3T        12  0    152
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR061TX
     A                                      SKIPB(061)
     A                                    74
     A                                      '副總:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '經理:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '覆核:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '製表:'
     A                                   138
     A                                      '            '
     A                                      UNDERLINE
