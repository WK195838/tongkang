     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE103S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     獎勵金價格補助鋼種設定維護作業
     A*            5.WRITER-DATE   101/02/16 2012AR641 S00WCJ
     A*            5.DATE-UPDATE
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束作業')
     A                                      CA07(07 '結束作業')
     A                                      CA12(12 '回前畫面')
     A*================================================================
     A          R AR103H
     A                                  1  2DATE
     A                                      EDTWRD('01  /  /  ')
     A                                  1 24'も　東和鋼鐵企業股份有限公司　も'
     A            S#SRID        10A  O  1 70
     A                                  2  2TIME
     A                                  2 25'★獎勵金價格補助鋼種設定維護★  '
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3  2
     A  30        S#MODE         6A  O  3 70DSPATR(RI)
     A*================================================================
     A          R AR103F2                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPTX         1Y 0B  9  3EDTCDE(Z)
     A                                      VALUES(0 3 4)
     A            S#MEXO         8A  O  9  8
     A*
     A            S#UPXM        10A  O    +2
     A*
     A*================================================================
     A          R AR103F2C                  SFLCTL(AR103F2)
     A                                      SFLSIZ(0025)
     A                                      SFLPAG(0024)
     A                                      SFLLIN(03)
     A                                      ROLLUP(91 '下頁')
     A                                      CA09(09 '列印')
     A*                                     SFLDROP(CF04)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  74                                  SFLCLR
     A                                      OVERLAY
     A                                  4  2'OP: 1=新增  4=刪除 '
     A                                  4 43'【第一列僅可執行1;其餘可4-
     A                                      】'
     A            S#LIN1        78A  O  5  2
     A                                  6  3'OP'
     A                                  6  7'鋼 種'
     A                                    +2'異動人員'
     A                                  6 31'OP'
     A                                  6 37'鋼 種'
     A                                    +2'異動人員'
     A            S#LIN2        78A  O  7  2
     A            S#OPTA         1Y 0B  8  3EDTCDE(Z)
     A                                      VALUES(0 1)
     A  31                                  DSPATR(PC)
     A            S#MENO         8A  B  8  8
     A  50                                  DSPATR(PC RI)
     A            S#NBR          4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR103F2M
     A                                      OVERLAY
     A            S#LIN3        78A  O 22  2
     A                                 23  2'F3=結束作業'
     A                                 23 16'F12=回前畫面'
     A                                 23 31'執行=存檔'
     A                                      DSPATR(RI)
     A            S#ERR         70A  O 24  2DSPATR(HI)
