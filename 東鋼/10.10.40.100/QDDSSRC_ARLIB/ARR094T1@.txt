     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR094T1
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     進倉履約折讓自動處理-檢核表
     A*           4.DATE-WRITTEN  103/01/20
     A*           5.WRITER        S00WCJ
     A*           6.UPDATE DATE
     A*
     A*
     A*****************************************************************
     A          R AR941TH
     A**************************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A                                     2
     A                                      '報表代號: ARR094T1'
     A                                    52
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   110
     A                                      '列表日期:'
     A                                    +1
     A                                      DATE
     A                                      EDTWRD('01  /  /  ')
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            T#USID        10        +1
     A                                    49
     A                                      '銷貨折讓自動處理金額分攤檢核表'
     A                                   110
     A                                      '列表時間:'
     A                                    +1
     A                                      TIME
     A                                     2
     A                                      '業務代號:'
     A                                      SPACEB(001)
     A            T#SAID         2        14
     A            T#SANM        10        +2
     A                                   110
     A                                      '頁　　次:'
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '處理年月:'
     A                                      SPACEB(001)
     A            T#YYMM         6  0     14
     A                                      EDTWRD('    /  ')
     A                                    +5
     A                                      '第'
     A            T#CONT         3  0     +0
     A                                      EDTCDE(4)
     A                                    +0
     A                                      '次處理'
     A            T#LIN1       125         1
     A                                      SPACEB(001)
     A                                     2
     A                                      '客戶代號'
     A                                      SPACEB(001)
     A                                    +1
     A                                      '客戶簡稱'
     A                                    +1
     A                                      '廠區'
     A                                    +1
     A                                      '訂單號碼'
     A                                    42
     A                                      '發票號碼'
     A                                    +1
     A                                      '產品編號'
     A                                    +1
     A                                      '磅單編號'
     A                                    +1
     A                                      '磅單項次'
     A                                    +1
     A                                      '銷貨數量'
     A                                    +1
     A                                      '折讓單價'
     A                                    +1
     A                                      '折讓金額'
     A            T#LIN2       125         1
     A                                      SPACEB(001)
     A*%%***********************************************************************
     A          R AR941T1
     A*%%***********************************************************************
     A                                     2
     A                                      ' '
     A                                      SPACEB(001)
     A N60        T#CUNO         6         4
     A N60        T#CUNM        10        +3
     A            T#AREA         1        26
     A            T#ORNO         6        33
     A            T#INNO        10        42
     A            T#PDNM         3        55
     A            T#TXNO         8        64
     A            T#TXIT         2  0     79
     A                                      EDTCDE(2)
     A            T#QTY1         7  0     86
     A                                      EDTCDE(2)
     A            T#AMT4         4  3     98
     A                                      EDTCDE(2)
     A            T#DMT1        12  0    109
     A                                      EDTCDE(2)
     A*****************************************************************
     A          R AR941T2
     A*%%***********************************************************************
     A                                    33
     A                                      '發票小計:'
     A                                      SPACEB(001)
     A            T#DMTA        12  0    109
     A                                      EDTCDE(1)
     A*****************************************************************
     A          R AR941T3
     A*%%***********************************************************************
     A                                    33
     A                                      '訂單小計:'
     A                                      SPACEB(001)
     A            T1DMTA        12  0    109
     A                                      EDTCDE(2)
     A*****************************************************************
     A          R AR941TR
     A*%%***********************************************************************
     A                                      SKIPB(010)
     A                                    46
     A                                      '查無區間內資料'
