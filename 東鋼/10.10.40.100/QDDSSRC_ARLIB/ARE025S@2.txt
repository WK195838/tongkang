     A*%%TS  SD  19970909  115509  S02LBU      REL-V5R1M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/INVDTL)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A                                      CF05(05)
     A                                  1 70'ARE025S-1'
     A            COMP          35   O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'**發票繳款金額轉出作業**'
     A            DEVNM         10   O  2 70
     A                                  9 30'發票號碼:'
     A            NO            10   B    +1
     A  42                                  DSPATR(PC RI)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'F03:結束'
     A                                 24 35'F12:回前'
     A                                 24 50'F05:查詢'
     A                                      DSPATR(RI)
     A***************************************************************
     A          R DETAIL
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12
     A                                      CF10
     A                                  1 70'ARE025S-2'
     A            COMP          35A  O  1 24
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'**發票繳款金額轉出作業**'
     A            DEVNM         10A  O  2 70
     A                                  4 20'轉出部份'
     A                                      DSPATR(RI)
     A                                  5 30'發票號碼:'
     A            NO            10A  O  5 42
     A                                  6 30'客戶編號:'
     A            CUNO           6A  O  6 42
     A                                  7 30'客戶名稱:'
     A            CUNM          10J  O  7 42
     A                                  8 30'訂單編號:'
     A            ORNO           6A  O  8 42
     A                                  9 30'發票種類:'
     A            TYP1           1A  O  9 42
     A                                  9 44'(1:出貨2:預收3:調整9:其他)-
     A                                      '
     A                                 10 30'開立廠區:'
     A            AREA           1A  O 10 42
     A                                 11 30'收款業務:'
     A            RVID           2A  O 11 42
     A                                 12 30'繳款金額:'
     A            RAMT           9Y 0O 12 42EDTCDE(J)
     A                                 13 28'已轉出金額:'
     A            CAMT           9Y 0O 13 42EDTCDE(J)
     A                                 14 28'可轉出金額:'
     A                                      DSPATR(HI)
     A            NBAL           9Y 0O 14 42EDTCDE(J)
     A                                      DSPATR(HI)
     A                                 15 26'本次轉出金額:'
     A            TAMT           9Y 0B 15 42EDTCDE(J)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A                                 16 26'本次轉出日期:'
     A            S#DATE         8Y 0B 16 42EDTWRD('    /  /  ')
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                 18 20'轉入部份'
     A                                      DSPATR(RI)
     A                                 19 24'欲轉入發票號碼:'
     A            NO1           10A  B 19 42
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A                                 20 28'　所屬訂單:'
     A            ORNO1          6   O 20 42
     A                                 21 28'欲轉入金額:'
     A            XAMT           9Y 0O 21 42EDTCDE(J)
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 19'PF03:結束'
     A                                 24 32'PF10:存檔'
     A                                 24 45'PF12:回前畫面'
     A*
     A***************************************************************
     A*  畫面三:瀏覽轉出入發票畫面
     A***************************************************************
     A          R AR025F3                   SFL
     A            S#ITEM         4Y 0O 12  3EDTCDE(3)
     A            S#OTAM        11Y 0O 12  8EDTCDE(J)
     A            S#OTNO        10A  O 12 25
     A            S#OTCU         6A  O 12 36
     A            S#OTOR         6A  O 12 43
     A            S#USER        10A  O 12 50
     A            S#TXDT         8Y 0O 12 62
     A            S#ACDT         8Y 0O 12 72
     A***************************************************************
     A          R AR025F3C                  SFLCTL(AR025F3)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN3)
     A            S#NBR3         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN3         5S 0H
     A            S#SFN3         4Y 0H
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE025S-3'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'★發票轉出查詢作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  O  4 14
     A                                  4 29'客戶簡稱:'
     A            S#CUNM        10O  O  4 41
     A                                  4 57'訂單號碼:'
     A            S#ORNO         6A  O  4 69
     A                                  5  2'發票號碼:'
     A            S#INNO        10A  O  5 14DSPATR(HI)
     A                                  5 29'發票日期:'
     A            S#INDT         8Y 0O  5 41EDTWRD('    /  /  ')
     A                                  5 57'開立廠區:'
     A            S#AREA         1A  O  5 69
     A                                  6  2'發票類別:'
     A            S#INTP         1A  O  6 14
     A                                  6 29'發票聯式:'
     A            S#KIND         1A  O  6 41
     A                                  6 57'課稅別:'
     A            S#TXTP         1A  O  6 69
     A                                  7  2'銷售別:'
     A            S#SATP         1A  O  7 14
     A                                  7 29'出貨業務:'
     A            S#SALE         2A  O  7 41
     A                                  7 57'收款業務:'
     A            S#RVID         2A  O  7 69
     A                                  8  2'發票金額:'
     A            S#INAM        11Y 0O  8 14EDTCDE(J)
     A                                      DSPATR(HI)
     A                                  8 33'餘額:'
     A            S#INBN        11Y 0O  8 41EDTCDE(J)
     A                                      DSPATR(HI)
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 10  2'項次'
     A                                 10 11'轉出金額'
     A                                 10 25'轉入發票'
     A                                 10 36'客戶'
     A                                 10 43'訂單'
     A                                 10 50'異動人員'
     A                                 10 61'異動日期　轉出日期'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR025F3M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  4'F03:結束'
     A                                 23 22'F12:回前'
     A                                 23 42'ENTER:執行'
     A            S#MSG3        70A  O 24  2DSPATR(HI)
