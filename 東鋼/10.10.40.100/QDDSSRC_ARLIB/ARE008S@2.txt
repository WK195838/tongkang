     A*%%TS  SD  19820726  160515  TPUSER      REL-V2R1M0  5738-PW1
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/TRNDTL)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARE008S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'**帳款更正作業（磅差）**'
     A            DEVNM         10A  O  2 70
     A                                  5 30'功　　能:'
     A            CODE           1A  B  5 42
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A  43                                  DSPATR(PR)
     A                                  5 44'(2:修改 4:查詢)'
     A                                 11 30'磅單編號:'
     A            NO             8A  B 11 42
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A*%%TS  SD  19820726  160515  TPUSER      REL-V2R1M0  5738-PW1
     A  88                                  SFLNXTCHG
     A            ITEM           2Y 0O 10  3
     A            ACNT           1A  B 10  7
     A  70                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A            PDNM           3A  B 10 10
     A  70                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A            QTY            7Y 0B 10 14
     A  70                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                      EDTWRD(' ,   ,   -')
     A            PRC            7Y 5B 10 25EDTWRD('  .     ')
     A  70                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A            AMT            9Y 0B 10 34
     A  70                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A                                      EDTWRD('   ,   ,   -')
9008 A            ORNO           9A  B 10 47
     A  70                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
9710 A            ACDT           8Y 0B 10 57EDTWRD('    /  /  ')
CLJ  A  70 77                               DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
9710 A            FLAG           1A  B 10 68
     A  58                                  DSPATR(RI)
     A  58                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A            IVNO          10A  B 10 70
     A  59                                  DSPATR(RI)
     A  59                                  DSPATR(PC)
     A  70 71                               DSPATR(PR)
     A            CUNO           6A  B 11 14
     A  70                                  DSPATR(PR)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            CUNM          10A  O 11 25
     A            SATP           1A  H
     A            RVID           1A  H
     A            SALE           1A  H
     A            IVTP           1A  H
     A            HLD            1A  H
     A            HFD1           1A  H
     A            HFD2           1A  H
     A            VUNO           7A  H
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A*%%TS  SD  19820726  160515  TPUSER      REL-V2R1M0  5738-PW1
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A*-------------------------------------------------------------
     A                                  1 70'ARE008S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 28'**帳款更正作業（磅差）**'
     A            DEVNM         10A  O  2 70
     A                                  4  3'磅單編號:'
     A            NO        R        O  4 15REFFLD(TXNO)
     A                                  4 50'單據日期:'
9710 A            DATE           8Y 0O  4 62EDTWRD('    /  /  ')
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  2'項　種　品'
     A                                  8  2'次　類　名　客戶編號'
     A                                  7 16'數　量'
     A                                  7 26'單價'
     A                                  7 35'金　　額'
     A                                  8 70'發票號碼'
     A                                  8 56'入帳日期　記'
     A                                  7 66'標'
     A                                  8 25'客戶名稱'
     A                                  7 47'訂單'
     A                                  8 47'編號'
     A***************************************************************
     A          R UND01
     A*%%TS  SD  19811001  084717  JEFF        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                 20  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 21  3'合　計:'
     A            TTLQTY         7Y 0O 21 14EDTWRD(' ,   ,   -')
     A            TTLAMT         9Y 0O 21 32EDTWRD('   ,   ,   -')
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:檢核'
     A                                 24 19'PF03:結束'
     A                                 24 32'PF10:存檔'
     A                                 24 45'PF12:回前畫面'
