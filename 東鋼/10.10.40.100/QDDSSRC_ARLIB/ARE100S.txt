     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE100S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     獎勵金客戶設定維護作業
     A*            5.WRITER-DATE   101/02/15 2012AR641 S00WCJ
     A*            5.DATE-UPDATE
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '結束作業')
     A                                      CA07(07 '結束作業')
     A                                      CA12(12 '回前畫面')
     A*================================================================
     A          R AR100H
     A                                  1  2DATE
     A                                      EDTWRD('01  /  /  ')
     A                                  1 24'も　東和鋼鐵企業股份有限公司　も'
     A            S#SRID        10A  O  1 70
     A                                  2  2TIME
     A                                  2 29'★獎勵金客戶設定維護★'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3  2
     A  30        S#MODE         6A  O  3 70DSPATR(RI)
     A*================================================================
     A          R AR100F2                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPTX         1Y 0B  9  2EDTCDE(Z)
     A                                      VALUES(0 2 3 4)
     A            S#CUXO         6A  O  9  6
     A            S#CUNM        10A  O  9 17
     A            S1PRIC         1   B  9 34VALUES(' ' 'Y')
     A  53                                  DSPATR(PC RI)
     A            S1RWRD         1   B  9 48VALUES(' ' 'Y')
     A  53                                  DSPATR(PC RI)
     A            S1SPCY         1   B  9 61VALUES(' ' 'Y')
     A  53                                  DSPATR(PC RI)
     A*
     A            S#UPXM        10A  O  9 70
     A*
     A*================================================================
     A          R AR100F2C                  SFLCTL(AR100F2)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(91 '下頁')
     A                                      CA09(09 '列印')
     A*                                     SFLDROP(CF04)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  74                                  SFLCLR
     A                                      OVERLAY
     A                                  4  2'OP: 1=新增 2=修改 3=複製 +
     A                                      4=刪除'
     A                                  4 43'【第一列僅可執行1;其餘可2,3,4-
     A                                      】'
     A            S#LIN1        78A  O  5  2
     A                                  6  2'OP'
     A                                  6  6'客戶代號'
     A                                    +1'客戶名稱'
     A                                    +1'價格補助(Y)'
     A                                    +1'獎勵金一(Y)'
     A                                    +1'特別補助(Y)'
     A                                    +1'異動人員'
     A            S#LIN2        78A  O  7  2
     A            S#OPTA         1Y 0B  8  2EDTCDE(Z)
     A                                      VALUES(0 1)
     A  31                                  DSPATR(PC)
     A            S#CUNO         6A  B  8  6
     A  50                                  DSPATR(PC RI)
     A            S#PRIC         1   B  8 34VALUES(' ' 'Y')
     A  51                                  DSPATR(PC RI)
     A            S#RWRD         1   B  8 48VALUES(' ' 'Y')
     A  51                                  DSPATR(PC RI)
     A            S#SPCY         1   B  8 61VALUES(' ' 'Y')
     A  51                                  DSPATR(PC RI)
     A            S#NBR          4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR100F2M
     A                                      OVERLAY
     A            S#LIN3        78A  O 22  2
     A                                 23  2'F3=結束作業'
     A                                 23 16'F12=回前畫面'
     A                                 23 31'執行=存檔'
     A                                      DSPATR(RI)
     A            S#ERR         70A  O 24  2DSPATR(HI)
