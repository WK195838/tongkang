     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR065T2
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     工程合約開立發票明細表
     A*           4.DATE-WRITTEN  85/12/18
     A*
     A*****************************************************************
     A          R AR065T2H
     A                                      SKIPB(003)
     A                                     2
     A                                      ' PRT-ID   :ARR065T2'
     A                                    46
     A                                      '東和鋼鐵企業股份有限公司'
     A                                    91
     A                                      '印表日期:'
     A                                   102
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    46
     A                                      '工程合約開立發票明細表'
     A                                    91
     A                                      '時　　間:'
     A                                   102
     A                                      TIME
     A                                      SPACEA(001)
     A                                    91
     A                                      '頁　　次:'
     A                                   102
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     2
     A                                      '地區別:'
     A            R#AREA         1A       +1
     A            R#ARNM         6A       +1
     A                                    25'客戶編號:'
     A            R#CUNO         6A       +1
     A            R#CUNM        10A       +1
     A                                    55'訂單號碼:'
     A            R#ORNO         6A       +1
     A                                    85'工程編號:'
     A            R#CTNO        10A       +1
     A                                      SPACEA(001)
     A                                     1'------'
     A                                    +1'----------'
     A                                    +1'------'
     A                                    +1'------------'
     A                                    +1'------------'
     A                                    +1'------------'
     A                                    +1'------------'
     A                                    +1'------------'
     A                                    +1'------------'
     A                                    +1'------------'
     A                                    +1'------------'
     A                                      SPACEA(001)
     A                                     2
     A                                      '發票號碼'
     A                                    16
     A                                      '發票日期'
     A                                    28
     A                                      '發票類別'
     A                                    40
     A                                      '發票聯式'
     A                                    50
     A                                      '品名'
     A                                    56
     A                                      '品名類別'
     A                                    69
     A                                      '單價'
     A                                    85
     A                                      '數量'
     A                                    98
     A                                      '金額'
     A                                   110'業務員'
     A                                      SPACEA(001)
     A                                     1'------'
     A                                    +1'----------'
     A                                    +1'------'
     A                                    +1'------------'
     A                                    +1'------------'
     A                                    +1'------------'
     A                                    +1'------------'
     A                                    +1'------------'
     A                                    +1'------------'
     A                                    +1'------------'
     A                                    +1'------------'
     A                                      SPACEA(001)
     A          R AR065T21
     A N44        R#NO          10A        4
     A N44        R#INDT         6  0     18
     A                                      EDTCDE(Y)
     A N44        R#TYPE         1A       32
     A N44        R#KIND         1A       44
     A            R#PDCD         3A       51
     A            R#ACNT         1A       60
     A            R#UPRC         5S 3     70EDTCDE(J)
     A            R#QTY          7S 0     80EDTCDE(J)
     A            R#AMT         11S 0     92EDTCDE(J)
     A            R#SALE         2A      113
     A                                      SPACEA(001)
     A**************************************************************************
     A          R AR065T22
     A**************************************************************************
     A N45                                28'訂單品名小計：'
     A            R#PCD1         3A       51
     A            R#QTY1         9S 0     78EDTCDE(J)
     A            R#AMT1        11S 0     92EDTCDE(J)
     A                                      SPACEA(001)
     A          R AR065T23
     A                                    28'訂單品名合計：'
     A            R#QTY2         9S 0     78EDTCDE(J)
     A            R#AMT2        11S 0     92EDTCDE(J)
     A                                      SPACEA(001)
     A          R AR065T25
     A                                    28'非繳款（稅、折等）合計：'
     A            R#AMT3        11S 0     92EDTCDE(J)
     A                                      SPACEA(001)
     A          R AR065T26
     A                                    28'已繳款合計：'
     A            R#AMT4        11S 0     92EDTCDE(J)
     A*****************************************************************
     A          R AR065T24
     A            R#DASH       128         2
     A                                      SPACEA(1)
     A*****************************************************************
     A          R AR065MG2                  SPACEB(3)
     A                                    19'*****無條件符合之資料！！'
     A                                      SPACEA(2)
