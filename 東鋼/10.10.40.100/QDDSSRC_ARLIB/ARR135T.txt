     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR135T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     新桃工程自用發票開立明細表
     A*           4.DATE-WRITTEN  98/02/25
     A*           5.WRITER        S02LBU
     A*
     A*
     A*****************************************************************
     A          R AR135T1H
     A*****************************************************************
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報表代號: ARR135T'
     A                                    60
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   131
     A                                      '列表日期:'
     A                                   142
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                    61
     A                                      '工程自用發票開立明細表'
     A                                   131
     A                                      '列表時間:'
     A                                   142
     A                                      TIME
     A                                     2
     A                                      '年　　月:'
     A                                      SPACEB(001)
     A            T#YMS          6  0O    14
     A                                      EDTWRD('    /  ')
     A                                    22
     A                                      '~'
     A            T#YME          6  0O    23
     A                                      EDTWRD('    /  ')
     A                                   131
     A                                      '頁　　次:'
     A                                   142
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*
     A*----------------------------------------------------------------
     A                                     2
     A                                      '發票號碼'
     A                                      SPACEB(002)
     A                                    13
     A                                      '發票日期'
     A                                    24
     A                                      '廠區'
     A                                    31
     A                                      '異動代號'
     A                                    42
     A                                      '發票客戶'
     A                                    53
     A                                      '客戶簡稱'
     A                                    64
     A                                      '品名'
     A                                    74
     A                                      '單　價'
     A                                    85
     A                                      '數　　　　量'
     A                                   104
     A                                      '　　出貨金額'
     A                                   123
     A                                      '　　稅　　額'
     A                                   139
     A                                      '專案編號'
     A*----------------------------------------------------------------
     A                                     2
     A                                      '----------'
     A                                      SPACEB(001)
     A                                    13
     A                                      '----------'
     A                                    24
     A                                      '------'
     A                                    31
     A                                      '----------'
     A                                    42
     A                                      '----------'
     A                                    53
     A                                      '----------'
     A                                    64
     A                                      '------'
     A                                    72
     A                                      '-----------'
     A                                    84
     A                                      '----------------'
     A                                   104
     A                                      '---------------'
     A                                   123
     A                                      '---------------'
     A                                   139
     A                                      '----------'
     A**************************************************************************
     A          R AR135T1D
     A**************************************************************************
     A            T#BLK          1   O     1
     A                                      SPACEB(001)
     A N50        T#INNO        10A  O     2
     A N50        T#DATE         8  0O    14
     A                                      EDTWRD('    /  /  ')
     A            T#AREA         1   O    26
     A            T#TRCD         2A  O    35
     A N50        T#CUNO         6A  O    44
     A N50        T#CUNM        10O  O    53
     A            T#PDNM         3A  O    65
     A            T#UPRC         8  3O    72
     A                                      EDTCDE(1)
     A            T#QTY         11  0O    85
     A                                      EDTCDE(1)
     A            T#AMT         11  0O   104
     A                                      EDTCDE(1)
     A            T#TAX         11  0O   123
     A                                      EDTCDE(1)
     A            T#PJNO        10A  O   139
     A*****************************************************************
     A          R AR135T1S
     A*****************************************************************
     A                                    58
     A                                      '發票小計：'
     A                                      SPACEB(001)
     A            T#SQTY        11  0O    85
     A                                      EDTCDE(1)
     A            T#SAMT        11  0O   104
     A                                      EDTCDE(1)
     A            T#STAX        11  0O   123
     A                                      EDTCDE(1)
     A                                      SPACEA(001)
     A*****************************************************************
     A          R AR135T1T
     A*****************************************************************
     A                                    60
     A                                      '總合計：'
     A                                      SPACEB(003)
     A            T#TQTY        11  0O    85
     A                                      EDTCDE(1)
     A            T#TAMT        11  0O   104
     A                                      EDTCDE(1)
     A            T#TTAX        11  0O   123
     A                                      EDTCDE(1)
     A*****************************************************************
     A          R AR135T1M
     A*****************************************************************
     A                                    19
     A                                      '*****無條件符合之資料！！'
     A                                      SPACEB(005)
     A*****************************************************************
