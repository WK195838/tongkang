     A*****************************************************************
     A*
     A*            *****   PROGRAM INTRODUCTION   *****
     A*
     A*            1.PROGRAM-ID    ARS201S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     區間發票試算訂單查詢試算單號副程式
     A*            5.DATE-WRITTEN  107/05/31 2018AR1168
     A*
     A*****************************************************************
     A                                      PRINT
     A                                      INDARA
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前螢幕')
     A          R AR01F1
     A                                      ASSUME
     A                                  1  2' '
     A          R AR01F2
     A                                      SFL
     A            S#OPT          1   B  5  4VALUES(' ' 'S')
     A* 37                                  DSPATR(ND)
     A            S#ITNO        10   O  5 11DSPATR(HI)
     A            S#DAT1         8  0O    +2EDTWRD('    /  /  ')
     A                                    +1'~'
     A            S#DAT2         8  0O    +1EDTWRD('    /  /  ')
     A          R AR01F2CL
     A*                                     CLRL(*NO) OVERLAY FRCDTA
     A                                      OVERLAY
     A                                       WINDOW(AR01F2MG)
     A*
     A                                      SFLCTL(AR01F2)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A                                      SFLPAG(6)
     A                                      SFLSIZ(7)
     A N73                                  SFLEND(*MORE)
     A  73                                  SFLCLR
     A                                  2  2'訂單編號:'
     A            S#ORNO         6   B    +1
     A* 37                                  DSPATR(ND)
     A  33                                  DSPATR(PC RI)
     A                                  3  3'(S)'
     A                                  4  2'選擇' DSPATR(UL)
     A                                  4 11'試算單號 '
     A                                      DSPATR(UL)
     A                                    +1'試算日期區間         '
     A                                      DSPATR(UL)
     A          R AR01F2MG
     A                                      WINDOW(6 20 14 50)
     A                                      WDWBORDER((*COLOR PNK) +
     A                                      (*CHAR '+-+||+-+'))
     A                                 12  2'操作提示:' COLOR(BLU)
     A                                    +1'F3,F12=回前頁'
     A                                      COLOR(BLU)
     A            S#ERR         24     13  2
