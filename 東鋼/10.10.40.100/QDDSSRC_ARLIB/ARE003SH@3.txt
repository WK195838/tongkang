     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/INVMST)
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      CA03(03)
     A                                      CF12(12)
     A          R TITLE
     A                                      OVERLAY
     A                                  1 70'ARE003SH-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'***  請款單維護作業  *** '
     A            DEVNM         10A  O  2 70
     A                                  4 24'（新增修改：型鋼不區分訂單適用）-
     A                                      '
     A                                  7 30'功　　能:'
     A            CODE           1A  B  7 42
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  7 44'(1:新增 2:修改'
     A                                  9 45'3:刪除 4:查詢 '
     A                                 11 45'5:列印 6:傳真)'
     A                                 13 30'客戶編號:'
     A            CUNO           6A  B 13 42
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A                                 15 30'請款單號:'
     A            APNO           6A  B 15 42
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
0806AA                                 17 16'螢幕顯示是否印出未確認折讓金額:'
0806AA            S#OPT          1A  B    +1VALUES('Y' 'N')
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A                                 17 44'(Y:印出 N:不印出)'
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A                                 24 35'PF12:回前'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            OPT            1A  B 10  3
     A  70                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A            INVNO     R        O 10  5REFFLD(INNO)
     A            S#INDT         8Y 0O 10 16EDTWRD('    /  /  ')
     A            S#ORNO         6A  O 10 27
     A            S#CTNO        10A  O 10 34
     A            S#XAMT         8Y 0O 10 45EDTCDE(J)
     A            EAMT           8Y 0O 10 57EDTCDE(J)
     A            NBAL           8Y 0O 10 69EDTCDE(J)
     A*
     A            AAMT           8Y 0H
     A            BAMT           8S 0H
     A            CAMT           8S 0H
     A            DAMT           7Y 0H
     A            OPT1           1A  H
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A                                      CF10
     A                                      CF07(07)
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARE003SH-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 28'***  請款單維護作業  *** '
     A            DEVNM         10A  O  2 70
     A                                  4  5'客戶編號:'
     A            CUNO      R        O  4 17REFFLD(INCUNO)
     A                                  4 28'客戶名稱:'
     A            CUNM      R        O  4 40REFFLD(INCUNM)
     A                                  5  5'請款編號:'
     A            APNO           6A  O  5 17
     A                                  5 26'收款業務員:'
     A            RVID      R        O  5 40REFFLD(INRVID)
     A                                  5 52'請款日期:'
     A            APDT           8Y 0B  5 64EDTWRD('    /  /  ')
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  7  1'選　　　　　'
     A                                  8  1'擇　發票號碼'
     A                                  8 46'折退金額　　已收金額　　發票餘額-
     A                                      '
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8 17'發票日期　訂單'
     A                                  8 34'工程編號'
     A***************************************************************
     A          R UND01
     A                                      OVERLAY
     A                                 21  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22  3'發票筆數:'
     A            S#INCT         4Y 0O 22 15EDTCDE(3)
     A                                 22 57'合　計:'
     A  74                                  DSPATR(ND)
     A            TTLAMT         9Y 0O 22 68EDTCDE(J)
     A  74                                  DSPATR(ND)
     A                                 23  4'ENTER:檢核'
     A                                 23 17'PF03:結束'
     A                                 23 29'PF10:存檔'
     A                                 23 41'PF12:回前畫面'
     A                                 23 57'PF07:LC編號'
     A            ERRMSG        70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR003W1
     A                                      WINDOW( 6 30 14 44)
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      CF11(11)
     A                                  2 10'*單據資料*'
     A                                  4  2'單據類別:'
     A            S#CODE         1A  B  4 14
     A  91                                  DSPATR(RI)
     A  70                                  DSPATR(PR)
     A                                  4 16'(1.信用狀2.支票3.匯款)'
     A                                  5  2'單據編號:'
     A            S#VNNO        20A  B  5 14
     A  91                                  DSPATR(RI)
     A  70                                  DSPATR(PR)
     A                                  6  2'開立日期:'
     A            S#DAT1         8Y 0B  6 14EDTWRD('    /  /  ')
     A  91                                  DSPATR(RI)
     A  70                                  DSPATR(PR)
     A                                  7  2'押匯日期:'
     A            S#DAT2         8Y 0B  7 14EDTWRD('    /  /  ')
     A  91                                  DSPATR(RI)
     A  70                                  DSPATR(PR)
     A                                  8  2'銀行分行:'
     A            S#BANK        16O  B  8 14
     A  91                                  DSPATR(RI)
     A  70                                  DSPATR(PR)
     A                                  9  2'單據幣別:'
     A            S#CURY         3A  B  9 14
     A  91                                  DSPATR(RI)
     A  70                                  DSPATR(PR)
     A                                 10  2'單據餘額:'
     A            S#BAMT        11Y 0B 10 14EDTCDE(2)
     A  91                                  DSPATR(RI)
     A  70                                  DSPATR(PR)
     A                                 12  3'F3:離開'
     A                                      DSPATR(HI)
     A                                 12 13'12:回上頁'
     A                                      DSPATR(HI)
     A N70                             12 26'F11:存檔'
     A                                      DSPATR(HI)
     A            S#ERW1        34A  O 13  2DSPATR(HI)
     A***************************************************************
