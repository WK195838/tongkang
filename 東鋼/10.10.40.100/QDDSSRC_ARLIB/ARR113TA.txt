     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR113TA
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     發票折扣依磅單分攤金額檢核表
     A*           4.AUTHOR        S00WCJ
     A*           5.DATE-WRITTEN  103/01/21 2014AR796
     A*           6.DATE-MODIFY
     A*
     A*****************************************************************
     A          R AR113T1H
     A*****************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A                                     2
     A                                      '報表代號:  ARR113TA'
     A                                    70
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   148
     A                                      '列表日期:'
     A                                    +1
     A                                      DATE
     A                                      EDTWRD('01  /  /  ')
     A                                     2
     A                                      ' USR - ID :'
     A                                      SPACEB(001)
     A            U#USID        10        16
     A                                    68
     A                                      '發票折扣依磅單分攤金額檢核表'
     A                                   148
     A                                      '列表時間:'
     A                                    +1
     A                                      TIME
     A                                     2
     A                                      '客戶名稱:'
     A                                      SPACEB(001)
     A            T#CUNH        10        15
     A                                   148
     A                                      '頁　　次:'
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '業務別員:'
     A                                      SPACEB(001)
     A            T#SALE         6        15
     A                                     2
     A                                      '廠　　區:'
     A                                      SPACEB(001)
     A            T#AREA         6        15
     A                                     2
     A                                      '發票日期: '
     A                                      SPACEB(001)
     A            T#IND1         8S 0     14
     A                                      EDTWRD('    /  /  ')
     A                                    25
     A                                      '~~'
     A            T#IND2         8S 0     28
     A                                      EDTWRD('    /  /  ')
     A                                     2
     A                                      '報表內容: '
     A                                      SPACEB(001)
     A            T#COD1         8        15
     A            T#LIN1       175         2
     A                                      SPACEB(001)
     A                                     3
     A                                      '發票日期'
     A                                      SPACEB(001)
     A                                    14
     A                                      '發票號碼'
     A                                    +1
     A                                      '磅單編號'
     A                                    +1
     A                                      '項次'
     A                                    43
     A                                      '出貨日期'
     A                                    55
     A                                      '客戶編號'
     A                                    66
     A                                      '訂單編號'
     A                                    79
     A                                      '銷貨數量'
     A                                    91
     A                                      '銷貨單價'
     A                                   106
     A                                      '銷貨金額'
     A                                   121
     A                                      '折扣數量'
     A                                   133
     A                                      '折扣單價'
     A                                   147
     A                                      '折扣金額'
     A                                   163
     A                                      '發票金額'
     A            T#LIN2       175         2
     A                                      SPACEB(001)
     A*%%***********************************************************************
     A          R AR113T1D
     A*%%***********************************************************************
     A                                     2
     A                                      ' '
     A                                      SPACEB(001)
     A N50        T#INDT         8  0      3
     A                                      EDTWRD('    /  /  ')
     A N51        T#INNO        10        14
     A            T#TXNO         8        26
     A            T#TXIT         2  0     38
     A                                      EDTCDE(4)
     A            T#DATE         8  0     43
     A                                      EDTWRD('    /  /  ')
     A            T#CUNO         6        56
     A            T#ORNO         6        68
     A            T#QTY1         7  0     80
     A                                      EDTCDE(1)
     A            T#PRC1         5  3     93
     A                                      EDTCDE(1)
     A            T#AMT1        11  0    103
     A                                      EDTCDE(1)
     A            T#QTY2         7  0    122
     A                                      EDTCDE(1)
     A            T#PRC2         5  3    136
     A                                      EDTCDE(1)
     A            T#AMT2         9  0    145
     A                                      EDTCDE(J)
     A            T#AMT3        11  0    160
     A                                      EDTCDE(1)
     A*****************************************************************
     A          R AR113T1S
     A*%%***********************************************************************
     A                                    63
     A                                      '發票小計：'
     A                                      SPACEB(001)
     A            T#INDS         8  0      3
     A                                      EDTWRD('    /  /  ')
     A            T#INNS        10        14
     A            T#SQT1         8  0     79
     A                                      EDTCDE(1)
     A            T#SAM1        12  0    102
     A                                      EDTCDE(1)
     A            T#SQT2         8  0    121
     A                                      EDTCDE(1)
     A            T#SAM2        10  0    143
     A                                      EDTCDE(J)
     A*%%***********************************************************************
     A          R AR113T1T
     A*%%***********************************************************************
     A                                    63
     A                                      '日期合計：'
     A                                      SPACEB(001)
     A            T#TQT1         8  0     79
     A                                      EDTCDE(1)
     A            T#TAM1        12  0    102
     A                                      EDTCDE(1)
     A            T#TQT2         8  0    121
     A                                      EDTCDE(1)
     A            T#TAM2        10  0    143
     A                                      EDTCDE(J)
     A*%%***********************************************************************
     A          R AR113T1A
     A*%%***********************************************************************
     A                                    63
     A                                      '總　　計：'
     A                                      SPACEB(001)
     A            T#AQT1         8  0     79
     A                                      EDTCDE(1)
     A            T#AAM1        12  0    102
     A                                      EDTCDE(1)
     A            T#AQT2         8  0    121
     A                                      EDTCDE(1)
     A            T#AAM2        10  0    143
     A                                      EDTCDE(J)
     A*****************************************************************
     A          R AR113T1M
     A*%%***********************************************************************
     A                                    49
     A                                      '無任何符合條件之資料!!!'
     A                                      SPACEB(001)
     A*%%***********************************************************************
