     A****************************************************************
     A*
     A*            UPDATE  DATE  100/06/16  S00WCJ (0006A)
     A*                           增加品名及退折日期為KEY
     A*                          103/02/20  S00WCJ (0302A)
     A*                           增加折讓銷退金額為KEY
     A*                          104/09/14  2015AR953  S00WCJ (0409A)
     A*                           產品代碼3碼擴5碼
     A*                          104/10/12  S00WCJ (0410A)
     A*                           增加備註欄位為KEY
     A*
     A*****************************************************************
     A                                      UNIQUE
     A          R RDISLOG                   TEXT('折讓銷退下載ＬＯＧ檔')
     A            ISFLAG         1A         COLHDG('處理代碼')
     A            ISTYPE         1A         COLHDG('發票類別')
     A            ISDECD         1A         COLHDG('作廢碼')
     A            ISDEDT         8S 0       COLHDG('作廢日期')
     A            ISCUNO         6A         COLHDG('客戶編號')
     A            ISCUNM        10O         COLHDG('客戶名稱')
     A            ISINDT         8S 0       COLHDG('退折日期')
     A            ISNO          10A         COLHDG('發票號碼')
     A            ISMARK         1A         COLHDG('區分碼')
     A* 1 =銷貨退回 2 =銷貨折讓
0409AA            ISPDNM         5A         COLHDG('品名代號')
     A            ISCHIN        32O         COLHDG('中文品名')
     A            ISQTY          7P 0       COLHDG('數量')
     A            ISUPRC         8P 3       COLHDG('單價')
     A            ISCAMT        11P 0       COLHDG('折讓／銷退金額')
     A            ISCTAX        11P 0       COLHDG('折讓／銷退稅額')
     A            ISTXTP         1A         COLHDG('稅別')
     A* 1 =應稅 2 =零稅 3 =免稅
     A            ISVNNO        10A         COLHDG('客戶統編')
     A            ISRESV         6A         COLHDG('備註')
     A            ISSEQN         5S 0       COLHDG('資料處理序號')
     A            ISFILE         6S 0       COLHDG('檔案序號')
     A****
     A          K ISNO
     A          K ISTYPE
0006AA          K ISINDT
0006AA          K ISPDNM
0302AA          K ISCAMT
0410AA          K ISRESV
