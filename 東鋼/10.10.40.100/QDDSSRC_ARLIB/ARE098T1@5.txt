     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARE098T1
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     區間發票開立試算表
     A*           4.DATE-WRITTEN  101/12/25 2012AR708
     A*           5.WRITER        S00WCJ
     A*           6.UPDATE  DATE  102/07/09 2013AR754 S00WCJ (0207A)
     A*                            增加備註欄位
     A*                           104/09/22 2015AR953 S00WCJ (0409A)
     A*                            產品代碼3碼擴5碼
     A*                           108/07/26 2019AR00048 S00WCJ (0807A)
     A*                            抬頭改為區間指定發票試算
     A*                          108/09/11 S00WCJ (0809A)
     A*                           配合磅單過至項次修改
     A*                          111/02/22 2022AR00011 S00WCJ (1102A)
     A*                           修改簽核欄位
     A*
     A*****************************************************************
     A          R AR98T11H
     A*****************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      ' PRT-ID  : ARE098T'
0809AA                                    50
     A                                      '東和鋼鐵企業股份有限公司'
0809AA                                   110
     A                                      '列表日期:'
     A                                    +1
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      ' USR-ID  :'
     A                                      SPACEB(001)
     A            U#USID        10        +1
0809AA                                    49
0807AA                                      '※區間指定發票差異試算表※'
0809AA                                   110
     A                                      '列表時間:'
     A                                    +1
     A                                      TIME
0809AA                                   110
     A                                      '頁　　次:'
     A                                      SPACEB(001)
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '試算單號:'
     A                                      SPACEB(002)
     A            R#NO          10        +1
     A                                    +1
     A                                      '訂單編號:'
     A            R#ORNO         6        +1
     A                                    +1'客戶編號:'
     A            R#CUNO         6        +1
     A                                    +1'客戶名稱:'
     A            R#CUNM        10        +1
     A                                    +1'發票開立方式:'
     A            R#IVYN         1        +1
     A  45                               105'區間開立'
     A N45                               105'指定開立'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
     A                                      ===================+
0809AA                                      ============+
     A                                      ================'
     A                                      SPACEB(001)
     A                                     1
     A                                      '日  期 '
     A                                      SPACEB(001)
     A                                    +1
     A                                      '磅單編號'
     A                                    +0
     A                                      '項次'
0809AA                                    +0
0809AA                                      '磅次'
0809AA                                    +0
0809AA                                      '徑度'
     A                                    +0
     A                                      '品名'
     A                                    +0
     A                                      '單價'
0809AA                                    54
     A                                      '數 量 '
0809AA                                    67
     A                                      '金     額'
0809AA                                    84
     A                                      '差異數量'
0809AA                                    94
     A                                      '差異單價'
0809AA                                   105
     A                                      '差異金額'
0809AA                                   115
     A                                      '調整後數量'
0809AA                                   130
     A                                      '調整後金額'
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
     A                                      ===================+
0809AA                                      ============+
     A                                      ================'
     A                                      SPACEB(001)
     A**************************************************************************
     A          R AR98T11D
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A  31        R#TXDE         8  0O     2EDTWRD('    /  /  ')
     A            R#TXNO         8   O    14
     A            R#ITEM         2  0O    24
0809AA            R#VRTM         2  0O    31
0809AA            R#PDS1         3   O    36
0809AA            R#PDNM         5   O    41
0809AA            R#UPRC         5  3O    46EDTCDE(K)
0809AA            R#QTY          7  0O    54EDTCDE(K)
0809AA            R#TAM1        11  0O    67EDTCDE(K)
0809AA            R#SQTY         7  0O    84EDTCDE(K)
0809AA            R#SPRC         5  3O    96EDTCDE(K)
0809AA            R#AMT          7  0O   105EDTCDE(K)
0809AA            R#UQTY         7  0O   117EDTCDE(K)
0809AA            R#UAMT         7  0O   132EDTCDE(K)
     A**************************************************************************
     A          R AR98T11L
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                     1
     A                                      ' '
     A**************************************************************************
     A          R AR98T11M
     A**************************************************************************
     A N35                                 1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
     A                                      ===================+
0809AA                                      ============+
     A                                      ================'
     A*                                     SPACEB(001)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A N35                                14 '品名小計:'
0809AA            RASPDM         5   O    41
0809AA            RATQTY         8  0     53EDTCDE(K)
0809AA            RATAM2        12  0     66EDTCDE(K)
0809AA            RATQY2         8  0     83EDTCDE(K)
0809AA            RATAMT        10  0    101EDTCDE(K)
0809AA            RAUQY2         8  0    116EDTCDE(K)
0809AA            RAUAM2        10  0    128EDTCDE(K)
     A
     A**************************************************************************
     A          R AR98T21M
     A**************************************************************************
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ==========================-
     A                                      ===================+
0809AA                                      ============+
     A                                      ================'
     A                                      SPACEA(001)
     A*----------------------------------------------------------------
     A                                    14 '合  計:'
0809AA            RBTQTY         8  0     53EDTCDE(K)
0809AA            RBTAM2        12  0     66EDTCDE(K)
0809AA            RBTQY2         8  0     83EDTCDE(K)
0809AA            RBTAMT        10  0    101EDTCDE(K)
0809AA            RBUQY2         8  0    116EDTCDE(K)
0809AA            RBUAM2        10  0    128EDTCDE(K)
     A
     A**************************************************************************
     A          R AR98T11T
     A**************************************************************************
     A                                      SKIPB(059)
     A                                    14
0207AA                                      '備 註：'
0207AA            R#TEX1        72        +1UNDERLINE
0207AA                                      SPACEA(001)
0207AA            R#TEX2        72        26UNDERLINE
     A**************************************************************************
     A          R AR98T21T
     A**************************************************************************
     A                                      SKIPB(063)
1102AA                                     5'複核：________________'
1102AA                                   +15'初核：________________'
1102AA                                   +15'製表人：_________________'
1102AA*                                    2
1102AA*                                     '副總:______________'
1102AA*                                   +5'經理:______________'
1102AA*                                   +5'副理:______________'
1102AA*                                   +5'複核:______________'
1102AA*                                   +5'製表人:_____________'
     A**************************************************************************
     A          R AR98T11X
     A**************************************************************************
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     1
     A                                      '===============================-
     A                                      =======================+
     A                                      ==接　下　頁==============-
     A                                      =======================+
     A                                      ========================'
