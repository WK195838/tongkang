     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARE094S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02LBU
     A*            4.FUNCTION     應收票據代開傳票後更正作業
     A*            5.DATE-WRITTEN  95/07/27
     A*            6.DATE-UPDATED 101/09/20  2012AR682  S00WCJ (0109A)
     A*
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CF03(03 '結束作業')
     A                                      CF12(12 '回前畫面')
     A*================================================================
     A          R AR094F1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 28'東和鋼鐵企業股份有限公司'
     A                                  1 70'ARE094S-1'
     A                                  2  2TIME
     A                                  2 25'★應收票據代開傳票後更正作業★'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3  2
     A*========
     A  32                              7 24'請輸入欲開放修改之繳款日及繳款書'
     A  32                                  COLOR(RED)
     A*
     A                                  9 29'繳款日期:'
     A            S#RVDT         8Y 0B  9 41EDTWRD('    /  /  ')
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A*
     A                                 11 29'繳款編號:'
     A            S#RVNO         6A  B 11 41
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A*
     A*========
     A                                 23  3'PF3=結束作業'
     A                                 23 20'PF12=回前畫面'
     A            S#ERR         70A  O 24  2DSPATR(HI)
     A*****************************************************************
     A          R AR094F2                   SFL
     A  70                                  SFLNXTCHG
     A            S#CHK          1A  B 12  2
     A            S#ACTP         1A  O 12  7
     A            S#USTP         2A  O 12 10
     A            S#NTTP         1A  O 12 14
0109AA            S#ACNO         6A  O 12 18
     A            S#PBID        14O  O 12 26
     A            S#PANO        10A  O 12 41
     A            S#PLAC         1A  O 12 52
     A            S#RLNO        10E  O 12 55
     A            S#RAMT        11Y 0O 12 66EDTWRD('  ,   ,   ,   ')
     A            S#NTNO        20A  O 13 18
     A            S#SANO        10A  O 13 41
     A            S#DUDT         8Y 0B 13 55EDTWRD('    /  /  ')
     A  33                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A            S#RESV        10O  O 13 68
     A            S#LLNO        10O  H
     A            S#SFI2         3S 0H
     A***************************************************************
     A          R AR094F2C                  SFLCTL(AR094F2)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A                                      CF10(10 '存檔')
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 28'東和鋼鐵企業股份有限公司'
     A                                  1 70'ARE094S-2'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 25'★應收票據代開傳票後更正作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A            S#SF2T        10O  O  3  2DSPATR(RI)
     A*----------------------------------------------------------------
     A                                  4  8'繳款編號:'
     A            S#RVNO         6A  O  4 20
     A                                  4 31'繳款部門:'
     A            S#DPNO         4A  O  4 43
     A                                  4 52'收款業務員:'
     A            S#RVID         1A  O  4 66
     A                                  5  8'客戶編號:'
     A            S#CUNO         6A  O  5 20
     A                                  5 27'客戶名稱:'
     A            S#CUNM        10O  O  5 39
     A                                  5 54'繳款日期:'
     A            S#RVDT         8Y 0O  5 66EDTWRD('    /  /  ')
     A N60                              6  2'請於欲解開管控之票號確認欄前上Ｙ'
     A                                      COLOR(RED)
     A  60                              6  2'請於欲修改之資料確認欄前上Ｙ'
     A                                      COLOR(RED)
     A                                  6 50'繳款金額合計:'
     A            S#NAMT        11Y 0O  6 66EDTCDE(1)
     A                                      DSPATR(HI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  1'確　借　款　票'
     A                                  8 51'付'
     A                                  9  1'　　貸　項　款　會計科目'
     A                                  9 28'付款銀行　　付款帳戶'
     A                                  9 51'款　相關號碼　　金　　　額'
     A                                 10  1'認　方　別　別　票據號碼'
     A                                 10 28'原繳款單號　存入帳號'
     A                                 10 51'地　到期日期  對　象　別'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR094F2M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  4'F01:說明'
     A                                 23 19'F03:結束'
     A                                 23 34'F12:回前'
     A  33                             23 49'ENTER:執行'
     A N33                             23 49'ENTER:檢核'
     A N33                             23 66'F10:存檔'
     A            S#MSG2        70A  O 24  2DSPATR(HI)
     A***************************************************************
