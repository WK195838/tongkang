     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARR108T1
     A*            2.PROGRAM-TYPE  PRTF
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     銷貨調整單開立發票彙總表
     A*            5.DATE-WRITTEN  112/06/06
     A*            6.DATE-UPDATE   113/08/15 S00WCJ (1308A)
     A*                             修改說明欄位長度，避免列印亂碼
     A*
     A*****************************************************************
     A          R AR08T11H
     A**************************************************************************
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報　　表: ARR108T1'
     A                                      SPACEB(001)
     A                                    68
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   149
     A                                      '印表人員:'
     A            U#USID        10A      160
     A            T#DAT2         8  0     28
     A                                      EDTWRD('    /  /  ')
     A                                      SPACEB(001)
     A*=======
     A                                     2
     A                                      '開立日期:'
     A            T#DAT1         8  0     14
     A                                      EDTWRD('    /  /  ')
     A                                    25
     A                                      '~~'
     A                                    68
     A                                      '銷貨調整單開立發票彙總表'
     A                                   149
     A                                      '印表日期:'
     A                                   160
     A                                      DATE
     A                                      EDTWRD('01  /  /  ')
     A*=======
     A                                     2
     A                                      '開立廠區:'
     A                                      SPACEB(001)
     A            T#AREA         6   O    13
     A                                    +1'客戶編號:'
     A            T#CUNS         6   O    +1
     A                                    +1'訂單編號:'
     A            T#ORNS         6   O    +1
     A                                    +1'~'
     A            T#ORNE         6   O    +1
     A                                    +1'調整品名:'
     A            T#PDNS         3   O    +1
     A                                   149
     A                                      '頁　　數:'
     A                                   160
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*=======
     A            T1LIN1       174A  O     2
     A                                      SPACEB(001)
     A*=======
     A                                     4
     A                                      '開立日期'
     A                                      SPACEB(001)
     A                                    17
     A                                      '客戶代號'
     A                                    33
     A                                      '客戶名稱'
     A                                    50
     A                                      '訂單編號'
     A                                    63
     A                                      '說　　明'
     A                                    86
     A                                      '發票號碼'
     A                                   104
     A                                      '調整單號'
     A                                   121
     A                                      '金　　額'
     A                                   141
     A                                      '稅  額'
     A                                   161
     A                                      '總金額'
     A*=======
     A            T1LIN2       174A  O     2
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR08T11D
     A**************************************************************************
     A                                     2
     A                                      ' '
     A                                      SPACEB(001)
     A            T#DATE         8  0O     3
     A                                      EDTWRD('    /  /  ')
     A            T#CUNO         6A  O    19
     A            T#CUNM        10A  O    33
     A            T#ORNO         6A  O    52
1308AA            T#TEXT        22A  O    63
     A            T#IVNO        10A  O    86
     A            T#NO           8A  O   105
     A            T1AMT         10S  O   121
     A                                      EDTCDE(1)
     A            T1TAX          9S  O   141
     A                                      EDTCDE(1)
     A            T1TAM         11S  O   159
     A                                      EDTCDE(1)
     A*****************************************************************
     A          R AR08T11L
     A*****************************************************************
     A            T1LIN3       174A  O     2
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR08T21S
     A*****************************************************************
     A            T#PDNM        13A  O    92
     A                                      SPACEB(001)
     A            T#PDNO         5A  O    +0
     A                                    +0
     A                                      '品名小計：'
     A            T#AMT2        12S  O    +0
     A                                      EDTCDE(1)
     A            T1TAX2        10S 0O   139EDTCDE(1)
     A            T1TAM2        12S 0O   158EDTCDE(1)
     A*****************************************************************
     A          R AR08T11T
     A*****************************************************************
     A                                   113
     A                                      '總計：'
     A                                      SPACEB(001)
     A            T#AMTT        12S  O   122
     A                                      EDTCDE(1)
     A            T1TAXT        10S 0O   139EDTCDE(1)
     A            T1TAMT        12S 0O   158EDTCDE(1)
     A                                      SPACEA(001)
     A*****************************************************************
     A          R AR08T21T
     A*****************************************************************
     A                                   109
     A                                      '品名合計：'
     A                                      SPACEB(001)
     A            T#AMT3        12S  O   122
     A                                      EDTCDE(1)
     A            T1TAX3        10S 0O   139EDTCDE(1)
     A            T1TAM3        12S 0O   158EDTCDE(1)
     A*****************************************************************
     A          R AR08T11M
     A**************************************************************************
     A                                    19
     A                                      '*****無符合條件之資料!!!'
     A                                      SPACEB(008)
     A**************************************************************************
     A*         R AR108T1Y
     A**************************************************************************
     A*                                   12
     A*                                     '主　　　管:'
     A*                                     SPACEB(008)
     A*                                   62
     A*                                     '審　　　核:'
     A*                                  112
     A*                                     '製　　　表:'
     A**************************************************************************
