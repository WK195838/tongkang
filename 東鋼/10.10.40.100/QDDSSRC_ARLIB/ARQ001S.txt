     A****************************************************************
     A*
     A*       UPDATE  DATE  104/09/24  2015AR953  S00WCJ (0409A)
     A*                      產品代碼3碼擴5碼
     A*
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARQ001S-1'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 26'***訂單應收帳款發票查詢***'
     A            DEVNM         10A  O  2 70
     A                                  7 27'訂單號碼:'
     A            W0101     R        B    +1REFFLD(INORNO INVMST)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                 10 27'發票號碼:'
     A            W0102     R        B    +1REFFLD(INNO INVMST)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A                                    +1'-'
     A            W0103     R        B    +1REFFLD(INNO INVMST)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SF1                       SFL
     A  88                                  SFLNXTCHG
     A            ACT            1A  B  9  2
     A            W01       R        O  9  4REFFLD(INNO INVMST)
     A            W02       R    8 00O  9 15REFFLD(ININDT INVMST)
     A                                      EDTCDE(Z)
     A            W03       R    9 00O  9 24REFFLD(INAAMT INVMST)
     A                                      EDTCDE(1)
     A            W04       R    8 00O  9 36REFFLD(INCAMT INVMST)
     A                                      EDTCDE(L)
     A            W05       R    9 00O  9 46REFFLD(INBAMT INVMST)
     A                                      EDTCDE(L)
     A            W06       R    8 00O  9 57REFFLD(INATAX INVMST)
     A                                      EDTCDE(L)
     A            W07       R    9 00O  9 67REFFLD(INNBAL INVMST)
     A                                      EDTCDE(L)
     A            W08            1A  O  9 78
     A***************************************************************
     A          R SFCTL1                    SFLCTL(SF1)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      CF03(03)
     A                                      CF12
     A                                      OVERLAY
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A  10                                  SFLEND
     A            NBR            4S 0H      SFLRCDNBR(CURSOR)
     A                                  1 70'ARQ001S-2'
     A            COMP          35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'***  訂單別發票查詢  ***'
     A            DEVNM         10A  O  2 70
     A                                  3  7'訂單號碼:'
     A            W0101     R        O  3 19REFFLD(INORNO INVMST)
     A                                  4  7'客戶編號:'
     A            INCUNO    R        O  4 19REFFLD(INCUNO INVMST)
     A            INCUNM    R        O  4 27REFFLD(INCUNM INVMST)
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6 35'折讓／'
     A                                  7 46'預收貨款'
     A                                  7 57'稅　額'
     A                                  7 67'發票餘額　廢'
     A                                  6 77'作'
     A                                  7 25'出貨金額　退回金額'
     A                                  7  3'發票號碼'
     A                                  7 16'日期'
     A                                  6 16'發票'
     A***************************************************************
     A          R UND01
     A                                      OVERLAY
     A                                 21  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22 11'合　計:'
     A            T03            9Y 0O 22 21EDTCDE(1)
     A            T04            7Y 0O 22 33EDTCDE(J)
     A            T05            8Y 0O 22 44EDTCDE(J)
     A            T06            7Y 0O 22 56EDTCDE(J)
     A            T07            8Y 0O 22 67EDTCDE(J)
     A                                 23  3'輸入"Y"查詢發票明細'
     A                                      DSPATR(RI)
     A                                 24  4'PF03:結束'
     A                                 24 21'PF12:回前畫面'
     A***************************************************************
     A          R SFLREC                    SFL
     A  88                                  SFLNXTCHG
     A            ITEM      R        O 11  3REFFLD(IVITEM INVDTL)
     A            ACNT      R        O 11  8REFFLD(IVACNT INVDTL)
0409AA            PDNM      R        O 11 11REFFLD(IVPDCD INVDTL)
     A            QTY            7Y 0O 11 17EDTWRD(' ,   ,   -')
     A            UPRC           5Y 3O 11 29EDTWRD('  .   ')
     A            AMT            9Y 0O 11 37EDTWRD('   ,   ,   -')
     A            ACDT           8Y 0O 11 50EDTWRD('    /  /  ')
     A            APNO      R        O 11 61REFFLD(IVAPNO INVDTL)
     A            ACNO      R        O 11 71REFFLD(IVACNO INVDTL)
 9201A            DECD      R        O 11 79REFFLD(IVDECD INVDTL)
     A***************************************************************
     A          R SFLCTL                    SFLCTL(SFLREC)
     A                                      SFLSIZ(0120)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARQ001S-3'
     A            COMP          35A  O  1 24
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'***  訂單別發票查詢  ***'
     A            DEVNM         10A  O  2 70
     A                                  3  5'發票號碼:'
     A            W01           10A  O  3 17
     A                                  3 48'請款單號:'
     A            APNO1          7A  O    +1
     A                                  4  5'客戶編號:'
     A            CUNO           6A  O  4 17
     A                                  4 24'客戶名稱:'
     A            CUNM          10J  O  4 36
     A                                  4 48'銷售別　:'
     A            TYPE           1A  O  4 60
     A                                  5  5'訂單編號:'
     A            ORNO           6A  O  5 17
     A                                  5 24'業務員　:'
     A            SALE           1A  O  5 36
     A                                  5 48'發票別　:'
     A            CHK            1A  O  5 60
     A                                  4 63'收款別　:'
     A            RVID           1A  O  4 75
     A                                  6  5'發票種類:'
     A            TYP1           1A  O  6 17
     A                                  6 19'(1:出貨2:預收3:調整9:其他)-
     A                                      '
     A                                  5 63'開立廠區:'
     A            AREA           1A  O  5 75
     A                                  7  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  2'項　種'
     A                                  9  2'目　類'
     A                                  9 11'品名'
     A                                  9 19'數　量'
     A                                  9 30'單價'
     A                                  9 37'　金　　額'
     A                                  9 50'入帳日期憑證編號傳票編號'
     A                                 10  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6 63'發票已作廢'
     A N74                                  DSPATR(ND)
     A  74                                  DSPATR(RI BL)
     A***************************************************************
     A          R ERRFMT
     A                                      OVERLAY
     A                                 21  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22 25'***總計:'
     A            TTLAMT         9Y 0O 22 37EDTCDE(J)
     A            ERRMSG        70A  O 23  2DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 19'PF03:結束'
     A                                 24 32'PF12:回前畫面'
