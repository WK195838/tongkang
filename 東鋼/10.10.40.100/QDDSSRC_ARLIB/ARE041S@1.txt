     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE041R
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02CLJ
     A*            4.FUNCTION     發票銷貨查詢作業
     A*            5.DATE-WRITTEN  88/02/05
     A*
     A***************************************************************
     A*  畫面一:輸入訂單
     A***************************************************************
     A          R AR041F1                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT1         1A  B 11  9VALUES('1' ' ')
     A  53                                  DSPATR(PC RI)
     A            S#ORNO         6A  O 11 15
     A            S#ORDT         8Y 0O 11 23EDTCDE(4)
     A            S#CTNO        10O  O 11 34
     A            S#CUNO         6A  O 11 47
     A            S#CUNM        10A  O 11 55
     A***************************************************************
     A          R AR041F1C                  SFLCTL(AR041F1)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA12(12)
     A                                      ROLLUP(91)
     A                                      ROLLDOWN(92)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN1)
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN1         5S 0H
     A            S#SFN1         4S 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE041S-1'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  2 30'★發票銷貨查詢作業★'
     A                                      DSPATR(RI)
     A                                  4  2'選項: 1 --> 進行該訂單之發票銷+
     A                                      貨查詢作業。'
     A                                  5 16'（可輸入訂單編號來移至最近的記錄+
     A                                      位置）'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  5'選項'
     A                                  8 25'日期'
     A                                  8 34'購案編號'
     A                                  8 12'訂單編號'
     A                                  8 47'客戶編號及名稱'
     A                                  9  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#INK1         6A  B 10 15
     A  51                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A***************************************************************
     A          R AR041F1M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  4'F03:結束'
     A                                 23 20'F12:回前'
     A                                 23 36'ENTER:執行'
     A            S#MSG1        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面二:選擇發票查詢或列印
     A***************************************************************
     A          R AR041F2                   SFL
     A            S#S2IT         4  0O 11  3EDTCDE(3)
     A            S#OPT2         1A  B 11  9VALUES(' ' '1')
     A            S#INNO        10A  O 11 11
     A            S#INDT         8Y 0O 11 22EDTCDE(4)
     A            S#INAM        11Y 0O 11 31EDTCDE(J)
     A            S#INBN        11Y 0O 11 47EDTCDE(J)
     A            S#INAP         6A  O 11 63
     A            S#INTP         1A  O 11 70
     A            S#KIND         1A  O 11 72
     A            S#SALE         2A  O 11 74
     A            S#RVID         2A  O 11 77
     A*
     A            S#AREA         1A  H
     A            S#SATP         1A  H
     A            S#INAA        11Y 0H
     A            S#INBA        11Y 0H
     A            S#INAT        11Y 0H
     A***************************************************************
     A          R AR041F2C                  SFLCTL(AR041F2)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF09(09)
     A                                      CA12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4Y 0H
     A*----------------------------------------------------------------
     A                                  1 70'ARE041S-2'
     A            S#COMP        35A  O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'★發票銷貨查詢作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  4  4'客戶編號:'
     A            S#CUNO         6A  O  4 16
     A            S#CUNM        10O  O  4 23
     A                                  4 43'訂單編號:'
     A            S#ORNO         6A  O  4 55
     A                                  6  2'選項: 1 --> 選擇該張發票進行查+
     A                                      詢或列印作業。（可同時選擇多筆）'
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  4'NO'
     A                                  8 10'發票號碼'
     A                                  8 21'發票日期'
     A                                  8 35'發票金額'
     A                                  8 51'發票餘額'
     A                                  8 63'請款　類聯出收'
     A                                  9  2'------------------+---+---+--------
     A                                      ---+---+---+------------------------
     A                                      +---+---+'
     A                                      DSPATR(HI)
     A            S#INK2        10A  B 10 11
     A  54                                  DSPATR(PC)
     A            S#INK3         8Y 0B 10 22EDTCDE(4)
     A***************************************************************
     A          R AR041F2M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 21  2'------------------+---+------------
     A                                      ---+---+---+------------------------
     A                                      +---+---+'
     A                                      DSPATR(HI)
     A*
     A                                 22 62'發票張數:'
     A            S#SFN2         4Y 0O    +1EDTCDE(3)
     A*
     A                                 23  4'F03:結束'
     A                                 23 20'F12:回前'
     A                                 23 36'F09:列印'
     A                                 23 52'ENTER:查詢'
     A            S#MSG2        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面三:顯示銷貨明細畫面
     A***************************************************************
     A          R AR041F3                   SFL
     A            S#TXCD         4A  O 12  4
     A            S#TXNO         8A  O 12  9
     A            S#TXIT         2Y 0O 12 18
     A            S#TXDT         8Y 0O 12 21EDTCDE(4)
     A            S#TXPD         3A  O 12 30
     A            S#TXQT         7Y 0O 12 34EDTCDE(J)
     A            S#TXUP         5Y 3O 12 45EDTWRD(' 0.   ')
     A            S#TXAM        11Y 0O 12 52EDTCDE(J)
     A            S#TXPC         8A  O 12 69
     A***************************************************************
     A          R AR041F3C                  SFLCTL(AR041F3)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA11(11)
     A                                      CA12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN3)
     A            S#NBR3         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN3         5S 0H
     A            S#SFN3         4S 0H
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE041S-3'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'★發票銷貨查詢作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  O  4 14
     A                                  4 29'客戶簡稱:'
     A            S#CUNM        10O  O  4 41
     A                                  4 57'訂單號碼:'
     A            S#ORNO         6A  O  4 69
     A                                  5  2'發票號碼:'
     A            S#INNO        10A  O  5 14DSPATR(HI)
     A                                  5 29'發票日期:'
     A            S#INDT         8Y 0O  5 41EDTCDE(4)
     A                                  5 57'開立廠區:'
     A            S#AREA         1A  O  5 69
     A                                  6  2'發票類別:'
     A            S#INTP         1A  O  6 14
     A                                  6 29'發票聯式:'
     A            S#KIND         1A  O  6 41
     A                                  6 57'課稅別:'
     A            S#TXTP         1A  O  6 69
     A                                  7  2'銷售別:'
     A            S#SATP         1A  O  7 14
     A                                  7 29'出貨業務:'
     A            S#SALE         2A  O  7 41
     A                                  7 57'收款業務:'
     A            S#RVID         2A  O  7 69
     A                                  8  2'發票金額:'
     A            S#INAM        11Y 0O  8 14EDTCDE(J)
     A                                      DSPATR(HI)
     A                                  8 33'銷貨:'
     A            S#WTAM        11Y 0O  8 41EDTCDE(J)
     A                                      DSPATR(HI)
     A                                  9  2'---------------------------------'
     A                                      DSPATR(HI)
     A                                    +1'銷貨明細'
     A                                      DSPATR(RI)
     A                                    +1'---------------------------------'
     A                                      DSPATR(HI)
     A                                 10  3'類別'
     A                                 10 10'編號'
     A                                 10 17'項'
     A                                 10 22'日期　品名'
     A                                 10 38'數量'
     A                                 10 46'單價'
     A                                 10 61'金額'
     A                                 10 68'調整磅單'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR041F3M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  4'F03:結束'
     A                                 23 20'F12:回前'
     A                                 23 36'F11:切換'
     A                                 23 52'ENTER:執行'
     A            S#MSG3        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面四:顯示發票明細畫面
     A***************************************************************
     A          R AR041F4                   SFL
     A            S#S4IT         2Y 0O 12  3
     A            S#IVAC         2A  O 12  7
     A            S#IVIT         2Y 0O 12 11
     A            S#IVDT         8Y 0O 12 15EDTCDE(4)
     A            S#IVPD         3A  O 12 25
     A            S#IVQT         7Y 0O 12 30EDTWRD(' ,   ,   -')
     A            S#IVPR         7Y 5O 12 42EDTWRD(' 0.     ')
     A            S#IVAM        11Y 0O 12 52EDTWRD('  ,   ,   ,   -')
     A            S#IVAP         8A  O 12 69
     A***************************************************************
     A          R AR041F4C                  SFLCTL(AR041F4)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CA11(11)
     A                                      CA12(12)
     A  71                                  SFLDSPCTL
     A  72                                  SFLDSP
     A  73                                  SFLCLR
     A  74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN4)
     A            S#NBR4         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN4         5S 0H
     A            S#SFN4         4S 0H
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE041S-4'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 30'★發票銷貨查詢作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A*----------------------------------------------------------------
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  O  4 14
     A                                  4 29'客戶簡稱:'
     A            S#CUNM        10O  O  4 41
     A                                  4 57'訂單號碼:'
     A            S#ORNO         6A  O  4 69
     A                                  5  2'發票號碼:'
     A            S#INNO        10A  O  5 14DSPATR(HI)
     A                                  5 29'發票日期:'
     A            S#INDT         8Y 0O  5 41EDTCDE(4)
     A                                  5 57'開立廠區:'
     A            S#AREA         1A  O  5 69
     A                                  6  2'發票類別:'
     A            S#INTP         1A  O  6 14
     A                                  6 29'發票聯式:'
     A            S#KIND         1A  O  6 41
     A                                  6 57'課稅別:'
     A            S#TXTP         1A  O  6 69
     A                                  7  2'銷售別:'
     A            S#SATP         1A  O  7 14
     A                                  7 29'出貨業務:'
     A            S#SALE         2A  O  7 41
     A                                  7 57'收款業務:'
     A            S#RVID         2A  O  7 69
     A                                  8  2'發票金額:'
     A            S#INAM        11Y 0O  8 14EDTCDE(J)
     A                                      DSPATR(HI)
     A                                  8 33'餘額:'
     A            S#INBN        11Y 0O  8 41EDTCDE(J)
     A                                      DSPATR(HI)
     A                                  9  2'---------------------------------'
     A                                      DSPATR(HI)
     A                                    +1'發票明細'
     A                                      DSPATR(RI)
     A                                    +1'---------------------------------'
     A                                      DSPATR(HI)
     A                                 10  6'種　項'
     A                                 10 15'入帳日'
     A                                 10 24'品名'
     A                                 10 34'數量'
     A                                 10 45'單價'
     A                                 10 57'金　　額'
     A                                 10 68'憑証編號'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR041F4M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  4'F03:結束'
     A                                 23 20'F12:回前'
     A                                 23 36'F11:切換'
     A                                 23 52'ENTER:執行'
     A            S#MSG4        70A  O 24  2DSPATR(HI)
