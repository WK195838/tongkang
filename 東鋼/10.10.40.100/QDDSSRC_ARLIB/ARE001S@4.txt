     A*%%TS  SD  19910506  110416  S02LYW      REL-V4R4M0  5769-PW1
     A*%%EC
     A****************************************************************
     A*
     A*       UPDATE  DATE  100/10/26  2011AR629 S00WCJ (0010A)
     A*                     104/09/18  2015AR953 S00WCJ (0409A)
     A*                      產品代碼3碼擴5碼
     A*
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/INVDTL)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R TITLE
     A                                      OVERLAY
     A                                      CF03(03)
     A                                  1 70'ARE001S-1'
     A            COMP          35   O  1 23
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2 28'*** 　發票維護作業　 ***'
     A            DEVNM         10   O  2 70
     A                                  5 30'功　　能:'
     A            CODE           1   B    +1
     A  41                                  DSPATR(PC RI)
     A                                    +1'(1:新增2:更正3:作廢4:查詢)'
     A                                  9 30'發票號碼:'
     A            NO            10   B    +1
     A  42                                  DSPATR(PC RI)
     A                                 11 30'發票聯式:'
     A            IVTP           1   B    +1
     A  43                                  DSPATR(PC RI)
     A                                    +1'(2:二聯式3:三聯式)'
     A                                 13 30'發票年月:'
     A            YYMM           6  0B    +1
     A  44                                  DSPATR(PC RI)
     A            ERRMSG        70A  O 23 02DSPATR(HI)
     A                                 24  5'ENTER:執行'
     A                                 24 20'PF03:結束'
     A***************************************************************
     A          R SFLREC                    SFL
     A*%%TS  SD  19811030  093920  JEFF        REL-V2R1M1  5738-PW1
     A  88                                  SFLNXTCHG
     A            ITEM      R        O 11  3REFFLD(IVITEM)
0409AA            ACNT      R        B 11  7REFFLD(IVACNT)
     A  71
     AO 70                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
0409AA            PDNM      R        B 11  9REFFLD(IVPDCD)
     A  70                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A            QTY            7Y 0B 11 15EDTWRD(' ,   ,   -')
     A  70                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A            UPRC           9Y 5B 11 26EDTWRD('   0.     ')
     A  70                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A            AMT            9Y 0B 11 37EDTWRD('   ,   ,   -')
     A  70                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A            ACDT           8Y 0B 11 50EDTWRD('    /  /  ')
     A  70                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A            APNO      R        O 11 61REFFLD(IVAPNO)
     A            ACNO      R        O 11 71REFFLD(IVACNO)
9111 A            S#DECD         1A  O 11 79
     A            HFLD1          1A  H
     A            HFLD2          1A  H
     A***************************************************************
     A          R SFLCTL                    SFLCTL(SFLREC)
     A*%%TS  SD  19820309  113321  VIN         REL-V2R1M1  5738-PW1
     A                                      SFLSIZ(0150)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10
     A                                      CF12
9105 A                                      CF04
     A  80                                  SFLCLR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  10                                  SFLEND
     A                                  1 70'ARE001S-2'
     A            COMP          35A  O  1 24
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            MOD            6A  O  2  2DSPATR(RI)
     A                                  2 28'*** 　發票維護作業　 ***'
     A            DEVNM         10A  O  2 70
     A                                  3  5'發票號碼:'
     A            NO            10A  O  3 17
     A                                  3 48'請款單號:'
     A            APNO1          7A  O    +1
0010AA                                    +1'發票已作廢'
     A N74                                  DSPATR(ND)
     A  74                                  DSPATR(RI)
     A  74                                  DSPATR(BL)
     A                                  4  5'客戶編號:'
     A            CUNO           6A  B  4 17
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A  70                                  DSPATR(PR)
     A                                  4 24'客戶名稱:'
     A            CUNM          10J  O  4 36
     A                                  4 48'銷售別　:'
     A            TYPE           1A  O  4 60
     A                                  5  5'訂單編號:'
     A            ORNO           6   B  5 17
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A  70                                  DSPATR(PR)
     A                                  5 24'業務員　:'
     A            SALE           1A  O  5 36
     A                                  5 48'發票別　:'
     A            CHK            1A  O  5 60
     A                                  4 63'收款別　:'
     A            RVID           1A  O  4 75
9105 A                                  6  5'發票類別:'
     A            TYP1           1A  B  6 17
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A  70                                  DSPATR(PR)
     A                                    +1'(1:出貨2:預收3:調整9:其他)-
     A                                      '
0010AA                                    +1'預收─鋼筋:'
0010AA            S#IVYN         1A  B    +1VALUES('Y' 'N')
0010AA                                    +1'(Y/N)'
     A                                  5 63'開立廠區:'
     A            AREA           1A  O  5 75
     A                                  7  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  2'項　種'
     A                                  9  2'目　類　品名'
     A*                                 9 11'品名'
     A                                  9 17'數　量'
     A                                  9 30'單價'
     A                                  9 37'　金　　額'
     A                                  9 50'入帳日期憑證編號傳票編號'
     A                                 10  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R ERRFMT
     A*%%TS  SD  19811030  093920  JEFF        REL-V2R1M1  5738-PW1
     A                                      OVERLAY
     A                                 21  3'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 22 25'***總計:'
     A            TTLAMT         9Y 0O 22 37EDTCDE(J)
9105 A                                 23  2'ENTER:執行'
 .   A                                    +1'PF03:結束'
 .   A                                    +1'PF10:存檔'
 .   A                                    +1'PF12:回前畫面'
 .   A N70                                +1'PF04:種類代號查詢'
 .   A            ERRMSG        70A  O 24  2DSPATR(HI)
9105 A*================================================================
 .   A          R AR001W
 .   A*%%TS  SD  19910506  110416  S02LYW      REL-V4R4M0  5769-PW1
 .   A                                      WINDOW(10 53 10 22)
 .   A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
 .   A                                       (*CHAR '        '))
 .   A                                  2  3'*種類代號查詢*'
 .   A                                  3  1'1出貨'
 .   A                                  4  1'2退貨（折讓帶入）'
 .   A                                  5  1'3折讓（折讓帶入）'
 .   A                                  6  1'4預收貨款'
 .   A                                  7  1'5稅額'
 .   A                                  8  1'6沖帳（繳款書代入）'
 .   A                                  9  3'執行鍵:結束查詢'
 .   A                                      DSPATR(HI)
