     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE037SC
     A*            2.PROGRAM-TYPE  RPG/400
     A*            3.AUTHOR        S02YSH
     A*            4.FUNCTION     新版營業繳款輸入程式（外銷專用)
     A*            5.DATE-WRITTEN  91/05/06
     A*            6.DATE-UPDATED  91/05/06
     A*                            100/07/29 S00WCJ (0007A)
     A*                            101/09/19 2012AR682 S00WCJ (0109A)
     A*
     A***************************************************************
     A*  畫面一:新增、修改、刪除、查詢、沖銷、複製、還原複製畫面
     A*
     A*  *IN33 : ON 表示為查詢狀態
     A***************************************************************
     A          R AR037F1                   SFL
     A  70                                  SFLNXTCHG
     A            S#OPT1         1Y 0B  9  4VALUES(0 2 3 4 5 6 8 9)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(RI)
     A            S#CUNO         6A  O  9  8
     A            S#CUNM        10O  O  9 16
     A            S#RVDT         8Y 0O  9 27EDTWRD('    /  /  ')
     A            S#RVNO         6A  O  9 39
     A            S#XAMT        11Y 0O  9 46EDTCDE(3)
     A            S#NAMT        11Y 0O  9 59EDTCDE(3)
     A            S#FL01         1A  O  9 73
     A            S#FL03         1A  O  9 75
     A            S#FL02         1A  O  9 77
     A***************************************************************
     A          R AR037F1C                  SFLCTL(AR037F1)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF06(06)
     A                                      CF12(12)
     A                                      ROLLUP(91)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A            S#NBR1         4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE037SC-1'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 28'★外銷繳款明細輸入作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A                                  4  2'選項:'
     A                                  4 11'2:修改'
     A                                    +1'3:試算'
     A                                    +1'4:刪除'
     A                                    +1'5:查詢'
     A                                    +1'6:沖銷'
     A                                    +1'8:確認'
     A                                    +1'9:確認還原'
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  6  2'選項'
     A                                  6  9'編號'
     A                                  6 16'客　戶'
     A                                  6 28'日期'
     A                                  6 48'沖銷合計'
     A                                  6 61'繳款合計'
     A                                  6 72'確印過'
     A                                  7  2'-----------------------------------
     A                                      ------------+---+---+----+---+---+--
     A                                      ---------'
     A                                      DSPATR(HI)
     A            S#KEY1         6A  B  8  8
     A  30                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A***************************************************************
     A          R AR037F1M                  OVERLAY
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------+---+---+----+---+---+--
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  2'F03:結束'
     A                                    +1'F12:回前'
     A                                    +1'F06:新增'
     A                                    +1'PGUP:上頁'
     A                                    +1'PGDN:下頁'
     A                                    +1'ENTER:執行'
     A            S#ERR1        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面二:輸入繳款明細畫面
     A***************************************************************
     A          R AR037F2                   SFL
     A  70                                  SFLNXTCHG
     A            S#SFI2         2Y 0O 12  2
     A            S#ACTP         1A  B 12  7VALUES(' ' 'C' 'D')
     A  33                                  DSPATR(PR)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A            S#USTP         2A  B 12 10DUP(35)
     A  33                                  DSPATR(PR)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A            S#NTTP         1A  B 12 14DUP(36)
     A  33                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
0109AA            S#ACNO         6A  B 12 18
     A  33                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A            S#PBID        14O  B 12 26DUP(37)
     A  33                                  DSPATR(PR)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A            S#RLNO        10E  B 12 42
     A  33                                  DSPATR(PR)
     A  49                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A            S#EXC2         8Y 4B 12 56EDTCDE(2)
     A  33                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A            S#SANO        10A  B 12 69
     A  33                                  DSPATR(PR)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A            S#NTNO        20A  B 13 18
     A  33                                  DSPATR(PR)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A            S#CURY         3A  B 13 40
     A  33                                  DSPATR(PR)
     A  50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A            S#RAMT        11Y 2B 13 45EDTCDE(2)
     A  33                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A            S#DUDT         8Y 0B 13 63EDTWRD('    /  /  ')
     A  33                                  DSPATR(PR)
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A***************************************************************
     A          R AR037F2C                  SFLCTL(AR037F2)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A                                      CF01(01)
     A                                      CF03(03)
     A N33                                  CF10(10)
     A                                      CF12(12)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN2)
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN2         5S 0H
     A            S#SFN2         4S 0H
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE037SC-2'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 28'★外銷繳款明細輸入作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A            S#SF2T         6O  O  3 70DSPATR(RI)
     A                                  3  2'繳款編號:'
     A            S#RVNO         6A  O  3 14
9710 A                                  3 22'報關方式:'
 .   A            S#TYPE         1A  B  3 34
     A  61
     AO 33                                  DSPATR(PR)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                  3 36'(1:台幣報關2:外幣報關)'
     A*
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  B  4 14
     A  61
     AO 33                                  DSPATR(PR)
     A  34                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                  4 22'客戶名稱:'
     A            S#CUNM        10O  O  4 34
     A                                  4 46'繳款日期:'
     A            S#RVDT         8Y 0B  4 58EDTWRD('    /  /  ')
     A  61
     AO 33                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A                                  5  2'繳款部門:'
     A            S#DPNO         4A  B  5 14
     A  61
     AO 33                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A                                  5 20'收款業務:'
     A            S#RVID         1A  B  5 32
     A  61
     AO 33                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A                                  5 34'財會匯率:'
     A            S#EXC1         8Y 4B  5 46EDTCDE(2)
     A  61
     AO 33                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A                                  5 57'報關匯率:'
     A            S#EXC3         8Y 4B  5 69EDTCDE(2)
     A  61
     AO 33                                  DSPATR(PR)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A                                  6 49'繳款金額合計:'
     A            S#NAMT        11Y 0O  6 65EDTCDE(1)
     A                                      DSPATR(HI)
     A N33                              6  3'借貸方清成空白，該項明細視同刪除-
     A                                      '
     A                                      DSPATR(RI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                  8  1'項　借　款　票'
     A                                  8 51'　'
     A                                  9  1'　　貸　項　款　會計科目'
     A                                  9 29'付款銀行　　相關號碼'
     A                                  9 52'　　銀行匯率　　存入帳號'
     A                                 10  1'次　方　別　別　票據號碼'
     A                                 10 38'幣別　　　金　　　額'
     A                                 10 63'到期日期'
     A                                 11  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR037F2M                  OVERLAY
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 22  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A                                 23  4'F01:說明'
     A                                 23 19'F03:結束'
     A                                 23 34'F12:回前'
     A  33                             23 49'ENTER:執行'
     A N33                             23 49'ENTER:檢核'
     A N33                             23 66'F10:存檔'
     A            S#ERR2        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A* 畫面二：輸入資料說明視窗
     A***************************************************************
     A          R AR037F2W
     A                                      WINDOW(8 14 14 62)
     A                                      TEXT('說明視窗')
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A                                  2  3'款項別'
     A                                      DSPATR(HI)
     A                                  2 18'票款別'
     A                                      DSPATR(HI)
     A                                  4  1'A1:一般貨款'
     A                                  4 16'A:支票'
     A                                  5  1'A2:預收貨款'
     A                                  5 16'B:背書支票'
     A                                  6  1'A3:延票利息'
     A                                  6 16'C:本票'
     A                                  7  1'A4:換票繳入'
     A                                  7 16'D:背書本票'
     A                                  8  1'A5:暫收款項'
     A                                  8 16'E:銀行本支票'
     A                                  9  1'Z5:其他'
     A                                  9 16'F:匯票'
     A                                 10 16'G:匯款'
     A                                 11 16'H:現金'
     A                                 12 16'I:信用狀'
     A                                 13 16'J:銀行存款'
     A                                  2 40'常用會計科目'
     A                                      DSPATR(HI)
     A                                  4 32'1111庫存現金'
     A                                  5 32'1114外幣存款'
     A                                  4 48'8112兌換損失'
     A                                  6 32'7119出口費用'
     A                                  5 48'8211利息費用'
     A                                  7 32'8246奇零尾數'
     A                                 11 32'7111（推）運　費'
     A                                 12 32'7112（推）檢驗費'
     A                                 13 32'7142（推）郵電費'
0109AA***************************************************************
0109AA* 畫面二：輸入資料說明視窗
0109AA***************************************************************
0109AA          R AR037F4W
0109AA                                      WINDOW(8 14 14 62)
0109AA                                      TEXT('說明視窗')
0109AA                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
0109AA                                       (*CHAR '        '))
0109AA                                      OVERLAY
0109AA                                      CF03(03)
0109AA                                      CF12(12)
0109AA                                  2  3'款項別'
0109AA                                      DSPATR(HI)
0109AA                                  2 18'票款別'
0109AA                                      DSPATR(HI)
0109AA                                  4  1'A1:一般貨款'
0109AA                                  4 15'A:支票'
0109AA                                  5  1'A2:預收貨款'
0109AA                                  5 15'B:背書支票'
0109AA                                  6  1'A3:延票利息'
0109AA                                  6 15'C:本票'
0109AA                                  7  1'A4:換票繳入'
0109AA                                  7 15'D:背書本票'
0109AA                                  8  1'A5:暫收款項'
0109AA                                  8 15'E:銀行本支票'
0109AA                                  9  1'Z5:其他'
0109AA                                  9 15'F:匯票'
0109AA                                 10 15'G:匯款'
0109AA                                 11 15'H:現金'
0109AA                                 12 15'I:信用狀'
0109AA                                 13 15'J:銀行存款'
0109AA                                  2 40'常用會計科目'
0109AA                                      DSPATR(HI)
0109AA                                  4 30'110101庫存現金'
0109AA                                  5 30'110104外幣存款'
0109AA                                  4 47'810201兌換損失'
0109AA                                  6 30'710109出口費用'
0109AA                                  5 47'820101利息費用'
0109AA                                  7 30'821204奇零尾數'
0109AA                                 11 30'710101（推）運　費'
0109AA                                 12 30'710102（推）檢驗費'
0109AA                                 13 30'710402（推）郵電費'
     A***************************************************************
     A*  畫面三:發票沖銷畫面
     A***************************************************************
     A          R AR037F3                   SFL
     A*%%TS  SD  19910506  162415  S02YSH      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#SFI3         3Y 0O 11  2
     A            S#STNO         8A  B 11  6
     A  33                                  DSPATR(PR)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A            S#STIT         2Y 0O 11 15
0007AA            S#INXA        11Y 0B 11 18EDTCDE(L)
     A  33                                  DSPATR(PR)
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(RI)
     A            S#INOR         6A  O 11 31
0007AA            S#INAM        11Y 0O 11 38EDTCDE(L)
0007AA            S#INBA        11Y 0O 11 51EDTCDE(L)
     A            S#CODE         1A  B 11 64
     A  33                                  DSPATR(PR)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
0007AA            S#INRA        11Y 0O 11 69EDTCDE(L)
     A            S#PDNM         3A  H
     A            S#HNXA        11S 0H
     A            S#UPRC         7S 5H
     A***************************************************************
     A          R AR037F3C                  SFLCTL(AR037F3)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      CF03(03)
     A N33                                  CF09(09)
     A N33                                  CF10(10)
     A                                      CF12(12)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A            S#NBR3         4S 0H      SFLRCDNBR(CURSOR)
     A*----------------------------------------------------------------
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A            S#COMP        35A  O  1 23
     A                                  1 70'ARE037SC-3'
     A                                  2  2TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 28'★外銷繳款明細輸入作業★'
     A                                      DSPATR(RI)
     A            S#DEVI        10A  O  2 70
     A            S#SF2T         6O  O  3 70DSPATR(RI)
     A                                  3  2'繳款編號:'
     A            S#RVNO         6A  O  3 14
     A                                  4  2'客戶編號:'
     A            S#CUNO         6A  B  4 14
     A  61
     AO 33                                  DSPATR(PR)
     A  34                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                  4 22'客戶名稱:'
     A            S#CUNM        10O  O  4 34
     A                                  4 46'繳款日期:'
     A            S#RVDT         8Y 0B  4 58EDTWRD('    /  /  ')
     A  61
     AO 33                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A                                  5  2'繳款部門:'
     A            S#DPNO         4A  B  5 14
     A  61
     AO 33                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A                                  5 20'收款業務:'
     A            S#RVID         1A  B  5 32
     A  61
     AO 33                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A                                  5 34'財會匯率:'
     A            S#EXC1         8Y 4B  5 46EDTCDE(2)
     A  61
     AO 33                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A                                  5 57'報關匯率:'
     A            S#EXC3         8Y 4O  5 69EDTCDE(2)
     A                                  6 49'繳款金額合計:'
     A            S#NAMT        11Y 0O  6 65EDTCDE(1)
     A                                  6  2'沖銷金額合計:'
     A            S#XAMT        11Y 0O  6 18EDTCDE(1)
     A                                      DSPATR(HI)
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A N33                              8  3'清除磅單號碼該筆明細視同刪除'
     A                                      DSPATR(RI)
     A                                  9  5'磅單號碼'
     A                                  9 20'沖銷金額'
     A                                  9 31'訂單'
     A                                  9 39'磅單金額'
     A                                  9 51'磅單餘額　全沖銷'
     A                                  9 71'未過已沖'
     A                                 10  2'------------------+---+---+--------
     A                                      ---+---+---+---+---+---+------------
     A                                      +---+---+'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR037F3M
     A                                      TEXT('按鍵及錯誤訊息')
     A                                 21  2'------------------+---+---+--------
     A                                      ---+---+---+---+---+---+------------
     A                                      +---+---+'
     A                                      DSPATR(HI)
     A                                 22  4'F03:結束'
     A                                 22 23'F12:回前'
     A  33                             22 48'ENTER:執行'
     A N33                             22 48'ENTER:檢核'
     A*N33                             23  4'F06:重設'
     A N33                             23  4'F09:依訂單載入'
     A N33                             23 23'F10:存檔'
     A            S#ERR3        70A  O 24  2DSPATR(HI)
     A***************************************************************
     A*  畫面四:依訂單載入發票
     A***************************************************************
     A          R AR037F3W
     A                                      WINDOW(8 44 8 24)
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      CF03(03)
     A                                      CF12(12)
     A                                  2  7'*訂單載入*'
     A                                  4  2'訂單單號:'
     A            S#ORNO         6A  B  4 14
     A  60                                  DSPATR(RI)
     A                                  6  3'F3/F12:離開'
     A                                      DSPATR(HI)
     A            S#ERRW        20A  O  7  2DSPATR(HI)
     A***************************************************************
     A* 畫面五：信用狀挑選視窗
     A***************************************************************
     A          R AR037F5                   SFL
     A            S#LLNO        20A  O  7  3
     A            S#BANK        14O  O    +2
     A            S#ENDT         8Y 0O    +2EDTWRD('    /  /  ')
     A***************************************************************
     A          R AR037F5C                  SFLCTL(AR037F5)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0010)
     A                                      WINDOW(3 24 19 52)
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF12(12)
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A  73                                  SFLCLR
     A N74                                  SFLEND
     A                                      SFLCSRRRN(&S#CRN5)
     A            S#NBR5         4S 0H      SFLRCDNBR(CURSOR)
     A            S#CRN5         5S 0H
     A            S#SFN5         4S 0H
     A*----------------------------------------------------------------
     A                                  1  6'★挑選信用狀★'
     A                                      DSPATR(RI)
     A                                    +6'ARE037S-5'
     A                                      DSPATR(HI)
     A                                  3  2'移動游標至該筆後，按輸入鍵即可。'
     A                                      DSPATR(HI)
     A                                  5  2'信用狀編號'
     A                                  5 27'開狀銀行'
     A                                  5 43'到期日'
     A                                  6  2'-----------------------------------
     A                                      ----------------'
     A                                      DSPATR(HI)
     A***************************************************************
     A          R AR037F5M
     A                                      WINDOW(AR037F5C)
     A                                      OVERLAY
     A                                 17  2'-----------------------------------
     A                                      ------'
     A                                      DSPATR(HI)
     A                                 18  2'F03:結束'
     A                                    +4'F12:回前'
