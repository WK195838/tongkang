     A*****************************************************************
     A*
     A*                >>  PROGRAM INTRODUCTION  <<
     A*
     A*            1.PROGRAM-ID    ARELCD
     A*            2.PROGRAM-TYPE  PF
     A*            3.AUTHOR        S00WCJ
     A*            4.FUNCTION     電子押匯介面檔
     A*            5.DATE-WRITTEN  112/05/31 2023AR00031
     A*            6.UPDATE  DATE
     A*
     A****************************************************************
     A*
     A*                                     UNIQUE
     A          R RARELCD                   TEXT('電子押匯介面檔')
     A            EDFLAG         1A         COLHDG('處理代碼')
     A            EDCUNO         6A         COLHDG('M客戶編號')
     A            EDRVNO        16A         COLHDG('M匯票號碼；繳款單')
     A            EDRVDT         8S 0       COLHDG('繳款單到期日')
     A            EDRLNO        16A         COLHDG('M信用狀號碼')
     A            EDEAMT        12S 0       COLHDG('M匯票金額')
     A            EDEDNM       128O         COLHDG('M貨品名稱')
     A            EDINDT         8S 0       COLHDG('M出貨日；發票日')
     A****
     A            EDBYID         8A         COLHDG('M買受人統編')
     A            EDPOCD        16A         COLHDG('M郵遞區號')
     A            EDBYAS       156O         COLHDG('M買受人地址')
     A            EDBYNM        40O         COLHDG('M買受人名稱')
     A****
     A            EDINNO        16A         COLHDG('M發票號碼')
     A            EDIVDT         8S 0       COLHDG('M發票日期')
     A            EDPDBF       256O         COLHDG('M產品摘要')
1207AA            EDGDAT        12S 2       COLHDG('M總計新台幣數字')
     A****
     A            EDTMDT         8S 0       COLHDG('傳輸日期')
     A            EDTMTM         6S 0       COLHDG('傳輸時間')
     A            EDTMOP        10A         COLHDG('傳輸人員')
     A            EDTAWS        10A         COLHDG('傳輸工作站')
     A            EDSTCL         1A         COLHDG('執行碼')
     A*
     A            EDXPDD         8S 0       COLHDG('上送日期')
     A            EDXPDT         6S 0       COLHDG('上送時間')
     A            EDXUSR        10A         COLHDG('上送人員')
     A            EDMAR3        30A         COLHDG('備註')
     A****
     A            ELCHDD         8S 0       COLHDG('異動日期')
     A            ELCHDT         6S 0       COLHDG('異動時間')
     A            ELCUSR        10A         COLHDG('異動人員')
     A            ELAPNM        20A         COLHDG('程式代碼')
     A****
     A            EDEDDT         8S 0       COLHDG('O押匯日期')
     A            EDAAMT        64O         COLHDG('O匯票中文金額')
     A            EDEQTY         8S 1       COLHDG('O數量')
     A            EDTAMT        12S 0       COLHDG('O總價')
     A            EDAPAD       156O         COLHDG('O申請人地址')
     A            EDMAR1       156O         COLHDG('O備註')
     A            EDTRNM        16O         COLHDG('O買受人稅籍編號')
     A            EDSPNO         9A         COLHDG('O發貨通知單編號')
     A            EDCKNR         9A         COLHDG('O檢查號碼')
     A            EDORNO         9A         COLHDG('O訂單編號')
     A            EDTTNB        12S         COLHDG('O總件數')
     A            EDGSWT        12S 2       COLHDG('O總重量')
     A            EDTSPP        12S 2       COLHDG('O運費總價')
     A            EDCSAT        12S 2       COLHDG('O貨款總價')
     A            EDFTCA        12S 2       COLHDG('O運費及貨款總額')
     A            EDDTAE        12S 2       COLHDG('O稅額；應稅')
     A            EDZOTE        12S 2       COLHDG('O零稅率')
     A            EDDTFE        12S 2       COLHDG('O免稅')
     A            EDSATX        12S 2       COLHDG('O營業稅')
     A            EDDSCT        12S 0       COLHDG('O折扣')
     A            EDADJT        12S 2       COLHDG('O角分調整')
     A            EDMAR2       256O         COLHDG('O備註')
     A            EDUNDR        56O         COLHDG('O負責人')
     A****
     A            EDITNO         5S 0       COLHDG('O項次')
     A            EDSPEC       256O         COLHDG('O規格說明')
     A            EDPIEC         9S 0       COLHDG('O件數')
     A            EDPCUT        32O         COLHDG('O件數單位')
     A            EDWEGT        12S 2       COLHDG('O重量')
     A            EDWEUT        32O         COLHDG('O重量單位')
     A            EDUTFT        12S 2       COLHDG('O運費單價')
     A            EDUTCT        12S 2       COLHDG('O貨款單價')
     A            EDFTAT        12S 2       COLHDG('O運費')
     A            EDCTAT        12S 2       COLHDG('O貨款')
     A*
     A          K EDRVNO
     A          K EDINNO
