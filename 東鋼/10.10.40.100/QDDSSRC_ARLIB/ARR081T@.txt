     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARR081T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     信用狀押匯提示明細表
     A*           4.DATE-WRITTEN  88/12/03
     A*           5.WRITER        S02CLJ
     A*
     A*
     A*****************************************************************
     A          R AR081T1H
     A                                      SKIPB(002)
     A                                      SPACEA(001)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      '報表代號: ARR081T'
     A                                    53
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   113
     A                                      '列表日期:'
     A                                   124
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      '列表人員:'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                    55
     A                                      '信用狀押匯提示明細表'
     A                                   113
     A                                      '列表時間:'
     A                                   124
     A                                      TIME
     A                                     2
     A                                      '押匯日期:'
     A                                      SPACEB(001)
     A            R#RVDT         8S 0O    14
     A                                      EDTWRD('    /  /  ')
     A                                   113
     A                                      '頁　　次:'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*----------------------------------------------------------------*********
     A                                     5
     A                                      '客戶'
     A                                      SPACEB(002)
     A                                    18
     A                                      '信用狀號碼'
     A                                    32
     A                                      '押匯金額'
     A                                    47
     A                                      '開狀銀行'
     A                                    62
     A                                      '入帳銀行'
     A                                    76
     A                                      '郵電費'
     A                                    87
     A                                      '信用狀餘額'
     A                                   101
     A                                      'ＬＣ到期日'
     A                                    +4
     A                                      '帳戶及餘額'
     A                                     2
     A                                      '------'
     A                                      SPACEB(001)
     A                                     8
     A                                      '----------'
     A                                    19
     A                                      '----------'
     A                                    30
     A                                      '--------------'
     A                                    45
     A                                      '--------------'
     A                                    +1
     A                                      '--------------'
     A                                    +1
     A                                      '----------'
     A                                    86
     A                                      '---------------'
     A                                   102
     A                                      '----------'
     A                                    +1
     A                                      '-------------------'
     A**************************************************************************
     A          R AR081T1D
     A                                      SPACEA(002)
     A            R#CUNO         6A  O     2
     A            R#CUNM        10O  O     8
     A            R#LLNO        10A  O    19
     A            R#RAMT        11S 0O    30
     A                                      EDTCDE(2)
     A            R#PBID        14O  O    45
     A            R#BANK        14O  O    +1
     A            R#LAMT        11S 0O    86
     A                                      EDTCDE(K)
     A            R#LEDT         8S 0O   102
     A                                      EDTWRD('    /  /  ')
     A            R#PBAL        11S 0O    +5
     A                                      EDTCDE(K)
     A**************************************************************************
     A          R AR081T1T
     A                                      SPACEB(002)
     A                                    16
     A                                      '**本日合計:'
     A            R#TRNA        11S 0O    30
     A                                      EDTCDE(2)
     A*****************************************************************
     A          R AR081T1M
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A                                    19
     A                                      '*****無條件符合之資料！！'
     A**************************************************************************
