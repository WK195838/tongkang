     A*%%TS  SD  19911004  131634  S02YSH      REL-V4R4M0  5769-PW1
     A*****************************************************************
     A*
     A*               >>   PROGRAM INTRODUCTION   <<
     A*
     A*            1.PROGRAM-ID    ARE049S
     A*            2.PROGRAM-TYPE  DSPF
     A*            3.AUTHOR        S02LYW
     A*            4.FUNCTION     承購客戶沖銷作業
     A*            5.DATE-WRITTEN  91/02/18
     A*
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 '離開')
     A                                      CA12(12 '回上頁')
     A*================================================================
     A          R AR049H
     A*%%TS  SD  19911004  131634  S02YSH      REL-V4R4M0  5769-PW1
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  1 24'も　東和鋼鐵企業股份有限公司　も-
     A                                      '
     A            S#SRID        10A  O  1 70
     A                                  2  2TIME
     A                                  2 23'★應收承購(E-FACTORING)繳款作+
     A                                      業★'
     A                                      DSPATR(RI)
     A            S#DEVN        10A  O  2 70
     A            S#USER        10A  O  3  2
     A  30        S#MODE         6A  O  3 70DSPATR(RI)
     A*================================================================
     A          R AR049F1
     A*%%TS  SD  19910218  164735  S02LYW      REL-V4R4M0  5769-PW1
     A                                      OVERLAY
     A                                 10 30'客戶代號：'
     A            S#CUNO         6A  B    +1
     A  41                                  DSPATR(PC)
     A                                 23  2'F3/F12=離開'
     A            S#ERR         70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR049F2                   SFL
     A*%%TS  SD  19910320  164331  S02LYW      REL-V4R4M0  5769-PW1
     A  70                                  SFLNXTCHG
     A            S#OP           1Y 0B 10  2EDTCDE(Z)
     A                                      VALUES(0 2 4 5 6 7)
     A  48                                  DSPATR(PC RI)
     A            S#RCNO         6A  O 10  6
     A            S#ENDT         8Y 0O 10 15EDTWRD('    /  /  ')
     A            S#TAMT         9Y 0O 10 28EDTCDE(1)
     A            S#FL01         1A  O 10 46
     A            S#CDA1         1A  O 10 57
     A            S#CDB1         1A  O 10 66
     A            S#CLOX         1A  O 10 75
     A*================================================================
     A          R AR049F2C                  SFLCTL(AR049F2)
     A*%%TS  SD  19910320  164331  S02LYW      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(91 '下頁')
     A N71                                  SFLDSPCTL
     A N72                                  SFLDSP
     A N73                                  SFLEND
     A  74                                  SFLCLR
     A                                      CA06(06 '新增')
     A  81                                  CF11(11 '刪除')
     A                                      OVERLAY
     A                                  4  2'客戶代號:'
     A            S#CUNO         6A  O  4 15
     A            S#CUNM        10A  O    +1
     A                                  5  2'OP:2=修改4=刪除'
     A                                  5 22'5=查詢6=確認7=確認還原'
     A            S#LIN1        78A  O  6  2
     A                                  7  2'OP'
     A                                  7  5'繳款單號'
     A                                  7 16'繳款日期'
     A                                  7 29'繳款金額'
     A                                  7 41'營業確認碼'
     A                                  7 54'下載碼'
     A                                  7 63'清算碼'
     A                                  7 72'結案碼'
     A            S#LIN2        78A  O  8  2
     A            S#KEY1         6A  B  9  6
     A            S#NBR2         4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR049F2M
     A                                      OVERLAY
     A            S#LIN3        78A    22  2
     A                                 23  2'F3=離開'
     A                                    +1'F12=回上頁'
     A                                    +1'PA1=下頁'
     A                                    +1'F6=新增'
     A                                      DSPATR(RI)
     A            S#ERR2        70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR049F3                   SFL
     A*%%TS  SD  19910327  104712  S02LYW      REL-V4R4M0  5769-PW1
     A  75                                  SFLNXTCHG
     A            S#ITEM         2S 0O 11  3
     A            S#APN1         6A  O 11  7
     A            S#INNO        10A  B 11 14
     A N84                                  DSPATR(PR)
     A  83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A            S#INDT         8Y 0O 11 26EDTWRD('    /  /  ')
     A            S#AMT1         9Y 0O 11 39EDTCDE(1)
     A            S#AMT2         9Y 0O 11 53EDTCDE(1)
     A            S#AMT3         9Y 0B 11 67EDTCDE(1)
     A N84                                  DSPATR(PR)
     A  90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A*================================================================
     A          R AR049F3C                  SFLCTL(AR049F3)
     A*%%TS  SD  19910327  140141  S02LYW      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A N76                                  SFLDSPCTL
     A N77                                  SFLDSP
     A N78                                  SFLEND
     A  79                                  SFLCLR
     A                                      CF07(07 '依請款單載入')
     A                                      CF08(08 '承購批號載入')
     A                                      CF09(09 '快速沖銷')
     A                                      CF10(10 '存檔')
     A                                      OVERLAY
     A                                  4  2'客戶代號:'
     A            S#CUNO         6A  O  4 14
     A            S#CUNM        10A  O  4 21
     A                                  5  2'繳款單號:'
     A            S#RCNO         6A  O  5 14
     A                                  5 25'繳款日期:'
     A            S#ENDT         8Y 0O  5 37EDTWRD('    /  /  ')
     A                                    +1'入帳日期:'
     A            S#ACDT         8Y 0O    +1EDTWRD('    /  /  ')
     A                                  6  2'繳款金額:'
     A            S#TAMT         9Y 0O    +1EDTCDE(1)
     A            S#LIN4        78A  O  7  2
     A                                  8  2'項'
     A                                  8  7'請款'
     A                                  8 16'發票'
     A                                  8 28'發票'
     A            S#AMTT         9Y 0O  8 67EDTCDE(2)
     A  91                                  DSPATR(RI)
     A                                  9  2'次'
     A                                  9  7'單號'
     A                                  9 16'號碼'
     A                                  9 28'日期'
     A                                  9 40'發票金額'
     A                                  9 54'發票餘額'
     A                                  9 68'繳款金額'
     A            S#LIN5        78A  O 10  2
     A            S#NBR3         4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR049F3M
     A                                      OVERLAY
     A            S#LIN6        78A    21  2
     A                                 22  2'F3=離開'
     A                                    +1'F12=回上頁'
     A                                    +1'PA1=下頁'
     A  80                                +1'F10=存檔'
     A  81N66                             +1'F9=快速沖銷'
     A*暫時停用F8,F9功能,恢復時拿掉IN66即可
     A  81                             23  2'F7=依請款單載入'
     A  81N66                             +1'F8=依承購批號載入'
     A            S#ERR3        70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR049F4                   SFL
     A*%%TS  SD  19911004  115149  S02YSH      REL-V4R4M0  5769-PW1
     A  93                                  SFLNXTCHG
     A            S#ITM2         2S 0O 10  4
     A            S#NTTP         1A  B 10 10
     A N84                                  DSPATR(PR)
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A            S#BANM        16O  B 10 15
     A N84                                  DSPATR(PR)
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A            S#RLNO        10A  B 10 33
     A N84                                  DSPATR(PR)
     A  89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A            S#RAMT        12Y 0B 10 44EDTCDE(1)
     A N84                                  DSPATR(PR)
     A  87                                  DSPATR(PC)
     A  87                                  DSPATR(RI)
     A            S#DUDT         8Y 0B 10 61EDTWRD('    /  /  ')
     A N84                                  DSPATR(PR)
     A  85                                  DSPATR(PC)
     A  85                                  DSPATR(RI)
     A            S#NTCD         1A  O 10 74
9111 A            S#RESV        30O  B 11 12
     A*================================================================
     A          R AR049F4C                  SFLCTL(AR049F4)
     A*%%TS  SD  19911004  115149  S02YSH      REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A N94                                  SFLDSPCTL
     A N95                                  SFLDSP
     A N96                                  SFLEND
     A  97                                  SFLCLR
     A                                      OVERLAY
9110 A                                      CF01(01 '說明')
     A                                  4  2'客戶代號:'
     A            S#CUNO         6A  O  4 14
     A            S#CUNM        10A  O  4 21
     A                                  5  2'繳款單號:'
     A            S#RCNO         6A  O  5 14
     A                                  5 25'繳款日期:'
     A            S#ENDT         8Y 0O  5 37EDTWRD('    /  /  ')
     A                                  5 48'入帳日期:'
     A            S#ACDT         8Y 0B  5 60EDTWRD('    /  /  ')
     A N84                                  DSPATR(PR)
     A  86                                  DSPATR(PC)
     A  86                                  DSPATR(RI)
     A            S#LIN4        78A  O  6  2
     A                                  7  2'項次'
     A                                  7  9'票款別'
     A                                  7 18'開立銀行'
     A                                  7 33'相關號碼'
     A                                  7 49'金　　額'
     A                                  7 60'到期日'
     A                                  7 71'兌現'
     A                                  8 14'備　　　　　　　　　註'
     A            S#LIN5        78A  O  9  2
     A            S#NBR4         4S 0H      SFLRCDNBR(CURSOR)
     A*================================================================
     A          R AR049F4M
     A                                      OVERLAY
     A            S#LIN6        78A    22  2
     A                                 23  2'F1=說明'
     A                                    +1'F3=離開'
     A                                    +1'F12=回上頁'
     A                                    +1'執行=沖銷明細'
     A            S#ERR4        70A  O 24  2DSPATR(HI)
     A*================================================================
     A          R AR049W1
     A*%%TS  SD  19910312  160642  S02YSH      REL-V4R4M0  5769-PW1
     A                                      WINDOW(8 44 8 24)
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                  2  5'*請款單載入*'
     A                                  4  2'請款單號:'
     A            S#APNO         6A  B  4 14
     A  82                                  DSPATR(RI)
     A                                  6  3'F3/F12:離開'
     A                                      DSPATR(HI)
     A            S#ERRW        20A  O  7  2DSPATR(HI)
     A*================================================================
     A          R AR049W2
     A*%%TS  SD  19910416  102244  S02YSH      REL-V4R4M0  5769-PW1
     A                                      WINDOW(8 44 8 32)
     A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                  2 10'*批號載入*'
     A                                  4  1'承購批號:'
     A            S#BHID        30A  B  5  2
     A  92                                  DSPATR(RI)
     A                                  6  3'F3/F12:離開'
     A                                      DSPATR(HI)
     A            S#ERW2        20A  O  7  2DSPATR(HI)
     A*================================================================
9110 A          R AR049W3
 .   A                                      WINDOW(10 15 11 20)
 .   A                                      WDWBORDER((*COLOR GRN) (*DSPATR RI)-
 .   A                                       (*CHAR '        '))
 .   A                                  2  4'*票款別查詢*'
 .   A                                  4  2'Ａ：支票'
 .   A                                  7  2'Ｇ：匯款'
 .   A                                  6  2'Ｈ：現金'
 .   A                                  5  2'Ｉ：信用狀'
 .   A                                  8  2'Ｊ：銀行存款'
 .   A                                 10  3'F3/F12=結束查詢'
 .   A                                      DSPATR(HI)
