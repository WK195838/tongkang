     A*****************************************************************
     A*
     A*           1.PROGRAM-ID    ARP072T
     A*           2.PROGRAM-TYPE  PRTF
     A*           3.FUNCTION     發票與銷貨差異代開傳票
     A*           4.DATE-WRITTEN  89/05/19
     A*           5.WRITER        S02YSH
     A*           6.UPDATE DATE  101/09/21 2012AR682 S00WCJ (0109A)
     A*                          104/09/23 2015AR953 S00WCJ (0409A)
     A*                           產品代碼3碼擴5碼
     A*
     A*****************************************************************
     A          R AR072TH
     A*****************************************************************
     A                                      SKIPB(003)
     A*----------------------------------------------------------------*********
     A                                     2
     A                                      ' PRT-ID  : ARP072T'
     A                                    50
     A                                      '東和鋼鐵企業股份有限公司'
     A                                   110
     A                                      '列表日期:'
     A                                   121
     A                                      DATE
     A                                      EDTCDE(Y)
     A                                     2
     A                                      ' USR-ID  :'
     A                                      SPACEB(001)
     A            U#USID        10A  O    +1
     A                                   +28
     A                                      '發票與銷貨差異入帳清單'
     A                                   110
     A                                      '列表時間:'
     A                                   121
     A                                      TIME
     A                                     2
     A                                      '入帳日期'
     A                                      SPACEB(001)
     A            R#DATE         8S  O    +1
     A                                      EDTWRD('    /  /  ')
     A                                    54
     A                                      '代開年月:'
     A            U#YYMM         6S 0O    66
     A                                      EDTWRD('    /  ')
     A                                   110
     A                                      '頁　　次:'
     A                                   121
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                     2
     A                                      '發票號碼'
     A                                      SPACEB(002)
     A                                    13
     A                                      '訂單'
     A                                    21
     A                                      '客戶名稱'
     A                                    33
     A                                      '借貸'
     A                                    41
     A                                      '會計科目'
     A                                    61
     A                                      '方式'
     A                                    67
     A                                      '品　名'
     A                                    81
     A                                      '更正'
     A                                    88
     A                                      '借方金額'
     A                                   100
     A                                      '貸方金額'
     A                                   112
     A                                      '部門'
     A                                   120
     A                                      '傳票編號'
     A            T#LINE       132         1
     A                                      SPACEB(001)
     A*****************************************************************
     A          R AR072T1
     A*****************************************************************
     A            T#IVNO        10         2
     A                                      SPACEB(001)
     A            T#ORNO         6        13
     A            T#CUNM        10        +2
     A            T#DC           1        +4
0109AA            T#NO           6        +5
     A            T#NONM        16        +1
     A            T#TYCO         2        +1
0409AA            T#DSC2         5        +1
0409AA            T#DSCN         8        +1
     A            T#CORT         2        +2
     A            T#DAMT         8  0     +3
     A                                      EDTCDE(2)
     A            T#CAMT         8  0     +2
     A                                      EDTCDE(2)
     A            T#DPNO         8       112
     A            T#VRNO         8       121
     A                                    +0
     A                                      '-'
     A            T#ITEM         3        +0
     A**************************************************************************
     A          R AR072T2
     A**************************************************************************
     A                                      SPACEB(002)
     A                                    40
     A                                      '入帳筆數:'
     A            T#RCNT         3S  O    55
     A                                      EDTCDE(J)
     A                                    70
     A                                      '借方合計:'
     A            T#CNTD         9  0     83
     A                                      EDTCDE(J)
     A                                   100
     A                                      '貸方合計:'
     A            T#CNTC         9  0    113
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR072T3
     A**************************************************************************
     A                                      SPACEB(002)
     A                                    40
     A                                      '品名'
     A                                    58
     A                                      '金額合計'
     A**************************************************************************
     A          R AR072T4
     A**************************************************************************
     A                                      SPACEB(001)
0409AA            T#PDN1         5        41
     A            T#AMT1        10  0     54
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR072T5
     A**************************************************************************
     A                                      SPACEB(001)
     A            T#PDM2         8        40
     A                                    48
     A                                      '小計'
     A            T#AMT2        10  0     54
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR072T6
     A**************************************************************************
     A                                      SPACEB(001)
     A                                    40
     A                                      '總合計'
     A            T#AMT3        10  0     54
     A                                      EDTCDE(J)
     A**************************************************************************
     A          R AR072TL
     A**************************************************************************
     A                                      SPACEB(001)
     A                                    41
     A                                      '---------------------------'
     A**************************************************************************
     A          R AR072TX
     A**************************************************************************
     A                                      SKIPB(059)
     A                                     2
     A                                      '<財會>'
     A                                    +0
     A                                      '主管:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '複核:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A                                    +0
     A                                      '入帳:'
     A                                    +0
     A                                      '            '
     A                                      UNDERLINE
     A*****************************************************************
     A          R AR072TM
     A*****************************************************************
     A                                      SPACEB(003)
     A                                      SPACEA(002)
     A*----------------------------------------------------------------
     A                                    19
     A                                      '*****無條件符合之資料！！'
     A*****************************************************************
